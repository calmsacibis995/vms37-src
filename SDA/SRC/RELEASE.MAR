	.TITLE	RELEASE DUMP FILE SPACE TO PAGE FILE
	.SBTTL	COPYRIGHT NOTICE
	.IDENT	'V03-000'
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

	.SBTTL	PROGRAM DESCRIPTION
;++
;   FACILITY
;
;	SYSTEM DUMP ANALYZER
;
;   ABSTRACT
;
;	PRIVILEGED CODE TO RELEASE DUMP SPACE TO PAGE FILE IF NECESSARY
;
;   ENVIRONMENT
;
;	NATIVE MODE, KERNEL MODE
;
;   AUTHOR
;
;	PETER H. LIPMAN, APRIL, 1982
;
;   MODIFIED BY:
;

	.SBTTL	DECLARATIONS
;
;	SYMBOL DEFINTIONS
;
	$DVIDEF			; DEVICE INFORMATION ITEM DEFINITIONS
	$IPLDEF			; PROCESSOR PRIORITY LEVEL DEFINITIONS
	$NAMDEF			; NAME BLOCK DEFINITIONS
	$PRDEF			; PROCESSOR REGISTER DEFINITIONS
	$SSDEF			; SYSTEM SERVICE CONDTION CODE DEF'S

	.SBTTL	SDA$RELEASE_DUMP - RELEASE DUMP BLOCKS
;---
;
;	SDA$RELEASE_DUMP
;
;	THIS ROUTINE RELEASES THE DUMP FILE BLOCKS TO THE PAGE FILE
;	IF THE FILE IS THE PAGE FILE THAT SYSINIT INITIALIZED AND
;	THE BLOCKS HAVE NOT PREVIOUSLY BEEN RELEASED
;
;   INPUTS:
;
;	4(AP) = NAM BLOCK ADDRESS FOR THE DUMP FILE
;
;   OUTPUTS:
;
;	R0 = SS$_WASSET IF THE DUMP FILE BLOCKS HAVE ACTUALLY BEEN
;	     RETURNED TO THE PAGE FILE
;	   = SS$_WASCLR IF THIS FILE WAS NOT THE PAGE FILE OR THE
;	     BLOCKS HAD PREVIOUSLY BEEN RELEASED
;	   = LBC IF ERROR
;
;---

SYSDEV_NAM_DSC:
	.ASCID	/SYS$SYSDEVICE/

	.ENTRY	SDA$RELEASE_DUMP,^M<R2,R3,R4,R5,R6>
	SUBL	#64,SP			; RESERVE DEVICE NAME BUFFER
	MOVL	SP,R2			; SAVE ITS ADDRESS
	MOVAL	-(SP),R3		; RESERVE RETURN LENGTH, ADR TO R3
	CLRL	-(SP)			; END OF ITEM LIST
	MOVQ	R2,-(SP)		; PUSH ADR TO RETURN LENGTH
					; PUSH ADR TO RETURN DEVICE NAME
	PUSHL	#<DVI$_DEVNAM>@16+64	; LENGTH OF DEVICE NAME BUFFER
					; AND ITEM CODE FOR DEVICE NAME
	MOVL	SP,R0			; ADDRESS OF ITEM LIST
	$GETDVI_S -
		EFN=#0 -
		DEVNAM=B^SYSDEV_NAM_DSC -
		ITMLST=(R0)
	BLBC	R0,30$
	$WAITFR_S EFN=#0
	MOVL	4(AP),R6		; GET NAM BLOCK ADDRESS
	DECW	(R3)			; DON'T COUNT TRAILING COLON
	CMPB	(R3),NAM$T_DVI(R6)	; SIZE OF DEVICE NAMES MATCH?
	BNEQ	20$			; BRANCH IF NOT
	CMPC3	(R3),(R2),NAM$T_DVI+1(R6) ; DEVICE NAMES MATCH?
	BNEQ	20$			; BRANCH IF NOT
	CMPC3	#6,NAM$W_FID(R6),G^EXE$GW_PGFL_FID ; IS THIS THE PAGE FILE?
	BNEQ	20$			; BRANCH IF NOT
	$CMKRNL_S B^RELEASE_DUMP_K	; RELEASE THE BLOCKS
	RET
20$:	MOVL	S^#SS$_WASCLR,R0
30$:	RET

	.SBTTL	RELEASE_DUMP_K - KERNEL MODE RELEASE
;---
;
;	RELEASE_DUMP_K
;
;	THIS KERNEL MODE ROUTINE RELEASES THE DUMP FILE BLOCKS
;	TO THE PAGE FILE IF THEY HAVE NOT ALREADY BEEN RELEASED.
;
;   INPUTS:
;
;	NONE
;
;   OUTPUTS:
;
;	R0 = SS$_WASSET IF THE DUMP FILE BLOCKS HAVE ACTUALLY BEEN
;	     RETURNED TO THE PAGE FILE
;	   = SS$_WASCLR IF THERE WERE NO BLOCKS TO RELEASE
;
;---

	.ENTRY	RELEASE_DUMP_K,^M<R2,R3>
	PUSHL	S^#SS$_WASCLR		; ASSUME NORMAL STATUS
10$:	SETIPL	40$			; AVOID RACE TO
	MOVL	G^EXE$GL_SAVEDUMP,R1	; TO FETCH AND CLEAR THIS
	BEQL	20$			; BRANCH IF ALREADY CLEAR
	MOVZWL	G^MMG$GW_MINPFIDX,R3	; GET PAGEFILE INDEX FOR PAGEFILE.SYS
	MOVL	@MMG$GL_PAGSWPVC[R3],R3	; PAGE FILE CONTROL BLOCK ADDRESS
	MOVL	#1,R0			; STARTING VBN TO RELEASE
	JSB	G^MMG$DEALLOCPAGFIL	; DEALLOCATE BLOCKS TO PAGE FILE
	CLRL	G^EXE$GL_SAVEDUMP	; NOTE BLOCKS RELEASED
	MOVL	S^#SS$_WASSET,(SP)	; STATUS FOR BLOCKS RELEASED
20$:	SETIPL	#0			; DONE WITH SYNCHRONIZATION
30$:	POPR	#^M<R0>
	RET
40$:	.LONG	IPL$_SYNCH

50$:	ASSUME	50$-10$ LE 512

	.END
