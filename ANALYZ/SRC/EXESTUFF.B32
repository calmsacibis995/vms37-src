%title 'EXESTUFF - Analyze Various Parts of an Image'
	module exestuff (
			ident='V03-000') = begin

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! Facility:	VAX/VMS Analyze Facility, Analyze Parts of an Image
!
! Abstract:	This module is responsible for analyzing various parts of
!		an image, including the header, patch text, and global
!		symbol table.
!
!
! Environment:
!
! Author: Paul C. Anagnostopoulos, Creation Date: 31 March 1981
!
! Modified By:
!
! , : Version
! 01 -
!--

%sbttl 'Module Declarations'
!
! Libraries and Requires:
!

library 'lib';
require 'objexereq';

!
! Table of Contents:
!

forward routine
	anl$image_header,
	anl$image_isd: novalue,
	anl$image_patch_text,
	anl$image_gst;

!
! External References:
!

external routine
	anl$check_flags,
	anl$check_symbol,
	anl$format_error,
	anl$format_flags,
	anl$format_hex,
	anl$format_line,
	anl$get_image_block,
	anl$object_eom,
	anl$object_gsd,
	anl$object_hdr,
	anl$interact,
	anl$object_record_size,
	anl$report_line,
	anl$report_page;

external
	anl$gb_interactive: byte;

!
! Own Variables:
!
! The following table defines the match control values used throughout.

own
	match_control: vector[8,long] initial(
				uplit byte(%ascic 'ISD$K_MATALL'),
				uplit byte(%ascic 'ISD$K_MATEQU'),
				uplit byte(%ascic 'ISD$K_MATLEQ'),
				uplit byte(%ascic 'ISD$K_MATNEV'));

%sbttl 'ANL$IMAGE_HEADER - Analyze Image Header'
!++
! Functional Description:	
!	This routine is responsible for analyzing an image header.  THis
!	includes formatting it in the report and checking its contents.
!
! Formal Parameters:
!	image_base	Return starting address of image here.
!	fixup_size	If a fixup section exists, return size here,
!	fixup_vbn	and VBN here.
!
! Implicit Inputs:
!	global data
!
! Implicit Outputs:
!	global data
!
! Returned Value:
!	If interactive session: true if we are to continue, false if not.
!
! Side Effects:
!
!--


global routine anl$image_header(image_base,fixup_size,fixup_vbn) = begin

own
	link_flags_def: vector[6,long] initial(
				4,
				uplit byte(%ascic 'IHD$V_LNKDEBUG'),
				uplit byte(%ascic 'IHD$V_LNKNOTFR'),
				uplit byte(%ascic 'IHD$V_NOP0BUFS'),
				uplit byte(%ascic 'IHD$V_PICIMG'),
				uplit byte(%ascic 'IHD$V_P0IMAGE'));

local
	status: long,
	hp: ref block[,byte],
	sp: ref block[,byte],
	last_vbn: long,
	vbn: long,
	isd: long,
	fixup_address: long;


! We are going to analyze the image header.  Get it.

anl$format_line(0,0,anl$_exehdr);
anl$report_line(-1);

status = anl$get_image_block(1,hp);

! If we couldn't get the first header block, or if it doesn't end with
! a %x'ff', then this can't be a native image.

if not .status or .hp[511,0,8,0] nequ %x'ff' then (
	anl$format_error(anl$_exenotnative);
	return false;
);

! Begin with the fixed fields at the beginning of the header.

anl$format_line(3,1,anl$_exehdrfixed);
anl$report_line(-1);

! Analyze the image identification info.

anl$format_line(0,2,anl$_exehdrimageid,2,hp[ihd$w_majorid],2,hp[ihd$w_minorid]);

! Analyze the header block count.

anl$format_line(0,2,anl$_exehdrblkcount,.hp[ihd$b_hdrblkcnt]);

! Analyze the image type code.  If shared, print the global section IDs and
! the match control.

selectoneu .hp[ihd$b_imgtype] of set
[ihd$k_exe]:	anl$format_line(0,2,anl$_exehdrtypeexe);

[ihd$k_lim]:	(anl$format_line(2,2,anl$_exehdrtypelim);
		anl$format_line(0,3,anl$_exehdrgblident,.hp[ihd$l_ident]);
		selectoneu .hp[ihd$v_matchctl] of set
		[isd$k_matall,
		 isd$k_matequ,
		 isd$k_matleq,
		 isd$k_matnev]:	anl$format_line(0,3,anl$_exehdrmatch,
					.match_control[.hp[ihd$v_matchctl]]);
		[otherwise]:	anl$format_error(anl$_exebadmatch,.hp[ihd$v_matchctl]);
		tes;);

[otherwise]:	anl$format_error(anl$_exebadtype,.hp[ihd$b_imgtype]);
tes;

! Analyze the I/O channel count.

if .hp[ihd$w_iochancnt] eqlu 0 then
	anl$format_line(0,2,anl$_exehdrchandef)
else
	anl$format_line(0,2,anl$_exehdrchancount,.hp[ihd$w_iochancnt]);

! Analyze the I/O section page count.

if .hp[ihd$w_imgiocnt] eqlu 0 then
	anl$format_line(0,2,anl$_exehdrpagedef)
else
	anl$format_line(0,2,anl$_exehdrpagecount,.hp[ihd$w_imgiocnt]);

! Analyze the linker-produced flags.  Don't get confused by the match control.

anl$format_flags(2,anl$_exehdrflags,.hp[ihd$l_lnkflags] and %x'00ffffff',link_flags_def);
anl$check_flags(.hp[ihd$l_lnkflags] and %x'00ffffff',link_flags_def);

! Analyze the system version, if specified.

if .hp[ihd$l_sysver] nequ 0 then
	anl$format_line(0,2,anl$_exehdrsysver,4,hp[ihd$l_sysver]);

! If the fixed portion is long enough to accomodate a fixup section
! virtual address (V3A and later), then remember the address.

if .hp+.hp[ihd$w_activoff] gtra hp[ihd$l_iafva] then
	fixup_address = .hp[ihd$l_iafva]
else
	fixup_address = 0;

! If this is an interactive session, give the user a chance to quit.

if .anl$gb_interactive then
	if not anl$interact() then
		return false;

! Now we are going to analyze the information in the activation section.
! It is always present.

anl$report_line(-1);
anl$format_line(3,1,anl$_exehdractive);
anl$report_line(-1);

sp = .hp + .hp[ihd$w_activoff];

! Analyze the three transfer addresses.

anl$format_line(0,2,anl$_exehdrxfer1,.sp[iha$l_tfradr1]);
anl$format_line(0,2,anl$_exehdrxfer2,.sp[iha$l_tfradr2]);
anl$format_line(0,2,anl$_exehdrxfer3,.sp[iha$l_tfradr3]);

! Make sure the thing ends with a trailing zero.

if .sp[12,0,32,0] nequ 0 then
	anl$format_error(anl$_exebadxfer0);

! If this is an interactive session, give the user a chance to quit.

if .anl$gb_interactive then
	if not anl$interact() then
		return false;

! Now we are going to analyze the stuff in the symbol table and debug section.
! It is always present.

anl$report_line(-1);
anl$format_line(3,1,anl$_exehdrsymdbg);
anl$report_line(-1);

sp = .hp + .hp[ihd$w_symdbgoff];

! Analyze the debug symbol table VBN and block count.

anl$format_line(0,2,anl$_exehdrdst,.sp[ihs$l_dstvbn],.sp[ihs$w_dstblks]);

! Analyze the global symbol table VBN and record count.

anl$format_line(0,2,anl$_exehdrgst,.sp[ihs$l_gstvbn],.sp[ihs$w_gstrecs]);

! If this is an interactive session, give the user a chance to quit.

if .anl$gb_interactive then
	if not anl$interact() then
		return false;

! Now we are going to tackle the image identification section.
! It is always present.

anl$report_line(-1);
anl$format_line(3,1,anl$_exehdrident);
anl$report_line(-1);

sp = .hp + .hp[ihd$w_imgidoff];

! First we analyze the image name.

anl$format_line(0,2,anl$_exehdrname,sp[ihi$t_imgnam]);
begin
local
	name_dsc: descriptor;

build_descriptor(name_dsc,.sp[0,0,8,0],sp[1,0,8,0]);
anl$check_symbol(name_dsc);
end;

! Now the image identification.

anl$format_line(0,2,anl$_exehdrfileid,sp[ihi$t_imgid]);

! Now the date and time of linking.

anl$format_line(0,2,anl$_exehdrtime,sp[ihi$q_linktime]);

! And the linker identification.

anl$format_line(0,2,anl$_exehdrlinkid,sp[ihi$t_linkid]);

! If this is an interactive session, give the user a chance to quit.

if .anl$gb_interactive then
	if not anl$interact() then
		return false;

! Now we are going to analyze the patch section.
! It may not necessarily exist.

anl$report_line(-1);
anl$format_line(3,1,anl$_exehdrpatch);
anl$report_line(-1);

if .hp[ihd$w_patchoff] nequ 0 then (
	sp = .hp + .hp[ihd$w_patchoff];

	! Begin with the Digital ECO bits.

	anl$format_line(0,2,anl$_exehdrdececo,.sp[ihp$l_eco1],.sp[ihp$l_eco2],.sp[ihp$l_eco3]);

	! And the user ECO bits.

	anl$format_line(0,2,anl$_exehdrusereco,.sp[ihp$l_eco4]);

	! Analyze the read/write and read-only patch area info.

	anl$format_line(0,2,anl$_exehdrrwpatch,.sp[ihp$l_rw_patadr],.sp[ihp$l_rw_patsiz]);
	anl$format_line(0,2,anl$_exehdrropatch,.sp[ihp$l_ro_patadr],.sp[ihp$l_ro_patsiz]);

	! Now the VBN of the patch command text.

	anl$format_line(0,2,anl$_exehdrtextvbn,.sp[ihp$l_patcomtxt]);

	! And the date of most recent patch.

	anl$format_line(0,2,anl$_exehdrpatchdate,sp[ihp$q_patdate]);

	! If this is an interactive session, give the user a chance to quit.

	if .anl$gb_interactive then
		if not anl$interact() then
			return false;
) else (

	! There is no patch section now.

	anl$format_line(0,2,anl$_exehdrnopatch);
);

! Analyze the image section descriptors.  These begin after all the above
! sections and can go on for multiple blocks.
! We also use this loop to search for the fixup section.  If we don't find
! one, we will inform the caller with zero fixup parameters.

.fixup_size = .fixup_vbn = 0;

anl$report_line(-1);
anl$format_line(3,1,anl$_exehdrisd);

last_vbn = .hp[ihd$b_hdrblkcnt];
vbn = 1;
sp = .hp + .hp[ihd$w_size];

incru isd from 1 do (

	! First we see if we have run out of ISDs in this block.  If so,
	! we advance to the next block.  We better not go too far.

	if .sp[0,0,8,0] eqlu %x'ff' then (
		increment (vbn);
		status = anl$get_image_block(.vbn,hp);
		if not .status then (
			anl$format_error(anl$_exehdrlong);
exitloop;
		);
		sp = .hp;
	);

	! Now we see if we are all done with the ISDs.  The last ISD is
	! a dummy with a length of 0.

exitif (.sp[isd$w_size] eqlu 0);

	! Seems we have an ISD to analyze.  Make sure it fits completely
	! within the block.

	if .sp[isd$w_size] gtru .hp+512-.sp then (
		anl$format_error(anl$_exehdrisdlong);
exitloop;
	);

	! Format and analyze the ISD.

	anl$image_isd(.sp,.isd);

	! If this is the first ISD, then we want to return its base address,
	! which is the starting address of the entire image.

	if .isd eqlu 1 then
		.image_base = .sp[isd$v_vpn]^9;

	! If we have a fixup section, let's see if this is it.  If so,
	! return its size and VBN.  If they are bad, tell the user.

	if .fixup_address neqa 0 then
		if .fixup_address eqla .sp[isd$v_vpg]^9 then
			if .sp[isd$w_pagcnt] eqlu 0 or .sp[isd$l_vbn] eqlu 0 then
				anl$format_error(anl$_exebadfixupisd)
			else (
				.fixup_size = .sp[isd$w_pagcnt];
				.fixup_vbn  = .sp[isd$l_vbn];
			);

	! If this is an interactive session, give the user a chance to quit.

	if .anl$gb_interactive then
		if not anl$interact() then
			return false;

	! Go on to the next ISD.

	sp = .sp + .sp[isd$w_size];
);

return true;

end;

%sbttl 'ANL$IMAGE_ISD - Analyze ISD Structure'
!++
! Functional Description:
!	This routine is responsible for formatting and analyzing an
!	Image Section Descriptor.
!
! Formal Parameters:
!	the_isd		Address of the ISD.
!	isd_number	The sequence number of this ISD.
!
! Implicit Inputs:
!	global data
!
! Implicit Outputs:
!	global data
!
! Returned Value:
!	none
!
! Side Effects:
!
!--


global routine anl$image_isd(the_isd,isd_number): novalue = begin

bind
	sp = the_isd: ref block[,byte];

own
	space_names: vector[4,long] initial(
				uplit byte (%ascic 'P0'),
				uplit byte (%ascic 'P1'),
				uplit byte (%ascic 'S0'),
				uplit byte (%ascic 'S1???')),

	isd_flags_def: vector[20,long] initial(
				18,
				uplit byte(%ascic 'ISD$V_GBL'),
				uplit byte(%ascic 'ISD$V_CRF'),
				uplit byte(%ascic 'ISD$V_DZRO'),
				uplit byte(%ascic 'ISD$V_WRT'),
				0,0,0,
				uplit byte(%ascic 'ISD$V_LASTCLU'),
				uplit byte(%ascic 'ISD$V_COPYALWAY'),
				uplit byte(%ascic 'ISD$V_BASED'),
				uplit byte(%ascic 'ISD$V_FIXUPVEC'),
				0,0,0,0,0,0,
				uplit byte(%ascic 'ISD$V_VECTOR'),
				uplit byte(%ascic 'ISD$V_PROTECT')),

	isd_types: vector[5,long] initial(
				uplit byte (%ascic 'NORMAL'),
				uplit byte (%ascic 'SHRFXD'),
				uplit byte (%ascic 'PRVFXD'),
				uplit byte (%ascic 'SHRPIC'),
				uplit byte (%ascic 'PRVPIC'));



! It is assumed that the ISD fits in the header block.  We can freely 
! reference the fields.

! Begin with a heading line for this ISD.

anl$report_line(-1);
anl$format_line(3,2,anl$_exehdrisdnum,.isd_number,.sp[isd$w_size]);

! Analyze the page count.

anl$format_line(0,3,anl$_exehdrisdcount,.sp[isd$w_pagcnt]);

! Analyze the base virtual page number and space bits.

anl$format_line(0,3,anl$_exehdrisdbase,.sp[isd$v_vpn]^9,.space_names[.sp[4,21,2,0]]);
if .sp[isd$v_p1] and .sp[isd$v_system] then
	anl$format_error(anl$_exebadisds1);

! Analyze the page fault cluster size.

if .sp[isd$b_pfc] eqlu 0 then
	anl$format_line(0,3,anl$_exehdrisdpfcdef)
else
	anl$format_line(0,3,anl$_exehdrisdpfcsiz,.sp[isd$b_pfc]);

! Analyze the ISD flags, ignoring the match control bits.

anl$format_flags(3,anl$_exehdrisdflags,.sp[isd$l_flags] and %x'00ffff8f',isd_flags_def);
anl$check_flags(.sp[isd$l_flags] and %x'00ffff8f',isd_flags_def);

! Analyze the ISD type code.

selectoneu .sp[isd$b_type] of set
[0 to 4]:		anl$format_line(0,3,anl$_exehdrisdtype,.isd_types[.sp[isd$b_type]]);

[isd$k_usrstack]:	anl$format_line(0,3,anl$_exehdrisdtype,uplit byte (%ascic 'USRSTACK'));

[otherwise]:		anl$format_error(anl$_exebadisdtype,.sp[isd$b_type]);
tes;

! If this is a demand-zero section, we are done.

if .sp[isd$v_dzro] then (
	if .sp[isd$w_size] gtru isd$c_lendzro then
		anl$format_error(anl$_exeisdlendzro);
	return;
);

! Analyze the base VBN.

anl$format_line(0,3,anl$_exehdrisdvbn,.sp[isd$l_vbn]);

! If this isn't a global section, we're done.

if not .sp[isd$v_gbl] then (
	if .sp[isd$w_size] gtru isd$c_lenpriv then
		anl$format_error(anl$_exeisdlenpriv);
	return;
);

! Analyze the global section identification.

anl$format_line(0,3,anl$_exehdrgblident,.sp[isd$l_ident]);

! Analyze the match control.

selectoneu .sp[isd$v_matchctl] of set
[isd$k_matall,
 isd$k_matequ,
 isd$k_matleq,
 isd$k_matnev]:	anl$format_line(0,3,anl$_exehdrmatch,.match_control[.sp[isd$v_matchctl]]);

[otherwise]:	anl$format_error(anl$_exebadmatch,.sp[isd$v_matchctl]);
tes;

! Analyze the global section name.

anl$format_line(0,3,anl$_exehdrisdgblnam,sp[isd$t_gblnam]);
begin
local
	name_dsc: descriptor;

build_descriptor(name_dsc,.sp[20,0,8,0],sp[21,0,8,0]);
anl$check_symbol(name_dsc);
end;

! We are done.

if .sp[isd$w_size] gtru isd$c_lenglbl then
	anl$format_error(anl$_exeisdlengbl);

return;

end;

%sbttl 'ANL$IMAGE_PATCH_TEXT - Print Image Patch Text'
!++
! Functional Description:
!	This routine is responsible for printing the patch text in the
!	analysis report.
!
! Formal Parameters:
!	none
!
! Implicit Inputs:
!	global data
!
! Implicit Outputs:
!	global data
!
! Returned Value:
!	If interactive session: true if we are to continue, false otherwise.
!
! Side Effects:
!
!--


global routine anl$image_patch_text = begin

local
	bp: ref block[,byte],
	sp: ref block[,byte],
	patch_vbn: long,
	length: signed long,
	take: long;
local
	local_described_buffer(record_dsc,80);


! The image header patch section has already been checked.  If this image
! doesn't have any patches, then we can leave.

anl$get_image_block(1,bp);
if .bp[ihd$w_patchoff] eqlu 0 then
	return true;
sp = .bp + .bp[ihd$w_patchoff];
if .sp[ihp$l_patcomtxt] eqlu 0 then
	return true;

! We seem to have patch text.  Let's eject the page and start with a heading.

anl$report_page();
anl$format_line(0,0,anl$_exepatch);
anl$report_line(0);
anl$report_line(0);

! We need the VBN of the patch text.  Get the first block.

patch_vbn = .sp[ihp$l_patcomtxt];
anl$get_image_block(.patch_vbn,bp);
sp = .bp;

! OK, now we are going to loop through the patch records in the patch
! text area.  We construct each record from the blocks of the image and
! print them.

loop (

	! Sit in a loop and build the next patch record.  PATCH_VBN is the
	! block number we are at.  SP points to the beginning of the record,
	! which is a length.  If not positive, that's the end of the 
	! patch text.

	length = .sp[0,0,16,1];
exitif (.length leq 0);

	if .length gtru 80 then (
		anl$format_error(anl$_exebadpatchlen,80);
exitloop;
	);
	sp = .sp + 2;

	record_dsc[len] = 0;
	loop (

		! If we have run off the end of this block, let's get another.

		if .sp geqa .bp+512 then (
			increment (patch_vbn);
			anl$get_image_block(.patch_vbn, bp);
			sp = .bp;
		);

		! If we have built the entire record, drop out.

	exitif (.length eql 0);

		! Take as many bytes as we can from this block to build
		! the record.  Adjust things.

		take = minu(.length, .bp+512-.sp);
		ch$move(.take,.sp, .record_dsc[ptr]+.record_dsc[len]);
		record_dsc[len] = .record_dsc[len] + .take;
		sp = .sp + .take + .take mod 2;
		length = .length - .take;
	);

	! Now we print the record.

	anl$format_line(0,1,anl$_anything,record_dsc);
);

! If this is an interactive session, let's find out if the user wants to
! continue or quit.

if .anl$gb_interactive then
	return anl$interact()
else
	return true;

end;

%sbttl 'ANL$IMAGE_GST - Analyze Global Symbol Table'
!++
! Functional Description:
!	This routine is responsible for analyzing the global symbol table
!	of a shareable image.  We format the information in the report and
!	check its validity.
!
! Formal Parameters:
!	none
!
! Implicit Inputs:
!	global data
!
! Implicit Outputs:
!	global data
!
! Returned Value:
!	If interactive session:  true if we are to continue, false if not.
!
! Side Effects:
!
!--


global routine anl$image_gst = begin

local
	bp: ref block[,byte],
	sp: ref block[,byte],
	gst_vbn: long,
	gst_record_count: long,
	record_number: long,
	length: long,
	take: long;
local
	local_described_buffer(record_dsc,obj$c_maxrecsiz);


! The global symbol table origin information has already been checked.
! If this isn't a shareable image or the information is missing, forget it.

anl$get_image_block(1,bp);
if .bp[ihd$b_imgtype] nequ ihd$k_lim or .bp[ihd$w_symdbgoff] eqlu 0 then
	return true;
sp = .bp + .bp[ihd$w_symdbgoff];
if .sp[ihs$l_gstvbn] eqlu 0 then
	return true;

! We seem to have a GST.  Let's eject the page and start with a heading.

anl$report_page();
anl$format_line(0,0,anl$_exegst);
anl$report_line(0);
anl$report_line(0);

! We need the VBN of the global symbol table and its record count.  Get
! the first block of the table.

gst_vbn = .sp[ihs$l_gstvbn];
gst_record_count = .sp[ihs$w_gstrecs];
anl$get_image_block(.gst_vbn,bp);
sp = .bp;

! OK, now we are going to loop through the object records in the global
! symbol table.  We construct each record from the blocks of the image and
! analyze them using the object file analysis routines.

incru record_number from 1 to .gst_record_count do (

	! Sit in a loop and build the next object record.  GST_VBN is the
	! block number we are at.  SP points to the beginning of the record,
	! which is a length.

	length = .sp[0,0,16,0];
	sp = .sp + 2;
	record_dsc[len] = 0;

	loop (

		! If we have run off the end of this block, let's get another.

		if .sp geqa .bp+512 then (
			increment (gst_vbn);
			anl$get_image_block(.gst_vbn, bp);
			sp = .bp;
		);

		! If we have built the entire record, drop out.

	exitif (.length eqlu 0);

		! Take as many bytes as we can from this block to build
		! the record.  Adjust things.

		take = minu(.length, .bp+512-.sp);
		ch$move(.take,.sp, .record_dsc[ptr]+.record_dsc[len]);
		record_dsc[len] = .record_dsc[len] + .take;
		sp = .sp + .take + .take mod 2;
		length = .length - .take;
	);

	! Now we can analyze the record, assuming it is a least one byte
	! in length.  Select on its type.

	if .record_dsc[len] gequ 1 then (

		selectoneu ch$rchar(.record_dsc[ptr]) of set
		[obj$c_hdr]:	anl$object_hdr(.record_number,record_dsc);

		[obj$c_gsd]:	anl$object_gsd(.record_number,record_dsc);

		[obj$c_eom]:	anl$object_eom(.record_number,record_dsc);

		[otherwise]:	(anl$format_error(anl$_exebadobj,.record_number,ch$rchar(.record_dsc[ptr]));
				anl$format_hex(1,record_dsc););
		tes;

		! Make sure that this record isn't longer than the maximum size
		! specified in the module header.

		anl$object_record_size(.record_dsc[len]);

		! Skip a couple of lines to make it look nice.

		anl$report_line(-1);
		anl$report_line(-1);

		! If this is an interactive session, let's find out if the
		! user wants to continue or quit.

		if .anl$gb_interactive then
			if not anl$interact() then
				return false;

	) else (

		! There was no record type.  Tell the user.

		anl$format_error(anl$_objnullrec,.record_number);
		anl$report_line(-1);
		anl$report_line(-1);
	);
);

return true;

end;

end eludom
