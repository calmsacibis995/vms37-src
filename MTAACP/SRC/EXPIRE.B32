
MODULE EXPIRE ( LANGUAGE ( BLISS32 ) ,
		IDENT = 'V03-000'
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  MTAACP
!
! ABSTRACT:
!	This module checks that either override expiration date
!	was a mount parameter or the file has expired.
!
! ENVIRONMENT:
!
!	Starlet operating system, including privileged system services
!	and internal exec routines.
!
!--

!
!
! AUTHOR:  D. H. GILLESPIE,	 CREATION DATE:  7-JUN-77
!
! MODIFIED BY:
!
!	V02-004	DMW00040	David Michael Walp	 2-Oct-1981
!		Handle Expiration date of " 00000"
!
!	V02-003	DMW00033	David Michael Walp	18-Aug-1981
!		Looked at MVL Override Bit when override option is used
!
!	V02-002	REFORMAT	Maria del C. Nasr	30-Jun-1980
!
!
!**

LIBRARY 'SYS$LIBRARY:LIB.L32';

REQUIRE 'SRC$:MTADEF.B32';


GLOBAL ROUTINE EXPIRED (ADDR) : COMMON_CALL =

!++
!
! FUNCTIONAL DESCRIPTION:
!	This routine determines if a file has expired or override expiration
!	was entered on mount.
!
! CALLING SEQUENCE:
!	EXPIRED(ARG1)
!
! INPUT PARAMETERS:
!	ARG1 - address of date in yyddd format
!
! IMPLICIT INPUTS:
!	CURRENT_VCB - address of current vcb, override expiration indicator
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	If file has not expired and override expiration is not set,
!	then error exit is taken with SS$_FILNOTEXP in user status.
!
! ROUTINE VALUE:
!	0 - file not expired
!	1 - file expired
!
! SIDE EFFECTS:
!	none
!
!--

    BEGIN

    EXTERNAL REGISTER
	COMMON_REG;

    EXTERNAL ROUTINE
	CALDAYNO,			! calculate day number given 64 bit 
	CONVDATE_J2R,			! convert ANSI Julian to reg date
	SYS$BINTIM	: ADDRESSING_MODE (ABSOLUTE),	! get binary time
	SYS$GETTIM	: ADDRESSING_MODE (ABSOLUTE);	! get system time

    LOCAL
	DATE_TYPE	: LONG,			! value of convert call
	REGDATE		: VECTOR [12, BYTE],	! input DD-MMM-YYYY
	DATE		: VECTOR [2],		! binary input date
	TODAY		: VECTOR [2],		! binary today
	DESC1		: VECTOR [2, LONG],	! descriptor
	MVL_ENTRY	: REF BBLOCKVECTOR [,MVL$K_LENGTH];
						! pointer to the MVL_ENTRY


    ! get a handle on the MVL entry
    !
    MVL_ENTRY = (.CURRENT_VCB [ VCB$L_MVL ]) +  MVL$K_FIXLEN;

    IF NOT (	 .CURRENT_VCB [ VCB$V_OVREXP ]
	     AND .MVL_ENTRY [ (.CURRENT_VCB[VCB$W_RVN]), MVL$V_OVERIDE ] )
    THEN
	BEGIN

	! first convert input date to date starlet can understand
	!
	DATE_TYPE = CONVDATE_J2R(REGDATE, .ADDR);

	! if the date was invalid then return not expired
	!
	IF .DATE_TYPE EQL 0 THEN RETURN 0;

	! if the date was Julian zero then return expired
	!
	IF .DATE_TYPE EQL 2 THEN RETURN 1;

	! now convert input date to 64 bit binary and get today's date in 64
	!  bit binary
	!
	DESC1[0]    = 12;
	DESC1[1]    = REGDATE;
	REGDATE[11] = %C' ';
	SYS$BINTIM(DESC1, DATE);
	SYS$GETTIM(TODAY);

	! calculate day number given 64 bit format
	!
	CALDAYNO(DATE, TODAY);

	! finally is the file expired
	!
	IF .DATE GTRU .TODAY THEN RETURN 0;

	END;

    RETURN 1;

    END;						! end of routine

END

ELUDOM
