
MODULE STRVIO (LANGUAGE (BLISS32) ,
		IDENT = 'V03-000'
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  MTAACP
!
! ABSTRACT:
!	This module requeues block virtual IO on the current unit
!
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--

!
! AUTHOR:  D. H. GILLESPIE,	 CREATION DATE:  30-AUG-1977
!
! MODIFIED BY:
!
!	V02-002	REFORMAT	Maria del C. Nasr	30-Jun-1980
!
!**

LIBRARY 'SYS$LIBRARY:LIB.L32';

REQUIRE 'SRC$:MTADEF.B32';

LINKAGE
    INS_QUE 	= JSB (REGISTER = 3, REGISTER = 5) : NOPRESERVE (1, 2, 4);

EXTERNAL
    CURRENT_UCB	: REF BBLOCK,		! address of current unit control block
    CURRENT_WCB	: REF BBLOCK,		! address of current window control block
    QUEUE_HEAD	: REF BBLOCK;		! address of ACP input queue head

EXTERNAL ROUTINE
    EXE$INSIOQ	: INS_QUE ADDRESSING_MODE (ABSOLUTE);

LOCK_CODE;


GLOBAL ROUTINE START_VIO : COMMON_CALL NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!	This routine queues blocked virtual IO to the current unit
!
! CALLING SEQUENCE:
!	START_VIO(), called in kernel mode
!
! INPUT PARAMETERS:
!	none
!
! IMPLICIT INPUTS:
!	CURRENT_UCB	- address of current unit control block to which all
!			  blocked virtual IO is to be queued
!	CURRENT_VCB	- address of current volume control block which contains
!		 	  the blocked virtual IO list head
!	CURRENT_WCB	- address of current window control block which is currently
!			  not mapping virtual IO
!	QUEUE_HEAD	- address of ACP input queue head to which mapping errors
!			  for this volume are queued
!
! OUTPUT PARAMETERS:
!	none
!
! IMPLICIT OUTPUTS:
!	window restored mapping to current unit
!	virtual IO requeued to that unit
!
! ROUTINE VALUE:
!	none
!
! SIDE EFFECTS:
!	this routine runs at fork level in order to synchronize with the mapping
!	portion of QIO processing and the magnetic tape driver
!
!--

    BEGIN

    EXTERNAL REGISTER
	COMMON_REG;

    LOCAL
	ENTRY	: REF BBLOCK,	! address of ACP queue entry
	FUNCTION,		! IO function code
	PACKET	: REF BBLOCK,	! address of IO request packet which is to be requeued
	UCB	: REF BBLOCK,	! address of current UCB
	VCB	: REF BBLOCK;	! address of  current volume control block

    UCB = .CURRENT_UCB;			! address of UCB to which io is to be queue
    VCB = .CURRENT_VCB;			! address of current VCB
    SET_IPL(.UCB[UCB$B_FIPL]);		! raise to fork level

    ! fix up map pointer with UCB which is to receive virtual IO
    !
    CURRENT_WCB[WCB$W_NMAP] = 1;
    (CURRENT_WCB[WCB$W_P1_COUNT])<0, 32> = .UCB;

    ! requeue all blocked io to current unit
    !

    WHILE 1
    DO
	BEGIN

	IF REMQUE(.VCB[VCB$L_BLOCKFL], PACKET)
	THEN
	    EXITLOOP;

	! may have been cleared when error was processed
	!
	PACKET[IRP$V_VIRTUAL] = 1;

	! this is here because INSIOQ does not preserve R5
	!
	UCB = .CURRENT_UCB;
	EXE$INSIOQ(.PACKET, .UCB);
	END;

    ! scan input queue for any mapping errors that belong to this volume
    ! put them at the tail of blocked IO list
    !
    ENTRY = .QUEUE_HEAD[AQB$L_ACPQFL];

    WHILE .ENTRY NEQA .QUEUE_HEAD
    DO
	BEGIN
	FUNCTION = .ENTRY[IRP$V_FCODE];

	IF  .FUNCTION EQL IO$_READPBLK
	    OR
	    .FUNCTION EQL IO$_WRITEPBLK
	THEN
	    BEGIN

	    IF .BBLOCK[.ENTRY[IRP$L_UCB], UCB$L_VCB] EQLA .VCB
	    THEN
		BEGIN
		ENTRY = .ENTRY[IRP$L_IOQBL];
		REMQUE(.ENTRY[IRP$L_IOQFL], PACKET);

		! may have been cleared when error was processed
		!
		PACKET[IRP$V_VIRTUAL] = 1;
		EXE$INSIOQ(.PACKET, .UCB);
		END;

	    END;

	ENTRY = .ENTRY[IRP$L_IOQFL];
	END;

    SET_IPL(0);
    END;

END

ELUDOM
