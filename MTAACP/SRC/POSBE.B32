MODULE POSBE (LANGUAGE (BLISS32) ,
		IDENT = 'V03-000'
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  MTAACP
!
! ABSTRACT:
!	This module positions to the beginning or to the end of the volume set
!
!
! ENVIRONMENT:
!
!	VMS operating system, including privileged system services
!	and internal exec routines.
!
!--

!
!
! AUTHOR:  D. H. GILLESPIE,	 CREATION DATE:  14-JUL-1977
!
! MODIFIED BY:
!
!	V02-004	REFORMAT	Maria del C. Nasr	30-Jun-1980
!
!
!**

LIBRARY 'SYS$LIBRARY:LIB.L32';

REQUIRE 'SRC$:MTADEF.B32';


GLOBAL ROUTINE POSITION_TO_END : COMMON_CALL NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!	This routine positions the volume set at the end
!
! CALLING SEQUENCE:
!	POSITION_TO_END()
!
! INPUT PARAMETERS:
!	NONE
!
! IMPLICIT INPUTS:
!	CURRENT_VCB - address of current vcb
!	CURRENT_WCB - address of current wcb
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	The volume set is positioned to the end
!	If the dummy file is found, then the tape is positioned before the dummy file
!
! USER ERRORS:
!	SS$_FILALRACC - can not postion to end if a file is already accessed
!
!--

    BEGIN

    EXTERNAL REGISTER
	COMMON_REG;

    LOCAL
	MVL	: REF BBLOCK,			! mag tape volume list
	LASTVOL;				! rvn of last volume

    EXTERNAL

	! address of current window control block
	!
	CURRENT_WCB	: REF BBLOCK;

    EXTERNAL ROUTINE
	CLOSE_FILE	: L$CLOSE_FILE,		! close file
	GET_START_HDR	: L$GET_START_HDR,	! get start headers
	SPACE_EOF	: COMMON_CALL;		! space to end of file

    ! this function is not legal if a file is open
    !

    IF .CURRENT_WCB NEQU 0
    THEN
	ERR_EXIT(SS$_FILALRACC);

    IF .CURRENT_VCB[VCB$V_PARTFILE]
    THEN
	CLOSE_FILE();

    WHILE 1
    DO
	BEGIN

	IF NOT GET_START_HDR()
	THEN
	    EXITLOOP;

	IF .CURRENT_VCB[VCB$W_CUR_NUM] EQLU 0
	THEN
	    EXITLOOP;

	SPACE_EOF();				! space to end of current file
	END;

    END;					! end of routine

END

ELUDOM
 
