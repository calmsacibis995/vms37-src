	.TITLE	SYSCANEVT - SYSTEM SERVICES TO CANCEL EVENTS
	.IDENT	'V03-000'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; D. N. CUTLER 4-OCT-76
;
; SYSTEM SERVICES TO CANCEL EVENTS
;
;	CANCEL TIMER
;	CANCEL WAKEUP
;
; MACRO LIBRARY CALLS
;
 
	$PCBDEF				;DEFINE PCB OFFSETS
	$PRDEF				;DEFINE PROCESS REGISTER NUMBERS
	$SSDEF				;DEFINE SYSTEM STATUS VALUES
	$TQEDEF				;DEFINE TQE OFFSETS
 
;
; LOCAL SYMBOLS
;
; ARGUMENT LIST OFFSET DEFINITIONS FOR CANCEL TIMER
;
 
REQIDT=4				;REQUEST IDENTIFICATION
ACMODE=8				;ACCESS MODE
 
;
; ARGUMENT LIST OFFSET DEFINITIONS FOR CANCEL WAKEUP
;
 
PIDADR=4				;ADDRESS OF PROCESS IDENTIFICATION
LOGNAM=8				;ADDRESS OF PROCESS NAME STRING DESCRIPTOR
	.PAGE
	.SBTTL	CANCEL TIMER REQUEST
;+
; EXE$CANTIM - CANCEL TIMER REQUEST
;
; THIS SERVICE PROVIDES THE CAPABILITY TO CANCEL ALL OR A SPECIFIED SUBSET
; OF TIMERS THAT WERE PREVIOUSLY ESTABLISHED.
;
; INPUTS:
;
;	REQIDT(AP) = REQUEST IDENTIFICATION (ZERO IMPLIES ALL).
;	ACMODE(AP) = ACCESS MODE (ALL EQUAL AND HIGHER ACCESS MODES).
;
;	R4 = CURRENT PROCESS PCB ADDRESS.
;
; OUTPUTS:
;
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = SS$_NORMAL - NORMAL COMPLETION.
;-
 
	.ENABL	LSB
	.ENTRY	EXE$CANTIM,^M<R2,R3,R4,R5>
	EXTZV	#0,#2,ACMODE(AP),R0	;GET SPECIFIED ACCESS MODE
	JSB	EXE$MAXACMODE		;MAXIMIZE ACCESS MODE WITH PREVIOUS MODE
	MOVL	PCB$L_PID(R4),R5	;SET PROCESS ID
	MOVZBL	#TQE$C_TMSNGL,R4	;SET TYPE OF ENTRY
	MOVL	REQIDT(AP),R3		;SET REQUEST IDENTIFICATION
	MOVL	R0,R2			;SET CANCELLATION ACCESS MODE
	BRB	10$			;FINISH IN COMMON CODE
	.PAGE
	.SBTTL	CANCEL WAKEUP
;+
; EXE$CANWAK - CANCEL WAKEUP
;
; THIS SERVICE PROVIDES THE CAPABILITY TO CANCEL ALL WAKE UP REQUESTS FOR
; A SPECIFIED PROCESS.
;
; INPUTS:
;
;	PIDADR(AP) = ADDRESS OF PROCESS IDENTIFICATION.
;	LOGNAM(AP) = ADDRESS OF PROCESS NAME STRING DESCRIPTOR.
;
;	R4 = CURRENT PROCESS PCB ADDRESS.
;
; OUTPUTS:
;
;	R0 LOW BIT CLEAR INDICATES FAILURE TO CANCEL WAKE UP REQUESTS.
;
;		R0 = SS$_ACCVIO - PROCESS NAME STRING OR PROCESS NAME
;			STRING DESCRIPTOR CANNOT BE READ BY CALLING ACCESS
;			MODE, OR PROCESS IDENTIFICATION CANNOT BE WRITTEN
;			BY CALLING ACCESS MODE.
;
;		R0 = SS$_IVLOGNAM - ZERO OR GREATER THAN MAXIMUM LENGTH
;			PROCESS NAME STRING SPECIFIED.
;
;		R0 = SS$_NONEXPR - NONEXISTENT PROCESS OF INVALID PROCESS
;			IDENTIFICATION SPECIFIED.
;
;		R0 = SS$_NOPRIV - PROCESS DOES NOT HAVE PRIVILEGE TO WAKE
;			SPECIFIED PROCESS.
;
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = SS$_NORMAL - NORMAL COMPLETION.
;-
 
	.ENTRY	EXE$CANWAK,^M<R2,R3,R4,R5>
	BSBW	EXE$NAMPID		;GET TARGET PROCESS ID
	BLBC	R0,20$			;IF LBC INVALID ARGUMENT
	MOVZBL	#TQE$C_WKSNGL,R4	;SET TYPE OF ENTRY
	MOVL	R1,R5			;SET TARGET PROCESS ID
10$:	SETIPL	#IPL$_TIMER		;RAISE IPL TO TIMER LEVEL
	BSBW	EXE$RMVTIMQ		;REMOVE ENTRIES FROM TIMER QUEUE
	MOVZWL	#SS$_NORMAL,R0		;SET NORMAL COMPLETION STATUS
20$:	SETIPL	#0			;ALLOW INTERRUPTS
	RET				;
	.DSABL	LSB
 
	.END
