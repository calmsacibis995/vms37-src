	.TITLE	SYSSETEXV - SYSTEM SERVICE SET EXCEPTION VECTOR
	.IDENT	'V03-000'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; D. N. CUTLER 10-AUG-76
;
; SYSTEM SERVICE SET EXCEPTION VECTOR
;
; MACRO LIBRARY CALLS
;
 
	$SSDEF				;DEFINE SYSTEM STATUS VALUES
 
;
; LOCAL SYMBOLS
;
; ARGUMENT LIST OFFSET DEFINITIONS
;
 
VECTOR=4				;VECTOR INDICATOR
HANDLR=8				;EXCEPTION HANDLER ADDRESS
ACMODE=12				;ACCESS MODE
PRVHND=16				;ADDRESS TO STORE PREVIOUS HANDLER ADDRESS
	.PAGE
	.SBTTL	SET EXCEPTION VECTOR
;+
; EXE$SETEXV - SET EXCEPTION VECTOR
;
; THIS SERVICE PROVIDES THE CAPABILITY TO ASSIGN A CONDITION HANDLER ADDRESS
; TO A SOFTWARE EXCEPTION VECTOR OR TO DEASSIGN A PREVIOUSLY ASSIGNED
; HANDLER ADDRESS.
;
; INPUTS:
;
;	VECTOR(AP) = VECTOR INDICATOR.
;		BIT ONE SET INDICATES LAST CHANCE VECTOR.
;		BIT ONE CLEAR AND BIT ZERO CLEAR INDICATES PRIMARY VECTOR.
;		BIT ONE CLEAR AND BIT ZERO SET INDICATES SECONDARY VECTOR.
;	HANDLR(AP) = EXCEPTION HANDLER ADDRESS.
;		A ZERO ADDRESS SPECIFIES NO HANDLER IS TO BE ASSIGNED.
;		A NONZERO ADDRESS SPECIFIES AN EXCEPTION HANDLER.
;	ACMODE(AP) = ACCESS MODE EXCEPTION HANDLER IS TO BE ASSIGNED TO.
;	PRVHND(AP) = ADDRESS TO STORE PREVIOUS HANDLER ADDRESS.
;
; OUTPUTS:
;
;	R0 LOW BIT CLEAR INDICATES FAILURE TO ASSIGN CONDITION HANDLER.
;
;		R0 = SS$_ACCVIO - LONGWORD WHICH IS TO RECEIVE PREVIOUS
;			CONDITION HANDLER ADDRESS CANNOT BE WRITTEN BY
;			CALLING ACCESS MODE.
;
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = SS$_NORMAL - NORMAL COMPLETION.
;-
 
	.PSECT	Y$EXEPAGED
EXE$SETEXV::				;SET EXCEPTION VECTOR
	.WORD	^M<R2,R3,R4,R5>		;ENTRY MASK
	EXTZV	#0,#2,ACMODE(AP),R0	;GET SPECIFIED ACCESS MODE
	JSB	EXE$MAXACMODE		;MAXIMIZE ACCESS MODE WITH PREVIOUS MODE
	MOVAQ	CTL$AQ_EXCVEC[R0],R5	;GET ADDRESS OF ACCESS MODE VECTORS
	BLBC	VECTOR(AP),10$		;IF LBC PRIMARY VECTOR
	TSTL	(R5)+			;POINT TO SECONDARY VECTOR
10$:	BBC	#1,VECTOR(AP),20$	;IF CLR, PRIMARY OR SECONDARY VECTOR
	MOVAL	CTL$AL_FINALEXC[R0],R5	;GET ADDRESS OF LAST CHANCE VECTORS
20$:	MOVL	PRVHND(AP),R4		;GET ADDRESS TO STORE PREVIOUS HANDLER
	BEQL	30$			;IF EQL NONE SPECIFIED
	MOVZWL	#SS$_ACCVIO,R0		;ASSUME ACCESS VIOLATION
	IFNOWRT	#4,(R4),40$		;CAN PREVIOUS HANDLER ADDRESS BE WRITTEN?
	MOVL	(R5),(R4)		;STORE PREVIOUS EXCEPTION HANDLER ADDRESS
30$:	MOVL	HANDLR(AP),(R5)		;SET NEW EXCEPTION HANDLER ADDRESS
	MOVZWL	#SS$_NORMAL,R0		;SET NORMAL COMPLETION STATUS
40$:	RET				;
 
	.END
