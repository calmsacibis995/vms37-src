	.TITLE	SYSGETTIM - SYSTEM SERVICE GET TIME
	.IDENT	'V03-001'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; D. N. CUTLER 30-SEP-76
;
; SYSTEM SERVICE GET TIME
;
; MODIFIED BY:
;
;	V03-001	KDM0086		KATHLEEN D. MORSE	02-APR-1982
;		CORRECTLY ACQUIRE SYSTEM TIME, EVEN IN THE CASE WHEN THE
;		SECONDARY PROCESSOR ACCESSES EXE$GQ_SYSTIME WHILE THE
;		PRIMARY PROCESSOR IS UPDATING IT (11/782 CASE).
;
;
; MACRO LIBRARY CALLS
;
 
	$SSDEF				;DEFINE SYSTEM STATUS VALUES
 
;
; LOCAL SYMBOLS
;
; ARGUMENT LIST OFFSET DEFINITIONS
;
 
TIMADR=4				;ADDRESS OF QUADWORD TO RECEIVE TIME
	.PAGE
	.SBTTL	GET TIME
;+
; EXE$GETTIM - GET TIME
;
; THIS SERVICE PROVIDES THE CAPABILITY TO RETRIEVE THE CURRENT SYSTEM TIME
; IN 64 BIT FORMAT.
;
; INPUTS:
;
;	TIMADR(AP) = ADDRESS OF QUADWORD THAT IS TO RECEIVE TIME.
;
; OUTPUTS:
;
;	R0 LOW BIT CLEAR INDICATES FAILURE TO RETRIEVE SYSTEM TIME.
;
;		R0 = SS$_ACCVIO - QUADWORD TO RECEIVE TIME CANNOT BE
;			WRITTEN BY CALLING ACCESS MODE.
;
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = SS$_NORMAL - NORMAL COMPLETION.
;-
 
	.PSECT	YEXEPAGED
EXE$GETTIM::				;GET TIME
	.WORD	0			;ENTRY MASK
	MOVL	TIMADR(AP),R1		;GET ADDRESS OF QUADWORD TO RECEIVE TIME
	MOVZWL	#SS$_ACCVIO,R0		;ASSUME QUADWORD NOT WRITABLE
5$:	IFNOWRT	#8,(R1),10$		;CAN QUADWORD BE WRITTEN?
	MOVQ	EXE$GQ_SYSTIME,(R1)	;STORE SYSTEM TIME IN QUADWORD
	CMPL	EXE$GQ_SYSTIME,(R1)	;VERIFY THAT THE VALUE ACQUIRED
	BNEQ	5$			; WAS NOT BEING MODIFIED DURING
	CMPL	EXE$GQ_SYSTIME+4,4(R1)	; THE ACQUISITION. THIS SYNCHS ACCESS BY
	BNEQ	5$			; THE SECONDARY IN THE 11/782 SYSTEM.
	MOVZWL	#SS$_NORMAL,R0		;SET NORMAL COMPLETION
10$:	RET				;
 
	.END
