	.TITLE	SYSDCLCMH - DECLARE CHANGE MODE OR COMPATIBILITY MODE HANDLER
	.IDENT	'V03-000'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; H. M. LEVY  17-MAR-77
;
; SYSTEM SERVICE DECLARE CHANGE MODE OR COMPATIBILITY MODE HANDLER
;
; MACRO LIBRARY CALLS
;
 
	$PSLDEF				;DEFINE PROCESSOR STATUS FIELDS
	$SSDEF				;DEFINE SYSTEM STATUS VALUES
 
;
; LOCAL SYMBOLS
;
; ARGUMENT LIST OFFSET DEFINITIONS
;
 
ADDRES=4				;ADDRESS OF NEW CHANGE MODE HANDLER
PRVHND=8				;ADDRESS OR RECIEVE OLD HANDLER ADDRESS
TYPE=12					;TYPE OF HANDLER TO DECLARE
	.PAGE
	.SBTTL	DECLARE CHANGE MODE OR COMPATIBILITY MODE HANDLER
;+
; EXE$DCLCMH - DECLARE CHANGE MODE OR COMPATIBILITY MODE HANDLER
;
; THIS SERVICE PROVIDES THE CAPABILITY TO SPECIFY A COMPATIBILITY MODE
; HANDLER FOR COMPATIBILITY MODE TRAPS, OR A CHANGE MODE HANDLER FOR
; THE CALLING ACCESS MODE.  A CHANGE MODE HANDLER MAY ONLY BE
; FROM SUPERVISOR OR USER MODES.
;
; INPUTS:
;
;	ADDRES(AP) = ADDRESS OF HANDLER TO BE DECLARED
;	PRVHND(AP) = ADDRESS TO RETURN OLD HANDLER ADDRESS
;	TYPE(AP)  = TYPE OF HANDLER TO BE DECLARED
;		0 -> DECLARE CHANGE MODE HANDLER FOR CURRENT MODE
;		1 -> DECLARE COMPATIBILITY MODE HANDLER
;
;	R4 = CURRENT PROCESS PCB ADDRESS.
;
; IMPLICIT INPUTS:
;
;	CTL$GL_CMSUPR = ADDRESS OF THE FIRST OF THREE VECTORS, THE FIRST
;			TWO BEING THE CHANGE MODE VECTORS FOR SUPERVISOR
;			AND USER MODES, THE THIRD BEING THE COMPATIBILITY
;			MODE VECTOR.
;
; OUTPUTS:
;
;	R0 LOW BIT CLEAR INDICATES FAILURE TO DECLARE CHANGE MODE HANDLER
;
;		R0 = SS$_ACCVIO - PREVIOUS HANDLER ADDRESS CANNOT BE
;			WRITTEN TO @PRVHND(AP) BY CALLING ACCESS MODE.
;
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = SS$_NORMAL - NORMAL COMPLETION.
;
; SIDE EFFECTS:
;
;	SERVICE WILL CAUSE A BUGCHECK IF CALLED TO DECLARE CHANGE
;	MODE HANDLER FROM KERNEL OR EXECUTIVE MODES.
;-
 
	.PSECT	Y$EXEPAGED
	.ENTRY	EXE$DCLCMH,^M<R4>
	MOVAL	@#CTL$GL_CMSUPR,R4	; ADDRESS OF VECTORS
	MOVL	#2,R0			; INDEX TO COMPAT. MODE HANDLER VECTOR
	BLBS	TYPE(AP),10$		; BRANCH IF DECLARE COMPAT. HANDLER SERVICE
	MOVPSL	R0			; READ CURRENT PSL
	EXTZV	#PSL$V_PRVMOD,#PSL$S_PRVMOD,R0,R0 ; EXTRACT PREVIOUS MODE
	SUBL	#2,R0			; SUBTRACT SUPERVISOR MODE BIAS
	BLSS	30$			; IF LSS, CALLED FROM KERNEL OR EXEC
10$:	MOVL	PRVHND(AP),R1		; GET ADDRESS TO RETURN PREVIOUS HANDLER
	BEQL	20$			; BRANCH IF NONE SPECIFIED
	IFNOWRT	#4,(R1),40$		; CAN IT BE WRITTEN?
	MOVL	(R4)[R0],(R1)		; YES, RETURN PREVIOUS HANDLER ADDRESS
20$:	MOVL	ADDRES(AP),(R4)[R0]	; SET NEW HANDLER ADDRESS
	MOVZWL	#SS$_NORMAL,R0		; NORMAL STATUS
	RET				;
30$:	BUG_CHECK IVSSRVRQST		; INVALID SYSTEM SERVICE REQUEST
	MOVZWL	#SS$_IVSSRQ,R0		; INVALID SERVICE REQUEST RETURN CODE
	RET				;
40$:	MOVZWL	#SS$_ACCVIO,R0		; ACCESS VIOLATION CODE
	RET

	.END
