	.TITLE	IPCONTROL - IPL 22 INTERRUPT PROCESSOR
	.IDENT	'V03-002'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;++
;
; FACILITY:
;
;	NONPAGED VAX/VMS EXEC
;
; ABSTRACT:
;
;	THIS MODULE CONTAINS THE ROUTINE NECESSARY TO PROCESS THE IPL 12
;	INTERRUPTS.  THESE ARE GENERATED MANUALLY FROM THE CONSOLE AS A
;	LAST DITCH EFFORT TO CORRECT SOME SYSTEM MALADY BEFORE REBOOTING
;	SYSTEM.
;
; AUTHOR:
;
;	L. MARK PILANT  28-JAN-1982
;
; MODIFIED BY:
;
;	V03-002	LMP0024		L. Mark Pilant		23-Apr-1982  13:45
;		Correct a problem introduced by some bit shaving.
;
;	V03-001	LMP0020		L. Mark Pilant		 2-Apr-1982  15:05
;		Modify the logic so that only a device that has mount
;		verification in progess can be disabled.
;
;	V02-001	LMP0010		L. Mark Pilant		 9-Feb-1982   9:00
;		Strip parity and other garbage from incoming characters.
;
;--
	.PAGE
	.SBTTL	USEFUL MACRO DEFINITIONS

	.MACRO	RJMP	ADDR
	JMP	ADDR			;CALLED ROUTINE DOES THE RSB
	.ENDM	RJMP

	.MACRO	CON_MSG	MESSAGE,RETURN
	MOVAB	W^MESSAGE,R2		;SET MESSAGE ADDRESS
.IF	B,	RETURN
	JSB	(R10)			;SEND THE MESSAGE
.IFF
	RJMP	(R10)			;SEND THE MESSAGE
.ENDC;	B,	RETURN
	.ENDM	CON_MSG
	.PAGE
	.SBTTL	SYMBOL DEFINITIONS

; MACRO LIBRARY CALLS

	$DCDEF
	$DDBDEF
	$DEVDEF
	$IRPDEF
	$PRDEF
	$UCBDEF
	$VCBDEF

; SPECIAL TERMINAL INPUT CHARACTERS

	IPC_C_BELL=	7		;BELL (FOR FULL BUFFER & ILLEGAL CHARS)
	IPC_C_LINFEED=	10		;LINE-FEED (ECHOED AFTER <CR>)
	IPC_C_CARRETN=	13		;CARRIAGE-RETURN
	IPC_C_CNTRLU=	21		;CONTROL-U (IGNORE THE LINE)
	IPC_C_CNTRLZ=	26		;CONTROL-Z (EXIT)
	IPC_C_SPACE=	32		;SPACE
	IPC_C_COLON=	58		;DEVICE NAME TERMINATOR
	IPC_C_CHAR_C=	67		;CHARACTER C (CANCEL MOUNT VERIFICATION)
	IPC_C_CHAR_X=	88		;CHARACTER X (ENTER XDELTA)
	IPC_C_UNDER=	95		;UNDERSCORE
	IPC_C_LOWERA=	97		;LOWER CASE A
	IPC_C_DELETE=	127		;DELETE A CHARACTER (RUBOUT)

; MISCELLANEOUS CONSTANTS

	IPC_C_MAXLINSIZ=	11	;MAXIMUM INPUT LINE SIZE
					; (THIS INCLUDE STORAGE FOR THE <CR>)

; LOCAL STORAGE OFFSETS

	$DEFINI	IPC

	$DEF	IPC_C_START
	$DEF	IPC_W_INPSIZE	.BLKW	1	;INPUT LINE SIZE IN BYTES
	$DEF	IPC_T_INPLINE	.BLKB	IPC_C_MAXLINSIZ	;STORAGE FOR THE TEXT
	$DEF	IPC_Q_DEVICE	.BLKQ	1	;DEVICE DESCRIPTOR
	$DEF	IPC_B_FLAGS	.BLKB	1	;USEFUL FLAGS
	_VIELD	IPC,0,<-		;DEFINE FLAG BITS
		<DELETE,,M>,-		;DELETE CHARACTER SEEN
		<NEWLINE,,M>,-		;STARTING A NEW LINE
		<CNTRLZ,,M>,-		;CONTROL-Z (EXIT) SEEN
		>
	$DEF	IPC_L_OLDGETNXT	.BLKL	1	;PREVIOUS CHAR INPUT ROUTINE

	$DEF	IPC_B_PAD	.BLKB	4-<<.-IPC_C_START>&7>	;LONGWORD GRANULARITY
	$DEF	IPC_C_LENGTH		;LENGTH OF LOCAL STORAGE

	$DEFEND	IPC
	.PAGE
	.SBTTL	ERROR MESSAGES AND MISC STRINGS

	.PSECT	WMOUNTVERMSG

IPC_ILLCOMMAND:
	.ASCIZ	\Illegal command\<13><10>
IPC_ILLDEVICE:
	.ASCIZ	\Illegal device\<13><10>
IPC_NOMNTVER:
	.ASCIZ	\Mount verification not in progress\<13><10>

IPC_CRLFSTR:
	.BYTE	IPC_C_CARRETN,IPC_C_LINFEED,0	;<CR>,<LF>,0

IPC_PROMPT:
	.ASCIZ	\IPC> \

	.SBTTL	CHARACTER VALIDITY TABLES

IPC_V_LEGAL:
	.LONG	^X04202000,-		;<CR>,<^U>,<^Z>
		^X07FF0001,-		;<SP>,<0-9>,<:>
		^X87FFFFFE,-		;<A-Z>,<_>
		^X87FFFFFE		;<a-z>,<RUB>
IPC_V_ARROW:
	.LONG	^XFFFFDB7F,0,0,0	;<BEL>,<LF>,<CR>
	.PAGE
	.SBTTL	COMMAND INTERPRETOR AND DISPATCHER

;++
; EXE$IPCONTROL - IPL 12 (DECIMAL) INTERRUPT HANDLER
;
; THIS MODULE IS CALLED WHEN AN IPL 12 INTERRUPT OCCURS.  THIS IS NORMALLY
; INVOKED FROM THE CONSOLE WHEN THE SYSTEM IS IN SOME DEAD LOCK CONDITION.
;
; INPUTS:
;
;	AN INPUT COMMAND LINE IS ACCEPTED FROM THE CONSOLE FOR ACTION.
;
; OUTPUTS:
;
;	THE APPROPRIATE ACTION IS TAKEN BASED UPON THE CONSOLE INPUT.
;	THIS IS:
;
;	    1)	CLEAR MOUNT VERIFICATION ON A SPECIFIED DEVICE
;	    2)	TRANSFER CONTROL TO XDELTA
;	    3)	EXIT (DISMISS THE IPL 12 INTERRUPT)
;
;--

	.PSECT	WIONONPAGED,LONG

EXE$IPCONTROL::
	PUSHR	#^M<R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>	;SAVE ALL REGS
	MOVAB	-IPC_C_LENGTH(SP),SP	;MAKE ROOM FOR LOCAL STORAGE
	MOVL	SP,R11			;SAVE ADDRESS OF LOCAL STORAGE BLOCK
	MOVAB	W^IPC_OUTPUTZ,R10	;SET ADDRESS OF .ASCIZ OUTPUT ROUTINE
	MOVZBL	#^X40,R9		;CONSOLE INTERRUPT ENABLE
10$:	MTPR	#0,#PR$_RXCS		;CLEAR INTERRUPT ENABLE
	MTPR	#0,#PR$_TXCS
	MOVAB	IPC_W_INPSIZE(R11),R6	;SET ADDRESS OF INPUT STRING SIZE
	MOVAB	IPC_T_INPLINE(R11),R7	;SET ADDRESS OF INPUT COMMAND BUFFER
	MOVAB	IPC_B_FLAGS(R11),R8	;SET ADDRESS OF THE FLAG BYTE
	CON_MSG	IPC_PROMPT		;ISSUE A PROMPT
	BSBW	IPC_GETLINE		;GET A COMMAND LINE FROM THE CONSOLE
	BBC	#IPC_V_CNTRLZ,(R8),15$	;XFER IF NO CONTROL-Z SEEN
	MOVAB	IPC_C_LENGTH(SP),SP	;ELSE CLEAN THE STACK
	BSBB	IPC_ENABLE		;GO RE-ENABLE THE CONSOLE
	POPR	#^M<R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>	;RESTORE REGS
	REI				;DISMISS THE INTERRUPT
15$:	TSTW	(R6)			;ANYTHING TYPED?
	BEQL	10$			;XFER IF NOT...RE-ISSUE THE PROMPT
20$:	CMPB	(R7),#IPC_C_CHAR_C	;ELSE CHECK FOR CANCEL
	BNEQ	30$			;XFER IF NOT
	BSBB	IPC_CANCEL		;ELSE GO CANCEL MOUNT VERIFICATION
	BRB	10$			;TRY FOR ANOTHER COMMAND
30$:	CMPB	(R7),#IPC_C_CHAR_X	;XFER TO XDELTA?
	BNEQ	40$			;XFER IF NOT
	BSBB	IPC_ENABLE		;GO RE-ENABLE THE CONSOLE
	BSBW	W^INI$BRK		;ELSE CAUSE XDELTA BREAKPOINT
	BRB	10$			;TRY FOR ANOTHER COMMAND

; ILLEGAL COMMAND GIVEN

40$:	CON_MSG	IPC_ILLCOMMAND		;ISSUE AN ERROR MESSAGE
	BRB	10$			;GO TRY FOR ANOTHER

; LOCAL ROUTINE TO RE-ENABLE THE CONSOLE FOR INTERRUPTS

IPC_ENABLE:
	MTPR	R9,#PR$_RXCS		;RE-ENABLE THE CONSOLE
	MTPR	R9,#PR$_TXCS
	RSB				;RETURN TO CALLER
	.PAGE
	.SBTTL	CANCEL A PENDING MOUNT VERIFICATION

;++
; IPC_CANCEL - DISABLE MOUNT VERIFICATION IF IN PROGRESS
;
; THIS ROUTINE DISABLES MOUNT VERIFICATION IF IT IS IN PROGRESS.  THIS IS
; DEFINED AS BEING IN THE STATE IN WHICH MOUNT VERIFICATION IS ENABLED
; AND EITHER THE DEVICE UCB HAS THE MOUNT VERIFICATION IN PROGRESS STATUS
; BIT SET OR THE IRP AT THE HEAD OF THE DEVICE IRP QUEUE HAS THE MOUNT
; VERIFICATION STATUS BIT SET.
;
; INPUTS:
;
;	R7 - ADDRESS OF THE CONSOLE INPUT LINE BUFFER
;
; OUTPUTS:
;
;	    1)	THE MOUNT VERFICATION BIT IS CLEARED, OR
;	    2)	A WARNING MESSAGE IS ISSUED
;
;--

IPC_CANCEL:
	MOVL	R7,R0			;SET ADDRESS OF COMMAND LINE
	TSTB	(R0)+			;SKIP OVER COMMAND CHARACTER
	CMPB	(R0)+,#^A/ /		;CHECK FOR CORRECT SEPARATOR
	BEQL	10$			;XFER IF CORRECT
	CON_MSG	IPC_ILLCOMMAND,RETURN	;ISSUE MESSAGE AND RETURN
10$:	CMPB	(R0)+,#IPC_C_UNDER	;ELSE CHECK FOR AN UNDERSCORE
	BEQL	10$			;XFER IF THERE IS ONE
	DECL	R0			;ELSE BACK UP OVER NON-UNDERSCORE
	CLRL	IPC_Q_DEVICE(R11)	;RESET DEVICE NAME SIZE
	MOVL	R0,IPC_Q_DEVICE+4(R11)	;SET STARTING ADDRESS OF DEVICE NAME
20$:	CMPB	(R0),#IPC_C_CARRETN	;END OF THE STRING?
	BEQL	60$			;XFER IF SO
	CMPB	(R0)+,#IPC_C_COLON	;ALTERNATE TERMINATOR?
	BEQL	60$			;XFER IF SO
	INCW	IPC_Q_DEVICE(R11)	;ELSE COUNT ONE MORE CHARACTER
	BRB	20$			;GO TRY THE NEXT CHARACTER

; FIND GENERIC DEVICE NAME (WITH CONTROLLER) AND UNIT NUMBER (IN BINARY)

60$:	MOVQ	IPC_Q_DEVICE(R11),R6	;GET DEVICE NAME DESCRIPTOR
	CLRL	R8			;RESET UNIT NUMBER
	CLRL	R0			;RESET DEVICE NAME SIZE
70$:	CMPB	(R7)[R0],#^A\0\		;SKIP ALL CHARACTERS EXCEPT DIGITS
	BLSS	80$
	CMPB	(R7)[R0],#^A\9\
	BLEQ	90$
80$:	AOBLSS	R6,R0,70$		;CONTINUE TILL DONE
	BRB	140$			;SHOULD NEVER GET HERE
90$:	MOVL	R0,R6			;SAVE NEW DEVICE NAME SIZE (MINUS UNIT)
100$:	CMPB	(R7)[R0],#^A\0\		;CHECK FOR A DIGIT
	BLSS	140$			;ERROR IF NOT
	CMPB	(R7)[R0],#^A\9\
	BGTR	140$
	CLRL	R1			;LONGWORD 0
	SUBB3	#^A\0\,(R7)[R0],R1	;CONVERT DIGIT TO BINARY
	MULL2	#10,R8			;MAKE ROOM FOR NEW DIGIT
	ADDL2	R1,R8			;TOTAL IN
	AOBLSS	IPC_Q_DEVICE(R11),R0,100$	;CONTINUE TILL DONE

; SEARCH THE I/O DATA BASE TO LOCATE THE DEVICE SPECIFIED

	MOVAL	IOC$GL_DEVLIST-DDB$L_LINK,R4	;SET THE START OF THE LIST
120$:	MOVL	DDB$L_LINK(R4),R4	;GET THE FIRST DDB IN THE LIST
	BEQL	140$			;XFER IF THE END
	MOVAB	DDB$T_NAME(R4),R0	;ELSE SET ADDRESS OF COUNTED NAME STRING
	CMPB	R6,(R0)+		;SIZES MATCH?
	BNEQ	120$			;XFER IF NOT, TRY THE NEXT DDB
	CMPC3	R6,(R0),(R7)		;ELSE CHECK ACTUAL DEVICE NAME
	BNEQ	120$			;XFER IF NO MATCH
	MOVAB	DDB$L_UCB-UCB$L_LINK(R4),R5	;SET START OF UCB LIST
130$:	MOVL	UCB$L_LINK(R5),R5	;GET A UCB POINTER
	BEQL	120$			;XFER IF THE END, GET NEXT DDB
	CMPW	UCB$W_UNIT(R5),R8	;ELSE CHECK FOR THE RIGHT UNIT
	BNEQ	130$			;XFER IF NOT THE RIGHT ONE
	CMPB	UCB$B_DEVCLASS(R5),#DC$_DISK	;IS DEVICE A DISK?
	BEQL	150$			;XFER IF SO
140$:	CON_MSG	IPC_ILLDEVICE,RETURN	;ELSE ISSUE MESSAGE AND RETURN
150$:	BBS	#UCB$V_MNTVERIP,UCB$W_STS(R5),160$	;XFER IF IN PROGRESS
	MOVL	UCB$L_IOQFL(R5),R3	;ELSE GET ADDR OF HEAD IRP
	BBC	#IRP$V_MVIRP,IRP$W_STS(R3),170$	;DOUBLE CHECK
160$:	MOVL	UCB$L_VCB(R5),R2	;GET VCB ADDRESS
	BICB2	#1@VCB$V_MOUNTVER,VCB$B_STATUS2(R2)	;ZAP MOUNT VERIFICATION
	RSB				;RETURN NOW
170$:	CON_MSG	IPC_NOMNTVER,RETURN	;ELSE ISSUE ERROR AND RETURN
	.PAGE
	.SBTTL	COMMAND LINE INPUT ROUTINE

;++
;
; IPC_GETLINE - GET A LINE OF INPUT FROM THE CONSOLE
;
; THIS ROUTINE INPUTS A LINE FROM THE CONSOLE. IT IGNORES LEADING SPACES,
; COMPRESSES MULTIPLE SPACES TO A SINGLE SPACE, CONVERTS LOWER CASE TO UPPER
; CASE, HANDLES CHARACTER (RUBOUT) AND LINE (CONTROL-U) DELETION, AND CHECKS
; THE LEGALITY OF ANY CHARACTER RECEIVED.
;
; INPUTS:
;
;	R6 - ADDRESS OF THE INPUT LINE SIZE STORAGE
;	R7 - ADDRESS OF THE INPUT LINE BUFFER
;	R11 - ADDRESS OF THE LOCAL STORAGE AREA
;
; OUTPUTS:
;
;	IPC_W_INPSIZE(R11) - SIZE OF USER INPUT LINE AFTER COMPRESSION
;	IPC_T_INPLINE(R11) - STORAGE FOR THE CONSOLE INPUT
;
;	R0,R1,R2, AND R8 ARE DESTROYED
;
;--

IPC_GETLINE:
	CLRB	(R8)			;RESET ALL FLAGS
	MOVL	R7,R2			;COPY BUFFER ADDRESS
10$:	CLRW	(R6)			;RESET INPUT LINE SIZE
	BISB2	#IPC_M_NEWLINE,(R8)	;NEW LINE ONLY USEFUL FLAG
20$:	MFPR	#PR$_RXCS,R0		;GET DEVICE STATUS
	BBC	#7,R0,20$		;XFER IF NOT READY
	MFPR	#PR$_RXDB,R0		;ELSE GET THE CHARACTER
	BITW	#^XFF00,R0		;ERROR OF NOT CONSOLE INPUT?
	BNEQ	20$			;YES, IGNORE
	BICB2	#^X80,R0		;ZAP PARITY BIT IF ANY
	BBC	R0,W^IPC_V_LEGAL,70$	;CHECK LEGALITY
	CMPB	R0,#IPC_C_DELETE	;CHECK FOR CHARACTER DELETE
	BNEQ	35$			;XFER IF NOT
	BBS	#IPC_V_NEWLINE,(R8),20$	;IGNORE IF NOTHING THERE
	BBSS	#IPC_V_DELETE,(R8),30$	;XFER IF HERE ONCE ALREADY
	MOVZBL	#^A/\/,R0		;SET DELIMITER
	BSBB	IPC_OUTCHR		;TYPE IT OUT
30$:	DECW	(R6)			;ANYTHING TO DELETE?
	BLSS	10$			;XFER IF NOT...SET NEW LINE
	MOVZBL	-(R2),R0		;GET CHARACTER
	BSBB	IPC_OUTCHR		;TYPE IT OUT
	BRB	20$			;GET NEXT CHARACTER FROM THE CONSOLE
35$:	CMPB	R0,#IPC_C_CNTRLZ	;END OF THE LINE?
	BNEQ	40$			;XFER IF NOT
	BISB2	#IPC_M_CNTRLZ,(R8)	;ELSE NOTE IT
	BRB	45$			;GO FINISH UP
40$:	CMPB	R0,#IPC_C_CNTRLU	;DELETE ENTIRE LINE?
	BNEQ	50$			;XFER IF NOT
45$:	BSBB	IPC_OUTCHR		;ECHO ^U
	BSBB	IPC_CRLF		;NEW LINE
	CLRW	(R6)			;NOTHING THERE (FORCE NEW PROMPT)
	RSB				;RETURN TO MAIN ROUTINE
50$:	CMPB	R0,#IPC_C_SPACE		;SPACE CHARACTER?
	BNEQ	60$			;XFER IF NOT
	BBS	#IPC_V_NEWLINE,(R8),20$	;XFER IF LEADING SPACE
	CMPB	-1(R2),#IPC_C_SPACE	;PREVIOUS CHARACTER A SPACE?
	BEQL	20$			;IGNORE IF SO
60$:	CMPW	(R6),#IPC_C_MAXLINSIZ	;TOO MANY CHARACTERS?
	BLSS	80$			;XFER IF NOT
70$:	MOVZBL	#IPC_C_BELL,R0		;ELSE SET NEW CHARACTER
	BSBB	IPC_OUTCHR		;LET USER KNOW
	BRB	20$			;GO GET THE NEXT CHARACTER
80$:	CMPB	R0,#IPC_C_LOWERA	;LOWER CASE CHARACTER?
	BLSS	90$			;XFER IF NOT
	BICB2	#^X20,R0		;ELSE CONVERT TO UPPER CASE
90$:	BBC	#IPC_V_DELETE,(R8),100$	;FINISH OUT DELETE STRING?
	PUSHR	#^M<R0>			;YES, SAVE CURRENT CHARACTER
	MOVZBL	#^A/\/,R0		;SET TERMINATOR
	BSBB	IPC_OUTCHR		;TYPE IT OUT
	POPR	#^M<R0>			;RESTORE ORIGINAL CHARACTER
100$:	MOVB	R0,(R2)+		;SAVE THE CHARACTER
	BICB2	#IPC_M_NEWLINE,(R8)	;NO LONGER EMPTY LINE
	BSBB	IPC_OUTCHR		;TYPE OUT THE CHARACTER
	CMPB	R0,#IPC_C_CARRETN	;END OF THE LINE?
	BNEQ	110$			;XFER IF MORE TO COME
	MOVZBL	#IPC_C_LINFEED,R0	;ELSE FOLLOW WITH A <LF>
	BSBB	IPC_OUTCHR
	RSB				;RETURN TO CALLER
110$:	INCW	(R6)			;ONE MORE CHARACTER
	BRW	20$			;GO GET ANOTHER
	.PAGE
	.SBTTL	MISCELLANEOUS CONSOLE OUTPUT ROUTINES

;++
;
; IPC_OUTCHR - OUTPUT A SINGLE CHARACTER
;
; THIS ROUTINE OUTPUTS A SINGLE CHARACTER TO THE CONSOLE.  IF IS A SPECIAL
; ACTION CONTROL CHARACTER (CR, BELL, OR LF) THEN IT IS TYPED DIRECTLY.
; OTHERWISE IT IS PRECEEDED BY AN UP-ARROW (^).
;
; INPUTS:
;
;	R0 - CHARACTER TO TYPE
;
; OUTPUTS:
;
;	NONE
;
;	R1 DESTROYED
;
;--

IPC_OUTCHR:
	PUSHL	R0			;SAVE THE CHARACTER
	BBC	(SP),W^IPC_V_ARROW,20$	;PRECEED WITH AN UP ARROW?
10$:	MFPR	#PR$_TXCS,R1		;YES, GET CONSOLE STATUS
	BBC	#7,R1,10$		;XFER IF NOT READY
	MTPR	#^A\^\,#PR$_TXDB	;ELSE TYPE THE UP ARROW
	BISB2	#^X40,(SP)		;CONVERT TO A PRINTABLE CHARACTER
20$:	MFPR	#PR$_TXCS,R1		;GET DEVICE STATUS
	BBC	#7,R1,20$		;XFER IF NOT READY
	MTPR	(SP)+,#PR$_TXDB		;ELSE OUTPUT THE CHARACTER
	RSB				;RETURN TO CALLER

;--
;
; IPC_CRLF - OUTPUT A <CR,LF> SEQUENCE
;
; THIS ROUTINE OUTPUTS A <CR,LF> SEQUENCE TO THE CONSOLE.  IT FALLS THROUGH
; TO THE STRING OUTPUT ROUTINE TO AVOID THE UNNEEDED SUBROUTINE CALL AND RETURN.
; (THE RETURN IS DONE BY THE STRING OUTPUT ROUTINE.)
;
; INPUTS:
;
;	NONE
;
; OUTPUTS:
;
;	NONE
;
;--

IPC_CRLF:
	MOVAB	W^IPC_CRLFSTR,R2	;SET MESSAGE ADDRESS
					;FALL THROUGH INTO STRING ROUTINE

;++
;
; IPC_OUTPUTZ - TYPE OUT AN .ASCIZ STRING ON THE CONSOLE
;
; THIS ROUTINE TYPES OUT A ZERO TERMINATED STRING ON THE CONSOLE.
;
; INPUTS:
;
;	R2 - ADDRESS OF THE STRING TO TYPE
;
; OUTPUTS:
;
;	NONE
;
;--

IPC_OUTPUTZ:
	MOVZBL	(R2)+,R0		;GET A CHARACTER
	BEQL	10$			;XFER WHEN DONE
	BSBB	IPC_OUTCHR		;ELSE TYPE OUT THE CHARACTER
	BRB	IPC_OUTPUTZ		;GO GET THE NEXT
10$:	RSB				;RETURN WHEN DONE

	.END

