	.TITLE	NLDRIVER - NULL DEVICE DRIVER
	.IDENT	'V03-000'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; D. N. CUTLER 8-AUG-77
;
; NULL DEVICE DRIVER
;
; MODIFIED BY:
;
;	V02-002	GWF0087		Gary W. Fowler		22-Jul-1981
;		Change psect to $$$115_DRIVER
;
; MACRO LIBRARY CALLS
;
 
	$IODEF				;DEFINE I/O FUNCTION CODES
 
;
; LOCAL DATA
;
; DRIVER PROLOGUE TABLE
;
 
	.PSECT	$$$105_PROLOGUE
NL$DPT::				;
	DPTAB	-			;DEFINE DRIVER PROLOGUE TABLE
		END=NL_END,-		;END OF DRIVER
		ADAPTER=UBA,-		;ADAPTER TYPE
		UCBSIZE=116,-		;UCB SIZE
		NAME=NLDRIVER		;DRIVER NAME
	DPT_STORE INIT			;CONTROL BLOCK INIT VALUES
	DPT_STORE REINIT		;CONTROL BLOCK RE-INIT VALUES
	DPT_STORE END			;
 
;
; DRIVER DISPATCH TABLE
;
 
	DDTAB	NL,-			;DRIVER DISPATCH TABLE
		0,-			;START I/O OPERATION
		0,-			;UNSOLICITED INTERRUPT
		FUNCTABLE,-		;FUNCTION DECISION TABLE ADDRESS
		0,-			;CANCEL I/O ENTRY POINT
		0,-			;ADDRESS OF REGISTER DUMP ROUTINE
		0,-			;SIZE OF DIAGNOSTIC BUFFER
		0			;SIZE OF ERROR LOG BUFFER
	.PAGE
	.SBTTL	NULL DEVICE FUNCTION DECISION TABLE
;+
; NULL DEVICE FUNCTION DECISION TABLE
;-
 
FUNCTABLE:				;FUNCTION DECISION TABLE
	FUNCTAB	,-			;LEGAL FUNCTIONS
		<READLBLK,-		;READ LOGICAL BLOCK
		 READVBLK,-		;READ VIRTUAL BLOCK
		 WRITEOF,-		;WRITE EOF
		 WRITELBLK,-		;WRITE LOGICAL BLOCK
		 WRITEVBLK>		;WRITE VIRTUAL BLOCK
	FUNCTAB	,-			;BUFFERED I/O FUNCTIONS
		<>			;NO BUFFERED I/O FUNCTIONS
	FUNCTAB	READ,-			;READ FUNCTIONS
		<READLBLK,-		;READ LOGICAL BLOCK
		 READVBLK>		;READ VIRTUAL BLOCK
	FUNCTAB	WRITE,-			;WRITE FUNCTIONS
		<WRITELBLK,-		;WRITE LOGICAL BLOCK
		WRITEOF,-		;WRITE EOF
		 WRITEVBLK>		;WRITE VIRTUAL BLOCK
	.PAGE
	.SBTTL	NULL DEVICE FUNCTION DECISION TABLE ROUTINES
;+
; READ - READ FUNCTIONS
; WRITE - WRITE FUNCTIONS
;
; THE NULL DEVICE IS A SPECIAL DEVICE IN THAT ALL WRITES ARE DISCARDED AND ALL
; READS RETURN AN END OF FILE STATUS. THERE IS NO ACTUAL DRIVER, BUT RATHER ALL
; PROCESSING IS DONE BY FUNCTION DECISION TABLE ROUTINES.
;
; INPUTS:
;
;	R0 = SCRATCH.
;	R1 = SCRATCH.
;	R2 = SCRATCH.
;	R3 = ADDRESS OF I/O REQUEST PACKET.
;	R4 = CURRENT PROCESS PCB ADDRESS.
;	R5 = ASSIGNED DEVICE UCB ADDRESS.
;	R6 = ADDRESS OF CCB.
;	R7 = I/O FUNCTION CODE BIT NUMBER.
;	R8 = FUNCTION DECISION TABLE DISPATCH ADDRESS.
;	R9 = SCRATCH.
;	R10 = SCRATCH.
;	R11 = SCRATCH.
;	AP = ADDRESS OF FIRST FUNCTION DEPENDENT PARAMETER.
;
; OUTPUTS:
;
;	IF THE FUNCTION IS A READ, THEN SS$_ENDOFFILE IS RETURNED.
;
;	IF THE FUNCTION IS A WRITE, THEN SS$_NORMAL IS RETURNED.
;-
 
	.ENABL	LSB
	.PSECT	$$$115_DRIVER
READ:	MOVZWL	#SS$_ENDOFFILE,R0	;SET END OF FILE STATUS
	BRB	10$			;
WRITE:	MOVZWL	#SS$_NORMAL,R0		;SET NORMAL COMPLETION
10$:	JMP	EXE$FINISHIOC		;FINISH I/O OPERATION
	.DSABL	LSB
NL_END:					;ADDRESS OF LAST LOCATION IN DRIVER
 
	.END
