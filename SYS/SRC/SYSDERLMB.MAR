	.TITLE	SYSDERLMB - DECLARE ERROR LOG MAILBOX YSSTEM SERVICE
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: EXECUTIVE, SYSTEM SERVICES
;
; ABSTRACT:
;	SYSDERLMB IMPLEMENTS THE DECLARE ERROR LOG MAILBOX SYSTEM SERVICE
;	WHICH ENABLES A DIAGNOSTIC OR OTHER PRIVILEGED PROGRAM TO REQUEST
;	THAT A COPY OF ALL ERROR LOG RECORDS BE WRITTEN A SPECIFIED MAILBOX
;	UNIT.
;
;
; ENVIRONMENT:
;	KERNEL MODE
; AUTHOR: R. I. HUSTVEDT	, CREATION DATE: 19-SEP-77
;
; MODIFIED BY:
;
;	, : VERSION
; 01	- 
;--
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

	$IPLDEF				; DEFINE IPL VALUES
	$PCBDEF				; DEFINE PCB OFFSETS
	$PRDEF				; DEFINE PROCESSOR REGISTERS
	$PRVDEF				; DEFINE PRIVILEGE BIT NUMBERS
	$SSDEF				; DEFINE STATUS CODE VALUES
;
; MACROS:
;

;
; EQUATED SYMBOLS:
;
UNIT=0					; UNIT FIELD OF ERROR LOG MAILBOX DESCRIPTOR
PID=4					; PROCESS ID FIELD OF ERROR LOG MAILBOX DESCR

MBXUNT=4				; MAILBOX UNIT ARGUMENT LIST OFFSET

;
; OWN STORAGE:
;

	.PAGE
	.SBTTL	SYSDERLMBX - DECLARE ERROR LOG MAILBOX
;++
; FUNCTIONAL DESCRIPTION:
;	EXE$DERLMBX SETS THE ERRORLOG MAILBOX DESCRIPTOR TO POINT TO A 
;	SPECIFIED MAILBOX UNIT.  ONLY ONE PROCESS MAY ASSIGN A MAILBOX UNIT
;	TO THE ERROR LOG AT ONE TIME.  IF THE ERROR LOG MAILBOX IS ALREADY
;	ASSIGNED TO ANOTHER PROCESS, THE ERROR STATUS CODE SS$_DEVALLOC
;	IS RETURNED.  WHEN A PROCESS IS FINISHED WITH THE ERROR LOG MAILBOX
;	IT SHOULD ASSIGN A UNIT NUMBER OF 0(ZERO).  EXE$RUNDWN WILL DO THIS
;	IN CASE THE IMAGE NEGLECTS TO EXIT CORRECTLY.
;
; CALLING SEQUENCE:
;	CALLG	ARGLIST,EXE$DERLMBX
;
; INPUT PARAMETERS:
;	MBXUNT(AP) - MAILBOX UNIT NUMBER
;	R4 - PCB ADDRESS OF CURENT PROCESS
;
; IMPLICIT INPUTS:
;	PCB AND PROCESS HEADER OF CURRENT PROCESS
;
; OUTPUT PARAMETERS:
;	R0 - COMPLETION STATUS CODE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	SS$_NORMAL   - NORMAL SUCCESSFUL COMPLETION
;	SS$_NOPRIV   - PROCESS DOES NOT HAVE DIAGNOSTIC PRIVILEGE
;	SS$_DEVALLOC - ERROR LOG MAILBOX UNIT ALREADY DECLARED BY ANOTHER
;		       PROCESS.
;
; SIDE EFFECTS:
;	NONE
;
;--


EXE$DERLMB::				;
	.WORD	^M<R2,R3,R4,R5>		; SAVE R2-R5

	MOVZWL	#SS$_NOPRIV,R0		; ASSUME NO PRIVILEGE
	IFNPRIV	DIAGNOSE,EXIT		; EXIT WITH ERROR STATUS IF NO PRIVILEGE
	MOVZWL	#SS$_DEVALLOC,R0	; ASSUME "DEVICE" ALLOCATED
	MOVAB	W^EXE$GQ_ERLMBX,R5	; SET ADDRESS OF ERRORLOG MAILBOX DESCRIPTOR
	DSBINT	#IPL$_SYNCH		; INHIBIT SYSTEM EVENT REPORTING
	TSTW	UNIT(R5)		; TEST FOR ALREADY ASSIGNED
	BEQL	10$			; NOT ASSIGNED
	CMPL	PCB$L_PID(R4),PID(R5)	; CHECK FOR CURRENT PROCESS
	BNEQ	EXITEN			; NO, EXIT WITH DEVALLOC ERROR
10$:	MOVL	PCB$L_PID(R4),PID(R5)	; SET PID OF OWNER
	MOVW	MBXUNT(AP),UNIT(R5)	; SET ERRORLOG MAILBOX UNIT NUMBER
	BNEQ	20$			; BR IF ACTUAL UNIT
	CLRL	PID(R5)			; ZAP PID OF MAILBOX OWNER
20$:	MOVL	#SS$_NORMAL,R0		; SET NORMAL COMPLETION STATUS
EXITEN:	ENBINT				; ENABLE SYSTEM EVENT REPORTING
EXIT:	RET				; RETURN


	.END
