	.TITLE	SYSSETMOD - SYSTEM SERVICES TO SET PROCESS MODES
	.IDENT	'V03-000'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; D. N. CUTLER 17-AUG-76
;
; SYSTEM SERVICES TO SET PROCESS MODES
;
;	SET RESOURCE WAIT MODE
;	SET SYSTEM SERVICE FAILURE EXCEPTION MODE
;	SET PROCESS SWAP MODE
;
; MACRO LIBRARY CALLS
;
 
	$PCBDEF				;DEFINE PCB OFFSETS
	$PRVDEF				;DEFINE PRIVILEGE BITS
	$PSLDEF				;DEFINE PROCESSOR STATUS FIELDS
	$SSDEF				;DEFINE SYSTEM STATUS VALUES
 
;
; LOCAL SYMBOLS
;
; ARGUMENT LIST OFFSET DEFINITIONS
;
 
ENBFLG=4				;FAILURE MODE INDICATOR
SWPFLG=4				;SWAP MODE INDICATOR
WATFLG=4				;WAIT MODE INDICATOR
	.PAGE
	.SBTTL	SET RESOURCE WAIT MODE
;+
; EXE$SETRWM - SET RESOURCE WAIT MODE
;
; THIS SERVICE PROVIDES THE CAPABILITY TO CONTROL WHETHER IMPLICIT WAITS
; ARE TO BE PERFORMED BY THE SYSTEM WHEN A REQUEST FOR A RESOURCE FAILS.
;
; INPUTS:
;
;	WATFLG(AP) = WAIT MODE INDICATOR.
;		LOW BIT CLEAR TO ENABLE IMPLIED RESOURCE WAIT.
;		LOW BIT SET TO DISABLE IMPLIED RESOURCE WAIT.
;
;	R4 = CURRENT PROCESS PCB ADDRESS.
;
; OUTPUTS:
;
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = SS$_WASCLR - NORMAL COMPLETION, RESOURCE WAIT WAS
;			PREVIOUSLY ENABLED.
;
;		R0 = SS$_WASSET - NORMAL COMPLETION, RESOURCE WAIT WAS
;			PREVIOUSLY DISABLED.
;-
 
	.PSECT	Y$EXEPAGED
	.ENTRY	EXE$SETRWM,^M<R4>
	MOVZBL	#PCB$V_SSRWAIT,R1	;SET RESOURCE WAIT BIT NUMBER
	BRB	SETMOD			;FINISH IN COMMON CODE
	.PAGE
	.SBTTL	SET SYSTEM SERVICE FAILURE EXCEPTION MODE
;+
; EXE$SETSFM - SET SYSTEM SERVICE FAILURE EXCEPTION MODE
;
; THIS SERVICE PROVIDES THE CAPABILITY TO CONTROL THE GENERATION OF SYSTEM
; SERVICE FAILURE EXCEPTIONS WHEN A SERVICE RETURNS A STATUS VALUE WITH A
; SEVERITY OF ERROR OR SEVERE ERROR.
;
; INPUTS:
;
;	ENBFLG(AP) = FAILURE MODE INDICATOR.
;		LOW BIT CLEAR TO DISABLE FAILURE EXCEPTIONS.
;		LOW BIT SET TO ENABLE FAILURE EXCEPTIONS.
;
;	R4 = CURRENT PROCESS PCB ADDRESS.
;
; OUTPUTS:
;
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = SS$_WASCLR - NORMAL COMPLETION, FAILURE EXCEPTIONS
;			WERE PREVIOUSLY DISABLED.
;
;		R0 = SS$_WASSET - NORMAL COMPLETION, FAILURE EXCEPTIONS
;			WERE PREVIOUSLY ENABLED.
;-
 
	.ENTRY	EXE$SETSFM,^M<R4>
	MOVPSL	R1			;READ CURRENT PSL
	EXTZV	#PSL$V_PRVMOD,#PSL$S_PRVMOD,R1,R1 ;EXTRACT PREVIOUS MODE
	ADDL	#PCB$V_SSFEXC,R1	;ADD BASE FAILURE ENABLE BIT NUMBER
	BRB	SETMOD			;FINISH IN COMMON CODE
	.PAGE
	.SBTTL	SET PROCESS SWAP MODE
;+
; EXE$SETSWM - SET PROCESS SWAP MODE
;
; THIS SERVICE PROVIDES THE CAPABILITY TO LOCK OR UNLOCK A PROCESS FROM
; THE BALANCE SET. LOCKED IN THE BALANCE SET PREVENTS A PROCESS FROM BEING
; SWAPPED OUT OF MEMORY.
;
; INPUTS:
;
;	SWPFLG(AP) = PROCESS SWAP MODE INDICATOR.
;		LOW BIT CLEAR TO DISABLE PROCESS SWAPPING.
;		LOW BIT SET TO ENABLE PROCESS SWAPPING.
;
;	R4 = CURRENT PROCESS PCB ADDRESS.
;
; OUTPUTS:
;
;	R0 LOW BIT CLEAR INDICATES FAILURE TO SET PROCESS SWAP MODE.
;
;		R0 = SS$_NOPRIV - PROCESS DOES NOT HAVE PRIVILEGE TO
;			CHANGE ITS SWAP MODE.
;
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = SS$_WASCLR - NORMAL COMPLETION, PROCESS WAS PREVIOUSLY
;			NOT LOCKED IN BALANCE SET.
;
;		R0 = SS$_WASSET - NORMAL COMPLETION, PROCESS WAS PREVIOUSLY
;			LOCKED IN BALANCE SET.
;-
 
	.ENABL	LSB
	.ENTRY	EXE$SETSWM,^M<R4>
	MOVZWL	#SS$_NOPRIV,R0		;ASSUME PROCESS CANNOT CHANGE SWAP MODE
	IFNPRIV	PSWAPM,20$		;PROCESS HAVE PRIVILEGE TO CHANGE SWAP MODE?
	MOVZBL	#PCB$V_PSWAPM,R1	;SET PROCESS SWAP BIT NUMBER
SETMOD:	MOVZWL	#SS$_WASSET,R0		;ASSUME PREVIOUSLY SET
	BBS	R1,PCB$L_STS(R4),10$	;IF SET, MODE CURRENTLY SET
	MOVZWL	#SS$_WASCLR,R0		;PREVIOUSLY CLEAR
10$:	INSV	ENBFLG(AP),R1,#1,PCB$L_STS(R4) ;SET NEW MODE
20$:	RET				;
	.DSABL	LSB
 
	.END
