	$BEGIN	RM2CREATE,017,F$RMSOPEN2C,<RELATIVE-SPECIFIC CREATE>


;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;++
; Facility: rms32
;
; Abstract:
;
;	this routine performs the relative file
;	organization-specific create processing.
;
; Environment:
;		star processor running starlet exec.
;
; Author: L F Laverdure,	 Creation Date: 7-DEC-1977
;
; Modified By:
;
;	V02-017	CDS0012		C Saether	 5-Feb-1982
;		Back out V02-016.  GBC now in record attributes.
;
;	V02-016 CDS0011		C Saether	 3-Jan-1982
;		Store GBC field from FAB to plg.
;
;	V02-015	CDS0010		C Saether	25-Aug-1981
;		Replace call to RM$ALLOC_BCB with RM$ALBLB.
;
;	V02-014 RAS0028		Ron Schaefer	20-Aug-1981
;		Change FAB$C_STM11 to FAB$C_STM.
;
;	V02-013 RAS0015		Ron Schaefer	7-Jul-1981
;		Correct record format check for stream format files.
;
;	V02-012	KPL0001		Peter Lieberwirth	24-Jul-1981
;		Fix broken branches.
;
;	V02-011	CDS0012		C SAETHER	28-Aug-1980	16:00
;		Fix sense of test in V009.
;
;	V010	REFORMAT	D M WALP	25-JUL-1980
;
;	V009	RAS0002		R SCHAEFER	27-NOV-1979	09:30
;		allow creation on arbitrary device (i.e. magtape) if in
;		bio mode.
;
;	V008	CDS0011		C SAETHER	24-JUL-1979	14:26
;		remove reference to ifb$w_bks_bytes
;
;	V007	RAN0003		L F LAVERDURE	19-OCT-1978	17:42
;		add file sharing code
;
;--
;


	.SBTTL	DECLARATIONS

;
; Include Files:
;

;
; Macros:
;

	$FABDEF
	$IFBDEF
	$CSHDEF
	$DEVDEF
	$BDBDEF
	$PLGDEF
	$RLSDEF
	$FRBDEF
	$RMSDEF

;
; Equated Symbols:
;

	FOP=FAB$L_FOP*8			; bit offset to fop

;
; Own Storage:
;


	.SBTTL	RM$CREATE2 - RELATIVE CREATE ROUTINE

;++
; RM$CREATE2
;
;	RM$CREATE2 -
;
;	this routine performs all of the file create
;	functions that are specific to the relative
;	file organization, including:
;
;	1. checking that sharing has not been specified in such a way
;	    that inter-process record locking is required.
;	2. checking that device is a disk if not bio mode
;	3. checking that record format is not undefined or stream
;	4. checking that bucket size and maximum record size are compatible
;	5. verifying maximum record number
;	6. checking xab chain validity
;	7. calling the common create routine
;	8. locking the prolog, initial formatting of the data buckets to zeroes
;	9. initializing and unlocking the prolog
;
; Calling sequence:
;
;	entered via case branch from rm$open
;	returns by jumping to rm$createxit
;
; Input Parameters:
;
;	r11	impure area address
;	r10	fwa address
;	r9	ifab address
;	r8	fab address
;
; Implicit Inputs:
;
;	the contents of the fab, ifab, & fwa.
;
; Output Parameters:
;
;	r0	status code
;	r1-r7	destroyed
;
; Implicit Outputs:
;
;	various fields in the ifab & fab are initialized.
;
; Completion Codes:
;
;	standard rms
;
; Side Effects:
;
;	none
;
;--



;
;  code to handle error conditons.
;  (note: this is not the entry point for the rm$create2 routine.)
;

ERRDEV:	$PSECT_BR	L$RMS2
	RMSERR	DEV			; device not disk
CREXIT:	BRW	RM$CREATEXIT		; go clean up
	$PSECT_RESTORE

;

ERRXIT:	BRW	RM$CREATEXIT		; go clean up

;

ERRRFM:	BRW	RM$CRE_ERRRFM		; rfm = udf or > vfc

;

ERRMRS:	BRW	RM$CRE_ERRMRS		; mrs < or = 0

;

ERRBKS:	$PSECT_BR	L$RMS2
	RMSERR	BKS			; bks > 32 or < cell size
	BRB	CREXIT			; go get out
	$PSECT_RESTORE

;

ERRMRN:	$PSECT_BR	L$RMS2
	RMSERR	MRN			; mrn < 0
	BRB	CREXIT			; go get out
	$PSECT_RESTORE


;++
;  entry point for relative-specific create
;
;--

RM$CREATE2::
	$TSTPT	CREATE2

;
;  check that device is disk
;

	BBS	#IFB$V_BIO,IFB$B_FAC(R9),-
		5$			; allow bio on any dev
	BBC	#DEV$V_RND,IFB$L_PRIM_DEV(R9),-
		ERRDEV			; branch if not disk

;
;  handle allocation request, if any
;

5$:	BSBW	RM$SETALLOC		; handle allocation xab and
					;  set deq and rtdeq
ERRXIT1:
	BLBC	R0,ERRXIT		; get out on error
	TSTL	FAB$L_ALQ(R8)		; any initial allocation?
	BNEQ	10$			; branch if yes
	INCL	FAB$L_ALQ(R8)		; no - need 1 block for prolog
10$:

;
;  check rfm and mrs parameters
;
; assume rfm already checked for gtr than maxrfm
;

	ASSUME	FAB$C_UDF EQ 0
	TSTB	IFB$B_RFMORG(R9)	; is rfm undefined?
	BEQL	ERRRFM			; branch if yes

	ASSUME	FAB$C_STM GT FAB$C_VFC
	CMPB	IFB$B_RFMORG(R9),-
		#FAB$C_STM 		; is rfm stream?
	BGEQU	ERRRFM			; branch if yes

	MOVW	FAB$W_MRS(R8),IFB$W_LRL(R9)-
					; set lrl from fab mrs
	BLEQ	ERRMRS			; branch if not > 0

;
;  compute cell size
;

	ADDW3	FAB$W_MRS(R8),#1,R0	; add in delete ctrl byte
	CMPB	IFB$B_RFMORG(R9),-
		#FAB$C_FIX		; fixed rec len?
	BEQL	30$			; branch if yes
	ADDW2	#2,R0			; add in record length field
	MOVZBL	IFB$B_FSZ(R9),R1	; get fsz
	ADDW2	R1,R0			; and add in giving tot. size
30$:


;
;  check cell size against bks
;

	MOVZBL	FAB$B_BKS(R8),R1	; copy bucket size from fab
	BNEQ	40$			; branch if speced

;
; default bucket size to min.
; required to contain 1 record
;

	DECW	R0			; round down
	DIVW3	#512,R0,R1		; get # blks - 1 for 1 record
	INCW	R1			; get # blks for 1 record
	INCW	R0			; restore cell size
40$:	MOVB	R1,IFB$B_BKS(R9)	; copy bucket size to ifab
	CMPB	R1,#32			; in range?
	BGTRU	ERRBKS			; branch if not
	ASHL	#9,R1,R1		; compute bucket size in bytes
	CMPW	R0,R1			; cell size < or = bucket size?
	BGTRU	ERRBKS			; branch if not
					;  set mrn value
	MOVL	FAB$L_MRN(R8),IFB$L_MRN(R9)
					; set mrn from fab
	BGTR	50$			; branch if > 0
	BLSS	ERRMRN			; error if < 0
	MOVL	#^X7FFFFFFF,IFB$L_MRN(R9)
					; default to max. pos #

;
;  go do create.
;  (note: this may be a 'create if', in which case return will be
;   made to rms0open if actually opened rather than created.)
;

50$:	BSBW	RM$CREATECOM		; do common create
	BLBC	R0,ERRXIT1		; get out on error
	PUSHL	R0			; save status code

;
;  file has been created.
;  allocate a lock bdb and bcb and lock the prolog.
;

	MOVL	R9,R10			; set r10 to ifab addr
	BBC	#IFB$V_BIO,IFB$B_FAC(R9),-
		52$			; continue unless block i/o
	BRW	90$			; avoid formatting for block io
52$:
	BBS	#IFB$V_NORECLK, (R10), 55$ ; Branch if not locking.
	BSBW	RM$ALBLB		; Get a lock BLB.
	BLBC	R0, 70$			; Branch on error.
55$:	$CACHE	VBN=#1,SIZE=#0,FLAGS=<LOCK,NOREAD,NOBUFFER>
	.IF	NE <$$RMSTEST & $$RMS_TBUGCHK>
	BLBC	R0,70$			; branch on error (bug)
	.ENDC


;
;  format file by writing zeroes to allocated space
;

	MOVL	#2,IFB$L_DVBN(R9)	; set first data vbn
	MOVL	#2,R1			; 1st block for zeroing
	BSBW	RM$SET_SIFB_ADR		; get sifab/ifab addr
	ADDL3	#1,IFB$L_HBK(AP),R6	; compute eof block
	MOVL	R6,IFB$L_EBK(AP)	; save it
	CMPL	R6,#2			; eof in vbn 2?
	BEQL	60$			; branch if yes (no need to zero)
	BSBW	RM$FMT_BKT2		; format (zero) data buckets
	BLBC	R0,RLNERR		; branch on error
60$:

;
;  get buffer for prolog and initialize prolog.
;

	$CACHE	VBN=#1,SIZE=#512,FLAGS=<LOCK,NOREAD>; get buffer for prolog

	.IF	NE <$$RMSTEST & $$RMS_TBUGCHK>
70$:	BLBC	R0,ERRBUG		; branch on error (bug)
	.ENDC
	PUSHR	#^M<R4,R5>		; save bdb and buffer addr
	MOVC5	#0,(SP),#0,#512,(R5)	; zero buffer
	POPR	#^M<R4,R5>		; restore bdb and buffer addr
	MOVW	#PLG$C_VER_NO,PLG$W_VER_NO(R5)
					; set version #
	MOVL	R6,PLG$L_EOF(R5)	; and eof vbn
	MOVW	IFB$L_DVBN(R9),PLG$W_DVBN(R5)
					; and first data vbn
	MOVL	IFB$L_MRN(R9),PLG$L_MRN(R5)
					; and max record number
	BSBW	RM$MAKSUM		; calculate and set checksum
	BISB2	#BDB$M_DRT!BDB$M_VAL,BDB$B_FLGS(R4)
					; say valid and dirty
	MOVL	#RLS$M_WRT_THRU,R3	; cause immediate write
	BSBW	RM$RELEASE		; release prolog
	BLBC	R0,RLSERR		; branch on error
90$:	BRW	RM$CREATEXIT1		; finish up create


;
; handle errors
;

ERRBUG:
RLNERR:	$PSECT_BR	L$RMS2		; failed zero data buckets
	PUSHL	R0			; store status
	$CACHE	VBN=#1,SIZE=#0,ERR=RLSXIT
					; re-get prolog bdb
	JSB	RM$RLNERR		; unlock prolog
RLSXIT:	BRW	RM$CREATEXIT1		; and get out
	$PSECT_RESTORE

;

RLSERR:	$PSECT_BR	L$RMS2
	RMSERR	WPL,(SP)		; prolog write error
	BRB	RLSXIT			; go clean up
	$PSECT_RESTORE


	.END
