	$BEGIN	RMS0CONN,017,F$RMSOPEN0,<DISPATCH ROUTINE FOR CONNECT>


;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; Facility: rms32
;
; Abstract:
;		this routine is the highest level control
;		routine to perform the $connect function.
;
; Environment:
;		star processor running starlet exec.
;
; Author: L F LAVERDURE,	 Creation Date: 5-JAN-1977
;
; Modified By:
;
;	V02-017	KDM0037		Kathleen D. Morse	12-Feb-1980
;		Change non-kernel references to SCH$GL_CURPCB to
;		CTL$GL_PCB instead.
;
;	V016	REFORMAT	D M WALP	25-JUL-1980
;
;	V015	RAN0003		R A NEWELL	20-DEC-1978	1:30
;		fixed pid bug
;
;	V014	RAN0002		R A NEWELL	1-SEP-1978	12:00
;		rms32 isam modification.  redefinition of entry point to
;		resolve out of range branches.
;
;
;
;--


	.SBTTL	DECLARATIONS

;
; Include Files:
;

;
; Macros:
;

	$FABDEF
	$RABDEF
	$IMPDEF
	$PIODEF
	$IFBDEF
	$IRBDEF
	$PCBDEF
	$ASBDEF
	$RMSDEF

;
; Equated Symbols:
;

	ROP=RAB$L_ROP*8			; bit offset to rop field

;
; Own Storage:
;

IRAB_SIZE_TBL:
	.BYTE	<IRB$C_BLN_SEQ+ASB$C_BLN_SEQ>/4
	.BYTE	<IRB$C_BLN_REL+ASB$C_BLN_REL>/4
	.BYTE	<IRB$C_BLN_IDX+ASB$C_BLN_IDX>/4
IRAB_START_TBL:
	.WORD	ASB$C_BLN_SEQ
	.WORD	ASB$C_BLN_REL
	.WORD	ASB$C_BLN_IDX


	.SBTTL	RMS$CONNECT - $CONNECT ROUTINE

;++
;  RM$CONNECT
;
;	RM$CONNECT-
;
;	this routine performs the highest level $connect
;	processing.  its functions include:
;
;	1. allocate and init an irab along with its asb
;	2. perform common setup
;	3. dipatch to organization-dependent routine
;
;
;
; Calling sequence:
;
;	entered from exec as a result of user's calling sys$connect
;	(e.g., by using the $connect macro).
;
; Input Parameters:
;
;	ap	user's argument list address
;
; Implicit Inputs:
;
;	the contents of the rab and the ifi field of the fab.
;
; Output Parameters:
;
;	r0	status code
;	r1	destroyed
;
; Implicit Outputs:
;
;	various fields of the rab are filled in to reflect
;	the status of the connected stream.  (see rms functional
;	spec for a complete list.)
;
;	an irab is initialized to reflect the connected stream.
;
;	a completion ast is queued if so specified by the user.
;
; Completion Codes:
;
;	standard rms (see functional spec for list).
;
; Side Effects:
;
;	none
;
;--


	$ENTRY	RMS$$CONNECT
	$TSTPT	CONNECT

;
;  perform common rab function setup, validating the
;  argument list and basic rab, and setting regs as
;  follows:  r11=impure area addr,  r9=isi, r8=rab addr, r7=caller's mode
;

	BSBW	RM$RABCHK		; return from setup only if aok

;
;  get ifab address via the fab
;

	MOVL	RAB$L_FAB(R8),R10	; get fab addr
	IFNORD	#FAB$C_BLN,(R10),ERRFAB	; must be able to access
	ASSUME	FAB$B_BID EQ 0
	CMPB	(R10),#FAB$C_BID	; is it a fab?
	BNEQ	ERRFAB			; branch if not
	CMPB	FAB$B_BLN(R10),#FAB$C_BLN
					; long enough?
	BLSS	ERRBLN
	MOVZWL	FAB$W_IFI(R10),R9	; get ifi
	MOVL	#IMP$L_IFABTBL/4,R0	; ifab table offset/4
	BSBW	RM$GTIADR		; get ifab addr (r9)
	BEQL	ERRIFI			; error if none
	.IF	NE $$RMSTEST&$$RMS_TBUGCHK
	ASSUME	IFB$B_BID EQ IFB$B_BLN-1
	CMPW	IFB$B_BID(R9),-
		#IFB$C_BID+<<IFB$C_BLN/4>*256>
	BNEQ	ERRBUG			; branch if not a valid ifab
	.ENDC
	BBS	#IFB$V_BUSY,(R9),ERRACT	; branch if ifab already in use
	BBS	#FAB$V_PPF_IND+<FAB$W_IFI*8>,-
		(R10),PPF_IND		; branch if indirect ppf
	MOVL	R9,R10			; save ifab addr in r10

;
;  check for only 1 irab unless multi-streams enabled
;

	MOVZBL	IFB$B_ORGCASE(R10),R5	; get file org index
	ASSUME	IFB$C_SEQ EQ 0
	BEQL	10$			; branch if sequential
					; (no multi streaming)
	CMPB	R5,#IFB$C_MAXORG	; is this a known file org?
	BLEQ	5$			; branch if yes
	CLRL	R5			; no - allocate seq. len irab
	BRB	10$			; check no multi-streams
5$:	BBS	#IFB$V_MSE,(R10),20$	; omit check if multi-streams enabled
10$:	TSTL	IFB$L_IRAB_LNK(R10)	; already got an irab?
	BNEQ	ERRCCR			; branch if yes (error)
20$:

;
;  allocate an irab along with its asb
;
;  allocate variable length irab based on organization.
;  note: the asb is located immediately before the irab proper
;	 and is pointed to via the asb addr field
;

	MOVZBL	IRAB_SIZE_TBL[R5],R2	; get asb/irab size in long words
	MOVL	R10,R1			; page having free space
	BSBW	RM$GETBLK		; allocate irab
	BLBC	R0,EX_NOSTR		; branch if none
	MOVZWL	IRAB_START_TBL[R5],R9	; get offset to irab proper
	ADDL2	R1,R9			; and make it an address
	MOVL	R1,IRB$L_ASBADDR(R9)	; save address of asb
	MOVB	9(R1),IRB$B_BLN(R9)	; save size of asb/irab
	BRB	INIT_IRAB		; finish irab initialization


;
;  error handling
;

ERRACT:	$PSECT_BR	L$RMS0
	BSBB	ERROR
	RMSERR_WORD	ACT		; fab function already active
	$PSECT_RESTORE

;
;					; or ppf irab busy
;

ERRIFI:	$PSECT_BR	L$RMS0
	BSBB	ERROR
	RMSERR_WORD	IFI		; invalid ifi value in fab
	$PSECT_RESTORE

;

ERRFAB:	$PSECT_BR	L$RMS0
	BSBB	ERROR
	RMSERR_WORD	FAB		; invalid fab
	$PSECT_RESTORE

;

ERRBLN:	$PSECT_BR	L$RMS0
	BSBB	ERROR
	RMSERR_WORD	BLN		; invalid block length
	$PSECT_RESTORE

;

ERRCCR:	$PSECT_BR	L$RMS0
	BSBB	ERROR			; can't connect rab
	RMSERR_WORD	CCR

;

ERROR:	MOVZWL	@(SP)+,R0		; in-line error code to r0
	BRW	RM$EX_NOSTR		; report error
	$PSECT_RESTORE

;

ERRBUG:	RMSTBUG	FTL$_BADIFAB		; invalid ifab table pointer


;
;  this is an indirect connect for a process permanent file
;
;  perform various checks to see if allowed
;

PPF_IND:
	MOVW	FAB$W_IFI(R10),-
		RAB$W_ISI(R8)		; save rat value in isi
	MOVL	R9,R10			; get ifab addr to right reg
	MOVL	IFB$L_IRAB_LNK(R9),R9	; get irab addr
	BEQL	ERRCCR			; branch if none
	BBS	#IRB$V_BUSY,(R9),ERRACT	; branch if busy

;
;  do miscellaneous context cleanups
;

	CLRW	IRB$W_CSIZ(R9)		; say no current record

;
;  return the isi value to the rab and do a structureless exit
;

20$:	INSV	IRB$B_PPF_ISI(R9),-
		#RAB$W_ISI*8,-
		#RAB$V_PPF_RAT,(R8)	; set table index into isi
	RMSSUC
EX_NOSTR:
	BRW	RM$EX_NOSTR		; do structureless exit

;
;  irab was allocated but no irab table slot  -
;  deallocate the irab and return an error
;

ERRNOSLT:
	$PSECT_BR	L$RMS0
	MOVL	R10,R3			; page addr of free space hdr
	MOVL	IRB$L_ASBADDR(R9),R4	; addr of asb/irab
	MOVB	IRB$B_BLN(R9),9(R4)	; and size
	BSBW	RM$RETBLK		; give it back
	BSBB	ERROR
	RMSERR_WORD	DME		; restore error code
	$PSECT_RESTORE


;
;  set irab bid, bln
;

INIT_IRAB:
	MOVB	#IRB$C_BID,IRB$B_BID(R9)

;
;  allocate and initialize a slot in the irab table
;

	MOVL	IMP$L_IRABTBL(R11),R5	; set table addr
	BSBW	RM$GTSLT		; and get a slot
	BLBC	R0,ERRNOSLT		; branch if none
	MOVW	R6,RAB$W_ISI(R8)	; store isi value
	MOVW	R6,IRB$W_OWN_ISI(R9)	; save isi value
	MOVL	@#CTL$GL_PCB,R0		; get addr of pcb for process
	MOVW	PCB$L_PID(R0),-
		IRB$W_OWN_ID(R9)	; get process id index

;
;  link the irab into the ifab's chain (at start)
;

	MOVL	R10,IRB$L_IFAB_LNK(R9)	; set ifab ptr
	MOVL	IFB$L_IRAB_LNK(R10),IRB$L_IRAB_LNK(R9)
	MOVL	R9,IFB$L_IRAB_LNK(R10)

;
;  set stream busy and call rm$rset_alt to perform various other setups
;

	SSB	#IRB$V_BUSY,(R9)	; set stream busy
	BSBW	RM$RSET_ALT		; no error possible
	.BYTE	0			; no in-line checks

;
;  dispatch to organization-dependent routine
;

	CASE	TYPE=B,-
		SRC=IFB$B_ORGCASE(R10),-
		DISPLIST=<RM$CONNECT1,RM$CONNECT2,RM$CONNECT3>
					; pickup correct routine
					; seq, rel, idx orgs

;++
;
;  connect for unknown org.  verify that only block i/o will be done,
;  giving error otherwise.
;
;--

	$PSECT_BR	L$RMS0
	RMSERR	ORG			; anticipate error
RM$CHKBIOCONN::
	BBC	#RAB$V_BIO+ROP,(R8),10$	; branch if bio rop option clear
	BBCC	#FAB$V_BRO,IFB$B_FAC(R10),10$
					; branch if fac not bro
	BISB2	#FAB$M_BIO,IFB$B_FAC(R10)
					; switch bro to bio
10$:	BBC	#FAB$V_BIO,IFB$B_FAC(R10),-
		ERRORG			; error if not block i/o
	BRW	RM$CONNECT_BIO		; connect for block i/o

;

ERRORG:	BSBW	RM$CCLN1		; deallocate irab
	BRW	RM$EX_NOSTR		; and get out
	$PSECT_RESTORE


	.END
