	$BEGIN	RMS0SETDD,002,L$RMS0MISC,<SET DEFAULT DIRECTORY>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;++
; Facility: rms32
;
; Abstract:
;	this routine sets (and optionally returns) the default directory
;	string in the process i/o control page.
;
; Environment:
;	vax/vms
;
; Author:
;	tim halvorsen	SEP-1979
;
; Modified By:
;
;	V02-002	REFORMAT	Ron Schaefer	30-Jul-1980	09:27
;		Reformat the source.
;
;	V001	PSK0018		P S Knibbe	20-MAR-1980
;		set def should not permit the user to specify
;		filename, type, or version.
;
;--
;

	.SBTTL	DEFINITIONS

;
; include files
;

;
; macros
;

	$FABDEF				; fab definitions
	$NAMDEF				; nam definitions
	$IFBDEF				; ifab definitions
	$FWADEF				; fwa definitions
	$PSLDEF				; psl definitions

;
; equated symbols
;

;
; own storage
;

	.SBTTL	RMS$SETDDIR, SET DEFAULT DIRECTORY STRING

;++
;
; RMS$SETDDIR:	set default directory string
;
;	this routine moves the given default directory string
;	to the process i/o control page.  the resultant string
;	is optionally returned to the caller.
;
; inputs:
;
;	4(ap) = address of descriptor of new default directory string
;		(optional - if not supplied, no change is made)
;	8(ap) = address of word to receive length of resultant string
;		(optional - if not supplied, no length is returned)
;	12(ap)= address of descriptor of buffer to receive resultant string
;		(optional - if not supplied, string is not returned)
;
; outputs:
;
;	r0 = status code (standard rms codes)
;
;	pio$gt_ddstring may be set to the resultant directory string
;--
;

	.ENABL	LSB
	$ENTRY	RMS$$SETDDIR

	$TSTPT	SETDDIR

	MOVAB	@#PIO$GT_DDSTRING,R5	; address of counted string
	MOVZBL	(R5)+,R6		; length of string

;
; if caller requested existing string, return it also
;

	MOVL	12(AP),R1		; address of buffer descriptor
	BEQL	5$			; branch if none
	IFNORD	#8,(R1),ERRIAL		; error if not readable
	CMPW	R6,(R1)			; buffer long enough?
	BGTRU	ERRIAL			; error if not
	IFNOWRT	(R1),@4(R1),ERRIAL	; error if not writable
	MOVC5	R6,(R5),#0,(R1),@4(R1)	; return existing string to caller

;
; if caller requested existing string length, return it
;

5$:	MOVL	8(AP),R1		; address to receive length
	BEQL	10$			; branch if not supplied
	IFNOWRT	#2,(R1),ERRIAL		; error if not writable
	MOVW	R6,(R1)			; return length to caller

;
; if a new string is not supplied, return the existing string
;

10$:	TSTL	4(AP)			; any input string given?
	BNEQ	20$			; branch if yes
	BRW	EXITOK			; if not, exit with success

;
; process error with caller's arguments
;

ERRIAL:	RMSERR	IAL
	BRW	EXIT

;
; allocate a fab on the stack to be used as input to xpfn
;

20$:	SUBL2	#FAB$C_BLN,SP		; allocate storage for fab
	MOVL	SP,R8			; save address of fab
	MOVC5	#0,(SP),#0,#FAB$C_BLN,(R8); zero fab
	ASSUME	FAB$B_BLN EQ FAB$B_BID+1
	MOVW	#FAB$C_BID+<FAB$C_BLN@8>,FAB$B_BID(R8); set id and length

;
; allocate a nam block on the stack to be used as input to xpfn
;

	SUBL2	#NAM$C_BLN,SP		; allocate storage for nam block
	MOVL	SP,R7			; save address of nam block
	MOVL	SP,FAB$L_NAM(R8)	; save address in fab
	MOVC5	#0,(SP),#0,#NAM$C_BLN,(R7); zero nam block
	ASSUME	NAM$B_BLN EQ NAM$B_BID+1
	MOVW	#NAM$C_BID+<NAM$C_BLN@8>,NAM$B_BID(R7); set id and length

;
; allocate an expanded string buffer for resultant string
;

	SUBL2	#NAM$C_MAXRSS,SP	; allocate storage for expanded name
	MOVL	SP,NAM$L_ESA(R7)	; save address in nam block
	MOVB	#NAM$C_MAXRSS,NAM$B_ESS(R7); set buffer length in nam block

;
; parse the input string.  a call frame is created here so that if
; fseti detects any errors and ret's, we never lose control.
;

	MOVL	4(AP),R1		; get address of input descriptor
	IFNORD	#8,(R1),ERRIAL		; error if not readable
	MOVB	(R1),FAB$B_FNS(R8)	; set length into fab
	MOVL	4(R1),FAB$L_FNA(R8)	; and address also
	PUSHL	R8			; fab address
	CALLS	#1,B^PARSE		; parse the string - get any rms errors
	BLBC	R0,EXIT			; branch if error

;
; store the resultant directory spec in the control region
;

	MOVZBL	NAM$B_ESL(R7),R4	; get resultant string length
	MOVL	NAM$L_ESA(R7),R5	; get resultant string address
	LOCC	R6,R4,(R5)		; find end of directory spec
	BEQL	ERRESL			; error if not found
	DECL	R0			; leave delimiter in string
	SUBL2	R0,R4			; chop off file name, type and version
	SUBB2	#2,R6			; get starting delimiter
	LOCC	R6,R4,(R5)		; find start of directory spec
	BEQL	ERRESL			; error if not found
	MOVAB	@#PIO$GT_DDSTRING,R3	; address to store string
	MOVB	R0,(R3)+		; store length of string
	MOVC3	R0,(R1),(R3)		; store string behind it
EXITOK:	MOVL	#1,R0
EXIT:	SSB	#16,R0			; mark as rms error
	RET				; return with status

ERRESL:	RMSERR	ESL
	BRB	EXIT

	.DSABL	LSB

;
;++
; PARSE:	subroutine to parse the input string.  this is a separate
;		procedure so that if any internal rms routine detects an
;		error, then it can perform a ret without our losing control.
;
; inputs:
;
;	4(ap),r8 = fab address
;
; outputs:
;
;	r0 = status
;	r6 = ending terminator for directory string (fwa$b_dirterm)
;	expanded name string is set
;--
;

PARSE:	.WORD	^M<R7>			; save nam address over call

;
; allocate internal ifab to be used only during this routine
;

	MOVAB	@#PIO$GW_PIOIMPA,R11	; set impure area address
	MOVL	#PSL$C_EXEC,R7		; caller's access mode
	BSBW	RM$FSETI_ALT		; allocate internal ifab

;
; set the previous mode to exec so that nam probes will work
;

	MOVPSL	-(SP)			; use current as next
	INSV	#PSL$C_EXEC,#PSL$V_PRVMOD,#PSL$S_PRVMOD,(SP) ; make prvmod=exec
	PUSHAB	B^10$			; just beyond rei
	REI

;
; parse the input string
;

10$:	BSBW	RM$XPFN			; parse/expand file name string
	BLBC	R0,90$			; branch if error
	MOVZBL	FWA$B_DIRTERM(R10),R6	; return directory terminator
	BBC	#FWA$V_WILD_DIR,(R10),90$ ; error if any wild cards
	RMSERR	DIR
90$:	BITW	#<FWA$M_TYPE!FWA$M_VERSION!FWA$M_NAME>,FWA$Q_FLAGS(R10)
					; name, type and version should not
					; be specified on set default
	BEQLU	95$			; they weren't specified
	RMSERR	DIR
95$:	PUSHR	#^M<R0,R6>		; save return status and terminator
	BSBW	RM$CLEANUP		; cleanup ifab, fwa
	POPR	#^M<R0,R6>		; restore status and terminator
	RET

	.END
