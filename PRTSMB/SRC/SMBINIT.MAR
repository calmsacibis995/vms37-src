	.TITLE	SMBINIT - SYMBIONT INITIALIZATION
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	VAX/VMS PRINT SYMBIONT
;
; ABSTRACT:	ONCE ONLY INITIALIZATION ROUTINE AND DATA
;
;
; ENVIRONMENT:	NATIVE/USER MODE NON-PRIVILEGED CODE
;
; AUTHOR:	H.B.BOSWELL + LEN KAWELL, CREATION DATE: 21-APR-77
;
; MODIFIED BY:
;
;	V02-004	MLJ0068		Martin L. Jack,	14-Jan-1982  22:54
;		Delete unnecessary initialization of RAB$L_UBF.
;
;	V02-003	GWF0087		Gary W. Fowler		23-Jul-1981
;		Set addresses of 2 user buffers.
;
;	V02-002	GWF0003		Gary W. Fowler		11-Sep-1980
;		Change protection on mailbox to remove write access to WORLD
;--
	.PAGE
	.SBTTL	DECLARATIONS

	PURE_SECTION	NAME=SMB_INITCODE

;
; INCLUDE FILES:
;
;	[PRTSMB.SRC]SMBPRE.MAR

;
; MACROS:
;

;
; EQUATED SYMBOLS:
;
	$JBCMSGDEF			;JOB CONTROLLER MESSAGES

;
; OWN STORAGE:
;

JBCMAILBOX:				;NAME FOR JOB CONTROLLER MAILBOX
	.LONG	20$-10$
	.LONG	10$
10$:	.ASCII	/_/
	.LONG	SYS$C_JOBCTLMB
	.ASCII	/:/
20$:

	.PAGE
	.SBTTL	SYMBIONT INITIALIZATION ROUTINE
;++
; FUNCTIONAL DESCRIPTION:
;
;	THIS ROUTINE PERFORMS ALL ONE TIME FUNCTIONS FOR THE
;	PRINT SYMBIONT.
;
; CALLING SEQUENCE:
;
;	MAIN ENTRY POINT OF SYMBIONT
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	R11 CONTAINS THE ADDRESS OF THE IMPURE DATA BLOCK
;	INIT DONE MESSAGE SENT TO SYMBIONT MANAGER
;
; IMPLICIT OUTPUTS:
;
;	CHANNEL ASSIGNED TO SYMBIONT MANAGER MAILBOX
;	MAILBOX CREATED FOR RECEIPT OF MANAGER MESSAGES
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	THIS ROUTINE DISPATCHES DIRECTLY TO THE SYMBIONT IDLE LOOP
;
;--
 
 
 
SMB_START:				;SYMBIONT INITIAL ENTRY
	.WORD	0			;ENTRY MASK

	MOVAL	W^SMB$HANDLER,(FP)	;SET CONDITION HANDLER ADDRESS

;
; DISABLE ASTS UNTIL MESSAGE IS SENT
;

	$SETAST_S	#0		;DISABLE ASTS

	MOVAL	W^SMB$G_DATA,R11	;SET ADDR OF IMPURE DATA BLOCK

;
; RUN-TIME INITIALIZATION OF DATA FIELDS
;

	MOVW	#SIM$K_SIZE,SD_W_MBREADLEN(R11) ;SET INITIAL MB READ LENGTH

	MOVW	#SMB$K_TBUFSIZ,SD_W_TBUFCNT(R11) ;SET LENGTH OF TEMP BUFFER
	MOVW	#SMB$K_TBUFSIZ,SD_W_TBUFSIZ(R11) ;SET LENGTH OF TEMP BUFFER
	MOVAL	SD_T_TBUF(R11),SD_A_TBUFADR(R11) ;SET ADDRESS OF TEMP BUFFER

	MOVAL	SD_G_FAB(R11),R6	;GET ADDRESS OF FAB
	MOVAL	SD_G_RAB(R11),R7	;GET ADDRESS OF RAB
	MOVAL	SD_G_NAM(R11),R8	;GET ADDRESS OF NAM BLK

	ASSUME	FAB$B_BID+1 EQ FAB$B_BLN
	MOVW	#FAB$C_BID+<FAB$C_BLN@8>,FAB$B_BID(R6) ;CREATE FAB
	ASSUME 	RAB$B_BID+1 EQ RAB$B_BLN
	MOVW	#RAB$C_BID+<RAB$C_BLN@8>,RAB$B_BID(R7) ;CREATE RAB
	ASSUME	NAM$B_BID+1 EQ NAM$B_BLN
	MOVW	#NAM$C_BID+<NAM$C_BLN@8>,NAM$B_BID(R8) ;CREATE NAM BLK

	MOVL	R8,FAB$L_NAM(R6)	;SET NAME BLOCK ADDRESS IN FAB
	MOVL	R6,RAB$L_FAB(R7)	;SET FAB ADDRESS IN RAB
	MOVW	#SMB$K_LBUFSIZ,RAB$W_USZ(R7) ;SET RECORD BUFFER SIZE
	SETBIT	RAB$V_RAH,RAB$L_ROP(R7)	;USE READ-AHEAD

	MOVZBL	#QIO$_NARGS,SD_G_QIOBLK(R11) ;SET QIO BLOCK LENGTH
	MOVZWL	#IO$_WRITELBLK,SD_G_QIOBLK+QIO$_FUNC(R11) ;SET I/O FUNCTION
	CLRW	SD_B_ERR_FLAGS(R11)	;CLEAR BOTH SETS OF FLAGS
	MOVAL	SD_T_LBUF(R11),SD_Q_BUFPNT(R11) ; SET FIRST ADDRESS
	MOVAL	SD_T_LBUF1(R11),SD_Q_BUFPNT+4(R11) ; SET SECOND ADDRESS

	ASSUME	STATE$_IDLE EQ 0

	CLRB	SD_B_STATE(R11)		;SET INITIAL STATE TO IDLE

;
; ASSIGN THE SYMBIONT MANAGERS MAILBOX
;

	$ASSIGN_S	JBCMAILBOX,-	;ASSIGN CHANNEL TO THE JOB CONTROLLER'S
		SD_W_JBCCHAN(R11)	;MAILBOX-CHANNEL NUMBER STORED HERE
	BLBS	R0,10$			;BR IF NO ERROR
	SIGNAL	JBC$_MBASGN,#0,R0	;SIGNAL THE ERROR
	BRB	20$			;EXIT

;
; CREATE SYMBIONT'S MAILBOX
;

10$:
	$CREMBX_S 	-		;CREATE A MAIL BOX FOR COMMANDS
		PROMSK = #^X0FF0F,-	; PROTECTION
		MAXMSG = #SIM$K_SIZE,-	;MAXIMUM MESSAGE SIZE
		BUFQUO = #2*SIM$K_SIZE,-; 2 MESSAGES MAX
		CHAN   = SD_W_MBCHAN(R11) ; CHANNEL OF CREATED MAILBOX GOES HERE
	BLBS	R0,30$			;BR IF NO ERROR
	SIGNAL	JBC$_SYMBCRE,#0,R0	;SIGNAL THE ERROR
20$:	$EXIT_S				;FORCE IMAGE EXIT

;
; GET MAILBOX CHANNEL INFO
;

30$:
	MOVAL	SD_W_MBCHAN(R11),R0	;SET ADDR OF CHANNEL
	BSBW	SMB$GETCHAN		;GET CHANNEL INFO

;
; SET UNSOLICITED AST FOR MY MAILBOX
;

	BSBW	SMB$SETMBAST		;SET THE MAILBOX AST
	MOVW	SD_T_TBUF+12(R11),R0	;SET MAILBOX UNIT NUMBER FOR INIT MSG
	MOVW	R0,SD_W_MBUNIT(R11)	;SAVE UNIT FOR $DELMBX
	BSBW	SMB$INIT_DONE		;SEND MGR THE INIT DONE MESSAGE

;
; ENABLE ASTS NOW
;

	$SETAST_S	#1		;ENABLE ASTS

	BRW	SMB$MAIN		;GOTO MAIN LOOP
 
	.END	SMB_START
