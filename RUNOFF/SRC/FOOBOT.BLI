	MODULE FOOBOT (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT:
!
!	Puts as many footnotes as possible at the bottom of the current page.
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: November, 1981
!
!	As created, the body of routine FOOBOT was extracted from NEWPAG.
!	Quite a few logic changes were made, and where footnote control use
!	to be from GCA, it is now from FNCT (FootNote Control Table).
!
! MODIFIED BY:
!
!	002	REM00002	Ray Marshall	2-December-1981
!		Merge in logic changes made since Rich took NEWPAG.BLI from
!		  which this was derived.
!		Fixup require file declarations so they now all come from REQ:
!		  and not the local directory.
!--
!
! TABLE OF CONTENTS:
!
FORWARD ROUTINE
    FOOBOT : NOVALUE;
!
! INCLUDE FILES:
!

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:FNCT.REQ';

REQUIRE 'REQ:HCT.REQ';

REQUIRE 'REQ:LSTBTS.REQ';

REQUIRE 'REQ:PHDEF.REQ';

REQUIRE 'REQ:TSF.REQ';
!
! MACROS:
!
! This macro makes certain that when blank lines at the top or bottom of a page
! are skipped change bars don't get output.
MACRO
    NO_BAR_SKIP (N) =
	BEGIN
	LOCAL
	    HOLD_BARS;
	HOLD_BARS = .TSF_BARS;
	TSF_BARS = FALSE;
	USKIPL (N);
	TSF_BARS = .HOLD_BARS;
	END			%;
!
! EQUATED SYMBOLS:
!

!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    FNCT : FNCT_DEFINITION,			!Footnote control table.
    FNESIZ : FN_EXT_SIZE_DEFINITION,
    HCT : HCT_DEFINITION,
    PHAN : PHAN_DEFINITION,
    TSF : TSF_DEFINITION;

EXTERNAL LITERAL 				!Error messages
    RNFFWF;

EXTERNAL ROUTINE
    ERML,
    FOOOUT,
    TPFFIT,
    TPFSKP,
    USKIPL;


GLOBAL ROUTINE FOOBOT : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	See ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

	!See if there are any footnotes to be output.
	IF 					!
	    (.FNCT_READY NEQ 0)
	THEN
	    !See if these footnotes should be output now.
	    !Check for two cases: one in which there's a footnote that's too big to fit on the page,
	    !and the other in which there's no such error.
	    IF 					!
		(.FNESIZ [ .FNCT_OLD] GTR (.PHAN_LLINES - ((IF				!(Skip over old footnotes, consider
								.HCT_HEADERS		! the first ready footnote)
							    THEN
								3 + (IF			!(3 is title line + 2 blank lines)
									.HCT_SUBTITLE
								     THEN
									  1
								     ELSE
									  0)
							     ELSE
								 0) + .HCT_LAYOUTN)) )
										!consider only the first ready footnote.)
										!(4 is for header size)
	    THEN
		!The first ready footnote is so lengthy that it requires more space
		!than allowed per page of the document, so force it out now.
		!Also, complain to the user.
		BEGIN
		ERML (RNFFWF);
		FOOOUT (1)							!Dump one footnote, namely the bad one.
		END
	    ELSE
		!These footnotes are not too lengthy, so see if there is
		!space at the bottom of the page to output them now.
		BEGIN

		LOCAL
		    HOW_MANY_FIT;

		!Find out how many footnotes of the ready ones can fit.
		HOW_MANY_FIT = TPFFIT ();
		IF 				!
		    (.HOW_MANY_FIT NEQ 0)
		THEN
		    !There are some footnotes that will fit.  Output them.
		    BEGIN

		    LOCAL
			HOLD_PAGING;

		    HOLD_PAGING = .PHAN_PAGING;
		    PHAN_PAGING = FALSE;
		    !Position to proper location on page.
		    NO_BAR_SKIP ( TPFSKP (.HOW_MANY_FIT));
		    FOOOUT (.HOW_MANY_FIT);			!Generate the footnotes now.
		    PHAN_PAGING = .HOLD_PAGING;
		    END
		END;

    END;					!End of routine FOOBOT

END						!End of module FOOBOT

ELUDOM
