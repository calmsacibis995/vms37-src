MODULE GNAMEO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Collects letters and digits to construct a name.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: April, 1978
!
! MODIFIED BY:
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:DIGIT.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:GNCC.REQ';

REQUIRE 'REQ:KC.REQ';

REQUIRE 'REQ:LETTER.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    KHAR;


GLOBAL ROUTINE GNAME (INPUT_STRING, OUTPUT_STRING) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	GNAME processes an unbroken sequence of letters
! and digits, stopping when
!	1) a non-letter or non-digit is found, or
!	2) the end of the INPUT_STRING is reached, or
!	3) OUTPUT_STRING is about to overflow.
! All characters accepted are transferred to OUTPUT_STRING.
!
! GNAME also checks to be sure that the first character is
! indeed a letter.
!
!
! FORMAL PARAMETERS:
!
!	INPUT_STRING is where the name is to come from.
!	The collected string of letters is left in OUTPUT_STRING.
!
! IMPLICIT INPUTS:
!
!
! IMPLICIT OUTPUTS:
!
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	See GNCC.REQ for completion codes returned.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    MAP
	OUTPUT_STRING : REF FIXED_STRING;

    BIND
	IRA = INPUT_STRING : REF FIXED_STRING;

    FS_INIT (OUTPUT_STRING);

    IF 						!
	NOT LETTER (.KHAR)			!First character must be a letter.
    THEN
	RETURN GNAME_NO_NAME;

    WHILE 1 DO
	BEGIN

	IF 					!
	    ( NOT LETTER (.KHAR))		!
	    AND ( NOT DIGIT (.KHAR))		!Stop on an illegal character
	THEN
	    RETURN GNAME_NORMAL;

	IF 					!
	    .FS_LENGTH (OUTPUT_STRING) EQL .FS_MAXSIZE (OUTPUT_STRING)	!
	THEN
	    (WHILE (LETTER (.KHAR)		!
		OR DIGIT (.KHAR)) DO (KCNS ()); RETURN GNAME_TOO_LONG);	!Throw away excess characters.

	FS_WCHAR (OUTPUT_STRING, 		!Convert to upper case and output the character.
	    (IF 				!
		LOWER_LETTER (.KHAR)		!
	    THEN UPPER_CASE (.KHAR) ELSE .KHAR));
	KCNS ()					!Get next character.
	END					!end of processing loop.

    END;					!End of GNAME

END						!End of module

ELUDOM
