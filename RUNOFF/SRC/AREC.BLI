MODULE ARECOO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Performs preliminary analysis on an uneaten string.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!
! MODIFIED BY:
!
!
!	006	RWF00006	Rich Friday	5-November-1981
!		All footnote control has been moved into FNCT.  Made
!		  appropriate changes herein.
!
!	005	REM00005	Ray Marshall	8-December-1981
!		Fill in revision history from all CMS versions.
!
!	004	REM00004	Ray Marshall	14-August-1981
!		Update copyright and format revision history.
!
!	003	KAD00003	Keith Dawson	21-October-1980
!		Added REQ: logical to REQUIRE statements.  For V1.124f of DSR.
!
!	002	KAD00002	Keith Dawson	17-October-1980
!		First version stored in the CMS library.  For V1.123 of DSR.
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:ARECCC.REQ';

REQUIRE 'REQ:LETTER.REQ';

REQUIRE 'REQ:KC.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:GCA.REQ';

REQUIRE 'REQ:FLGT.REQ';

REQUIRE 'REQ:FNCT.REQ';

!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL LITERAL
    RINTES : UNSIGNED (8);

EXTERNAL
    FNCT : FNCT_DEFINITION,
    FLGT : FLAG_TABLE [FLAG_COUNT],
    GCA : VECTOR [GCA_SIZE],
    IRA : FIXED_STRING,
    KHAR;


GLOBAL ROUTINE AREC =

!++
! FUNCTIONAL DESCRIPTION:
!
!	AREC is used to determine what the current record looks like.
!	It also detects the '.!' and '.;' forms of comments, as well
!	as .BREAK commands indicated by a single '.' followed by
!	either white space or end of record.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	See ARECCC.REQ for a description of completion codes.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    !First see if there is anything at all on the record.

    IF 						!
	.KHAR EQL RINTES
    THEN 					!
    !The record is empty.
	RETURN END_OF_STRING;

    !Remember start of possible command for error analysis later.
    GCA_COM_START = CH$PLUS (.FS_NEXT (IRA), -1);
    !Check for the <ENDFOOTNOTE flag> if footnotes are being
    !collected, and the <ENDFOOTNOTE flag> is enabled.

    IF 						!
	.FNCT_COLLECTING
	AND (.KHAR EQL .FLGT [EFO_FLAG, FLAG_CHARACTER])	!
	AND (.FLGT [EFO_FLAG, FLAG_ENABLED])
    THEN 					!
    !Yup: it was the <ENDFOOTNOTE flag>.
	RETURN END_FOOTNOTE;

    !Check for the <CONTROL flag> (usually a '.')

    IF 						!
	(.KHAR EQL .FLGT [CON_FLAG, FLAG_CHARACTER])	!
	AND (.FLGT [CON_FLAG, FLAG_ENABLED])
    THEN 					!
	BEGIN					! <CONTROL flag> followed by something.
	KCNS ();					! Get next character for additional checking.
	!See if this is a special form of .BREAK.

	IF 					!
	    (.KHAR EQL RINTES)			!end of record?
	    OR (.KHAR EQL %C' ')		!space?
	    OR (.KHAR EQL %O'011')		!tab?
	THEN 					!
	!Yup: it was a special form of .BREAK.
	    RETURN CF_BREAK;

	!Two consecutive <CONTROL flag>s force a single one as a normal character.

	IF 					!
	    .KHAR EQL .FLGT [CON_FLAG, FLAG_CHARACTER]
	THEN 					!
	    RETURN CF_CF;			! Two <CONTROL flag>s.

	!See if it's a comment starting like '.!' or '.;'.

	IF 					!
	    (.FLGT [COM_FLAG, FLAG_ENABLED]
	     AND (.KHAR EQL .FLGT [COM_FLAG, FLAG_CHARACTER]))
	    OR (.KHAR EQL %C';')
	THEN 					!
	!Yup: it's a comment.
	    RETURN CF_COMMENT;

	!The last possibility is that it's a command, even though
	!that's the usual case!!

	IF 					!
	    LETTER (.KHAR)
	THEN 					!
	    RETURN CF_LETTER;

	RETURN CF_OTHER;			! Not recognized.
	End					! End analysis of <CONTROL flag>.
    ELSE 					!
	BEGIN					! Something other than <CONTROL flag>.

	IF 					!
	    (.KHAR EQL %C' ')			!space?
	    OR (.KHAR EQL %O'011')		!tab?
	THEN 					!
	!First character is a space or tab.
	    RETURN FIRST_SPACE;

	IF 					!
	    .KHAR EQL %C';'
	THEN 					!
	!First character is a ';'.
	    RETURN FIRST_SEMI;

	END;

    RETURN NOT_SPACE;				! None of the special cases tried above.
    END;					!End of AREC

END						!End of module

ELUDOM
