MODULE TPROOO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Routines for checking how full the text portion of a page is.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!
! MODIFIED BY:
!
!
!	006	REM00006	Ray Marshall	17-December-1981
!		Built up revision history based on CMS library generations.
!
!	005	RWF00005	Rich Friday	11-November-1981
!		Footnote control was moved to its own control area in its own
!		  require file (FNCT.REQ).  Appropreate changes have been made.
!		  Also, a new routine has been added:  TPFEQL determines if
!		  exactly enough space is available in the text portion of a
!		  page to fit one or more footnotes there.
!
!	004	REM00004	Ray Marshall	25-August-1981
!		Update Copyright.
!
!	003	KAD00003	Keith Dawson	21-October-1981
!		Fixup require file declarations so they now all come from REQ:
!		  and not the local directory.
!
!	002	KAD00002	Keith Dawson	17-October-1980
!		I (Ray Marshall) don't know what (if any) changes were made
!		  on or before this date.  This is the first generation in
!		  the CMS library.
!--

!
! TABLE OF CONTENTS:
!
!
FORWARD ROUTINE
    TPR,
    TPBEQL,
    TPFEQL,
    TPFFIT,
    TPFSKP,
    TPFSIZ;

! INCLUDE FILES:
!

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:FNCT.REQ';

REQUIRE 'REQ:HCT.REQ';

REQUIRE 'REQ:PHDEF.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    FNCT : FNCT_DEFINITION,
    FNESIZ : FN_EXT_SIZE_DEFINITION,
    FNISIZ : FN_INT_SIZE_DEFINITION,
    HCT : HCT_DEFINITION,
    PHAN : PHAN_DEFINITION;


GLOBAL ROUTINE TPR (COUNT) =

!++
! FUNCTIONAL DESCRIPTION:
!
!    TPR is used to see if COUNT lines are still available in
!    the ***text*** portion of a page.  The text portion does
!    ***not*** include footers, but does include footnotes.
!
! FORMAL PARAMETERS:
!
!	'COUNT' indicates how many lines are to be available.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	Returns TRUE if the specified number of lines are available,
!	otherwise returns FALSE.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    IF 						!
	.PHAN_TOP_PAGE
    THEN
	RETURN TRUE;

    IF 						!
	NOT .PHAN_PAGING
    THEN
	RETURN TRUE;

    IF 						!
	(.COUNT + .PHAN_LINES_TP + .HCT_LAYOUTN) GTR .PHAN_LLINES
    THEN
	RETURN FALSE
    ELSE
	RETURN TRUE

    END;					!End of TPR


GLOBAL ROUTINE TPBEQL (COUNT) =

!++
! FUNCTIONAL DESCRIPTION:
!
!    TPBEQL is used to see if ***EXACTLY*** COUNT lines are still available in
!    the ***text*** portion of a page.  The text portion does
!    ***not*** include footers, but does include footnotes.
!
! FORMAL PARAMETERS:
!
!	'COUNT' indicates how many lines are to be available.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	Returns TRUE if ***exactly*** the specified number of lines are available,
!	otherwise returns FALSE.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    IF 						!
	(.COUNT + .PHAN_LINES_TP + .HCT_LAYOUTN) NEQ .PHAN_LLINES
    THEN
	RETURN FALSE
    ELSE
	RETURN TRUE

    END;					!End of TPBEQL


GLOBAL ROUTINE TPFEQL =

!++
! FUNCTIONAL DESCRIPTION:
!
!    TPFEQL is used to see if ***EXACTLY*** enough space is available in the
!    ***text*** portion of the page to fit one or more footnotes there.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	Returns the number of footnotes for which exactly enough room is available.
!	Returns 0 if no footnotes will fit.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	TOTAL_FIT_SIZE;

    TOTAL_FIT_SIZE = 0;				!Don't know if any footnotes will fit yet.

    !Now, loop through the list of footnotes that are elegible to go out.
    !Quit either when you run out of footnotes to look at, or you run out
    !of footnotes that will fit.
    INCR I FROM 1 TO .FNCT_READY DO
	BEGIN
	!First check to see if there is enough room at all for this footnote.
	IF
	    .PHAN_LLINES GEQ (.TOTAL_FIT_SIZE + .FNESIZ [.I - 1 + .FNCT_OLD] + .PHAN_LINES_TP + .HCT_LAYOUTN)
	THEN
	    !Ok, we know there's a chance.  Now check to see if we're exactly at the
	    !right spot.
	    IF
		TPBEQL (.TOTAL_FIT_SIZE + .FNESIZ [.I - 1 + .FNCT_OLD])
	    THEN
		!Found the exact position where some footnotes can be output
		BEGIN
		!Ideally, we could just exit with the value of I indicating the
		!number of footnotes that will exactly fit.  However, there is a
		!strange case in which the footnote generates no text.  For example,
		!the user might have input a footnote containing only indexing commands.
		!The following adjusts for that, if that is the case.
		INCR J FROM .I TO .FNCT_READY - 1 DO
		    IF
			.FNESIZ [.J  + .FNCT_OLD] EQL 0 		!Look ahead one more footnote
		    THEN
			!Ok, the next footnote is a zero-length one.
			I = .I + 1
		    ELSE
			EXITLOOP;
		
		!Now, finally, we've included any zero-length footnotes.
		RETURN .I
		END
	    ELSE
		!Didn't fit exactly, but still fit.  Add its size to the total
		!size of footnotes that will fit so far at this spot.
		BEGIN
		TOTAL_FIT_SIZE = .TOTAL_FIT_SIZE + .FNESIZ [.I - 1 + .FNCT_OLD];
		END
	ELSE
	    !Nothing fits.  Some previous footnotes may have fitted, but this one
	    !is just a bit too large.
	    RETURN 0
	END;

    !Falling through the loop means either no footnotes at all were found,
    !or, we were not exactly at the correct position for outputting at least one footnote,
    !even though there might be footnotes ready.
    RETURN 0

    END;					!End of TPFEQL

GLOBAL ROUTINE TPFFIT =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Checks to see how many footnotes could be output in the space remaining on the page.
!	This routine doesn't check as closely as TPFEQL: it's ok for extra space to be available.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	Returns the number of footnotes for which sufficient space is available on
!	the current page.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	TOTAL_FIT_SIZE;

    TOTAL_FIT_SIZE = 0;				!Don't know if any footnotes will fit yet.

    !Loop through the list of read footnotes, and quit when you find the first one
    !that won't fit anymore.
    INCR I FROM 1 TO .FNCT_READY DO
	IF				!**NOTE:  The following logical expression is almost equivalent to
					!	   TPR (.TOTAL_FIT_SIZE + .FNESIZ [.I - 1 + .FNCT_OLD])
					!	   except that you can't use TPR when checking for space for
					!	   footnotes.  That's because when it's time to check for
					!	   footnote space, PHAN_TOP_PAGE is TRUE, and when that's the case
					!	   TPR always returns TRUE, which would ultimately result in all
					!	   ready footnotes being output, even if there's not enough space.
	    .PHAN_LLINES GEQ (.TOTAL_FIT_SIZE + .FNESIZ [.I - 1 + .FNCT_OLD] + .PHAN_LINES_TP + .HCT_LAYOUTN)
	THEN
	    TOTAL_FIT_SIZE = .TOTAL_FIT_SIZE + .FNESIZ [.I - 1 + .FNCT_OLD]	!Add size to running total.
	ELSE
	    RETURN .I - 1;			!The last footnote looked at is the last one that will fit.

    !Falling through the loop means that all the footnotes will fit.
    RETURN .FNCT_READY

    END;					!End of TPFFIT

GLOBAL ROUTINE TPFSKP (FOOTNOTE_COUNT) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Computes how many lines need to be skipped in order to be
!	precisely at the position for outputting a certain number of footnotes.
!
! FORMAL PARAMETERS:
!
!	FOOTNOTE_COUNT indicates how many of the ready footnotes are to be output.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	TOTAL_FIT_SIZE;


    TOTAL_FIT_SIZE = 0;
    !Add up sizes of the specified number of footnotes.
    INCR I FROM 1 TO .FOOTNOTE_COUNT DO
	TOTAL_FIT_SIZE = .TOTAL_FIT_SIZE + .FNESIZ [.I - 1 + .FNCT_OLD];	!(Forget old footnotes.)

    RETURN (.PHAN_LLINES - (.TOTAL_FIT_SIZE + .PHAN_LINES_TP + .HCT_LAYOUTN))

    END;					!End of TPFSKP

GLOBAL ROUTINE TPFSIZ (FOOTNOTE_COUNT) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Computes how many lines a specified number of footnotes occupies.
!
! FORMAL PARAMETERS:
!
!	FOOTNOTE_COUNT indicates how many of the ready footnotes are to be counted.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	Returns the number of lines that the footnotes will take up.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	TOTAL_FIT_SIZE;


    TOTAL_FIT_SIZE = 0;
    !Add up sizes of the specified number of footnotes.
    INCR I FROM 1 TO .FOOTNOTE_COUNT DO
	TOTAL_FIT_SIZE = .TOTAL_FIT_SIZE + .FNESIZ [.I - 1 + .FNCT_OLD];	!(Forget old footnotes.)

    RETURN .TOTAL_FIT_SIZE

    END;					!End of TPFSIZ

END						!End of module

ELUDOM
