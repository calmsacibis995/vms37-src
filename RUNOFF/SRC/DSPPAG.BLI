MODULE DSPPAO (	!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Processes the .DISPLAY APPENDIX, .DISPLAY CHAPTER, .DISPLAY NUMBER, and .DISPLAY SUBPAGE commands.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1979
!
! MODIFIED BY:
!
!--
!
! TABLE OF CONTENTS:
!

!
! INCLUDE FILES:
!
REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:CONVRT.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:HCT.REQ';

REQUIRE 'REQ:PAGEN.REQ';

REQUIRE 'REQ:PHDEF.REQ';

REQUIRE 'REQ:RUNHAN.REQ';

!
! MACROS:
!
!Although the SET_DISPLAY macro has four parameters, it always
!appears as if it's being called with just one.  That's because
!all the display names (e.g., SCT_APPEN_D) are really macros 
!defining fields, and they expand into a "comma list" containing
!four items.
MACRO
    SET_DISPLAY (a,b,c,d) =
	BEGIN
	!The display characteristics take effect on the next
	!page, at the very latest.  So that is always safe to set.
	NPAGEN [a,b,c,d] = .DISPLAY_CODE;
	!At the top of the first page this takes effect immediately, since nothing
	!has been output yet at all.  However, at the top of any other pages you 
	!have to be careful.  If the user has given a .LAYOUT command that
	!causes the page number to be centered at the bottom, the page number
	!has not yet been output even if .PHAN_TOP_PAGE is set.  In that case
	!you need to let NEWPAG finish the page and then it's ok to use the
	!display characteristics on the next page.  On the other hand,
	!if you're in the middle of the page it's ok to set the display
	!characteristics immediately because the page number hasn't gone
	!out yet, unless you're doing the standard layout.
	!Perhaps another way of describing what's going on here is simply to
	!say that you can't let the display characteristics of the page number
	!get out of synch with what's appeared or not appeared so far.
	!Note that there is a very close coordination with the workings of
	!NEWPAG implied here.
	IF					!
	    .PHAN_TOP_FIRST
	    OR
	    (	(NOT .PHAN_TOP_PAGE)
		AND (.HCT_LAYOUT NEQ LAYOUT_STANDARD)	)
	THEN
	    PAGEN [a,b,c,d] = .DISPLAY_CODE
	END
						%;

!
! EQUATED SYMBOLS:
!

!
! OWN STORAGE:
!

!
! EXTERNAL REFERENCES:
!
EXTERNAL
    HCT : HCT_DEFINITION,
    IRA : FIXED_STRING,
    PHAN : PHAN_DEFINITION,
    PAGEN : PAGE_DEFINITION,
    NPAGEN : PAGE_DEFINITION;

EXTERNAL ROUTINE
    GETDD,
    RSKIPS;

GLOBAL ROUTINE DSPPAG (HANDLER) : NOVALUE =	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT for a general description.
!
! FORMAL PARAMETERS:
!
!	HANDLER indicates which command is to be processed.
!
! IMPLICIT INPUTS:
!
!	Very close coordination with the workings of NEWPAG is implied.
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    LOCAL
	GETDD_RESULT,
	DISPLAY_CODE;

    !Skip spaces and tabs before the display descriptor.
    RSKIPS (IRA);

    !And now actually try to get the discriptor.
    GETDD_RESULT = GETDD (DISPLAY_CODE);

    !Ignore an invalid descriptor
    IF
	.GETDD_RESULT EQL -1
    THEN
	RETURN;

    !Distinguish between missing display code and one that
    !is given.
    IF
	.GETDD_RESULT EQL 0
    THEN
	!No display code supplied
	BEGIN
	!Supply the standard display as the default
	DISPLAY_CODE = (SELECTONE .HANDLER OF
			SET
			[H_DISPLAY_APPEN] : TCONVRT_LET_UPP;
			[H_DISPLAY_CHAPT] : TCONVRT_DEC_NOZ;
			[H_DISPLAY_NUMBE] : TCONVRT_DEC_NOZ;
			[H_DISPLAY_SUBPA] : TCONVRT_LET_UPP;
			TES );
	END;

    SELECTONE .HANDLER OF
	SET
	
	[H_DISPLAY_APPEN] : SET_DISPLAY (SCT_APPEN_D);
	[H_DISPLAY_CHAPT] : SET_DISPLAY (SCT_CHAPT_D);
	[H_DISPLAY_NUMBE] : SET_DISPLAY (SCT_PAGE_D);

	[H_DISPLAY_SUBPA] : SET_DISPLAY (SCT_SUBPG_D);
	TES;

    END;			!End of DSPPAG
END				!End of module
ELUDOM
