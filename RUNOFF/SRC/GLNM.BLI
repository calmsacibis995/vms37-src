MODULE GLNMOO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Get a list of numbers
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!
! MODIFIED BY:
!
!	003	RWF000003	Rich Friday	4-September-1981
!		Added support for NO numeric parameter on the .NUMBER LIST
!		  directive by not skipping characters after the last item in a
!		  parameter list when a non-null valid number is processed.
!
!	002	KAD000002	Keith Dawson	21-October-1980
!		Modify require's to point to the logical REQ:.
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:DIGIT.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:KC.REQ';

REQUIRE 'REQ:NMLST.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    IRA : FIXED_STRING,
    KHAR,
    NMLST : NUMBER_LIST;

EXTERNAL ROUTINE
    GETNUM,
    RSKIPS;


GLOBAL ROUTINE GLNM (COUNT) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Calls GETNUM to pick up "count" numbers, at most.
! Stops when a character is encountered that is not a comma,
! and GETNUM has previously returned a null value.
!
! FORMAL PARAMETERS:
!
!	'COUNT' specifies the maximum number of numbers wanted.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	GETNUM_RESULT,
	KOUNT,
	NUMBER_SIGN,
	NUMBER_VALUE,
	NUMBER_LENGTH;

    NMLST_COUNT = 0;
    !If COUNT is greater than zero, then the list can be that long,
    !at most.  If COUNT is less than or equal to zero, pick up as many
    !numbers as space is reserved for.

    IF 						!
	.COUNT GTR 0
    THEN 					!
	KOUNT = .COUNT
    ELSE
	KOUNT = NMLST_MAXSIZE;

    INCR I FROM 1 TO .KOUNT DO
	BEGIN
	GETNUM_RESULT = GETNUM (IRA, NUMBER_VALUE, NUMBER_SIGN, NUMBER_LENGTH);
	RSKIPS (IRA);				!Skip spaces and tabs trailing a number.

	IF 					!
	    .GETNUM_RESULT
	THEN
	    BEGIN				!Number was ok.

	    IF 					!
		.NUMBER_LENGTH EQL 0
	    THEN
		BEGIN

		IF 				!
		    .KHAR EQL %C','
		THEN
		    BEGIN			!It really is a null number, not the end of the list
		    NMLST_COUNT = .I;
		    NMLST_VALUE (.I) = 0;
		    NMLST_DESCR (.I) = NM_NULL;
		    KCNS ();			!Skip the ','
		    END
		ELSE
		    BEGIN			!No more numbers in the list.
		    RETURN;
		    END

		END
	    ELSE
		BEGIN				!Not a null number
		RSKIPS (IRA);			!Skip trailing spaces and tabs.

		IF 				!
		    (.KHAR EQL %C',')
		    AND (.I NEQ .KOUNT)		!Don't skip any characters after the last item in the list
		THEN
		    KCNS ();			!Skip trailing comma.

		NMLST_COUNT = .I;
		NMLST_VALUE (.I) = .NUMBER_VALUE;
		NMLST_DESCR (.I) = (IF .NUMBER_SIGN EQL 1	!
		THEN NM_PLUS ELSE IF .NUMBER_SIGN EQL -1	!
		    THEN NM_MINUS ELSE NM_UNSIGNED);
		END

	    END
	ELSE
	    BEGIN				!Number in error.
	    RSKIPS (IRA);			!Skip trailing spaces and tabs.

	    IF 					!
		(.KHAR EQL %C',')
		AND (.I NEQ .KOUNT)		!Don't skip any characters after the last item in the list
	    THEN
		KCNS ();				!Skip trailing comma.

	    NMLST_COUNT = .I;
	    NMLST_VALUE (.I) = .NUMBER_VALUE;
	    NMLST_DESCR (.I) = NM_BAD;
	    END;

	END;

    END;					!End of GLNM

END						!End of module

ELUDOM
