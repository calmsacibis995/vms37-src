MODULE SUBPAO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Processes the following commands:
!		.END SUBPAGE		.SUBPAGE
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: November, 1978
!
! MODIFIED BY:
!
!	004	KAD000004	K. A. Dawson	12 August 1981
!		For VERSION V1.126h	  
!		This bug-fix got lost by V124f. Reinstated here.
!
!	003	KAD00003	K. A. Dawson	
!		For VERSION V1.126e
!		Fix subpage-numbering problem with layout 1 or 2 where the 
!		subpage	number sometimes gets lost.
!
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:PAGEN.REQ';

REQUIRE 'REQ:PHDEF.REQ';

REQUIRE 'REQ:RUNHAN.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    NPAGEN : PAGE_DEFINITION,
    PAGEN : PAGE_DEFINITION,
    PHAN : PHAN_DEFINITION;

EXTERNAL ROUTINE
    GCPAGE,
    OUTCRG;


GLOBAL ROUTINE SUBPAG (HANDLER_CODE) : NOVALUE = 	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	See ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	Handler_code specifies which command is to be processed.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    SELECTONE .HANDLER_CODE OF
	SET

	[H_SUBPAGE] :
	    BEGIN
	    GCPAGE ();				!Generate code for a new page;
	    OUTCRG ();				!Force top of page.

	    IF 					!
		!Turn on subpaging if not already turned on.
		.NPAGEN [SCT_SUB_PAGE] EQL 0
	    THEN
		BEGIN

		NPAGEN [SCT_SUB_PAGE] = 1;	!1 is 'A' in binary.

		NPAGEN [SCT_PAGE] = .PAGEN [SCT_PAGE];
		END;

	    !If at the top of the document, start subpaging immediately
	    IF					!
		.PHAN_TOP_FIRST
	    THEN
		BEGIN
		PAGEN [SCT_SUB_PAGE] = .NPAGEN [SCT_SUB_PAGE];
		NPAGEN [SCT_SUB_PAGE] = .NPAGEN [SCT_SUB_PAGE] + 1;
		END;

	    RETURN;
	    END;

	[H_END_SUBPAGE] :
	    BEGIN
	    GCPAGE ();				!Generate code for a new page;
	    OUTCRG ();				!Force to top of new page.
	    !Do not clear subpage counter yet -- until the page number
	    ! is actually put out (version 1.124e).
!!!	    PAGEN [SCT_SUB_PAGE] = 0;		!Clear subpage counter.
	    NPAGEN [SCT_SUB_PAGE] = 0;		!...
	    NPAGEN [SCT_PAGE] = .PAGEN [SCT_PAGE] + 1;!...
	    RETURN;
	    END;
	TES;

    END;					!End of SUBPAG

END						!End of module

ELUDOM
