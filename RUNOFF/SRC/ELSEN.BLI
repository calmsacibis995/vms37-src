MODULE ELSENO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Processes .END LIST and .END NOTE commands
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: January, 1980
!
! MODIFIED BY:
!
!	006	REM00006	Ray Marshall	13-December-1981
!		Updated revision history based on CMS library generations.
!
!	005	RWF00005	Rich Friday	5-November-1981
!		Footnote control was moved to its own control area in its own
!		  require file (FNCT.REQ).  Appropreate changes have been made.
!
!	004	REM00004	Ray Marshall	25-August-1981
!		Updated copyright notice.
!
!	003	KAD00003	Keith Dawson	21-October-1980
!		Fixup require file declarations so they now all come from REQ:
!		  and not the local directory.
!
!	002	KAD00002	Keith Dawson	17-October-1980
!		I (Ray Marshall) don't know what (if any) changes were made,
!		  but this is the first entry in the CMS library.
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!
REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:CLIST.REQ';

REQUIRE 'REQ:FNCT.REQ';

REQUIRE 'REQ:GCA.REQ';

REQUIRE 'REQ:NUMPRM.REQ';

REQUIRE 'REQ:PHDEF.REQ';

REQUIRE 'REQ:RUNHAN.REQ';

REQUIRE 'REQ:SCA.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!

LITERAL
    LFSPAC = 1,					!Spacing after end of a list
    NFSPAC = 2;					!Spacing after end of a note

!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    FNCT : FNCT_DEFINITION,
    GCA : GCA_DEFINITION,
    LSTCNT : REF COUNTED_LIST,			!.LIST ELEMENT counters and controls.
    NUMPRM : NUMPRM_DEFINE,
    PHAN : PHAN_DEFINITION,
    SCA : SCA_DEFINITION;

!

EXTERNAL LITERAL 				!Error messages
    RNFIFT,
    RNFINM;

EXTERNAL ROUTINE
    ERMA,
    GCPOS,
    GCSKIP,
    STKFRM;


GLOBAL ROUTINE ELSEN (HANDLER_CODE) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	See ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE indicates which command is to be processed.
!
! IMPLICIT INPUTS:
!
!	NUMPRM contains a number, as processed by GETNUM.
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    LOCAL
	DEFAULT_SPACING,
	FINAL_SPACING;

    !Pop the .LIST and .NOTE stack.
    IF 					!
	NOT STKFRM (.HANDLER_CODE)
    THEN
	RETURN;				!Stack underflow, etc

    !Set up default .END xxxx spacing.
    SELECTONE .HANDLER_CODE OF
	SET

	[H_END_NOTE] :
	    BEGIN
	    DEFAULT_SPACING = NFSPAC*.SCA_SPACING;
	    END;

	[H_END_LIST] :
	    BEGIN
	    DEFAULT_SPACING = LFSPAC*.SCA_SPACING;
	    !Drop back one level in list counters.
	    LSTCNT [CL_INDEX] = .LSTCNT [CL_INDEX] - 1;
	    END;

	TES;

	!General coding for both .END LIST and .END NOTE.

	    !Generate final spacing after list
	    IF
		(NOT .NUM_RESULT)		!If the number is bad,
		OR (.NUM_LENGTH EQL 0)		!or no number was specified,
	    THEN
		!Use default .END xxxx spacing.
		FINAL_SPACING = .DEFAULT_SPACING
	    ELSE
		!User has specified the .END xxxx skip.
		!Validate it and then do it.
		BEGIN
		FINAL_SPACING = .NUM_VALUE*.SCA_SPACING;	!Convert to .SPACING lines.

		IF				!
		    .FINAL_SPACING LSS 0
		THEN 				!
		    !User said something like .END xxxx -5,
		    !which means to position to 5 lines from the
		    !bottom.
		    IF 				!
			.FNCT_COLLECTING	!This is TRUE if user is inside a footnote.
		    THEN 				!
			BEGIN			!.END xxxx with negative value is illegal in a footnote.
			ERMA (RNFIFT, FALSE);
			FINAL_SPACING = .DEFAULT_SPACING
			END
		    ELSE 				!

			IF 				!
			    ABS (.FINAL_SPACING) GEQ .PHAN_LLINES
			THEN 			!
		    				!User said something like .END xxxx -9999.
			    BEGIN
			    !Complain to the user
			    ERMA (RNFINM, FALSE);
			    !Use default spacing instead of erroneous spacing
			    FINAL_SPACING = .DEFAULT_SPACING
			    END

			ELSE
			    !It's a valid negative skip
			    BEGIN
			    GCPOS(.NUM_VALUE);
			    RETURN
			    END
		END;


	!If we arrive here, it's either .END xxxx with no skip specified,
	!or else the skip is a positive skip.
	!Bad skip specifications continue here too.


	!Check for .END xxxx 0
	IF
	    .FINAL_SPACING NEQ 0
	THEN
	    !SCA_SPACING is subtracted off because when ENDCHR gets
	    !called again (eventually) it will add that many blank
	    !lines.
	    GCSKIP ( MAX(1, .FINAL_SPACING - (.SCA_SPACING - 1)) );

    END;					!End of ELSEN

END						!End of module

ELUDOM
