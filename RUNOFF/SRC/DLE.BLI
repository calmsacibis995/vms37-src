MODULE DLEOOO (	!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Processes the .DISPLAY ELEMENTS command.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1979
!
! MODIFIED BY:
!
!--
!
! TABLE OF CONTENTS:
!

!
! INCLUDE FILES:
!
REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:CLIST.REQ';

REQUIRE 'REQ:CONVRT.REQ';

REQUIRE 'REQ:FSPACK.REQ';

!
! MACROS:
!

!
! EQUATED SYMBOLS:
!

!
! OWN STORAGE:
!

!
! EXTERNAL REFERENCES:
!
EXTERNAL
    LSTCHR : REF VECTOR,
    LSTCNT : REF COUNTED_LIST,
    LSTLCH : REF VECTOR,
    LSTRCH : REF VECTOR,
    LSTLDD : REF VECTOR,
    FS01 : FIXED_STRING,
    IRA : FIXED_STRING;

EXTERNAL ROUTINE
    GETDD,
    GETQC,
    GETQS,
    RSKIPS,
    SKPSEP;

GLOBAL ROUTINE DLE (HANDLER) : NOVALUE =	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT for a general description.
!
! FORMAL PARAMETERS:
!
!	HANDLER is a dummy parameter passed for conformance only.
!
! IMPLICIT INPUTS:
!
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    LOCAL
	DISPLAY_CODE,
	GETDD_RESULT,
	GETQC_RESULT;

    !Turn off bulleting (if any) so that this command takes precedence.
    LSTCHR [.LSTCNT [CL_INDEX] - 1] = 0;

    !Skip spaces and tabs before the first display descriptor.
    RSKIPS (IRA);

    !Attempt to get a character enclosed in quotes
    GETQC_RESULT = GETQC ();

    !Quit if an error occurred.
    IF
	.GETQC_RESULT EQL -2
    THEN
	RETURN;

	!If anything quoted was specified, save it
	IF
	    .GETQC_RESULT NEQ -1
	THEN
	    LSTLCH [.LSTCNT [CL_INDEX] - 1] = .GETQC_RESULT;

	!Now attempt to get a display descriptor.
	!First skip a separator
	SKPSEP (IRA);
	!Now get a descriptor and save it.
	CASE GETDD(DISPLAY_CODE) FROM -1 TO +1 OF
	    SET
	 
	    [-1]:
		!Quit if command was in error.
		RETURN;

	    [0]:
		!Do nothing if nothing supplied
		0;

	    [+1]:
		!Save descriptor
		LSTLDD [.LSTCNT [CL_INDEX] - 1] = .DISPLAY_CODE;
	    
	    TES;

    !Skip parameter separator, to try and get the next quoted character.
    SKPSEP (IRA);

    !Now try to get another character enclosed in quotes.
    GETQC_RESULT = GETQC ();

    !Quit if an error, or nothing supplied
    IF
	(.GETQC_RESULT EQL -2)
	OR (.GETQC_RESULT EQL -1)
    THEN
	RETURN;

    !User said something, so save it.
    LSTRCH [.LSTCNT [CL_INDEX] - 1] = .GETQC_RESULT;

    END;			!End of DLE
END				!End of module
ELUDOM
