MODULE DOCASO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Processes .UPPER CASE and .LOWER CASE commands, and
!	    other case translation requests.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!
! MODIFIED BY:
!
! 	, : VERSION
! 01	-
!--

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    SETCAS : NOVALUE;

!
! INCLUDE FILES:
!

REQUIRE 'REQ:GCA.REQ';

REQUIRE 'REQ:RUNHAN.REQ';

REQUIRE 'REQ:SCA.REQ';

REQUIRE 'REQ:SETCAS.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    GCA : GCA_DEFINITION,
    SCA : SCA_DEFINITION;


GLOBAL ROUTINE DOCASE (HANDLER_CODE) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Establishes the default case translation rules until they
!	are changed.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE indicates the command that is to be processed.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	GCA_CASE is set to remember the case set by the most
!	recent .UPPER CASE or .LOWER CASE command.
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    IF 						!
	.HANDLER_CODE EQL H_UPPER_CASE
    THEN 					!
	BEGIN
	GCA_CASE = LEAVE_CASE;
	SETCAS (LEAVE_CASE);
	END
    ELSE 					!
	BEGIN
	GCA_CASE = FORCE_LOWER;
	SETCAS (FORCE_LOWER);
	END;

    END;					!End of DOCASE


GLOBAL ROUTINE SETCAS (TRANSLATION) : NOVALUE = 	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine sets up case rules.
!
! FORMAL PARAMETERS:
!
!	TRANSLATION can have one of the values defined in SETCAS.REQ.
!
! IMPLICIT INPUTS:
!
!	SCA_WORD_SET:
!	If being called as a result of a command, then SCA_WORD_SET
!	will be FALSE, so case translation will be established
!	for everything.  If not being called as a result of a command,
!	case rules for the current word take precedence over the
!	default case translation.  For example, assuming the normal
!	flag character definitions, and the capitalize flag enabled,
!	the sequence ^^<lower generates 'LOWER', as should <^^lower.
!	The latter situation is what the SCA_WORD_SET test catches.
!	In any case, the requested case translation takes effect as
!	soon as a new word is started, or SCA_WORD_SET becomes false.
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    CASE .TRANSLATION FROM SETCASE_LOW TO SETCASE_HIGH OF
	SET

	[FORCE_UPPER] :
	    !Force all letters to upper case.
	    BEGIN
	    IF 					!
		NOT .SCA_WORD_SET
	    THEN 				!
		!Case rules take effect immediately.
		BEGIN
		SCA_WRD_FC_LT = %C'A' - %C'a';
		SCA_WRD_FC_UT = 0;
		SCA_WRD_OC_LT = %C'A' - %C'a';
		SCA_WRD_OC_UT = 0;
		END;

	    SCA_FCBE_LT = %C'A' - %C'a';
	    SCA_FCBE_UT = 0;
	    SCA_OCBE_LT = %C'A' - %C'a';
	    SCA_OCBE_UT = 0;
	    SCA_FC_LT = %C'A' - %C'a';
	    SCA_FC_UT = 0;
	    SCA_OC_LT = %C'A' - %C'a';
	    SCA_OC_UT = 0;
	    END;

	[FORCE_LOWER] :
	    !Force all letters to lower case.
	    BEGIN

	    IF 					!
		NOT .SCA_WORD_SET
	    THEN 				!
		!Case translation takes effect immediately.
		BEGIN
		SCA_WRD_FC_LT = 0;
		SCA_WRD_OC_LT = 0;
		SCA_WRD_FC_UT = %C'a' - %C'A';
		SCA_WRD_OC_UT = %C'a' - %C'A';
		END;

	    SCA_FC_LT = 0;
	    SCA_OC_LT = 0;
	    SCA_FC_UT = %C'a' - %C'A';
	    SCA_OC_UT = %C'a' - %C'A';
	    SCA_FCBE_LT = 0;
	    SCA_FCBE_UT = %C'a' - %C'A';
	    SCA_OCBE_LT = 0;
	    SCA_OCBE_UT = %C'a' - %C'A';
	    END;

	[LEAVE_CASE] :
	    !Don't change case at all.
	    BEGIN

	    IF 					!
		NOT .SCA_WORD_SET
	    THEN 				!
		!Case rules take effect immediately.
		BEGIN
		SCA_WRD_FC_LT = 0;
		SCA_WRD_FC_UT = 0;
		SCA_WRD_OC_LT = 0;
		SCA_WRD_OC_UT = 0;
		END;

	    SCA_FCBE_LT = 0;
	    SCA_FCBE_UT = 0;
	    SCA_OCBE_LT = 0;
	    SCA_OCBE_UT = 0;
	    SCA_FC_LT = 0;
	    SCA_FC_UT = 0;
	    SCA_OC_LT = 0;
	    SCA_OC_UT = 0;
	    END;

	[FIRST_CAPS] :
	    !Capitalize words.
	    BEGIN
	    LOCAL
		OTHER_CASE;

	    IF!
		.GCA_CASE EQL FORCE_LOWER
	    THEN
		!For files all in upper case, force remaining
		!characters to lower case.
		OTHER_CASE = %C'a' - %C'A'
	    ELSE
		!For files from terminals having both upper and
		!lower case, leave remaining characters alone.
		OTHER_CASE = 0;
!
	    IF 					!
		NOT .SCA_WORD_SET
	    THEN 				!
		!Case rules take effect immediately.
		BEGIN
		SCA_WRD_FC_LT = %C'A' - %C'a';
		SCA_WRD_FC_UT = 0;
		SCA_WRD_OC_LT = 0;
		SCA_WRD_OC_UT = .OTHER_CASE;
		END;

	    SCA_FCBE_LT = %C'A' - %C'a';
	    SCA_FCBE_UT = 0;
	    SCA_OCBE_LT = 0;
	    SCA_OCBE_UT = .OTHER_CASE;
	    SCA_FC_LT = %C'A' - %C'a';
	    SCA_FC_UT = 0;
	    SCA_OC_LT = 0;
	    SCA_OC_UT = .OTHER_CASE;
	    END;

	[ONE_CAP] :
	    !Capitalize first word, all other letters to lower case.
	    BEGIN
	    !Case translation takes effect immediately.
	    SCA_WRD_FC_LT = %C'A' - %C'a';
	    SCA_WRD_OC_LT = 0;
	    SCA_WRD_FC_UT = 0;
	    SCA_WRD_OC_UT = %C'a' - %C'A';

	    SCA_FC_LT = 0;
	    SCA_OC_LT = 0;
	    SCA_FC_UT = %C'a' - %C'A';
	    SCA_OC_UT = %C'a' - %C'A';
	    SCA_FCBE_LT = 0;
	    SCA_FCBE_UT = %C'a' - %C'A';
	    SCA_OCBE_LT = 0;
	    SCA_OCBE_UT = %C'a' - %C'A';
	    END;
	TES;

    END;					!End of SETCAS

END						!End of module

ELUDOM
