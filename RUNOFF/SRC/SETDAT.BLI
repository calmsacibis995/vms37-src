MODULE SETDAO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Sets the date to a user-specified date.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: April, 1979
!
! MODIFIED BY:
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!
REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:NMLST.REQ';

!
! MACROS:
!
MACRO
    DOPARM (P,MAX) =
	!Process this parameter only if it was specified.
	IF					
	    .NMLST_DESCR (P) NEQ NM_NULL
	THEN
	    BEGIN
	    IF					!
		.NMLST_DESCR (P) EQL NM_PLUS
		OR .NMLST_DESCR (P) EQL NM_MINUS
	    THEN
		TEMP_DATIM [3-P] = .TEMP_DATIM [3-P] + .NMLST_VALUE (P)
	    ELSE
		TEMP_DATIM [3-P] = .NMLST_VALUE (P);

	    !Check for negative or GTR MAX value
	    IF					!
		.TEMP_DATIM [3-P] LEQ 0	!
		OR .TEMP_DATIM [3-P] GTR MAX	!
	    THEN
		!Illegal number value.
		BEGIN
		ERMA (RNFINM, FALSE);
		RETURN;
		END
	    END			%;
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    DATTIM : VECTOR [6],			!Date and time.
    NMLST : NUMBER_LIST;			!GLNM returns command parameters here

!
EXTERNAL LITERAL				!Error messages
    RNFINM;

!

EXTERNAL ROUTINE
    ERMA,
    GLNM,
    RNODAT;

!


GLOBAL ROUTINE SETDAT (HANDLER_CODE) : NOVALUE = 	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	SETDAT calls GLNM to pick up the day, month, and year, in that order.
!	If the user says nothing, then the date is set to the system date.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE is not examined.  It is passed to this routine
!	for reasons of conformity only.  It will always have the
!	value H_SET_DATE.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	TEMP_DATIM : VECTOR [6];

    GLNM (3);					!Request the day, month, and year.

    IF 						!
	.NMLST_COUNT EQL 0
    THEN
	BEGIN
	!Get entire system date/time.
	RNODAT (TEMP_DATIM);
	!Save just the system time part.
	DATTIM [0] = .TEMP_DATIM [0];		!Year
	DATTIM [1] = .TEMP_DATIM [1];		!Month
	DATTIM [2] = .TEMP_DATIM [2];		!Day
	RETURN;
	END;

    !Assume the most complicated case: that the user is
    !adjusting settings by adding/subtracting values.
    !Make a copy of the current time.
    TEMP_DATIM [0] = .DATTIM [0];		!Year
    TEMP_DATIM [1] = .DATTIM [1];		!Month
    TEMP_DATIM [2] = .DATTIM [2];		!Day

    !Validate the day, if given
    DOPARM (1,31);				!First parameter can have a maximum value of 31

    !Validate the month, if given
    IF						!
	.NMLST_COUNT GEQ 2
    THEN
	DOPARM (2,12);				!Second paramater can have a maximum value of 12

    !Validate the year, if given
    IF
	.NMLST_COUNT GEQ 3
    THEN
	DOPARM (3,9999);				!Maximum of 9999 

    !Make computed time currend
    DATTIM [0] = .TEMP_DATIM [0];		!Year
    DATTIM [1] = .TEMP_DATIM [1];		!Month
    DATTIM [2] = .TEMP_DATIM [2];		!Day

    END;					!End of SETDAT

END						!End of module

ELUDOM
