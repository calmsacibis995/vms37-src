MODULE LAYOUO (	!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Processes the .LAYOUT command
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: April, 1979
!
! MODIFIED BY:
!
!--
!
! TABLE OF CONTENTS:
!

!
! INCLUDE FILES:
!
REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:HCT.REQ';

REQUIRE 'REQ:NUMPRM.REQ';

REQUIRE 'REQ:PHDEF.REQ';

!
! MACROS:
!

!
! EQUATED SYMBOLS:
!

!
! OWN STORAGE:
!

!
! EXTERNAL REFERENCES:
!
EXTERNAL
    HCT : HCT_DEFINITION,
    IRA : FIXED_STRING,
    NUMPRM : NUMPRM_DEFINE,
    PHAN : PHAN_DEFINITION;

EXTERNAL LITERAL			!Error messages
    RNFINM;

EXTERNAL ROUTINE
	ERMA,
	GETNUM,
	SKPSEP;

GLOBAL ROUTINE LAYOUT (HANDLER_CODE) : NOVALUE = 	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT, above
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE is a dummy, used only for conformance with other routines.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    !If user said .LAYOUT without any number, set up for standard layout.
    !Ditto if user said .LAYOUT 0
    !Ditto if he input some crocky number.
    IF				!
	.NUM_LENGTH EQL 0	!Nothing specified
	OR (.NUM_VALUE EQL 0)	!.LAYOUT 0
    THEN
	BEGIN
	!The standard layout starts with the next page, at the latest.
	HCT_LAYOUT_NP = LAYOUT_STANDARD;
	HCT_LAYOUTN_NP = 0;
	!Change layout immediately if at top of first page.
	IF
	    .PHAN_TOP_FIRST
	THEN
	    BEGIN
	    HCT_LAYOUT = LAYOUT_STANDARD;
	    HCT_LAYOUTN = 0;
	    END;
	RETURN;
	END;

    !See if user said .LAYOUT 1,n or .LAYOUT 2,n or .LAYOUT 3,n
    IF				!
	.NUM_VALUE GTR 3	!
	OR (.NUM_VALUE LSS 0)	!
    THEN
	!Illegal layout specified.
	BEGIN
	ERMA (RNFINM, FALSE);
	RETURN;
	END;

    !User did say .LAYOUT 1 ......., so process it.
    HCT_LAYOUT_NP = .NUM_VALUE;		!Page layout encoding number is given directly by the user.
    !Now, skip either a comma and/or spaces to get the second parameter.
    SKPSEP (IRA);		!
    !Now, attempt to get the second parameter, if supplied.
    NUM_RESULT = GETNUM (IRA, NUM_VALUE, NUM_SIGN, NUM_LENGTH);
    !Do some preliminary validation
    IF				!
	NOT .NUM_RESULT
    THEN
	!Erroneous number
	BEGIN
	HCT_LAYOUTN_NP = 1;
	RETURN;
	END;

    IF				!
	.NUM_VALUE LEQ 0
    THEN			!
	! Zero or a negative number not allowed
	BEGIN
	ERMA (RNFINM, FALSE);
	HCT_LAYOUTN_NP = 1;
	RETURN;
	END
    ELSE
	HCT_LAYOUTN_NP = .NUM_VALUE;

    !If at the top of the first page,
    !the new layout takes effect immediately.
    IF
	.PHAN_TOP_FIRST
    THEN
	BEGIN
	HCT_LAYOUTN = .HCT_LAYOUTN_NP;
	HCT_LAYOUT = .HCT_LAYOUT_NP;
	END;


    END;			!End of LAYOUT
END				!End of module
ELUDOM
