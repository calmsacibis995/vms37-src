MODULE NOTEOO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Processes the .NOTE command
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: June, 1978
!
! MODIFIED BY:
!
!	004	REM00004	Ray Marshall	May 1981.
!		For V1.126e.  Call to GETLIN changed:  added one parameter
!		indicating whether or not to output the text of the command.
!
!	003	KAD00003	K. A. Dawson	February 1981.
!		For V1.126d.  Made NOTE turn on fill and justify, unless user
!		has said .NAJ.  In any event, NOTE turns on fill.  This is an
!		incompatible change.
!
!--
!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:GCA.REQ';

REQUIRE 'REQ:PDT.REQ';

REQUIRE 'REQ:RUNHAN.REQ';

REQUIRE 'REQ:SCA.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!

LITERAL
    NASPAC = 1,					!Spacing after "NOTE"
    NHSPAC = 2;					!Spacing before .NOTE heading

!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    GCA : GCA_DEFINITION,
    PDT : REF PDT_DEFINITION,
    SCA : SCA_DEFINITION;

!

EXTERNAL ROUTINE
    CENTXT,
    GCSKIP,
    GETLIN,
    GTPC,
    STKFRM;


GLOBAL ROUTINE NOTE (HANDLER_CODE) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE indicates which command is to be processed.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

	    LOCAL
		SKIP;

	    IF 					!
		NOT STKFRM (.HANDLER_CODE)
	    THEN
		RETURN;				!Quit if stack overflow

!
	    SKIP = NHSPAC*.SCA_SPACING;
	    GTPC (.SKIP + (NASPAC + .PDT_TP + 1)*.SCA_SPACING);
	    GCSKIP (MAX (0, .SKIP - (.SCA_SPACING - 1)));
	    !Set up formatting definitions
	    SCA_FILL = TRUE;
	    SCA_JUSTIFY = .GCA_AUTOJUST OR .SCA_JUSTIFY;	!Turn on justification unless user has said .NAJ.
	    SCA_CROCK = .SCA_JUSTIFY;
	    BEGIN				!Margin adjustment

	    LOCAL
		MARGIN_ADJUST;

	    IF 					!
		.SCA_LM EQL 0
	    THEN
		MARGIN_ADJUST = .GCA_NOTE_PRIM
	    ELSE
		MARGIN_ADJUST = .GCA_NOTE_ALT;

	    !Defensive margin computation in case margins are very restrictive

	    IF 					!
		(.SCA_LM + .MARGIN_ADJUST) LSS .SCA_RM
	    THEN
		SCA_LM = .SCA_LM + .MARGIN_ADJUST;

	    IF 					!
		(.SCA_RM - .MARGIN_ADJUST) GTR .SCA_LM
	    THEN
		SCA_RM = .SCA_RM - .MARGIN_ADJUST;

	    END;				!End of margin adjustment computations
	    !
	    !Determine if text was given on the .NOTE command

	    IF 					!
		NOT CENTXT (CH$PTR (UPLIT ('NOTE')), 4)
	    THEN
	    !Text after ".NOTE".  Fetch and output text centered.
		GETLIN (TRUE, FALSE, 0, TRUE);

	    !Prepare for the text that will probably come.
	    GCSKIP (.SCA_SPACING);
    END;					!End of NOTE

END						!End of module

ELUDOM
