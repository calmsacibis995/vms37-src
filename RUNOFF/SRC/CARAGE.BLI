MODULE CARAGO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Processes the .PAGE, .SKIP, .BLANK, .TEST PAGE,
!	    .BREAK, and .SPACING commands.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!
! MODIFIED BY:
!
!	003	RWF00003	Rich Friday	6-November-1981
!		All footnote control has been moved into FNCT.  Made
!		  appropriate changes herein.
!		Dependence on GCA has been removed.  Therefore, it is
!		  no longer REQUIREd in.
!
!	002	KAD00001	K. A. Dawson	30-DECEMBER-1980
!		Added count of pending footnotes, if any, to the test-page code
!		that is generated.  This was done for V1.124i of DSR.
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:FNCT.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:PHDEF.REQ';

REQUIRE 'REQ:NUMPRM.REQ';

REQUIRE 'REQ:RUNHAN.REQ';

REQUIRE 'REQ:SCA.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    FNCT : FNCT_DEFINITION,
    IRA : FIXED_STRING,
    MRA : REF FIXED_STRING,
    NUMPRM : NUMPRM_DEFINE,
    PHAN : PHAN_DEFINITION,
    SCA : SCA_DEFINITION;

EXTERNAL LITERAL 				! Error messages
    RNFIFT,
    RNFINM,
    RNFMNL;

EXTERNAL ROUTINE
    ERMA,
    GCPAGE,
    GCPOS,
    GCSKIP,
    GTPC,
    LOUT,
    TPFFIT,
    TPFSIZ;


GLOBAL ROUTINE CARAGE (HANDLER_CODE) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE indicates which command is to be processed.
!
! IMPLICIT INPUTS:
!
!	NUMPRM is expected to contain a number, as processed by GETNUM.
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    !Some commands take a number.

    IF 						!
	(.HANDLER_CODE NEQ H_BREAK)		!
	AND (.HANDLER_CODE NEQ H_PAGE)
    THEN 					!

	IF 					!
	    NOT .NUM_RESULT
	THEN
	!Ignore command if number was bad.
	    RETURN;

    SELECTONE .HANDLER_CODE OF
	SET

	[H_BLANK, H_SKIP] :
	    BEGIN

	    IF 					!
		.NUM_LENGTH EQL 0
	    THEN 				!
		NUM_VALUE = 1;			!Default if no parameter.

	    IF 					!
		.HANDLER_CODE EQL H_SKIP
	    THEN 				!
	    !Commands .SKIP and .BLANK mean the same
	    !thing, except that .SKIP means ".SPACING"
	    !blank lines, whereas .BLANK means exactly
	    !as many blank lines as specified.
		NUM_VALUE = .NUM_VALUE*.SCA_SPACING;

	    IF 					!
		.NUM_VALUE LSS 0
	    THEN 				!

		IF 				!
		    .FNCT_COLLECTING
		THEN 				!
		    BEGIN			!A .SKIP with negative value is illegal in a footnote.
		    ERMA (RNFIFT, FALSE);
		    RETURN;
		    END
		ELSE 				!
		    BEGIN

		    IF 				!
			ABS (.NUM_VALUE) GEQ .PHAN_LLINES
		    THEN 			!
		    				!User said something like .SKIP -9999.
			BEGIN
			ERMA (RNFINM, FALSE);
			RETURN;
			END;			!It's a valid .SKIP/.BLANK with  a

		    				!negative count.
		    GCPOS (.NUM_VALUE);
		    RETURN;
		    END;

	    !Generate code for the conditional skip.
	    GCSKIP (.NUM_VALUE);
	    END;

	[H_BREAK] :
	    BEGIN
	    !Everything associated with .BREAK is contained in
	    !the tables, that are interpreted by DOCM.
	    0
	    END;

	[H_PAGE] :
	    BEGIN
	    !Generate code for the conditional page.
	    GCPAGE ();
	    END;

	[H_SPACING] :
	    BEGIN

	    IF 					!
		.NUM_LENGTH EQL 0
	    THEN 				!
	    !No number provided.
		BEGIN
		ERMA (RNFMNL, FALSE);
		RETURN;
		END;

	    IF 					!
		(.NUM_VALUE LEQ 0)		!
		OR (.NUM_VALUE GTR 5)
	    THEN 				!
	    !Number not between 0 and 5.
		BEGIN
		ERMA (RNFINM, FALSE);
		RETURN;
		END;

	    SCA_SPACING = .NUM_VALUE;
	    END;

	[H_TEST_PAGE] :
	    BEGIN

	    IF 					!
		.NUM_LENGTH EQL 0
	    THEN 				!
	    !No number provided.
		BEGIN
		ERMA (RNFMNL, FALSE);
		RETURN;
		END;

	    IF 					!
		.NUM_VALUE LEQ 0
	    THEN 				!
	    !Negative number not allowed.
		BEGIN
		ERMA (RNFINM, FALSE);
		RETURN;
		END;

	    !Add count of pending footnote lines, if any, to the given test-page value.
	    GTPC (.NUM_VALUE + TPFSIZ (TPFFIT()));!Generate code for a test page.
	    END;
	TES;

    END;					!End of CARAGE

END						!End of module

ELUDOM
