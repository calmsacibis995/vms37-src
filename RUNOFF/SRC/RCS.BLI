MODULE RCSOOO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Recognizes a string of keywords that identifies a command.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!
! MODIFIED BY:
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:GSLUCC.REQ';

REQUIRE 'REQ:KWITEM.REQ';

!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
    FIND,
    GSLU,
    RSKIPS;

EXTERNAL
    FS01 : FIXED_STRING,			!Common work area for misc. tasks.
    IRA : FIXED_STRING,
    LP1 : VECTOR;


GLOBAL ROUTINE RCS =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Beginning with LP1, RCS collects keywords in an attempt
!	to recognize a command.  If a command is recognized, the
!	item descriptor is
!	returned.  If the command is not recognized, zero is returned.
!	There is no capability for "partially" recognizing a command.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	Either the item descriptor associated with the command,
!	or zero if the command is not recognized.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	X : REF VECTOR;

    LOCAL
	RESULT;

    X = LP1;

    WHILE 1 DO
	BEGIN
	FS_INIT (FS01);
	RESULT = GSLU (IRA, FS01);

	IF 					!
	    .RESULT EQL GSLU_NONE
	THEN
	    BEGIN
	    FS_WCHAR (FS01, %C'@');
	    END;

	RESULT = FIND (.FS_START (FS01), 	!
	    .FS_LENGTH (FS01), 			!
	    .X);

	IF 					!
	    .RESULT EQL 0
	THEN
	    BEGIN				!
	    !This coding makes it possible for the user
	    !to omit keywords, when there is no ambiguity.
	    IF!
		(CH$RCHAR(.FS_START(FS01)) EQL %C'@')
		 AND (.X [-1] EQL 1)
	    THEN
		RESULT = .X [0]			!Trailing keyword is optional.
	    ELSE
		RETURN 0;			!In this case, it is ambiguous

!!!	    IF 					!
!!!		CH$NEQ (1, 			!
!!!		    .FS_START (FS01), 		!
!!!		    1, 				!
!!!		    CH$PTR (%C'@'))		!
!!!		AND .X [-1] NEQ 1
!!!	    THEN
!!!		RETURN 0			!In this case, it is ambiguous.
!!!	    ELSE
!!!		RESULT = .X [0];		!Trailing keyword is optional.

	    END;

	BEGIN

	BIND
	    ITEM = RESULT : REF VECTOR;

	IF 					!
	    .CHAIN_CONTINUES (ITEM)
	THEN
	    BEGIN
	    RSKIPS (IRA);
	    X = .NEXT_LIST (ITEM);
	    END
	ELSE
	    RETURN .ITEM

	END
	END

    END;					!End of RCS

END						!End of module

ELUDOM
