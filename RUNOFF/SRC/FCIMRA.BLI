MODULE FCIMRO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Special handling for first character written into the MRA
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE:	June, 1978
!
! MODIFIED BY:
!
!	006	REM00006	Ray Marshall	13-December-1981
!		Built up revision history based on CMS library generations.
!
!	005	RWF00005	Rich Friday	20-November-1981
!		Fixed change bar bug where a change bar would be put on the
!		  blank line preceeding the line in which only a portion of
!		  it is flagged for a change bar.  To put it another way: now,
!		  if only a portion of a given line, not including the first
!		  word, is inclosed between .BB/.EB directives, only that line
!		  will have the change bar on it.  Previously, if the
!		  preceeding output line was blank, it would have also had a
!		  change bar on it -- now it wont.  Is that clear?  /  Ray
!
!	004	REM00004	Ray Marshall	25-August-1981
!		Updated copyright notice.
!
!	003	KAD00003	Keith Dawson	21-October-1980
!		Fixup require file declarations so they now all come from REQ:
!		  and not the local directory.
!
!	002	KAD00002	Keith Dawson	17-October-1980
!		I (Ray Marshall) don't know what (if any) changes were made
!		  on or before this date.  This is the first generation in
!		  the CMS library.
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:FSPACK.REQ';

!!REQUIRE 'GCA.REQ';

!!REQUIRE 'REQ:LSTBTS.REQ';

REQUIRE 'REQ:PDT.REQ';

REQUIRE 'REQ:SCA.REQ';

REQUIRE 'REQ:TSF.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:

EXTERNAL
!!    GCA : GCA_DEFINITION,
    MRA : REF FIXED_STRING,
    PDT : REF PDT_DEFINITION,
    SCA : SCA_DEFINITION,
    TSF : TSF_DEFINITION;

!

EXTERNAL ROUTINE
    GCPOS,
    GCSKIP,
    GTPC,
    NEGIND,
!!    OUTCRG,
    XMARG;


GLOBAL ROUTINE FCIMRA : NOVALUE = 		!

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine is called prior to putting the first text character
!	into the MRA.  It generates sufficient spaces to take care of
!	the left margin, takes care of paragraph indentation, and also
!	line spacing.
!	Prior to the call on FCIMRA, commands such as .SKIP, .TEST PAGE,
!	.LEFT MARGIN, .INDENT, etc have saved certain carriage control
!	information.  FCIMRA finishes processing this information
!	before the first actual text character gets put into the MRA.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	LEFT_MARGIN;				!Temporary computation of left margin.

!!!*****The following commented out code was necessary only when there existed just a single
!!!*****bit (TSF_BARS) for change bars. In that case it wasn't possible to cleanly say whether
!!!*****or not the change bar applies to blank lines, or the text, or both.  When TSF_H_BARS
!!!*****was invented, this commented out kludge code became unnecessary, as LSTOPS could sort it
!!!*****out cleanly and consistantly.
!!!    !Prevent change bars from being generated for blank lines
!!!    !generated by "un-bared" commands.
!!!
!!!    IF 						!
!!!	.TSF_INT_VL NEQ 0
!!!    THEN 					!
!!!
!!!	IF 					!
!!!	    .GCA_BARS XOR .TSF_BARS
!!!	THEN 					!
!!!	    OUTCRG ();

    LEFT_MARGIN = 0;

    IF 						!
	.SCA_PARA_PND
    THEN 					!
	BEGIN					!Start a paragraph.

	LOCAL
	    SKIP,
	    TEST_PAGE;

!

	IF 					!
	    .PDT_SKIP LSS 0
	THEN 					!
	    SKIP = (.SCA_SPACING + 1)/2
	ELSE 					!
	    SKIP = .PDT_SKIP*.SCA_SPACING;

	TEST_PAGE = (.PDT_TP + 1)*.SCA_SPACING + .SKIP;

	IF 					!
	    .TEST_PAGE NEQ 0
	THEN 					!
	    BEGIN				!Code for a test page command
	    GTPC (.TEST_PAGE);
	    END;

	IF 					!
	    .PDT_SKIP NEQ 0
	THEN 					!
	    BEGIN				!Code for skipping lines.

	    IF 					!
		.PDT_SKIP LSS 0
	    THEN 				!
		GCPOS (.PDT_SKIP*.SCA_SPACING)
	    ELSE 				!
		GCSKIP (.PDT_SKIP*.SCA_SPACING);

	    END;

	END
    ELSE 					!
	BEGIN					!Not a paragraph: just a new line.

	IF 					!
	    .SCA_SPACING GTR 1
	THEN 					!
	    BEGIN				!Code for skipping between lines.
	    GCSKIP (.SCA_SPACING - 1);
	    END;

	END;

    LEFT_MARGIN = .SCA_LM + .SCA_INDENT;

    IF 						!
	.LEFT_MARGIN LSS 0
    THEN 					!
	BEGIN
	NEGIND ();
	LEFT_MARGIN = 0;
	END;

    IF 						!
	.LEFT_MARGIN GEQ .SCA_RM
    THEN 					!
	BEGIN
	XMARG ();
	LEFT_MARGIN = 0;
	END;

    INCR I FROM 1 TO .LEFT_MARGIN DO
	FS_WCHAR (MRA, %C' ');

    TSF_INT_HL = .LEFT_MARGIN;
    TSF_EXT_HL = .LEFT_MARGIN;
!
    SCA_INDENT = 0;
    SCA_PARA_PND = FALSE;			!No paragraph pending.
    SCA_WRD_PNTR = .FS_NEXT (MRA);
    END;					!End of FCIMRA

END						!End of module

ELUDOM
