MODULE COOVBB (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: General purpose routine
!
! ABSTRACT: Convert a binary number into a vector of characters and
!	    return the result and character count.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1979
!
! MODIFIED BY:
!
!--

!


GLOBAL ROUTINE CONVBB (BINARY_NUMBER, KHARACTERS, KHARACTER_COUNT, BASE) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Converts 'binary_number' to a vector of characters,
!	returning them in 'kharacters'; kharacter_count is the
!	number of digits converted.
!	The absolute value of 'binary_number' is converted,
!	so that the user is responsible for handling negative numbers.
!	The number will be converted according to the value of BASE.
!
! FORMAL PARAMETERS:
!
!	See FUNCTIONAL DESCRIPTION
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    OWN
	DIGITS : INITIAL (CH$PTR(UPLIT('0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ')));

    MAP
	KHARACTERS : REF VECTOR;

    LOCAL
	LEFT_TO_CONVERT;

    .KHARACTER_COUNT = 0;
    LEFT_TO_CONVERT = ABS (.BINARY_NUMBER);

    DO
	BEGIN
	KHARACTERS [..KHARACTER_COUNT] = CH$RCHAR( CH$PLUS(.DIGITS, (.LEFT_TO_CONVERT MOD .BASE)));
	LEFT_TO_CONVERT = .LEFT_TO_CONVERT/.BASE;
	.KHARACTER_COUNT = ..KHARACTER_COUNT + 1;
	END
    UNTIL .LEFT_TO_CONVERT EQL 0;

    RETURN;
    END;					!End of CONVBB

END						!End of module

ELUDOM
