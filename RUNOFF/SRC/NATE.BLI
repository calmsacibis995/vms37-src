MODULE NATEOO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Normalize an action table entry
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: April, 1978
!
! MODIFIED BY:
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!
!
! MACROS:
!
!
! EQUATED SYMBOLS:
!

REQUIRE 'REQ:ATCODE.REQ';

EXTERNAL LITERAL
    RINTES : UNSIGNED (8);

LITERAL
    TAB_CHARACTER = %O'11';

!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    ATABLE : VECTOR [128];			!The action table


GLOBAL ROUTINE NATE (KHARACTER) : NOVALUE = 	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	NATE's responsibility is to ensure that an ATABLE entry
!	for a particular character corresponds to what that character
!	usually represents.
!
! FORMAL PARAMETERS:
!
! KHARACTER indicates which action table entry should be reset.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    !Note that the order of statements in the SELECTONE statement is
    !important.  This is because some classifications (e.g. A_PUNCT)
    !must be made before others (e.g., A_OTHER), since they share
    !characters in common.
    ATABLE [.KHARACTER] = (SELECTONE .KHARACTER OF
	SET
	[RINTES] :				 A_INT_ESC;
	[TAB_CHARACTER] :			 A_TAB;
	[0 TO %O'37'] :				 A_CONTROL;
	[%O'40'] :				 A_SPACE;
	[%C'!', %C'.', %C'?', %C';', %C':'] :	 A_PUNCT;
	[%O'41' TO %O'57'] :			 A_OTHER;
	[%C'0' TO %C'9'] :			 A_DIGIT;
	[%O'72' TO %O'100'] :			 A_OTHER;
	[%C'A' TO %C'Z'] :			 A_U_LETTER;
	[%O'133' TO %O'140'] :			 A_OTHER;
	[%C'a' TO %C'z'] :			 A_L_LETTER;
	[%O'173' TO %O'176'] :	  		 A_OTHER;
	[%O'177'] :				 A_CONTROL;
	TES);
    RETURN;
    END;					!End of NATE

END						!End of module

ELUDOM
