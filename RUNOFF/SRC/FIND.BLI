MODULE FINDOO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Uses a binary search to look up a keyword.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!
! MODIFIED BY:
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:TPROB.REQ';				!Completion codes returned by TPROBE

!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
    TPROBE;


GLOBAL ROUTINE FIND (KEY, SIZE, LIST) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Searches LIST in an attempt to recognize KEY.
!	A binary search algorithm is used.
!
! FORMAL PARAMETERS:
!
!	KEY is the word that is to be identified; SIZE is its length.
!	LIST is a list of keywords, as defined in the module LP1.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	If KEY is recognized, the address of the corresponding
!	entry in LIST is returned.  Otherwise, zero is returned.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    MAP
	LIST : REF VECTOR;

    LOCAL
	RESULT,
	TOP,
	BOTTOM,
	HALF_OF_RANGE,
	MIDPOINT;

    TOP = 0;
    BOTTOM = .LIST [-1] - 1;

    WHILE 1 DO
	BEGIN
	HALF_OF_RANGE = (.BOTTOM - .TOP + 1)/2;
	MIDPOINT = .TOP + .HALF_OF_RANGE;
	RESULT = TPROBE (.KEY, .SIZE, .LIST [.MIDPOINT]);

	CASE .RESULT FROM 0 TO 3 OF
	    SET

	    [FOUND] :
		RETURN .LIST [.MIDPOINT];

	    [GIVE_UP] :
		RETURN 0;

	    [TRY_ABOVE] :
		BEGIN

		IF 				!
		    .MIDPOINT EQL .TOP
		THEN 				!
		    RETURN 0
		ELSE 				!
		    BOTTOM = .MIDPOINT - 1;

		END;

	    [TRY_BELOW] :
		BEGIN

		IF 				!
		    .MIDPOINT EQL .BOTTOM
		THEN 				!
		    RETURN 0
		ELSE 				!
		    TOP = .MIDPOINT + 1;

		END;
	    TES;

	END

    END;					!End of FIND

END						!End of module

ELUDOM
