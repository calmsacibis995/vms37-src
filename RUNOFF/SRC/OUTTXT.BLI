MODULE OUTTXO (					!
		IDENT = 'V03-000'!
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Outputs a single line of text optionally centered.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: April, 1979
!
! MODIFIED BY: K. A. Dawson, Feb. 5, 1981
!
!	 002	KAD00002	K. A. Dawson 
!		Set change-bar character (in TSF & SCA) to fix .LO2 - .BB -
!		page-number bug.
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!
REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:TSF.REQ';

REQUIRE 'REQ:SCA.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
EXTERNAL LITERAL
    RINTES : UNSIGNED(8);

!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    MRA : REF FIXED_STRING,
    SCA : SCA_DEFINITION,
    TSF : TSF_DEFINITION;

EXTERNAL ROUTINE
    ENDCHR,
    OUTNJ;


GLOBAL ROUTINE OUTTXT (TEXT_PTR, TEXT_LENGTH, CENTERED) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Outputs a generated line of text in the "official" way.
!	This means that an MRA/TSF pair have to be allocated and set up.
!	This routine is needed when there is no convenient MRA/TSF pair
!	around that can be used for outputting text.
!
! FORMAL PARAMETERS:
!
!	TEXT_PTR is a CH$PTR to the text to be centered and output.
!	TEXT_LENGTH is the length of the text.
!	CENTERED is the page width in which the text should be centered.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    LOCAL
	HOLD_MRA,
	HOLD_SCA : VECTOR[SCA_SIZE],
	HOLD_TSF,
	TEMP_MRA : FIXED_STRING[100],
	TEMP_TSF : VECTOR[TSF_SIZE],
	PTR;

    !Remember location of current TSF
    HOLD_TSF = .TSF;
    !Set up substitute TSF
    INCR I FROM 0 TO TSF_SIZE - 1 DO TEMP_TSF[.I] = 0;
    TSF = TEMP_TSF;
    !Set change-bar character to space.
    TSF_BAR_CHAR = %C' ';

    !Remember location of current MRA
    HOLD_MRA = .MRA;
    !Set up substitute MRA;
    FS_MAXSIZE(TEMP_MRA) = 100;
    FS_INIT(TEMP_MRA);
    MRA = TEMP_MRA;

    !Copy SCA before setting up a new one.
    INCR I FROM 0 TO SCA_SIZE -1 DO HOLD_SCA[.I] = .SCA[.I];

    !Initialize SCA.
    INCR I FROM 0 to SCA_SIZE - 1 DO SCA[.I] = 0;
    SCA_FC_CASE = TRUE;
    SCA_RM = 150;
    SCA_LM = 0;
    SCA_SPACING = 1;
    SCA_FC = TRUE;
    SCA_FILL = TRUE;
    SCA_JUSTIFY = TRUE;
    SCA_CROCK = TRUE;
    SCA_WRD_PNTR = .FS_START (MRA);
    SCA_WRD_CPEND = RINTES;
    !Set change-bar character(s) to space.
    SCA_BAR_CHAR = %C' ';
    SCA_WRD_BAR_CHR = %C' ';

    PTR = .TEXT_PTR;

    INCR I FROM 1 TO .TEXT_LENGTH DO
        ENDCHR (CH$RCHAR_A (PTR));

    !Center the text
    TSF_ADJUST = MAX ((.CENTERED - .TEXT_LENGTH)/2, 0);
    !Output the line containing the centered text.
    OUTNJ ();

    !Restore original SCA
    INCR I FROM 0 TO SCA_SIZE - 1 DO SCA[.I] = .HOLD_SCA[.I];

    !Restore original MRA
    MRA = .HOLD_MRA;

    !Restore original TSF
    TSF = .HOLD_TSF;

    END;					!End of OUTTXT

END						!End of module

ELUDOM
