MODULE COOVBL (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: General purpose routine
!
! ABSTRACT: Convert a binary number into a vector of characters and
!	    return the result and character count.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: April, 1979
!
! MODIFIED BY:
!
!--

!

! OWN STORAGE:
!
OWN
    TABLE : INITIAL ( CH$PTR(UPLIT('0ABCDEFGHIJKLMNOPQRSTUVWXYZ')) );

GLOBAL ROUTINE CONVBL (BINARY_NUMBER, KHARACTERS, KHARACTER_COUNT, ULM) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Converts 'binary_number' to a vector of characters,
!	returning them in 'kharacters'; kharacter_count is the
!	number of characters that result.
!	The absolute value of 'binary_number' is converted,
!	so that the user is responsible for handling negative numbers.
!	ULM determines what the string of letters looks like:
!		-1 means return all in upper case.
!		 0 means return all in lower case.
!		+1 means capitalize first letter.
!
! FORMAL PARAMETERS:
!
!	See FUNCTIONAL DESCRIPTION
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    MAP
	KHARACTERS : REF VECTOR;

    LOCAL
	LEFT_TO_CONVERT;

    .KHARACTER_COUNT = 0;
    LEFT_TO_CONVERT = ABS (.BINARY_NUMBER);

    !Catch the special case, when the number is zero.
    IF						!
	.LEFT_TO_CONVERT EQL 0			!
    THEN
	BEGIN
	KHARACTERS [0] = CH$RCHAR(.TABLE);	!First character is the special 'zero' character.
	.KHARACTER_COUNT = 1;
	RETURN;
	END;

    !The normal case, when the number is not zero.
    DO
	BEGIN
	LOCAL
	    GROUP,
	    UNITS;

	GROUP = (.LEFT_TO_CONVERT - 1)/26;
	UNITS = .LEFT_TO_CONVERT - (26*.GROUP);
	KHARACTERS [..KHARACTER_COUNT] = CH$RCHAR(CH$PLUS(.TABLE,.UNITS));
	.KHARACTER_COUNT = ..KHARACTER_COUNT + 1;
	LEFT_TO_CONVERT = .GROUP;
	END
    UNTIL .LEFT_TO_CONVERT EQL 0;

    !Apply case rules
    IF						!
	.ULM EQL -1
    THEN
	!User is content with all in upper case.
	RETURN;

    !Convert the necessary number of letters to upper case.
    INCR I FROM 0 TO (..KHARACTER_COUNT - .ULM - 1) DO
	    KHARACTERS [.I] = .KHARACTERS [.I] - %C'A' + %C'a';

    RETURN;
    END;					!End of CONVBL

END						!End of module

ELUDOM
