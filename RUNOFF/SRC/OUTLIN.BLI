MODULE OUTLIO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Either saves information described by TSF or causes it to
!		be output immediately.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!
! MODIFIED BY:
!
!
!	011	REM00011	Ray Marshall	14-December-1981
!		Built up revision history based on CMS library generations.
!
!	010	RWF00010	Rich Friday	20-November-1981
!		Fairly extensive changes were made as a result of the re-
!		  design of footnote processing.
!		Footnote control was moved to its own control area in its own
!		  require file (FNCT.REQ).  Appropreate changes have been made.
!		Fixed change bar bug where a change bar would be put on the
!		  blank line preceeding the line in which only a portion of
!		  it is flagged for a change bar.  To put it another way: now,
!		  if only a portion of a given line, not including the first
!		  word, is inclosed between .BB/.EB directives, only that line
!		  will have the change bar on it.  Previously, if the
!		  preceeding output line was blank, it would have also had a
!		  change bar on it -- now it wont.  Is that clear?  /  Ray
!
!	009	REM00009	Ray Marshall	25-August-1981
!		Updated copyright notice and initial reformatting of revision
!		  history.
!
!   	008	KAD00008	K. A. Dawson	12-February-1981
!		V1.126d. Check for pending footnote with no word waiting, in 
!		  OUTNJ and OUTJ.
!
!	007	KAD00007	Keith Dawson	21-October-1980
!		Fixup require file declarations so they now all come from REQ:
!		  and not the local directory.
!
!	006	KAD00006	K. A. Dawson	20-October-1980
!	 	For VERSION 1(124)  Fix bug: change-bars w/footnotes can cause
!	          blank lines to go awry.
!
!	005	KAD00005	K. A. Dawson	17-October-1980
!		I (Ray Marshall) don't know what (if any) changes were made
!		  on or before this date.  This is the first generation in
!		  the CMS library.
!--

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    OUTCRG : NOVALUE,
    OUTJ : NOVALUE,
    OUTNJ : NOVALUE;

!
! INCLUDE FILES:
!
REQUIRE 'REQ:RNODEF.REQ';				!RUNOFF capabilities

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:FNCT.REQ';

REQUIRE 'REQ:FOOFIL.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:GCA.REQ';

REQUIRE 'REQ:LSTBTS.REQ';

REQUIRE 'REQ:NBITS.REQ';

REQUIRE 'REQ:OUTOPT.REQ';

REQUIRE 'REQ:SCA.REQ';

REQUIRE 'REQ:TSF.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!

EXTERNAL LITERAL
    RINTES : UNSIGNED (8);

!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    FNCT : FNCT_DEFINITION,
    FRA : FIXED_STRING,
    GCA : GCA_DEFINITION,
    MRA : REF FIXED_STRING,
    OUTOPT : OUTOPT_DEFINE,
    SCA : SCA_DEFINITION,
    TSF : TSF_DEFINITION;

EXTERNAL LITERAL 				!Error messages
    RNFCJL,
    RNFLOC;

EXTERNAL ROUTINE
    ENDWRD,
    ERM,
    FOOFIL,
    LOUT,
    PUTNDY,
    PUTTXT,
    SCL,
    UNPUS;
%IF	NOT FILE_INDEX
%THEN
EXTERNAL ROUTINE
    XOUT;
%FI

GLOBAL ROUTINE OUTLIN (JUSTIFY) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	OUTLIN is called when MRA is full, and the text should be
!	output.  OUTLIN routes normal text lines directly to LOUT,
!	for output.  Other records are passed to other processors.
!	For example, footnote records get saved in the footnote
!	file, and indexing records get sent to the indexing routines.
!
! FORMAL PARAMETERS:
!
!	JUSTIFY indicates whether or not the line should be justified.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN


    IF 					!
	.TSF_INDEX			!Is this TSF something for the index?
	OR .TSF_BTC			!Is this TSF something for the table of contents?
    THEN
	BEGIN				!This record describes an index or table of contents entry
	LOCAL
	    IADDR,			!Address of text (as opposed to CH$PTR)
	    INT_HL,			!Internal length of text.
	    IPTR;			!CH$PTR to the text.

	INT_HL = .TSF_INT_HL;		!Save internal length of text.
	IPTR = .FS_START(MRA);		!Save pointer to text.
	IADDR = (.FS_START(MRA))<0,%BPADDR,0> + %BLISS(BLISS36);

	TSF_LINES = 0;			!This does not generate any text.

	!Direct indexing and table of contents information from the footnote to
	!the footnote file.  Such information from the main body of the
	!document goes directly to the processing routine.
	IF
	    .FNCT_COLLECTING		!If collecting footnotes,
	THEN
	    FOOFIL (FOO_WRIT)		!Save information in footnote file.
	ELSE
	    BEGIN			!Otherwise,
	    !If user wants to debug the index or table of contents, regurgitate the information.
	    IF				!
		(NOT .GCA_SKIP_OUT)
	    THEN
		!User wants to see information on this document page
		IF
		    .GCA_DEBUG_INDEX
		    AND .TSF_INDEX
		THEN
		    !Echo an index entry
		    UNPUS (0)
		ELSE
		    IF
			.GCA_DEBUG_TOC	!
			AND .TSF_BTC
		    THEN
			!Echo a table of contents entry
			UNPUS (1);

	!Send index entries to either the in-core index routine, or
	!to the .BIX file.  The latter happens only if the user said /BIX.
	!The former happens only on -10s, -20s, and VAXes, and never if
	!the user said /BIX.
%IF	NOT FILE_INDEX
%THEN
	    IF!
		NOT .GCA_BIX
	    THEN
		IF
		    .TSF_INDEX
		THEN
		    XOUT (.INT_HL, .IPTR, .TSF_FIRST_XTN, .TSF_H_BARS);
%FI
	    IF!
		.GCA_BIX
	    THEN
		IF
		    .TSF_INDEX
		THEN
		    PUTNDY (.INT_HL, .IADDR, .TSF_FIRST_XTN, .TSF_H_BARS);

	    !Send table of contents records to the .BTC file
	    IF					!
		.TSF_BTC
	    THEN
		PUTTXT (.INT_HL, .IPTR, .TSF_MAJOR, .TSF_MINOR);
	    END;

	RETURN;
	END;

    !This is a record full of "normal" text to be output.

    IF 						!
	NOT .FNCT_EXPANDING		!Expanding footnotes?
    THEN
    !Set up justification for all lines except those that are
    !being fetched from the footnote file.  For these records,
    !the information was computed before they were written to
    !the footnote temporary file.
	BEGIN
	TSF_JUSTIFY = .JUSTIFY;
	TSF_JUST_ALG = .TSF_JUST_ALG + 1;	!Justification algorithm feedback for next time.
	TSF_PADDING = .SCA_RM - .TSF_EXT_HL;	!Space count for padding.
	END;

    IF 						!
	.TSF_PADDING LSS 0			!
	AND NOT .FNCT_COLLECTING
    THEN
	BEGIN
	!This can happen only if a word is encountered that does
	!not fit onto the line between the margins.  It can also
	!happen as a side effect of breaking a word that does not
	!fit in TSF (see ENDCHR, where this is done).
	!The effect of the error handling is that the line is
	!output without justification, but with nothing removed.
	!The resulting line may have text exceeding the right margin.
	ERM (RNFCJL, 0, 0);

	IF 					!
	    NOT .FNCT_EXPANDING		!Expanding footnotes?
	THEN
	    !Output line and page only if this is not happening
	    !inside a footnote.  If it's happening inside a footnote
	    !then the line and page numbers will be the wrong ones.
	    ERM (RNFLOC, 0, 0);

	TSF_PADDING = 0;
	END;

    IF 						!
	NOT .FNCT_COLLECTING		!Collecting footnotes?
    THEN
    !Output some text directly to the document.
	BEGIN
	FS_INIT (FRA);
	LOUT ();
	END
    ELSE
	!Save this record in the footnote file for use later.
	BEGIN
	!Compute the number of lines represented by this record.
	!TSF_LINES currently contains a count of the number of
	!lines that will be generated when the code generated by
	!the Gxxxx routines (see module GCODE) is "executed".

	IF 					!
	    .TSF_UND				!
	    AND .OUTOPT_UND_SEP
	THEN
	!Add an extra line if underlining is done by putting something
	!on an extra line (e.g. dashes under the text).
	    TSF_LINES = .TSF_LINES + 1;

	IF 					!
	    .TSF_EXT_HL NEQ 0
	THEN
	    TSF_LINES = .TSF_LINES + 1;

	!Now actually save the record.
	FOOFIL (FOO_WRIT);
	END;

    IF 						!
	NOT .FNCT_EXPANDING		!Expanding footnotes?
    THEN
	!Throw away leftovers from line just output.
	!Note that it wouldn't hurt anything to always do this.  It's just that
	!it's a waste of time if footnotes are being output.
	BEGIN
	TSF_INT_HL = 0;
	TSF_EXT_HL = 0;
	TSF_INT_VL = 0;
	TSF_NBITS = 0;
	TSF_BARS = FALSE;			!Turn off change bars.
	TSF_H_BARS = FALSE;			!...
	TSF_JUS_CNT = 0;
	TSF_FIRST_XTN = 0;
	TSF_LAST_XTN = 0;
	SCA_WRD_LST_JUS = 0;
	SCA_WRD_LST_UND = FALSE;
	SCA_WRD_LST_SP = 0;
	TSF_TEXT = FALSE;			!There's nothing there now.
	TSF_ADJUST = 0;
	TSF_NEXT_REG = 0;
	TSF_LINES = 0;
	TSF_FOOTW = 0;				!No footnotes attached to this line.
	! Clean up the MRA by moving the last word in the buffer to the left.
	SCL ();
	END;

    END;					!End of OUTLIN


GLOBAL ROUTINE OUTNJ : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Forces out the current text, without allowing it to be justified.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    IF 						!
	( NOT .SCA_FC)				!
	OR ( NOT .SCA_FILL)			!
!!	OR (.SCA_WRD_F_XTN NEQ 0)		!Might be an index entry only.
    THEN
	ENDWRD (FALSE, FALSE, FALSE)
    ELSE
	BEGIN
	IF
	    (.SCA_WRD_FOOTW NEQ 0)		!Footnotes attached to this word?
	THEN
	    !Take care of pending footnote.
	    BEGIN
	    TSF_FOOTW = .TSF_FOOTW + .SCA_WRD_FOOTW;	!Update count of footnotes associated with this line.
	    SCA_WRD_FOOTW = 0				!Make sure footnotes don't get counted twice
	    END;

	!Is there an index entry associated with this word?
	IF 					!
	    .SCA_WRD_F_XTN NEQ 0
	THEN
	    !Take care of pending index entry.
	    BEGIN

	    IF 					!
		.TSF_FIRST_XTN EQL 0
	    THEN
		TSF_FIRST_XTN = .SCA_WRD_F_XTN;

	    TSF_LAST_XTN = .SCA_WRD_L_XTN;
	    END;
	END;

    SCA_WRD_F_XTN = 0;
    SCA_WRD_L_XTN = 0;
    OUTLIN (FALSE);				!Don't justify line.
    TSF_JUST_ALG = 0;				!Reset justification algorithm.
    SCA_CONT = FALSE;				!Don't allow concatenation (.NO SPACE)
    END;					!End of OUTNJ


GLOBAL ROUTINE OUTJ : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Forces out the current text, causing it to be justified.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    IF 						!
	( NOT .SCA_FC)				!
	OR ( NOT .SCA_FILL)			!
!!	OR (.SCA_WRD_F_XTN NEQ 0)		!Might be an index entry only.
    THEN
	ENDWRD (FALSE, FALSE, FALSE)
    ELSE
	BEGIN
	IF
	    (.SCA_WRD_FOOTW NEQ 0)		!Footnotes attached to this word?
	THEN
	    !Take care of pending footnote.
	    BEGIN
	    TSF_FOOTW = .TSF_FOOTW + .SCA_WRD_FOOTW;	!Update count of footnotes associated with this line.
	    SCA_WRD_FOOTW = 0			!Make sure footnotes are not counted twice
	    END;

	IF 					!
	    .SCA_WRD_F_XTN NEQ 0
	THEN
	    !Take care of pending index entry.
	    BEGIN

	    IF 					!
		.TSF_FIRST_XTN EQL 0
	    THEN
		TSF_FIRST_XTN = .SCA_WRD_F_XTN;

	    TSF_LAST_XTN = .SCA_WRD_L_XTN;
	    END;
	END;

    SCA_WRD_F_XTN = 0;
    SCA_WRD_L_XTN = 0;
    OUTLIN (TRUE);				!Justify line.
    TSF_JUST_ALG = 0;				!Reset justification algorithm.
    SCA_CONT = FALSE;				!Don't allow concatenation (.NO SPACE)
    END;					!End of OUTJ

GLOBAL ROUTINE OUTCRG : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Clears out carriage control sequences from the TSF.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    !This is a record full of "normal" text to be output.
    FS_INIT (FRA);
    TSF_JUSTIFY = FALSE;
    TSF_PADDING = 0;

    !If collecting footnotes, go through OUTNJ to make sure blank lines
    ! get put in the right place.
    IF				!
	.FNCT_COLLECTING	!Collecting footnotes?
    THEN
	OUTNJ ()
    ELSE
	LOUT ();
    !Throw away leftovers from line just output.
    TSF_INT_HL = 0;
    TSF_EXT_HL = 0;
    TSF_INT_VL = 0;
    TSF_NBITS = 0;
    TSF_BARS = FALSE;				!Turn off change bars.
    TSF_H_BARS = FALSE;				!...
    TSF_JUS_CNT = 0;
    TSF_FIRST_XTN = 0;
    TSF_LAST_XTN = 0;
    TSF_TEXT = FALSE;				!There's nothing there now.
    TSF_ADJUST = 0;
    TSF_FOOTW = 0;
    
    FS_INIT (MRA);
    SCA_WRD_PNTR = .FS_START (MRA);
    TSF_NEXT_REG = 0;
    END;					!End of OUTCRG

END						!End of module

ELUDOM
