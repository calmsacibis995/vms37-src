MODULE OFTOOO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Handles all .FLAGS and .NO FLAGS commands
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!
! MODIFIED BY:
!
!	004	REM00004	Ray Marshall	6-December-1981
!		Special cased the .FLAGS SUBINDEX directive to allow the
!		  specified character to be the same as the current INDEX flag
!		  if that is also different then the current SUBINDEX flag.
!		Fill in revision history from CMS library and file comparisons.
!
!	003	REM00003	Ray Marshall	25-August-1980
!		Update copyright and reformat revision history
!
!	002	KAD00002	Keith Dawson	21-October-1980
!		Modified REQUIREs by adding the REQ: logical.
!
!	001	KAD00001	Keith Dawson	June-1980
! 		For VERSION 1(123)
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:ATCODE.REQ';

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:FLGT.REQ';

REQUIRE 'REQ:GCA.REQ';

REQUIRE 'REQ:RUNHAN.REQ';

REQUIRE 'REQ:SCA.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    ATABLE : VECTOR,
    FLGT : FLAG_TABLE,
    GCA : GCA_DEFINITION,
    SCA : SCA_DEFINITION;

EXTERNAL
    FLTSO : VECTOR;

BIND
    SEARCH_ORDER =
FLTSO : VECTOR;

EXTERNAL ROUTINE
    FNDFLG,
    GETONE,
    NATE;


GLOBAL ROUTINE OFT (HANDLER_CODE, WHICH_FLAG) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE indicates which command is to be processed.
!	WHICH_FLAG indicates to which flag the command applies.
!
! IMPLICIT INPUTS:
!
!	This routine relies on the fact that the HANDLER_CODEs
!	for .FLAG commands are all less than the HANDLER_CODEs
!	for .NO FLAG commands.
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    !Flag enabling and disabling can occur at two different, independent
    !levels.  These levels correspond to the
    !	1) .FLAGS ALL and .NO FLAGS ALL commands, and the
    !	2) .FLAG <flag name> and .NO FLAG <flag name> commands.
    !Associated with each flag is a TRUE/FALSE setting that indicates
    !whether or not it should be recognized.  And associated with each
    !flag is also a character that defines which input character is to
    !be recognized as representing that flag.
    !Assume that you have some character 'X' that represents the UNDERLINE flag.
    !'X' will be recognized as a flag only if.
    !	1) The user as said .FLAGS ALL (which is the default)
    !	**AND**
    !	2) The user has issued a .FLAG UNDERLINE command.
    !Note that the user may issue these commands at any time, and one
    !command does not imply the other.
    !For example, if he has said .NO FLAGS ALL, then 'X' will not be
    !recognized as a flag, even if he says .FLAG UNDERLINE; if he says
    !FLAG UNDERLINE when .NO FLAGS ALL is in effect, RUNOFF simply remembers
    !the fact that the UNDERLINE flag is enabled, and makes it possible
    !to recognize that flag after the user says .FLAGS ALL.
    !
    !The implementation of this is as follows:
    !Normally, each position in ATABLE tells what a particular character is.
    !If a character represents a flag the corresponding ATABLE entry notes
    !that fact.  Otherwise, the ATABLE entry says something else.
    !When the user says .NO FLAG UNDERLINE (above example) the ATABLE
    !entry corresponding to the character 'X' is changed to indicate
    !that 'X' is a normal character.  When he says .FLAG UNDERLINE,
    !the ATABLE entry is reset to indicate that 'X' represents a flag.
    !If the user says .NO FLAGS ALL, then again, the ATABLE entry (and
    !those for all other applicable flags) is set to inhibit flag recognition.
    !However, there is another difference between .NO FLAG UNDERLINE
    !and .NO FLAGS ALL.  The FLAG TABLE indicates the actual setting
    !of each flag.  The .FLAGS ALL and .NO FLAGS ALL commands do not change
    !those settings; they change ATABLE only.
    !The .FLAG UNDERLINE command may not only change ATABLE (if the user
    !said .FLAGS ALL) but the FLAG TABLE as well.

    SELECTONE .HANDLER_CODE OF
	SET

	[H_NO_FLAGS_ALL] :
	    BEGIN

	    INCR I FROM 0 TO .SEARCH_ORDER [-1] - 1 DO
		BEGIN
		!Disable recognition of only those flags that
		!are marked as enabled.

		IF 				!
		    .FLGT [.SEARCH_ORDER [.I], FLAG_ENABLED]
		THEN 				!
		!Set ATABLE entry to 'normal' setting.
		    NATE (.FLGT [.SEARCH_ORDER [.I], FLAG_CHARACTER]);

		END;

	    !Remember that flags are not to be recognized.
	    SCA_FLAGS = FALSE;
	    !Remember the last .FLAGS ALL/.NO FLAGS ALL command.
	    GCA_FLAG_CMD = H_NO_FLAGS_ALL;
	    END;

	[H_FLAGS_ALL] :
	    BEGIN

	    INCR I FROM 0 TO .SEARCH_ORDER [-1] - 1 DO
		BEGIN
		!Enable recognition of only those flags that are
		!marked as enabled.

		IF 				!
		    .FLGT [.SEARCH_ORDER [.I], FLAG_ENABLED]
		THEN
		!Mark corresponding ATABLE entry as representing a flag.
		    ATABLE [.FLGT [.SEARCH_ORDER [.I], FLAG_CHARACTER]] = A_FLAG;

		END;

	    !Remember that flags are to be recognized.
	    SCA_FLAGS = TRUE;
	    !Remember last .FLAGS ALL/.NO FLAGS ALL command.
	    GCA_FLAG_CMD = H_FLAGS_ALL;
	    END;

	[OTHERWISE] :
	    BEGIN

	    IF 					!
		.HANDLER_CODE LSS H_NO_FLAGS_ALL
	    THEN
		BEGIN				!It's a .FLAGS type command

		LOCAL
		    HOLD_KHAR,
		    ALTERNATE_FLAG;	!This will = WHICH_FLAG unless we are
					!processing SUBINDEX, in which case it
					!will then point to the INDEX flag.

		IF .WHICH_FLAG EQL SBX_FLAG
		    THEN ALTERNATE_FLAG = IND_FLAG
		    ELSE ALTERNATE_FLAG = .WHICH_FLAG;

		!Get the new flag character, if any.
		HOLD_KHAR = GETONE (.WHICH_FLAG, .ALTERNATE_FLAG);

		IF 				!
		    .HOLD_KHAR EQL 0
		THEN
		!No new flag character.  Use present one.
		    HOLD_KHAR = .FLGT [.WHICH_FLAG, FLAG_CHARACTER];

		!Now, search the flag table to see if the new flag character
		!is currently being used to represent some other flag.

		IF NOT 				!
		    (.WHICH_FLAG EQL COM_FLAG	!
		    OR .WHICH_FLAG EQL CON_FLAG !
		    OR .WHICH_FLAG EQL EFO_FLAG	!
		    OR .WHICH_FLAG EQL SBX_FLAG)
		THEN
		    BEGIN			!Normal cases
		    FLGT [.WHICH_FLAG, FLAG_ENABLED] = FALSE;
		    !The above causes FNDFLG to find some other
		    !flag represented by the same character,
		    !if one exists.

		    IF 				!
			(FNDFLG (.FLGT [.WHICH_FLAG, FLAG_CHARACTER]) EQL FLAG_COUNT + 1)	!
			AND .SCA_FLAGS		!Work has already been done if .NO FLAGS ALL.
		    THEN
			BEGIN			!
			!No other flag is represented
						!by this character.
			!Set the ATABLE entry to show a 'normal' character.
			NATE (.FLGT [.WHICH_FLAG, FLAG_CHARACTER]);
			END;

		    IF 				!
			.SCA_FLAGS
		    THEN
			BEGIN
			!Make ATABLE entry show this character is a flag.
			ATABLE [.HOLD_KHAR] = A_FLAG;
			END;

		    END;

		FLGT [.WHICH_FLAG, FLAG_CHARACTER] = .HOLD_KHAR;	!Save new flag character.
		FLGT [.WHICH_FLAG, FLAG_ENABLED] = TRUE;	!Flag is enabled.
		END
	    ELSE
		BEGIN				!It's a .NO FLAGS type command
		FLGT [.WHICH_FLAG, FLAG_ENABLED] = FALSE;	!Flag is disabled.

		IF NOT 				!
		    (.WHICH_FLAG EQL COM_FLAG	!
		    OR .WHICH_FLAG EQL CON_FLAG !
		    OR .WHICH_FLAG EQL EFO_FLAG	!
		    OR .WHICH_FLAG EQL SBX_FLAG)
		THEN
		    BEGIN			!Normal cases

		    IF 				!
			(FNDFLG (.FLGT [.WHICH_FLAG, FLAG_CHARACTER]) EQL FLAG_COUNT + 1)	!
			AND .SCA_FLAGS
		    THEN
		    !Set corresponding ATABLE entry to indicate it's a 'normal' character.
			NATE (.FLGT [.WHICH_FLAG, FLAG_CHARACTER])
		    ELSE
		    !Because this character represents another
		    !flag as well, leave ATABLE alone so that
		    !the other flag will be recognized.
			(0);

		    END;

		END;

	    END;
	TES;

    END;					!End of OFT

END						!End of module

ELUDOM
