MODULE RTERMO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Routine for normal termination of a document.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: April, 1978
!
! MODIFIED BY:
!
!	017	KAD00017	Keith Dawson	06-January-1982
!					Fixed 2 bugs:
!
!		1) If user said ".page" at end of file, was not getting a <FF>
!		   at the end. HOLD_PHAN_TOP_PAGE was getting set in the wrong
!		   place, before OUTNJ () instead of after it.
!
!		2) If user omitted .END FOOT at end of document, DSR would
!		   infinite-loop!
!
!	016	REM00016	Ray Marshall	16-December-1981
!		Merged in changes made to this module since Rich took it from
!		  the base level version.  Filled in revision history to match.
!		Built up revision history based on CMS library generations.
!
!	015	RWF00015	Rich Friday	16-November-1981
!		Fairly extensive changes were made as a result of the re-
!		  design of footnote processing.  For one thing, NEWPAG was
!		  divided into FOOBOT, BOTPAG, & TOPPAG.  Now, instead of
!		  calling NEWPAG, we selectively call the specific function
!		  we need.  There were many logic changes in this edit.
!
!	014	REM00014	Ray Marshall	30-November-1981
!		Added support for new /[NO]LOG qualifier.
!
!	013	REM00013	Ray Marshall	16-October-1981
!		Eliminated ERR & PC1 messages by adding the %X plural
!		  translation to the text of ERD & PCT respectively.
!
!	012	REM00012	Ray Marshall	7-October-1981
!		Modified logic determining wether to use "page output" or
!		  "pages output".  The former is now only used for 1 page;
!		  it previously was also used for 0 pages.
!		The LOM error is no longer used to report the number of lines
!		  of error messages output.  I expanded the ERD informational
!		  message to include the number of "diagnostics messages"
!		  reported.  I also added a new message for reporting the
!		  singular form of ERD -- it's ERR.  If you look at MSGTXT.REQ,
!		  you will also notice that the general format of ERD, ERR, and
!		  NED are now similar.
!		Reformatted this revision history.  Somehow, it got missed in
!		  the wave of work done by Dot Smith.  Probably my fault.  I
!		  also picked up a little more information by pulling all of
!		  the generations from the STEP library and comparing them.
!
!	011	KAD00011	Keith Dawson	25-August-1981
!		Fix bug introduced by /QUICK.
!
!	010	KAD00010	Keith Dawson	21-August-1981
!		Copyright updated by REM.
!		Use new termination message if user said /QUICK.
!
!	009	KAD00009	Keith Dawson	21-October-1980
!		Added logical REQ: to all REQUIRE statements.
!
!	008	KAD00008	Keith Dawson	17-October-1980
!		I (Ray Marshall) don't know what (if any) changes were made
!		  on or before this date.  This is the first generation in
!		  the CMS library.
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:CLHOPS.REQ';

REQUIRE 'REQ:FNCT.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:GCA.REQ';

REQUIRE 'REQ:HCT.REQ';

REQUIRE 'REQ:PHDEF.REQ';

REQUIRE 'REQ:RUNHAN.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    FNCT : FNCT_DEFINITION,
    FRA : FIXED_STRING,
    GCA : GCA_DEFINITION,
    HCT : HCT_DEFINITION,
    PHAN : PHAN_DEFINITION,
    TTEIOB : $XPO_IOB (),
    TTIIOB : $XPO_IOB (),
    TTOIOB : $XPO_IOB (),
    TSIIOB : $XPO_IOB ();

EXTERNAL LITERAL 				!messages and/or errors
    RNFEFD,
    RNFERD,
    RNFNED,
    RNFPCT;

EXTERNAL ROUTINE
    BOTPAG,
    CLH,
    CLSNDX,
    CLSTOC,
    CPAGEB,
    ERM,
    ERML,
    FN,
    FOOBOT,
    FOOOUT,
    OUTNJ,
    PUTMSG,
    REMNEG,
    REMMRG,
    TOPPAG,
    TPR,
    TSTTFE,
    UFORM,
    USKIPL;


GLOBAL ROUTINE RTERM : NOVALUE = 		!

!++
! FUNCTIONAL DESCRIPTION:
!
!	RUNOFF termination.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	HOLD_PHAN_TOP_PAGE,
	STATUS;

    TSTTFE (0);					!Check for too few ends
    REMNEG ();					!Check for unreported bad backdents.
    REMMRG ();					!Check for unreported bad margin settings.

    IF 						!Detect a missing .END FOOTNOTE
	.FNCT_COLLECTING			!command and
    THEN 					!forcefully terminate the footnote.
	BEGIN					!...
	ERML (RNFEFD);				!...
	FN (H_END_FOOTNOTE);			!...
	END;					!...

    OUTNJ ();
    HOLD_PHAN_TOP_PAGE = .PHAN_TOP_PAGE;	!Remember if the user said .PAGE as the last thing in his .RNO file.

    !Force out waiting footnotes, if any.
    IF 						!
	.FNCT_COUNT NEQ 0
    THEN
	!There are footnotes that have to be output.
	IF 					!
	    NOT .PHAN_PAGING			!
	    OR (.PHAN_LLINES GEQ 2*.PHAN_SLINES)
	THEN
	    !If not paging, just put out footnotes immediately.
	    !Ditto, if the page is rather long.
	    BEGIN
	    !Special case: We're at the very top of the first page.
	    IF
		.PHAN_TOP_FIRST
	    THEN
		TOPPAG ();

	    FOOOUT (.FNCT_COUNT)		!Dump them all at once.
	    END
	ELSE
	    !There's a whole pile of footnotes left over, and they
	    !have to be output at the bottom of the page.  This may involve
	    !spreading them out across several pages.
	    BEGIN

	    !Special case:  We're at the very top of the first page.
	    IF
		.PHAN_TOP_FIRST
	    THEN
		TOPPAG ();

	    !Loop over all remaining footnotes
	    WHILE TRUE DO
	    BEGIN
	    !First get rid of as many footnotes as possible.
	    FOOBOT ();

	    !Now see if there's any more to be output.  If not, we're at
	    !the real end of the document (almost).
	    IF
		.FNCT_COUNT GTR 0
	    THEN
		!Because not all footnotes were output, we have to start a new
		!page, so that successive iterations through this loop force out
		!additional ones.
		BEGIN
		!First, finish the bottom of the page
		BOTPAG ();
		!And now start the next page.
		TOPPAG ();
		END
	    ELSE
		!All footnotes have been output.  Termination of this
		!page is handled in a special way.
		EXITLOOP
	    END

	    END;

    !If the page number is to be centered at the bottom of the
    !page then position there and output it.
    IF						!
	.HCT_LAYOUTN NEQ 0
    THEN
        CPAGEB ();

    !If /PAUSE or /SIMULATE force next page before
    !generating summary information.
    IF 						!
	.PHAN_PAUSE				!
	OR .PHAN_SIMULATE
    THEN
	BEGIN

	!Always force blank lines at end of document when in
        !in /SIMULATE mode.
	!If skipping output, PHAN_BOTTOM has already been set by the last
	!page actually output.  If this is the "normal case", i.e., user didn't
	!specify /PAGES, then this is the time to update PHAN_BOTTOM, since
	!NEWPAG won't get called to do it.
	IF 					!
	    NOT .GCA_SKIP_OUT
	THEN
	    PHAN_BOTTOM = .PHAN_LINES_TP;	!UFORM expects PHAN_BOTTOM set.
	
	!Force on generation of output so blank lines get output
	GCA_SKIP_OUT = FALSE;

	UFORM ();
	END
    ELSE
	!Not /SIMULATE or /PAUSE
	IF
	    .HOLD_PHAN_TOP_PAGE AND (NOT .GCA_CMD_QUICK)	!Do not call CLH if user
	THEN						!said /NOOUTPUT.
	    !User said .PAGE after all text was finished, intending
	    !to generate a form-feed
	    BEGIN
	    FS_INIT (FRA);
	    FS_WCHAR (FRA, %O'14');		!Insert a form feed
	    CLH (CLH_OUT_NOCRLF)
	    END;

    CLSNDX();				!Close binary index, if one.
    CLSTOC();				!Close binary table of contents, if one.

    !If any errors were detected,
    IF  .GCA_FEHLER

		!Report number of lines of error messages.
	THEN	PUTMSG (RNFERD, .GCA_ERRCNT, 0)

	ELSE

    !If no errors detected
	    !and /LOG was specified,
	    IF	.TERMINATION_LOG

    		!then just identify version and report 0 diagnostics.
		THEN	PUTMSG (RNFNED, 0, 0);

    !The following IF supplies the following logic:  If /NOOUT was specified,
    !this block isn't done.  If not, and either /LOG was specified or errors
    !were encountered, then perform the block.

    IF	((.TERMINATION_LOG OR .GCA_FEHLER) AND (NOT .GCA_CMD_QUICK))
    THEN				!If so, do not report pages.

	!This block determines whether or not there is currently a valid page
	!count.  If not, it attempts to calculate one based on the number of
	!lines output.
	!
	!Then it writes the second line of the termination message to the
	!log device.  This line reports the number of pages output and
	!idintifies the output filename.

	BEGIN

	!If this is a document broken up into pages, then count logical pages.
	!Otherwise, (i.e. .NO PAGING) figure out # of spooled pages this is.
	IF  (NOT .PHAN_PAGING)

	THEN
	!Not a document organized into pages.  Assume spooler will break it up.
	    GCA_PAGE_CNT = .GCA_PAGE_CNT + (.PHAN_LINES_TP + .PHAN_SLINES - 1)/.PHAN_SLINES
	ELSE
	!Not .NO PAGING, so bump count only if last page is not empty.
	    IF  (.PHAN_LINES_TP GTR 0)
	    THEN
		GCA_PAGE_CNT = .GCA_PAGE_CNT + 1;

	!Report how many pages generated and to what file.
	PUTMSG (RNFPCT, .GCA_PAGE_CNT, 0);

	END;

    !Close all files
    STATUS = $XPO_CLOSE (IOB = TTEIOB);
    STATUS = $XPO_CLOSE (IOB = TTIIOB);
    STATUS = $XPO_CLOSE (IOB = TTOIOB);

    IF 						!
	.TSIIOB [IOB$V_OPEN]
    THEN
	STATUS = $XPO_CLOSE (IOB = TSIIOB);

    RETURN;
    END;					!End of RTERM

END						!End of module

ELUDOM
