!Version 001 -- 16-OCT-1980
!For DSR V1.124f
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!	FIXED_STRING is a structure that
!		contains a string of an expected maximum length,
!		plus additional locations to contain information related
!		to the status of the string.
STRUCTURE
	FIXED_STRING[I;N=1]	=
		[4*%UPVAL + CH$ALLOCATION(N*%UPVAL)]
		(FIXED_STRING - %UPVAL*( 1 - I) );

!	The following literals are names for the fields allocated above.
!		These names are not to be used by a user.
LITERAL
	FS__FCP		= 01,	! CH$PTR to first character position.
	FS__NCP		= 02,	! CH$PTR to next character position.
	FS__MAX		= 03,	! Capacity of the counted string.
	FS__LEN		= 04;	! Current length.


!	FS_ALLOCATE is used to allocate and initialize a fixed length string.
MACRO
	FS_ALLOCATE(FS_NAME,FS_MAXL)	=

		FS_NAME:FIXED_STRING[FS_MAXL]
	INITIAL(0,
		0,
		FS_MAXL,
		0)
			%;

!	Control information is accessed via the following macros.
MACRO
	FS_START(FS_NAME)	=
		FS_NAME[FS__FCP]	%,
	FS_NEXT(FS_NAME)	=
		FS_NAME[FS__NCP]	%,
	FS_MAXSIZE(FS_NAME)	=
		FS_NAME[FS__MAX]	%,
	FS_LENGTH(FS_NAME)	=
		FS_NAME[FS__LEN]	%,
	FS_REMAINS(FS_NAME)	=		! Does the same as FS_LENGTH.
		FS_LENGTH(FS_NAME)	%;

!	FS_INIT initializes a fixed length string to the null string.
MACRO
	FS_INIT(FS_NAME)	=
		begin
		FS_LENGTH(FS_NAME)	= 0;
		FS_START(FS_NAME)	= CH$PTR(FS_NAME[5]);
		FS_NEXT(FS_NAME)	= .FS_START(FS_NAME);
		end
					%;

!	FS_WCHAR puts a character into the fixed length string.
MACRO
	FS_WCHAR(FS_NAME,KHAR)	=
		begin
		CH$WCHAR_A(KHAR,FS_NEXT(FS_NAME));		!Write character and advance pointer.
		FS_LENGTH(FS_NAME) = .FS_LENGTH(FS_NAME) + 1;	!Bump total length count.
		end
					%;


!	FS_RCHAR gets a character from the fixed length string.
MACRO
	FS_RCHAR(FS_NAME,KHAR)	=
		begin
		KHAR			= CH$RCHAR_A(FS_NEXT(FS_NAME));	!Read character and advance pointer.
		FS_LENGTH(FS_NAME)	= .FS_LENGTH(FS_NAME) - 1;	!Decrement remainder count.
		end
					%;

!			End of FSPACK.REQ
