MODULE TITLEO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Processes the following commands:
!		.TITLE		.SUBTITLE		.NO SUBTITLE
!		.AUTOSUBTITLE	.NO AUTOSUBTITLE	.DATE
!		.NO DATE	.HEADERS LOWER		.HEADERS ON
!		.HEADERS UPPER	.HEADERS MIXED		.NO NUMBER
!		.FIRST TITLE	.NO HEADERS
!
!	    Also called by HL to handle header level 1's when .AUTOSUBTITLE
!	    is in effect.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: July, 1978
!
! MODIFIED BY:
!
!	010	REM00010	Ray Marshall	16-December-1981
!		Built up revision history based on CMS library generations.
!
!	009	RWF00009	Rich Friday	11-November-1981
!		As part of the footnote re-design, processing of the .PAGING
!		  and .NO PAGING directives has been moved to PANPA which is
!		  a new module.
!
!	008	KAD00008	Keith Dawson	9-October-1981
!		Disabled check for length of subtitle if processed via
!		autosubtitle (that is, called with argument of -1).
!
!	007	REM00007	Ray Marshall	25-August-1981
!		Modified .PAGING directive processing such that it will now
!		force a new page if appropriate.  i.e. added a call to GCPAGE.
!
!	006	REM00006	Ray Marshall	24-August-1981
!		Updated copyright.
!
!	005	KAD00005	Keith Dawson	21-October-1980
!		Fixup require file declarations so they now all come from REQ:
!		  and not the local directory.
!
!
!	004	KAD00004	Keith Dawson	17-October-1980
!		I (Ray Marshall) don't know what (if any) changes were made
!		  on or before this date.  This is the first generation in
!		  the CMS library.
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:CLIST.REQ';

REQUIRE 'REQ:CONVRT.REQ';

REQUIRE 'REQ:FLGT.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:GCA.REQ';

REQUIRE 'REQ:HCT.REQ';

REQUIRE 'REQ:KC.REQ';

REQUIRE 'REQ:LSTBTS.REQ';

REQUIRE 'REQ:NUMPRM.REQ';

REQUIRE 'REQ:PAGEN.REQ';

REQUIRE 'REQ:PHDEF.REQ';

REQUIRE 'REQ:RUNHAN.REQ';

REQUIRE 'REQ:SCA.REQ';

REQUIRE 'REQ:TSF.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!

EXTERNAL LITERAL
    RINTES : UNSIGNED (8);

!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    FLGT : FLAG_TABLE,
    GCA : GCA_DEFINITION,
    HCT : HCT_DEFINITION,
    IRA : FIXED_STRING,
    KHAR,
    MRA : REF FIXED_STRING,
    NUMPRM : NUMPRM_DEFINE,
    PAGEN : PAGE_DEFINITION,
    NPAGEN : PAGE_DEFINITION,
    PHAN : PHAN_DEFINITION,
    SBTMRA : FIXED_STRING,
    SBTTSF : VECTOR,
    SCA : SCA_DEFINITION,
    TTABLE : COUNTED_LIST,
    TITMRA : FIXED_STRING,
    TITTSF : VECTOR,
    TSF : TSF_DEFINITION;

EXTERNAL LITERAL 				!Error messages
    RNFTTL;

EXTERNAL ROUTINE
    ERMA,
    ENDWRD,
    RSKIPS,
    SCANT;


GLOBAL ROUTINE TITLES (HANDLER_CODE) : NOVALUE = 	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	Handler_code usually specifies which command is to be processed.
!	However, if its value is -1 then this routine is being called
!	by HL to process a header level 1.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	FIRST_XTN,
	LAST_XTN,
	HOLD_INDENT,
	HOLD_MRA,
	HOLD_TAB_COUNT,
	HOLD_TSF,
	SCA_HOLD : SCA_DEFINITION;

    SELECT .HANDLER_CODE OF
	SET

	[H_AUTOSUBTITLE] :
	    BEGIN
	    !Ignore command if numeric parameter was bad
	    IF					!
		NOT .NUM_RESULT
	    THEN				!
		RETURN;

	    IF					!
		.NUM_SIGN
	    THEN
		!User said .AUTOSUBTITLE +n or -n.
		!Adjust last value, whatever it was.
		NUM_VALUE = .GCA_MAX_ASTN + .NUM_VALUE
	    ELSE
		IF				!
		    .NUM_LENGTH EQL 0		!
		THEN
		    !User said .AUTOSUBTITLE with no value, so recall last value.
		    NUM_VALUE = .GCA_MAX_ASTN;

	    !Now, the effective value has been computed, so just store it.
	    GCA_AUTOSUBT = .NUM_VALUE;
	    GCA_MAX_ASTN = .NUM_VALUE;		!Remember for .NO AUTOSUBTITLE.
	    RETURN;
	    END;

	[H_NO_AUTOSUBTIT] :
	    BEGIN
	    GCA_AUTOSUBT = FALSE;
	    RETURN;
	    END;

	[H_DATE] :
	    BEGIN
	    HCT_DATE = TRUE;
	    RETURN
	    END;

	[H_NO_DATE] :
	    BEGIN
	    HCT_DATE = FALSE;
	    RETURN;
	    END;

	[H_NO_NUMBER] :
	    BEGIN
	    !Page numbering is turned off at the start of the next page, at the latest.
	    HCT_NMPG_NP = FALSE;
	    !At the top of the first page, or in the middle of a page, turn it
	    !off immediately.
	    IF
		.PHAN_TOP_FIRST
		OR NOT .PHAN_TOP_PAGE
	    THEN
		HCT_NUMBER_PAGE = FALSE;
	    RETURN;
	    END;

	[H_FIRST_TITLE] :
	    BEGIN
	    PHAN_HEADER = TRUE;			!Force header for this page.
	    RETURN;
	    END;

	[H_HEADERS_LOWER] :
	    BEGIN
	    HCT_HD_CASE = HCT_HD_LOWER;
	    !Set up case rules for 'index'
	    NPAGEN [SCT_INDEX_D] = TCONVRT_LET_LOW;
	    IF
		.PHAN_TOP_FIRST
	    THEN
		PAGEN [SCT_INDEX_D] = TCONVRT_LET_LOW;
	    RETURN;
	    END;

	[H_HEADERS_ON] :
	    BEGIN
	    HCT_HEADERS = .PHAN_CMD_PAGING;	!Turn on headers if not /NOPAGING
	    RETURN;
	    END;

	[H_HEADERS_MIXED] :
	    BEGIN
	    HCT_HD_CASE = HCT_HD_MIXED;
	    !Set case rules for 'index' ==> 'Index'
	    NPAGEN [SCT_INDEX_D] = TCONVRT_LET_MIX;
	    IF					!
		.PHAN_TOP_FIRST
	    THEN
		PAGEN [SCT_INDEX_D] = TCONVRT_LET_MIX;
	    RETURN;
	    END;

	[H_HEADERS_UPPER] :
	    BEGIN
	    HCT_HD_CASE = HCT_HD_UPPER;
	    !Set case rules for 'index' ==> 'INDEX'
	    NPAGEN [SCT_INDEX_D] = TCONVRT_LET_UPP;
	    IF						!
		.PHAN_TOP_FIRST
	    THEN
		PAGEN [SCT_INDEX_D] = TCONVRT_LET_UPP;
	    RETURN;
	    END;

	[H_NO_HEADERS] :
	    BEGIN
	    HCT_HEADERS = FALSE;
	    RETURN;
	    END;

	[H_TITLE, -1, H_SUBTITLE] :
	    BEGIN
	    !Preliminaries for both .TITLE and .SUBTITLE
	    FIRST_XTN = 0;				!Clear transaction numbers
	    LAST_XTN = 0;				!...

	    !Preserve and reset indentation so SCANT doesn't try to
	    !indent the title/subtitle.
	    HOLD_INDENT = .SCA_INDENT;
	    SCA_INDENT = 0;

	    !Remember current buffer status.
	    HOLD_TSF = .TSF;
	    HOLD_MRA = .MRA;
	    END;

	[H_TITLE] :
	    BEGIN
	    !Preliminaries for .TITLE

	    !Switch to title buffer and descriptor.
	    TSF = TITTSF;
	    MRA = TITMRA;
	    END;

	[-1, H_SUBTITLE] :
	    BEGIN
	    !Preliminaries for .SUBTITLE

	    !Switch to subtitle buffer and descriptor.
	    TSF = SBTTSF;
	    MRA = SBTMRA;
	    HCT_SUBTITLE = TRUE;		!Signal subtitles wanted.
	    END;

	[H_TITLE, -1, H_SUBTITLE] :
	    BEGIN
	    !Common processing for both commands

	    !Initialize title/subtitle descriptor.
	    INCR I FROM 0 TO TSF_SIZE - 1 DO
		TSF [.I] = 0;

	    !Initialize title/subtitle buffer
	    FS_INIT (MRA);

	    !Copy SCANT tables so we can restore them later.
	    INCR I FROM 0 TO SCA_SIZE - 1 DO
		SCA_HOLD [.I] = .SCA [.I];

	    !Reset those parts of the SCANT tables that are not inherited
	    !from the main body of the text.
	    SCA_FILL = FALSE;
	    SCA_JUSTIFY = FALSE;
	    SCA_RM = 150;				!Maximum right margin so SCANT won't start a new line.
	    SCA_LM = 0;					!No left margin, so title/subtitle is flush left.
	    SCA_WRD_INT_L = 0;				!Internal length is zero for now.
	    SCA_WRD_EXT_L = 0;				!External length is zero for now.
	    SCA_SPACING = 1;				!Don't let .SPACING stick to this text.
	    SCA_WRD_ISEQN = 0;
	    SCA_WRD_DRAFT = 0;
	    SCA_WRD_DRAFT_F = %C' ';
	    SCA_WRD_BARS = 0;
	    SCA_WRD_BAR_CHR = 0;
	    SCA_WRD_SEQN_F = 0;
	    SCA_WRD_IPAGEN = 0;
	    SCA_WRD_FOOTW = 0;
	    SCA_WRD_F_XTN = 0;
	    SCA_WRD_L_XTN = 0;
	    SCA_WRD_LC_PNCT = 0;
	    SCA_WRD_LST_SP = 0;
	    SCA_WRD_LST_JUS = 0;
	    SCA_WRD_LST_UND = 0;
	    SCA_WRD_CPEND = RINTES;				!No pending character right now.
	    SCA_WRD_PNTR = .FS_START (MRA);

	    !Preserve tab count; temporarily set it to zero
	    !so tabs get treated like spaces.
	    HOLD_TAB_COUNT = .TTABLE [CL_INDEX];
	    TTABLE [CL_INDEX] = 0;

	    !Skip all spaces and tabs before the actual text.
	    RSKIPS(IRA);

	    !Scan the remainder of this input line.
	    SCANT ();

	    !Drop trailing spaces, unless at least one of them
	    !is underlined.
	    !SCA_WRD_CPEND is equal to RINTES if a space/tab
	    !was encountered after the last character on the line.
	    IF 					!
		.SCA_WRD_CPEND EQL RINTES
	    THEN
		IF 				!
		    .SCA_WRD_LST_UND EQL 0
		THEN
		    !No pending underline character.
		    BEGIN
		    !When dropping trailing spaces, also set
		    !intra-line pointer and counter back appropriately.
		    FS_LENGTH (MRA) = .FS_LENGTH (MRA) - .SCA_WRD_LST_SP;
		    FS_NEXT (MRA) = CH$PLUS (.FS_NEXT (MRA), -.SCA_WRD_LST_SP);
		    !And now really do drop those trailing spaces.
		    SCA_WRD_LST_SP = 0;
		    END;

	    !
	    !Finish last word on line so that TSF contains the entire
	    !title or subtitle.
	    ENDWRD (FALSE, FALSE, FALSE);		!No filling, no justifying, no hyphenation.

	    !TSF and MRA now contain the complete text description and the
	    !text itself.

	    !Pick up transaction numbers to take care of an <INDEX flag>
	    !discovered during processing of a title or subtitle.
	    !If called as a subroutine (.HANDLER_CODE EQL -1)
	    !it's the caller's responsibility.
	    IF 					!
		.HANDLER_CODE NEQ -1
	    THEN
		BEGIN
		FIRST_XTN = .TSF_FIRST_XTN;
		LAST_XTN = .TSF_LAST_XTN;
		!Remove transaction numbers from title/subtitle
		TSF_FIRST_XTN = 0;
		TSF_LAST_XTN = 0;
		END;

	    !Check to see that the text retrieved is not too long
	    IF 					!
		.TSF_EXT_HL GTR .GCA_LWIDTH
		AND
		.HANDLER_CODE NEQ -1
	    THEN
		BEGIN
		!Title or subtitle is too wide.
		ERMA (RNFTTL, FALSE);

		!Forget everything that was collected.
		INCR I FROM 0 TO TSF_SIZE - 1 DO
		    TSF [.I] = 0;

		FS_INIT (MRA);
		END;

	    !Restore RUNOFF state prior to .TITLE/.SUBTITLE
	    TSF = .HOLD_TSF;
	    MRA = .HOLD_MRA;
	    SCA_INDENT = .HOLD_INDENT;

	    !If an <INDEX flag> was found, associate the transaction numbers
	    !with the primary text buffer.
	    IF 					!
		.TSF_FIRST_XTN EQL 0
	    THEN
		TSF_FIRST_XTN = .FIRST_XTN;

	    IF 					!
		.LAST_XTN NEQ 0
	    THEN
		TSF_LAST_XTN = .LAST_XTN;

	    !When restoring SCA, leave in effect, as side effects, ^^ and \\
	    !NOTE: This is support of a RUNOFF crock.
	    INCR I FROM SCA_CASE_SIZE TO SCA_SIZE - 1 DO
		SCA [.I] = .SCA_HOLD [.I];

	   !Restore status of tabs.
	    TTABLE [CL_INDEX] = .HOLD_TAB_COUNT;
	    RETURN;
	    END;

	[H_NO_SUBTITLE] :
	    BEGIN
	    HCT_SUBTITLE = FALSE;
	    RETURN;
	    END;
	TES;

    END;					!End of TITLES

END						!End of module

ELUDOM
