MODULE GETQCO (					!
		IDENT = 'V03-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY: RUNOFF document formatter
!
! ABSTRACT: Attempts to get a single character enclosed in quotes.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1979
!
! MODIFIED BY:
!
!--

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

REQUIRE 'REQ:BOOL.REQ';

REQUIRE 'REQ:FSPACK.REQ';

REQUIRE 'REQ:GETQSC.REQ';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    FS01 : FIXED_STRING;

!

EXTERNAL LITERAL 				!Error messages
    RNFMQS,
    RNFQST;

EXTERNAL ROUTINE
    ERMA,
    GETQS;


GLOBAL ROUTINE GETQC =

!++
! FUNCTIONAL DESCRIPTION:
!
!	GETQC attempts to get a quoted string that consists of precisely one character.
!	If such an animal is found, the character enclosed in quotes is returned.  If
!	no quoted character is found, the value -2 is returned.  The value -2 is returned
!	in the case of an error, in which case an error message will have been issued as well.
!	Note that if the user specifies an empty quoted string, the value 0 is returned.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	See above
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	GETQS_RESULT;

    !Initialize working storage.
    FS_INIT (FS01);
    !Attempt to get a quoted string.
    GETQS_RESULT = GETQS (FS01);

    !See if user did specify a quoted string.
    IF .GETQS_RESULT EQL GETQS_NONE
    THEN
    !No quoted string found.
	RETURN -1;

    IF 						!
	(.GETQS_RESULT EQL GETQS_TOO_LONG)	!
	OR (.FS_LENGTH (FS01) GTR 1)		!User can specify only a single character.
    THEN
	BEGIN
	ERMA (RNFQST, FALSE);
	RETURN -2
	END
    ELSE 					!

	IF 					!
	    .GETQS_RESULT EQL GETQS_BAD
	THEN
	!Something's wrong with the quoted string.
	    BEGIN
	    ERMA (RNFMQS, TRUE);
	    RETURN -2
	    END
	ELSE 					!
	!The user has correctly specified a single quoted character.
	    BEGIN

	    LOCAL
		X;

	    !Return the quoted character

	    IF .FS_LENGTH (FS01) EQL 0
	    THEN
	    !User input an empty quoted string.
		RETURN 0;

	    X = .FS_START (FS01);
	    RETURN CH$RCHAR (.X)
	    END

    END;					!End of GETQC

END						!End of module

ELUDOM
