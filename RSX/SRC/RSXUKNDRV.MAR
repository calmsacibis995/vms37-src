	.TITLE	RSXUKNDRV - VAX/VMS RSX11M AME UNKNOWN DEVICE DRIVER
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************

;++
; FACILITY:
;
;	VAX/VMS RSX11M AME
;
; ABSTRACT:
;
;	UNKNOWN DEVICE DRIVER
;
; AUTHOR: R.HEINEN 4-NOV-76
;
; MODIFIED BY:
;
;	102	TMH0002		T. Halvorsen	10-Mar-1980
;		Do not wipe register r0 in previous change so
;		that a garbage value is not returned for i/o status.
;
;	101	TMH0001		T. Halvorsen	15-Jan-1980
;		Map RSX P1 thru P6 directly into the low word of
;		the corresponding VMS P1 thru P6 so that a native
;		mode driver has access to all the RSX parameters.
;--
;
; EXTERNAL SYMBOLS
;
	$RSXDEF			; DEFINE RSX SYMBOLS

	.SBTTL	RSX$UKN_DRV - RSX11M AME UNKNOWN DEVICE DRIVER
 
;++
	.PSECT	RSXCODE,NOWRT,SHR,PIC
; RSX$UNK_DRV - RSX11M AME UNKNOWN DEVICE DRIVER 
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE PROCESSES I/O DIRECTED TO AN UNKNOWN DEVICE.
;
; INPUTS:
;
;	R0 = SUCCESS (PRESET)
;	R2 = ADDRESS OF THE QIO CONTEXT BLOCK
;	R3 = MAJOR FUNCTION CODE
;	R5 = ADDRESS OF THE DPB AREA
;	R6 = ADDRESS OF THE IMAGE STACK
;	R7 = ENTIRE FUNCTION CODE
;	R8 = ADDRESS OF THE LUT
;	R10 = ADDRESS OF THE IMAGE IMPURE SECTION
;	R11 = ADDRESS OF THE IMAGE
;
; OUTPUTS:
;
;	R2,R6,R8,R10,R11 ARE PRESERVED
;--
RSX$UKN_DRV::				; UNKNOWN DEVICE DRIVER
	MOVAB	I_SCRATCH+28(R10),R4	; ADDRESS OF VMS P1
	MOVZWL	(R5)+,(R4)+		; RSX P1 - VMS P1
	MOVZWL	(R5)+,(R4)+		; RSX P2 - VMS P2
	MOVZWL	(R5)+,(R4)+		; RSX P3 - VMS P3
	MOVZWL	(R5)+,(R4)+		; RSX P4 - VMS P4
	MOVZWL	(R5)+,(R4)+		; RSX P5 - VMS P5
	MOVZWL	(R5)+,(R4)+		; RSX P6 - VMS P6
	CASE	R3,<-			;
		RSX$IOKILL,-		; I/O KILL
		UKN_WLB,-		; WRITE LOGICAL BLOCK
		UKN_RLB,-		; READ LOGICAL BLOCK
		RSX$DLVRQIOAST,-	; ATTACH DEVICE
		RSX$DLVRQIOAST,-	; DETACH DEVICE
		RSX$IEIFC,-		; FUNCTION CODE 5
		RSX$IEIFC-		; FUNCTION CODE 6
		RSX$IEIFC,-		; ILLEGAL
		RSX$IEIFC,-		; ILLEGAL
		RSX$DLVRQIOAST,-	; FUNCTION CODE 9
		RSX$IEIFC,-		; ILLEGAL
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		UKN_RLB,-		; READ VIRTUAL BLOCK
		UKN_WLB,-		; WRITE VIRTUAL BLOCK
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		RSX$DLVRQIOAST,-	; ACP FUNCTIONS
		>
	BRW	RSX$IEIFC		; ERROR EXIT

	.SBTTL	READ VIRTUAL FUNCTION
;
; READ VIRTUAL BLOCK
;
UKN_RVB:				; READ VIRTUAL BLOCK
	MOVZWL	#IO$_READVBLK,I_SCRATCH+12(R10); SET FUNCTION CODE
	BRW	RSX$ISSUQIO		; PERFORM THE I/O
;
; READ LOGICAL BLOCK
;
UKN_RLB:				; READ LOGICAL BLOCK
	MOVZWL	#IO$_READLBLK,I_SCRATCH+12(R10); SET THE FUNCTION CODE
	BRW	RSX$ISSUQIO		; PERFORM THE FUNCTION
;
; WRITE VIRTUAL BLOCK
;
UKN_WVB:				; WRITE VIRTUAL BLOCK
	MOVZWL	#IO$_WRITEVBLK,I_SCRATCH+12(R10); SET FUNCTION CODE
	BRW	RSX$ISSUQIO		; PERFORM THE I/O
;
; WRITE LOGICAL BLOCK
;
UKN_WLB:				; WRITE LOGICAL BLOCK
	MOVZWL	#IO$_WRITELBLK,I_SCRATCH+12(R10); SET THE FUNCTION CODE
	BRW	RSX$ISSUQIO		; PERFORM THE FUNCTION
	.END
