$ ! 
$ ! [RSX.COM] RSXBLD.COM - BUILD VAX/VMS RSX11M AME
$ !
$ !	ASSUMES:
$ !
$ !	LIB$:RSXMAC.SML,EXEMC.MLB
$ !
$ GOTO 'PHASE'
$
$INIT:
$ !
$ !	CREATE DIRECTORIES
$ ! 
$ @SYSBLDCOM$:CREATEDIR
$ EXIT
$
$UPDATE:
$ !
$ !	Create updated sources.
$ !
$ DELETE SRC$:*.*;*
$ SRCUPDATE RSXSYMBOL.MAR
$ SRCUPDATE RSXMSG.MSG
$ EXIT
$
$SRCLIB:
$ ! 
$ !	DELETE AND RECREATE THE MACRO LIBRARY "LIB$:R.MLB"
$ ! 
$ DELSHRLIB LIB$:R.MLB
$ DELETE LIB$:RSX11MAC.MAR;*
$ MESSAGE/MDL=SRC$:RSXMSG/NOOBJ/NOLIST SRC$:RSXMSG
$ MCR MDL LIB$:RSXMSG=SRC$:RSXMSG
$ MCR LBR LIB$:R1.TMP=SYS$LIBRARY:RSXMAC.SML/EX:QIOSY$:IOERR$:SPCIO$:FILIO$
$ MCR LBR LIB$:R2.TMP=SYS$LIBRARY:RSXMAC.SML/EX:DEF$L:DEF$I:FCSBT$
$ MCR LBR LIB$:R3.TMP=SYS$LIBRARY:RSXMAC.SML/EX:NBOFF$:TTSYM$:DRERR$
$ MCR LBR LIB$:R4.TMP=SYS$LIBRARY:EXEMC.MLB/EX:HDRDF$:LBLDF$
$ COPY LIB$:*.TMP LIB$:RSX11MAC.MAR
$ DELETE LIB$:*.TMP;*
$ !
$ ! CONVERT MACRO SOURCE FROM OCTAL DEFAULT TO DECIMAL
$ !
$ MCR TECO @COM$:RSXCVTMAC
$ PURGE LIB$:RSX11MAC.MAR
$ LIB/MAC/CREATE LIB$:R SRC$:RSXSYMBOL,LIB$:RSX11MAC,RSXMSG
$ CPYSHRLIB LIB$:R.MLB			! Share file with SYS
$ EXIT
$
$ASSEM:
$ !
$ !	DELETE OBJECTS AND LISTING FILES, THEN ASSEMBLE THE AME
$ !
$ DELETE OBJ$:*.OBJ;*,LIS$:*.LIS;*
$ @SYSBLDCOM$:FASM 'FACNAM' "" NOLIB 'UPDATES'
$ EXIT
$
$OBJLIB:
$ ! 
$ !	CONCATENATE OBJECTS AND BUILD OBJECT LIBRARY
$ ! 
$ @SYSBLDCOM$:OBJLIB RSX
$ EXIT
$
$LNKLIB:
$ !
$ ! Null LNKLIB phase
$ !
$ EXIT
$
$LINK:
$ !
$ !	LINK RSXUSR.EXE
$ !
$ DELETE EXE$:RSXUSR.EXE;*, MAP$:RSXUSR.MAP;*
$ LINK /SHARE=EXE$:RSXUSR /MAP=MAP$:RSXUSR /FULL /CROSS -
	/NOSYSSHR /NOTRACE -
	LIB$:RSX/INCLUDE=RSXUNKNOWN,-	! UNKNOWN DIRECTIVE TEMPLACE
	SYS$INPUT/OPTIONS
GSMATCH=LEQUAL,2,0			! MATCH CONTROL, RELEASE 2
BASE=%X21000				! MUST BE LAST CLUSTER
					! TO BE REPLACABLE IN THE FIELD
$ CPYRESLNK EXE$:RSXUSR.EXE
$ !
$ !	LINK RSXSHR.EXE
$ !
$ DELETE EXE$:RSXSHR.EXE;*,MAP$:RSXSHR.MAP;*
$ LINK /SHARE=EXE$:RSXSHR /MAP=MAP$:RSXSHR /FULL /CROSS -
	/NOTRACE /NOSYSSHAR -
	LIB$:RSX/INCLUDE=( -
		RSXALUN,RSXASTSRV,RSXDATAS,RSXEFN,RSXEXCEPTION,-
		RSXFILEIO,RSXFLOAT,RSXINFO,RSXIO,RSXPARSE,-
		RSXPMD,RSXQIO,RSXSTART,RSXSUBROUTINES,RSXTERMIN,-
		RSXTRAP,RSXTSKCTL,-
		RSXCRDRV,RSXDSKDRV,RSXLPDRV,RSXMTDRV,RSXRMSDRV,-
		RSXTTYDRV,RSXUKNDRV,RSXMSG),-
	SYS$INPUT/OPTIONS
RESLNK$:RSXUSR/SHARE
GSMATCH=LEQUAL,2,0			! MATCH CONTROL, RELEASE 2
BASE=%X10000				! HOLE FOR RSX ADDRESS SPACE
$ CPYRESLNK EXE$:RSXSHR.EXE
$ !
$ !	LINK RSX.EXE
$ !
$ DELETE EXE$:RSX.EXE;*,MAP$:RSX.MAP;*
$ LINK /NOSYSSHR /EXE=EXE$:RSX/NOTRACE/MAP:MAP$:RSX/FULL/CROSS -
	SYS$INPUT/OPTIONS
CLUSTER=RSX,%X17000,,LIB$:RSX/INCLUDE=RSXMAIN ! BACKTRANS MUST ALSO BE AT %X17000
RESLNK$:RSXSHR/SHARE
$ EXIT
$
$RESULT:
$ ! 
$ !	COPY RSX.EXE AND RSX.MAP TO SYSTEM AREA
$ ! 
$ CPYSYSLIB EXE$:RSXSHR.EXE
$ CPYSYSLIB EXE$:RSXUSR.EXE
$ CPYRESEXE EXE$:RSX.EXE
$ CPYRESMAP MAP$:RSX.MAP
$ !
$ !	BUILD RSX.LSS
$ !
$ CPYRESLIS SRC$:RSXSYMBOL.MAR,LIS$:*.LIS RSX.LSS
