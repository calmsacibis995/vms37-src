	.NLIST
;	Version 'V03-000'
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; ALTERED:
; EDWIN H. MARISON  11 MAY 76  15:39
;STRUCTURED PROGRAMMING MACROS
;
;	** SUPMAC **
;
;;;;;	V0043
;
.MACRO	SMACIT
	.MCALL	SMCALL
	$SUPMC=43
	$DIDDO=0
	$EF$=0
	$L$=0
	$T=0
	$L=0
	$R=-1
	IIII=-1
	SMCALL			; DEFINE SUPMAC
.ENDM
;
.MACRO	SMCALL
	.MCALL	..PUSH,..POP,..GET,..TAG,..BRAN,.EMIT,.EMITL,.EMITR
	.MCALL	.IFOPR,.IS,.GENBR,.OPADD,.OPSUB
	.MCALL	RNE,REQ,RLT,RGE,RGT,RLE,RPL,RMI,RHI,RLOS,RHIS,RLO,RCS,RCC
	.MCALL	IF,.OR,.LEAVE,.GOTO,$SET$K$,OR,AND,DO,THEN,ELSE
	.MCALL	WHILE,CASE,FOR,TO,DOWNTO,FOR.ABS,REPEAT,UNTIL,THRU
	.MCALL	END,BEGIN,$$END,LEAVE,JUMPTO,GOTO,PUSH,POP,$CALL,RETURN
	.MCALL	LET,.SIMPLE,.ARITH
	.MCALL	ORB,ANDB,IFB,UNTILB,WHILEB,ON.ERROR,ON.NOERROR,FI
	.MACRO	SMCALL
	.ENDM
.ENDM	SMCALL
;
;
.MACRO	..PUSH	AA1,AA2
	IIII=IIII+1
	.IRP	TT,\IIII
		TYPS'TT = AA1
		LBLS'TT = AA2
	.ENDM
.ENDM
.MACRO	..POP	AA1,AA2
	.IF LT IIII
	.LIST
	.ERROR  ;STACK UNDERFLOW;
	$Y$=IIII
	.NLIST
	.MEXIT
	.IFF
	.IRP	TT,\IIII
		AA1 = TYPS'TT
		AA2 = LBLS'TT
	.ENDM
	.ENDC
	IIII=IIII-1
.ENDM
;
.MACRO	..GET	PPP,AA1,AA2
	.IRP	TT,\PPP
		AA1=TYPS'TT
		AA2=LBLS'TT
	.ENDM
.ENDM	..GET
;
.MACRO	..TAG	TG,WH
.EMITL	<WH''TG':>
.ENDM
;
.MACRO	..BRAN	BRA,TG,WH
.EMIT	<BRA WH''TG>
.ENDM
;
.MACRO	.EMIT	VAL
$E$=1
	.IIF	DF	LST$$	.LIST MEB
								VAL
	.IIF	DF	LST$$	.NLIST MEB
.ENDM
;
.MACRO	.EMITL	VAL
	.IIF	DF	LST$$	.LIST
							VAL
	.IIF	DF	LST$$	.NLIST
.ENDM
;
.MACRO	.EMITR	S1,S2
	.EMIT	<S1>
	.IIF	GT	S2-1	.EMITR	<S1>,\S2-1
.ENDM

.MACRO	.IFOPR	OPRA,RELA,TTG
	.EMIT	OPRA
	.IF	GT	$R
		.IIF	EQ	$L$	.EMIT	<B'RELA L'TTG>
		.IIF	EQ	$L$-1	.EMIT	<B'RELA B'TTG>
		.IIF	EQ	$L$-2	.EMIT	<B'RELA E'TTG>
		.IIF	EQ	$L$-3	.EMIT	<B'RELA TTG>
	.IFF
		.IIF	EQ	$L$	R'RELA	L'TTG
		.IIF	EQ	$L$-1	R'RELA	B'TTG
		.IIF	EQ	$L$-2	R'RELA	E'TTG
		.IIF	EQ	$L$-3	R'RELA	TTG
	.ENDC
	..PUSH	0,$T
	$I$=1
.ENDM
.MACRO	.IS	Q1,Q2,QB,QT
	.IIF	DIF	<RESULT>,<Q1>,.ERROR ; IF SYNTAX ERROR Q1 IS;
	.IIF	IDN	<RESULT>,<Q1>,.IFOPR <<.=.>>,Q2,QT
.ENDM
;
.MACRO	.GENBR	A1,B1,C1,D1,E1

$I$=0
.IIF	IDN	<SET.IN>	<B1>	.IFOPR <<BIT'D1 A1,C1>>,NE,E1
.IIF	IDN	<OFF.IN>	<B1>	.IFOPR <<BIT'D1 A1,C1>>,EQ,E1
.IIF	IDN	<#0>		<C1>	.IFOPR <<TST'D1 A1>>,B1,E1
.IIF	IDN	<#0>		<A1>	.IFOPR <<TST'D1 C1>>,B1,E1
.IIF	IDN	<IS>		<B1>	.IS A1,C1,D1,E1
.IIF	EQ	$I$			.IFOPR <<CMP'D1 A1,C1>>,B1,E1
.ENDM




.MACRO	.OPADD	V1,V2,V3
	.IIF	IDN	<V2>	<#1>	.EMIT	<INC'V3 V1>
	.IIF	NE	$E$		.MEXIT
	.IIF	IDN	<V2>	<CARRY>	.EMIT	<ADC'V3 V1>
	.IIF	NE	$E$		.MEXIT

	.EMIT	<ADD'V3 V2,V1>
.ENDM
;
;
.MACRO	.OPSUB	V1,V2,V3
	.IIF	IDN	<V2>	<#1>	.EMIT	<DEC'V3 V1>
	.IIF	NE	$E$		.MEXIT
	.IIF	IDN	<V2>	<CARRY>	.EMIT	<SBC'V3 V1>
	.IIF	NE	$E$		.MEXIT

	.EMIT	<SUB'V3 V2,V1>
.ENDM

.MACRO	RNE	LOC
.EMIT	<BEQ LOC>
.ENDM
.MACRO	REQ	LOC
.EMIT	<BNE LOC>
.ENDM
.MACRO	RLT	LOC
.EMIT	<BGE LOC>
.ENDM
.MACRO	RGE	LOC
.EMIT	<BLT LOC>
.ENDM
.MACRO	RGT	LOC
.EMIT	<BLE LOC>
.ENDM
.MACRO	RLE	LOC
.EMIT	<BGT LOC>
.ENDM
.MACRO	RPL	LOC
.EMIT	<BMI LOC>
.ENDM
.MACRO	RMI	LOC
.EMIT	<BPL LOC>
.ENDM
.MACRO	RHI	LOC
.EMIT	<BLOS LOC>
.ENDM
.MACRO	RLOS	LOC
.EMIT	<BHI LOC>
.ENDM
.MACRO	RHIS	LOC
.EMIT	<BLO LOC>
.ENDM
.MACRO	RLO	LOC
.EMIT	<BHIS LOC>
.ENDM
.MACRO	RCS	LOC
.EMIT	<BCC LOC>
.ENDM
.MACRO	RCC	LOC
.EMIT	<BCS LOC>
.ENDM


.MACRO	IF	A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z,BB
	.IIF	B	<A>	.MEXIT
$O$=0
$I$=0
	.IF	IDN	<LEAVE> <D>
	.IIF	NB	<EE>	.LEAVE	A,BE,C,BB,EE
	.ENDC
	.IIF	NE	$I$		.MEXIT
	.IF	IDN	<GOTO>	<D>
	.IIF	NB	<EE>	.GOTO	A,BE,C,BB,EE
	.ENDC
	.IIF	NE	$I$		.MEXIT
	.IIF	IDN	<D>	<OR>	.OR	A,BE,C,BB
	.IIF	IDN	<D>	<ORB>	.OR	A,BE,C,BB
	.IF EQ	$O$
		.IIF EQ $L$-1	.GENBR A,BE,C,BB,\$T2
		.IIF NE $L$-1	.GENBR A,BE,C,BB,\$T
	.ENDC
$T=$T+1
	.IIF	B	<D>,.MEXIT
;
	D	EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	.IIF	LE	$DIDDO	.MEXIT
..POP	$$T,$$T
..TAG	\$$T,L
$DIDDO=$DIDDO-1
.ENDM

.MACRO	.OR	OA,OB,OC,OD
	$R=-$R
	$O$=1
	$SV$=$L$
	$L$=0
 .IIF NE $L$-1   	.GENBR	OA,OB,OC,OD,\$T
 .IIF EQ $L$-1   	.GENBR	OA,OB,OC,OD,\$T2
	$L$=$SV$
	$R=-$R
.ENDM
;
;
;
.MACRO	.LEAVE	LA,LB,LC,LD,LE
	$L$=2
	.IIF	IDN	<LE>	<LOOP>	$SET$K$	IIII
	.IIF	DIF	<LE>	<LOOP>	$K$=LE
	$R=-$R
	.GENBR	LA,LB,LC,LD,\$K$
	..POP	$$TT,$$TT
	$R=-$R
	$L$=0
.ENDM
;
;
.MACRO	.GOTO	GA,GB,GC,GD,GEE
	$R=-$R
	$L$=3
	.GENBR	GA,GB,GC,GD,GEE
	..POP	$$TT,$$TT
	$R=-$R
	$L$=0
.ENDM
;
;
.MACRO	$SET$K$	BBB
	$K$L=-1
	$K$=-1
	.IF	LT	BBB
		.ERROR	; NO LOOP BLOCK;
		.MEXIT
	.ENDC
	..GET	BBB,$K$T,$K$L
	$XXX$ = 0
	.IIF	EQ	$K$T-1	$XXX$ = 1 	;WHILE
	.IIF	EQ	$K$T-2	$XXX$ = 1 	;FOR
	.IIF	EQ	$K$T-5	$XXX$ = 1 	;REPEAT
	.IIF	EQ	$K$T-6	$XXX$ = 1 	;THRU
	.IIF	EQ	$XXX$	$SET$K$	BBB-1
	$K$=$K$L
.ENDM	$SET$K$

.MACRO	OR	EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z,BB
	.IIF	B	<EE>	.MEXIT
	.IIF	IDN	<H>	<OR>	..POP	$T1,$T
	.IIF	IDN	<H>	<ORB>	..POP	$T1,$T
	IF'BB	EE,F,G,H
	.IF	DIF	<H>	<OR>
	.IF	DIF	<H>	<ORB>
	..POP	$$TT,$$TT
	..POP	$$T,$$T
	$$S=0
	.IIF	IDN	<H>,<AND>,$$S=1
	.IIF	IDN	<H>,<ANDB>,$$S=1
	.IIF	IDN	<H>,<GOTO>,$$S=2
	.IIF	IDN	<H>,<LEAVE>,$$S=2
	.IF	NE	$$S
		.IF	EQ,$$S-1
			..PUSH	0,$$TT
			..TAG	\$$T,L
			.ERROR	; AND (ANDB) SEEN AFTER AN OR (ORB);
			.MEXIT
		.ENDC
		..TAG	\$$T,L
		THEN	H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	.IFF
	..TAG	\$$T,L
	.IIF	B	<H>	..PUSH	0,$$TT
	.IIF	B	<H>	.MEXIT
	H	I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	.ENDC
	.IF	LE	$DIDDO
	..PUSH 0,$$TT
	.IFF
	..TAG	\$$TT,L
	$DIDDO=0
	.ENDC
	.MEXIT
	.ENDC
	.ENDC
	H	I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
.ENDM
;
;
.MACRO	AND	EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z,BB
	.IIF	B	<EE>	.MEXIT
	$$S=0
	.IIF	IDN	<ORB>,<H>,$$S=1
	.IIF	IDN	<OR>,<H>,$$S=1
	.IF	GT	$$S
	..POP	$$T,$$T
	IF'BB	EE,F,G,H
	..TAG	\$$T,L
	H	I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	.IFF
	..POP	$T,$T
	IF'BB	EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	$$S=0
	.IIF	IDN	<H>,<LEAVE>,$$S=1
	.IIF	IDN	<H>,<GOTO>,$$S=1
	.IF	NE	$$S
	..TAG	\$T,L
	$T=$T+1
	.ENDC
	.ENDC
.ENDM
;
;

.MACRO	DO	EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	.IIF	B	<EE>,.MEXIT
		LET EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	$DIDDO=$DIDDO+1
.ENDM
;
;
.MACRO	THEN EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	.IIF	B	<EE>	.MEXIT
	EE F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	$DIDDO=$DIDDO+1
.ENDM

.MACRO	ELSE
	..POP	$T1,$T2
	.IF	NE	$T1
	.ERROR		; $T1 ELSE SEEN IN OTHER THAN IF BLOCK;
	..PUSH	$T1,$T2
	.MEXIT
	.ENDC
	..BRAN	BR,\$T,L
	..TAG	\$T2,L
	..PUSH	$T1,$T
	$T=$T+1
.ENDM

.MACRO	WHILE A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,BB
	$DIDDO=-1
	..TAG	\$L,B
 IF A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,BB
	.IF	EQ	$DIDDO
		..BRAN	BR,\$L,B
		..POP	$$T,$$T
		..TAG	\$$T,L
	.IFF
		..PUSH	1,$L
	.ENDC
	$DIDDO=0
	$L=$L+1
.ENDM

.MACRO	CASE	A,BBB,C
	.IIF	DIF	<A>	<TOP>	.EMIT	<MOV A,-(SP)>
	.EMIT	<ASL @SP>
	.IRP	TT,\$T
	.EMIT	<JSR PC,L'TT>
		.ENDM
	..PUSH	4,$T
	$T=$T+1
.ENDM


.MACRO	FOR	A,BBB,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,MG
 .IIF DIF <F>,<BY>,FOR A,BBB,C,D,EE,BY,#1,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,MG
 .IIF DIF <F>,<BY>,.MEXIT
;
	.SIMPLE	A,BBB,C
	..TAG	\$L,B
	.IF B <H>
		..PUSH	2,$L
		.IRP	TT,\<$L>
			.MACRO	$.$'TT
				D	A,EE,G,TT,MG
			.ENDM
		.ENDM
	.IFF
		$SDO=$DIDDO
		H I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z
		D A,EE,G,\$L
		.IIF GT $DIDDO-$SDO $DIDDO=$DIDDO-1
	.ENDC
	$L=$L+1
.ENDM


.MACRO	TO	A1,A2,A3,A4,MG
	$E$=0
	.OPADD	A1,A3
	.EMIT	<CMP A1,A2>
	.IF	IDN	<MG>	<ABS>
	.EMIT	<BLO B'A4>
	.IFF
	.EMIT	<BLT B'A4>
	.ENDC
	.EMITL	<E'A4:>
.ENDM



.MACRO	DOWNTO	A1,A2,A3,A4,MG
	$E$=0
	.OPSUB	A1,A3
	.EMIT	<CMP A1,A2>
	.IF	IDN	<MG>	<ABS>
	.EMIT	<BHI B'A4>
	.IFF
	.EMIT	<BGT B'A4>
	.ENDC
	.EMITL	<E'A4:>
.ENDM

.MACRO	FOR.ABS	A,BBB,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z
	FOR	A,BBB,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,ABS
.ENDM	FOR.ABS

.MACRO	REPEAT	A,BB,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z
	..TAG	\$L,B
	..PUSH	5,$L
	$L=$L+1
	.IIF	B	<A>	.MEXIT
	.IF	IDN	<A>	<UNTILB>	
	UNTILB BB,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z
	.MEXIT
	.ENDC
	.IF	IDN	<A>	<UNTIL>	
	UNTIL BB,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z
	.MEXIT
	.ENDC
	LET A,BB,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z
.ENDM



.MACRO	UNTIL  A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z,BB
	$L$=1
	..POP	$T1,$T2
	.IF	NE	$T1-5
	.ERROR	; UNTIL SEEN IN OTHER THAN REPEAT BLOCK;
	.MEXIT
	.ENDC
	IF A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z,BB
	..POP	$$T,$$T
	..TAG	\$T2,E
	$L$=0
.ENDM


.MACRO	THRU	A,BB,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V
	.NTYPE	A3,A
	.IF	NE	A3&70
	.ERROR	; A MUST BE A REGISTER;
	.MEXIT
	.ENDC
	.IIF	NB	<BB>,LET A,BB,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V
	..TAG	\$L,B
	..PUSH	6,$L
	.IRP	TT,\$L
	.MACRO	$.$'TT
	.EMIT	<SOB A,B'TT>
	.ENDM
	.ENDM
	$L=$L+1
.ENDM

.MACRO	END   COMM
	..POP	$T1,$T2
	.IF	EQ	$T1
	.IIF	EQ	$EF$-1	.PRINT	; MISSING END - IF;
	..TAG	\$T2,L			;-0- 	IF
	.MEXIT
	.ENDC
;
	.IF	EQ	$T1-3
	.IIF	EQ	$EF$-1	.PRINT	; MISSING END - BEGIN;
	..TAG	\$T2,E			;-3-	BEGIN/END
	.MEXIT
	.ENDC
;
	.IF	EQ	$T1-5
	.IIF	EQ	$EF$-1	.PRINT	; MISSING END - REPEAT;
	..BRAN  BR,\$T2,B		;-5-	REPEAT
	..TAG	\$T2,E		
	.MEXIT
	.ENDC
;
	.IF	EQ	$T1-1			;-1-	WHILE
	.IIF	EQ	$EF$-1	.PRINT	; MISSING END - WHILE;
		..BRAN	BR,\$T2,B
		..POP	$$T,$$T
		..TAG	\$$T,L
		..TAG	\$T2,E
		.MEXIT
	.ENDC
;
	.IF	EQ	$T1-2			;-2-	FOR
	.IIF	EQ	$EF$-1	.PRINT	; MISSING END - FOR;
		.IRP	TT,<\$T2>
			$.$'TT
		.ENDM
	.MEXIT
	.ENDC
;
	.IF	EQ	$T1-4			;-4-	CASE
	.IIF	EQ	$EF$-1	.PRINT	; MISSING END - CASE;
		..TAG	\$T2,L
		.EMIT	<ADD (SP)+,@SP>
		.EMIT	<MOV @(SP)+,-(SP)>
		.EMIT	<JSR PC,@(SP)+>
		.MEXIT
	.ENDC
;
	.IF	EQ	$T1-6			; -6- THRU
	.IIF	EQ	$EF$-1	.PRINT	; MISSING END - THRU;
	.IRP	TT,\$T2
	$.$'TT
	..TAG	\$T2,E
	.ENDM
	.MEXIT
	.ENDC
;
	.ERROR		; $T STRANGE SUPER-MAC STACK VALUE;
.ENDM
;
.MACRO	BEGIN	BNAME
	.IIF NB	<BNAME>		BNAME=$L
	..PUSH	3,$L
	..TAG	\$L,B
	$L=$L+1
.ENDM

.MACRO	$$END
	.IF	GE	IIII
		$EF$=1
		END
		$EF$=0
		$$END
	.ENDC
.ENDM

.MACRO	LEAVE AAAA,A,BB,C,D,EE,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U
	.IIF	B	<AAAA>	.MEXIT
	.IIF	IDN	<AAAA>,<LOOP>,$SET$K$	IIII
	.IIF	DIF	<AAAA>,<LOOP>,$K$=AAAA
	.IRP	QQ,\$K$
	.EMIT	<BR E'QQ>
	.ENDM
.ENDM
;
;
.MACRO	JUMPTO	AAAA,A,B,C,D,E,F,H,G,I,J,K,L,M,N,O,P,Q,R,S,T,U
.EMIT	<JMP AAAA>
.ENDM
;
.MACRO	GOTO	BBBB,A,BB,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U
	.IIF	B	<BBBB>	.MEXIT
.EMIT	<BR BBBB>
.ENDM
;

.MACRO	PUSH	P0,P1,P2,P3,P4,P5,P6,P7,P8,P9,Q1,Q2,Q3,Q4,Q5,Q6,Q7,Q8,Q9,Q10,Q11,Q22
	.IF NB P0
	.EMIT	<MOV P0,-(SP)>
	PUSH	P1,P2,P3,P4,P5,P6,P7,P8,P9
	.ENDC
.ENDM
;
.MACRO	POP	P0,P1,P2,P3,P4,P5,P6,P7,P8,P9,Q0,Q1,Q2,Q3,Q4,Q5,Q6,Q7,Q8,Q9,Q22
	.IF NB P0
	.EMIT	<MOV (SP)+,P0>
	POP	P1,P2,P3,P4,P5,P6,P7,P8,P9
	.ENDC
.ENDM

.MACRO	$CALL	NAME,ARGS,WHERE,TYPE,A,BB,C,D,E,F,G,H,I,J,K,LL,M,N,O,P,Q,RR,S
; NOTE: **** WHERE AND TYPE NOT IMPLEMENTED ****
	.IIF	B	<ARGS>	.EMIT	<JSR PC,NAME>
	.IIF	B	<ARGS>	.MEXIT
	.IF	NB	WHERE
	.ERROR	; WHERE .... ARG'S MUST BE WITHIN <>;
	.MEXIT
	.ENDC
.$T=0
.MACRO	$GENM	BBT,BBP
	.IIF	GT	BBP-5	.ERROR	; 6 ARG MAX (R0-R5);
	.IIF	GT	BBP-5	.MEXIT
	.IIF	DIF	<BBT> <R'BBP>	.EMIT	<MOV BBT,R'BBP>
.ENDM
	.IRP	T,<ARGS>
	.IIF	NB	<T>	$GENM	T,\.$T
	.$T=.$T+1
	.ENDM
	.EMIT	<JSR PC,NAME>
.ENDM


.MACRO	RETURN	STA,REG,A,BE,C,D,E,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE
	.IF	NB	STA
	.IF	DIF	<WITH>,<STA>
	.IF	DIF	<ERROR>,<STA>
	.IF	DIF	<NOERROR>,<STA>
		.IF IDN <WORD>,<A>
		.EMIT	<MOV STA,-(SP)>
		.EMIT	<ROL (SP)+>
		.IFF
		.EMIT	<MOVB STA,-(SP)>
		.EMIT	<ROLB (SP)+>
		.ENDC
	.IFF
		.EMIT	<CLC>
	.ENDC
	.IFF
		.EMIT	<SEC>
	.ENDC
	.ENDC
	.ENDC
	.IIF	B	<REG>	.EMIT	<RTS PC>
	.IIF	NB	<REG>	.EMIT	<RTS REG>
.ENDM


.MACRO	LET	A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V
	.SIMPLE	A,BE,C,D,EE
	.IIF	B		<D>	.MEXIT
	.IIF	B		<EE>	.MEXIT
	.IIF	B		<F>	.MEXIT
	.IIF	IDN	<!>	<D>	LET EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V
	.IIF	IDN	<!>	<D>	.MEXIT
	.IIF	IDN	<!>	<EE>	LET F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V
	.IIF	IDN	<!>	<EE>	.MEXIT
	.IIF	IDN	<!>	<F>	LET G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V
	.IIF	IDN	<!>	<F>	.MEXIT
	.IF	LT	$Y$
		LET A,BE,A,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V
		.MEXIT
	.ENDC
		LET  A,BE,A,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V
.ENDM


.MACRO	.SIMPLE	X1,X2,X3,X4,X5,X6
	.IF	DIF	<X2>	<:=>
	.IF	DIF	<X2>	<:W=>
	.IF	DIF	<X2>	<:==>
	.IF	DIF	<X2>	<:B=>
	.ERROR	; X2 NOT A LEGAL ASSIGNMENT OPERATOR;
	.MEXIT
	.ENDC
	.ENDC
	.SIMPLE	X1,:=,X3,X4,X5,B
	.MEXIT
	.ENDC
	.ENDC

	.IIF	IDN	<X1>	<PUSH>	.SIMPLE	-(SP),X2,X3,X4,X5,X6
	.IIF	IDN	<X1>	<PUSH>	.MEXIT
	.IIF	IDN	<X1>	<TOP>	.SIMPLE	@SP,X2,X3,X4,X5,X6
	.IIF	IDN	<X1>	<TOP>	.MEXIT
$Y$=0
$Z$=0
	.IIF	IDN	<CARRY> <X1>	$Z$=-1
	.IIF	IDN	<C.BIT>	<X1>	$Z$=-1
	.IIF	IDN	<V.BIT>	<X1>	$Z$=-2
	.IIF	IDN	<OFF>	<X3>	$Y$=7
	.IIF	IDN	<FALSE>	<X3>	$Y$=7
	.IIF	IDN	<CLEARED> <X3>	$Y$=7
	.IIF	IDN	<0>	<X3>	$Y$=7
	.IIF	IDN	<#0>	<X3>	$Y$=7
	.IIF	IDN	<ON>	<X3>	$Y$=4
	.IIF	IDN	<SET>	<X3>	$Y$=4
	.IIF	IDN	<TRUE>	<X3>	$Y$=4
	.IF	LT	$Z$
	.IIF	EQ	$Z$+$Y$-6	.EMIT	<CLC>
 	.IIF	EQ	$Z$+$Y$-5	.EMIT	<CLV>
 	.IIF	EQ	$Z$+$Y$-3	.EMIT	<SEC>
 	.IIF	EQ	$Z$+$Y$-2	.EMIT	<SEV>
 	.MEXIT
	.ENDC
;
	.IIF	IDN	<NOT>	<X3>	$Y$=-1
	.IIF	IDN	<->	<X3>	$Y$=-2
	.IIF	IDN	<POP>	<X3>	$Y$=5
	.IIF	IDN	<TOP>	<X3>	$Y$=6
	.IIF	IDN	<PUSH>	<X3>	$Y$=10
  .IF	DIF	<X1> <X3>
	.IIF	EQ	$Y$-7		.EMIT	<CLR'X6 X1>
	.IF	DIF	<@SP>,<X1>
	.IIF	EQ	$Y$-6		.EMIT	<MOV'X6 @SP,X1>
	.ENDC
	.IIF	EQ	$Y$-5		.EMIT	<MOV'X6 (SP)+,X1>
	.IIF	EQ	$Y$-4		.EMIT	<MOV'X6 #-1,X1>
	.IIF	EQ	$Y$		.EMIT	<MOV'X6 X3,X1>
  .ENDC
  .IF	DIF	<X1> <X4>
	.IIF	LT	$Y$		.EMIT	<MOV'X6 X4,X1>
  .ENDC
	.IIF	IDN	<+1>	<X4>	$Y$=-5
	.IIF	IDN	<-1>	<X4>	$Y$=-6
	.IF	LT	$Y$
		.IIF	EQ	$Y$+1	.EMIT	<COM'X6 X1>
		.IIF	EQ	$Y$+2	.EMIT	<NEG'X6 X1>
		.IIF	EQ	$Y$+3	.EMIT	<TST'X6 X1>
		.IIF	EQ	$Y$+4	.EMIT	<SWAB X1>
		.IIF	EQ	$Y$+5	.EMIT	<INC'X6 X1>
		.IIF	EQ	$Y$+6	.EMIT	<DEC'X6 X1>
		.MEXIT
	.ENDC
;
	.IIF	B	<X4>		.MEXIT
	.IIF	IDN	<X4>	<!>	.MEXIT
	.IIF	IDN	<X4>	<IS>	.MEXIT
$E$=0
	.IIF	IDN	<X5>	<TOP>	.ARITH	X1,@SP,X6,X4
	.IIF	IDN	<X5>	<POP>	.ARITH	X1,(SP)+,X6,X4
	.IIF	EQ	$E$		.ARITH	X1,X5,X6,X4
.ENDM
;
.MACRO	.ARITH	Y1,Y2,Y3,Y4
	.IIF	IDN	<Y1>	<-(SP)>	.ARITH	@SP,Y2,Y3,Y4
	.IIF	IDN	<Y1>	<-(SP)>	.MEXIT
	.IIF	IDN	<+>	<Y4>	.OPADD	Y1,Y2,Y3
	.IIF	IDN	<->	<Y4>	.OPSUB	Y1,Y2,Y3
	.IIF	NE	$E$		.MEXIT
	.IIF	IDN	<NAND>	<Y4>	.EMIT	<BIC'Y3 Y2,Y1>
	.IIF	IDN	<OFF.BY> <Y4>	.EMIT	<BIC'Y3 Y2,Y1>
	.IIF	NE	$E$		.MEXIT
	.IIF	IDN	<SET.BY> <Y4>	.EMIT	<BIS'Y3 Y2,Y1>
	.IIF	IDN	<OR>	<Y4>	.EMIT	<BIS'Y3 Y2,Y1>
	.IIF	NE	$E$		.MEXIT
	.IIF	IDN	<L.SHIFT> <Y4>	.EMITR	<ASL'Y3 Y1>,Y2
	.IIF	IDN	<R.SHIFT> <Y4>	.EMITR	<ASR'Y3 Y1>,Y2
	.IIF	IDN	<L.ROTATE> <Y4>	.EMITR	<ROL'Y3 Y1>,Y2
	.IIF	IDN	<R.ROTATE> <Y4>	.EMITR	<ROR'Y3 Y1>,Y2
	.IIF	NE	$E$		.MEXIT
	.IIF	IDN	<*>	<Y4>	.EMIT	<MUL Y2,Y1>
	.IIF	IDN	</>	<Y4>	.EMIT	<DIV Y2,Y1>
	.IIF	IDN	<SHIFT>	<Y4>	.EMIT	<ASH Y2,Y1>
	.IIF	IDN 	<C.SHIFT> <Y4>	.EMIT	<ASHC Y2,Y1>
	.IIF	NE	$E$		.MEXIT
	.IIF	IDN	<XOR>	<Y4>	.EMIT	<XOR Y2,Y1>
	.IIF	EQ	$E$		.ERROR	; Y4 NOT A LEGAL OPERATOR;
.ENDM
;
;
;



.MACRO	ORB	EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	OR	EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z,B
.ENDM
;
.MACRO	ANDB	EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	AND	EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z,B
.ENDM
;
.MACRO	IFB	A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z
	IF	A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,TE,U,V,W,X,Y,Z,B
.ENDM
;
;
.MACRO	UNTILB A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z
	UNTIL	A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,B
.ENDM
;
.MACRO	WHILEB A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z
	WHILE	A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,B
.ENDM
;
.MACRO	ON.NOERROR	A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S
	IF RESULT IS,CC,A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S
.ENDM
;
.MACRO	ON.ERROR	A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S
	IF RESULT IS,CS,A,BE,C,D,EE,F,G,H,I,J,K,LL,M,N,O,P,Q,R,S
.ENDM
.MACRO	FI	COMM
	END	<COMM>
.ENDM
;
;

	.LIST
