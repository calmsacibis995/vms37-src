	IDENT	0002,DMPID,<DUMP ID AREA>
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; PAUL J KOBAR	10-AUG-78
;
; MODIFIED BY:
;
;	0002	ACG0153		Andrew C. Goldstein,	29-Feb-1980  20:17
;		Fix uses of MOVB to register
;
;+
; DMPID
;
; FORMAT AND PRINT THE INFORMATION FROM THE IDENTIFICATION AREA
;
; INPUT
;	R2 = ARGBLK ADDRESS
;	R5 = HEADER BLOCK BUFFER ADDRESS
;
;-

;
;	DEFINE TEXT FORMATS FOR $EDMSG (IN EDPUT)
;
	.NLIST	BEX
	PURE.D

;
;	DEFINE IDENTIFICATION AREA LABEL TEXT
;
ID0:	.ASCII /IDENTIFICATION AREA%N/
ID1:	.ASCII /	I.FNAM,%N/
ID12:	.ASCII /	I.FTYP,%N/
ID13:	.ASCII /	I.FVER		%3R.%R;%O%N/
ID2:	.ASCII /	I.RVNO		%O%N/
	.ASCIZ //
ID3:	.ASCII /	I.RVDT		%2A-%3A-%2A%N/
ID4:	.ASCII /	I.RVTI		%2A:%2A:%2A%N/
ID5:	.ASCII /	I.CRDT		%2A-%3A-%2A%N/
ID6:	.ASCII /	I.CRTI		%2A:%2A:%2A%N/
ID7:	.ASCII /	I.EXDT		%2A-%3A-%2A%N/
	.ASCIZ //


	.EVEN

;
	PURE.I

DMPID::
;
;IDENTIFICATION AREA
;	R4=ADDRESS OF ID AREA IN BLOCK BUFFER
;
;FORMAT NEXT RECORD
;	I.FNAM,I.FTYP,I.FVER,I.RVNO
;
	MOV	#ID0,R1
	CLR	R4
	BISB	H.IDOF(R5),R4	;ADJUST TO ID AREA
	ASL	R4
	ADD	R5,R4
	MOV	ARGBLK,R2
	MOV	I.FNAM(R4),(R2)+
	MOV	I.FNAM+2(R4),(R2)+
	MOV	I.FNAM+4(R4),(R2)+
	MOV	I.FTYP(R4),(R2)+
	MOV	I.FVER(R4),(R2)+
	MOV	I.RVNO(R4),(R2)+
	MOV	ARGBLK,R2
	CALL	EDPUT

;
;FORMAT NEXT RECORD
;	I.RVDT,I.RVTI,I.CRDT,I.CRTI,I.EXDT
;

	MOV	#ID3,R1
	MOV	#15.,R3		;SET LOOP FOR BYTE ADDR INIT
LID:	MOV	R4,(R2)+
	SOB	R3,LID
	MOV	ARGBLK,R2
	ADD	#I.RVDT,(R2)+		;PUT BYTE ADDR'S IN ARGBLK
	ADD	#I.RVDT+2,(R2)+
	ADD	#I.RVDT+5,(R2)+
	ADD	#I.RVTI,(R2)+
	ADD	#I.RVTI+2,(R2)+
	ADD	#I.RVTI+4,(R2)+
	ADD	#I.CRDT,(R2)+
	ADD	#I.CRDT+2,(R2)+
	ADD	#I.CRDT+5,(R2)+
	ADD	#I.CRTI,(R2)+
	ADD	#I.CRTI+2,(R2)+
	ADD	#I.CRTI+4,(R2)+
	ADD	#I.EXDT,(R2)+
	ADD	#I.EXDT+2,(R2)+
	ADD	#I.EXDT+5,(R2)+
	MOV	ARGBLK,R2
	CALL	EDPUT

	RETURN
	.END
