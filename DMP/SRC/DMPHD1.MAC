	IDENT	0001,DMPHD1,<DUMP HEADER AREA,PART 1>
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; PAUL J KOBAR	10-AUG-78
;
;+
; DMPHD1
;
; THIS ROUTINE FORMATS AND PRINTS OUT RECORDS CONTAINING
; HEADER AREA INFORMATION.
;
; INPUT
;	R2  = ARGBLK ADDRESS
;	R5 = HEADER BLOCK BUFFER ADDRESS
;
; OUTPUT
;	RECORDS TO OUTPUT FILE
;
;
	.NLIST	BEX
	PURE.D
;
;	DEFINE HEADER AREA LABEL TEXT
;
HDR0:	.ASCII /HEADER AREA%N/
HDR1:	.ASCII /	H.IDOF  	%B%N/
HDR2:	.ASCII /	H.MPOF		%B%N/
HDR31:	.ASCII /	H.FNUM,%N/
HDR32:	.ASCII /	H.FSEQ		(%O,%O)%N/
HDR4:	.ASCII /	H.FLEV		%O%N/
HDR5:	.ASCII /	H.FOWN		[%B,%B]%N/
	.ASCIZ //
HDR6:	.ASCII /	H.FPRO		[%VA]%N/
HDR71:	.ASCII /	H.UCHA		%B = %VA%N/
HDR72:	.ASCII /	H.SCHA		%B = %VA%N/
	.ASCIZ //

;
;	MORE ASCII TEXT STRINGS
;		NOTE - THESE STRINGS MUST REMAIN ADJACENT
;
UCHA1:	.ASCII / UC.CON!/
UCHA2:	.ASCII / UC.DLK /
SCHA1:	.ASCII / SC.MDL!/
SCHA2:	.ASCII / SC.BAD /
	.EVEN
;
	PURE.I
;
;HEADER AREA
;
DMPHD1::
;
;FORMAT A RECORD
;	H.IDOF,H.MPOF,H.FNUM,H.FSEQ,H.LEV,H.OWN
;
	MOV	#HDR0,R1	;SET FORMAT SOURCE
	MOV	ARGBLK,R2	;
	MOV	R5,(R2)		;MOVE H.IDOF
	ADD	#H.IDOF,(R2)+
	MOV	R5,(R2)
	ADD	#H.MPOF,(R2)+
	MOV	H.FNUM(R5),(R2)+	;FILE NUMBER
	MOV	H.FSEQ(R5),(R2)+	;FILE SEQUENCE
	MOV	H.FLEV(R5),(R2)+		;LEVEL
	MOV	R5,(R2)		;GROUP NUM
	ADD	#H.PROJ,(R2)+
	MOV	R5,(R2)		;MEMBER NUM
	ADD	#H.PROG,(R2)+
	MOV	ARGBLK,R2
	CALL 	EDPUT
;
;FORMAT NEXT RECORD
;	H.FPRO,H.UCHA,H.SCHA
;
;
	MOV	#HDR6,R1	;SET FORMAT ADDRESS
	MOV	R2,R0
	ADD	#20.,R0		;R0 IS 10 WORDS PAST R2
	MOV	R0,(R2)		;TO BE STRING COUNT
	MOV	R0,2(R2)	;STRING ADDR
	MOV	#4,R3		;LOOP COUNT
	MOV	H.FPRO(R5),R4	;PROTECTION WORD
PR1:	BIT	#FP.RDV,R4	;READ?
	BNE	PR2
	MOVB	#'R,(R0)+
PR2:	BIT	#FP.WRV,R4	;WRITE?
	BNE	PR3
	MOVB	#'W,(R0)+
PR3:	BIT	#FP.EXT,R4	;EXTEND?
	BNE	PR4
	MOVB	#'E,(R0)+
PR4:	BIT	#FP.DEL,R4	;DELETE?
	BNE	PRNEXT
	MOVB	#'D,(R0)+
PRNEXT:	MOVB	#',,(R0)+	;SEPARATOR

	.REPT	4
	ASR	R4		;SHIFT RIGHT 4
	.ENDR

	SOB	R3,PR1
	COM	(R2)
	ADD	R0,(R2)		;BYTE COUNT IN ARGBLK
UC0:	MOV	R5,4(R2)	;USER CHAR'TCS
	ADD	#H.UCHA,4(R2)	;H.UCHA BYTE ADDR IN ARGBLK
	CLR	6(R2)		;CLEAR COUNT
	MOV	#UCHA2,10(R2)	;PRE-SET STRING ADDR
UC1:	BITB	#UC.DLK,H.UCHA(R5)
	BEQ	UC2
	ADD	#8.,6(R2)	;UC.DLK BIT ON
UC2:	BITB	#UC.CON,H.UCHA(R5)
	BEQ	SC0
	MOV	#UCHA1,10(R2)	;UC.CON BIT ON
	ADD	#7,6(R2)

SC0:	MOV	R5,12(R2)	;SYSTEM	CHAR'TCS
	ADD	#H.SCHA,12(R2)	;H.SCHA BYTE ADDR IN ARGBLK
	CLR	14(R2)		;CLEAR COUNT
	MOV	#SCHA2,16(R2)	;PRE-SET STRING ADDR
SC1:	BITB	#SC.BAD,H.SCHA(R5)
	BEQ	SC2
	ADD	#8.,14(R2)	;SC.BAD BIT ON
SC2:	BITB	#SC.MDL,H.SCHA(R5)
	BEQ	SCEND
	MOV	#SCHA1,16(R2)	;SC.MDL BIT ON
	ADD	#7,14(R2)
SCEND:
	CALL	EDPUT
	RETURN
	.END
