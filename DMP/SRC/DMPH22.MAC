	IDENT	0001,DMPH22,<DUMP ODS2 HEADER AREA,PART 2>
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; PAUL J KOBAR	10-AUG-78
;
;+
; DMPH22
;
; FORMAT AND PRINT OUT RECORDS CONTAINING INFORMATION FROM
; THE HEADER AREA.
;
; INPUT
;	R2 = ARGBLK ADDRESS
;	R5 = HEADER BLOCK BUFFER ADDRESS
;
; OUTPUT
;	NONE
;
;-

;
;	DEFINE TEXT FORMATS FOR $EDMSG (IN EDPUT)
;
	.NLIST	BEX
;
; DEFINE ODS2 HEADER OFFSETS
;
	.MCALL	FHDL2$
;
	FHDL2$
;
	PURE.D
;
;	DEFINE HEADER AREA LABEL TEXT
;
HDR8:	.ASCII /	H.UFAT%N/
HDR81:	.ASCII /		F.RTYP	%B = %5A%N/
HDR82:	.ASCII /		F.RATT	%B = %VA%VA%VA%N/
	.ASCIZ //
HDR83:	.ASCII /		F.RSIZ	%O = %D.%N/
HDR85:	.ASCII /		F.HIBK	H:%O L:%P = %T.%N/
HDR86:	.ASCII /		F.EFBK	H:%O L:%P = %T.%N/
HDR87:	.ASCII /		F.FFBY	%O = %D.%N/
	.ASCIZ //
HDR88:	.ASCII /		(REST)%N/
HDR89:	.ASCII /		%P %P %P %P %P %P %P %P%N/
HDR8A:	.ASCII /		%P%N/
	.ASCIZ //


;
;	MORE ASCII TEXT STRINGS
;		NOTE - THESE STRINGS MUST REMAIN ADJACENT
;

RTYP0:	.ASCII /UNDEF/
RTYP1:	.ASCII /R.FIX/
RTYP2:	.ASCII /R.VAR/
RTYP3:	.ASCII /R.SEQ/

RATT0:	.ASCII / FD.FTN!/
RATT1:	.ASCII / FD.CR !/
RATT3:	.ASCII / FD.BLK /
	.EVEN
	PURE.I

DMPH22::
;
;FORMAT NEXT RECORD
;	H.UFAT(F.RTYP,F.RATT)
;
;

	MOV	#HDR8,R1
	MOV	R5,R0
	ADD	#H.UFAT,R0	;ADJUST FOR OFFSETS
	MOV	R0,(R2)
	ADD	#F.RTYP,(R2)	;F.RTYP ADDR
RT1:	CMPB	#1,F.RTYP(R0)
	BNE	RT2
	MOV	#RTYP1,2(R2)	;R.FIX
	BR	RTEND
RT2:	CMPB	#2,F.RTYP(R0)
	BNE	RT3
	MOV	#RTYP2,2(R2)	;R.VAR
	BR	RTEND
RT3:	CMPB	#3,F.RTYP(R0)
	BNE	RTBAD
	MOV	#RTYP3,2(R2)	;R.SEQ
	BR	RTEND
RTBAD:	MOV	#RTYP0,2(R2)	;UNDEFINED
RTEND:

	MOV	R0,4(R2)
	ADD	#F.RATT,4(R2)
	CLR	R3		;REG TO INDICATE MULTIPLE RATT'S
	CLR	6(R2)		;CLEAR COUNTS AND INIT STRING ADDRS
	MOV	#RATT0,10(R2)
	CLR	12(R2)
	MOV	#RATT1,14(R2)
	CLR	16(R2)
	MOV	#RATT3,20(R2)
RA0:	BITB	#FD.BLK,F.RATT(R0)	;IS FD.BLK ON?
	BEQ	RA1
	MOV	#7,16(R2)
	BIS	#1,R3
RA1:	BITB	#FD.CR,F.RATT(R0)	;IS FD.CR ON?
	BEQ	RA3
	MOV	#7,12(R2)
	ADD	R3,12(R2)
	BIS	#1,R3
RA3:	BITB	#FD.FTN,F.RATT(R0)	;IS FD.FTN ON?
	BEQ	RAEND
	MOV	#7,6(R2)
	ADD	R3,6(R2)
RAEND:	CALL	EDPUT

;
;FORMAT NEXT RECORD
;	H.UFAT(F.RSIZ,F.HIBK,F.EFBK,F.FFBY)
;
;

	MOV	#HDR83,R1
	MOV	R5,R0		;ADJUST FOR OFFSETS
	ADD	#H.UFAT,R0
	MOV	F.RSIZ(R0),(R2)+	;
	MOV	F.RSIZ(R0),(R2)+
	MOV	F.HIBK(R0),(R2)+
	MOV	F.HIBK+2(R0),(R2)+
	MOV	R0,(R2)
	ADD	#F.HIBK,(R2)+
	MOV	F.EFBK(R0),(R2)+
	MOV	F.EFBK+2(R0),(R2)+
	MOV	R0,(R2)
	ADD	#F.EFBK,(R2)+
	MOV	F.FFBY(R0),(R2)+
	MOV	F.FFBY(R0),(R2)+
	MOV	ARGBLK,R2
	CALL	EDPUT

;
;FORMAT NEXT RECORD
;	H.UFAT(REST)
;

	MOV	#HDR88,R1
	MOV	R5,R2
	ADD	#H.UFAT,R2
	ADD	#F.FFBY+2,R2
	CALL	EDPUT

;
	RETURN
	.END
