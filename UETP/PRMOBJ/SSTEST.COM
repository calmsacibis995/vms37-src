$	SET NOVERIFY
$	ON WARNING THEN STOP	! STOP COM PROCEDURES ON ANY ERROR
$	DEFINE /GROUP SYSTST$ABORT NO ! ASSUME NO ABORT REQUESTED
$	DEFINE /GROUP SYSTST$ERRS 1 ! ASSUME 1 ERROR UNTIL END
$	_:=""
$	SHOW SYM _		! DISPLAY A BLANK LINE
$	SHOW DAYTIME		! PRINT TIME OF DAY
$	_:="   BEGINNING OF SSTEST PROCESSING"
$	SHOW SYM _
$	_:=""
$	SHOW SYM _		! DISPLAY A BLANK LINE
$	_:="   SEE SETEXEC.COM FOR THE STATUS OF EACH"
$	SHOW SYM _
$	_:="     IMAGE EXECUTED BELOW, AND GLOBALS2.COM"
$	SHOW SYM _
$	_:="     FOR DEFINITIONS OF THE STATUS CODES."
$	SHOW SYM _
$	_:=""
$	SHOW SYM _		! DISPLAY A BLANK LINE
$	_:="   FOLLOWING IS A DIR SYS$SYSTEM:SYS.STB"
$	SHOW SYM _
$	DIR SYS$SYSTEM:SYS.STB
$ !
$ !	ESTABLISH A VALUE FOR SYSTST$RES (TEST MODULE RESIDENCE AREA) AND
$ !	DO THE LOGICAL NAME ASSIGNMENT.
$ !
$	TEMP:="SYS$SYSTEM:"		! USE SYSTEM DEV IF INHOUSE SYSTEM
$	IF P1.EQS."INHOUSE" THEN GOTO C1 ! INHOUSE MUST BE 1ST ARGUMENT
$	_:="SYS$DISK"			! PICK UP DEFAULT DISK IF KITTED SYSTEM
$	TEMP:='F$LOGICAL(_)		! ... TRANSLATE TO GET DISK STRING
$	_:='F$DIRECTORY()		! GET DEFAULT DIRECTORY STRING
$	TEMP:='TEMP''_'			! CONCAT TO GET DEF DISK, DIRECTORY
$ C1:
$	DEFINE /SYSTEM SYSTST$RES 'TEMP' ! DEFINE RESIDENCE AREA LOGICAL NAME
$	@SYSTST$RES:GLOBALS1
$	PHASE == SCAN
$ !	******** NOTE -- GRPS SYMBOL BELOW MUST HAVE A VALUE EQUAL
$ !                      TO THE NUMBER OF GROUPS APPEARING IN THE
$ !	                 SCAN.COM COMMAND FILE.     *********
$ !
$ !
$ !
$ !
$	GRPS:=9			! NUMBER OF SYSTEM SERVICE GROUPS
$	QUITEXEQ == NO
$	BARG == NO		! SET BAD ARGUMENT SWITCH TO NO
$	HELP=NO
$	AN:=0			! ARGUMENT NUMBER
$	R == ZEROES		! SET R INDICATOR TO ZEROES
$DOALL:
$	X:=1			! INDICATE 1ST GROUP
$ SETGF:
$	F'X == R		! SET FLAGS FOR CURR GROUP TO 0'S OR 1'S
$	X=X+1			! INDICATE NEXT GROUP
$	IF X.LE.GRPS THEN GOTO SETGF ! SET GROUP FLAGS UNTIL ALL GRPS DONE
$NEXTARG:
$	AN=AN+1
$	IF AN.GT.8 THEN GOTO EXHELP
$	TEMP:='Q'P'AN
$	P:== 'TEMP
$	IF P.NES."" .OR. AN.NE.1 THEN GOTO SKIPH
$	HELP = YES		! NO ARGS SPECIFIED -- IMPLIED HELP
$	QUITEXEQ == YES		! SKIP EXECUTION PHASE
$ SKIPH:
$	IF P.EQS."" THEN GOTO EXHELP
$	R == ONES
$	PR:== ""
$	IF P.EQS."INHOUSE" THEN GOTO NEXTARG ! IGNORE ARG, ALREADY DONE ABOVE
$	IF P.EQS."HELP" THEN GOTO DOHELP
$	IF P.EQS."ALL" THEN GOTO DOALL
$	IF (P.GES."_A").AND.(P.LES."_Z") THEN GOTO MINUS
$ SCANSS:
$	@SYSTST$RES:SCAN	! SCAN SERV & GROUP NAMES
$	IF BARG.EQ.YES THEN GOTO BADARG ! IF BAD ARG, PROCESS & CONT
$	GOTO NEXTARG		! GO LOOK AT NEXT INPUT ARGUMENT
$MINUS:
$	R == ZEROES
$	PR:== "_"
$	GOTO SCANSS
$DOHELP:
$	HELP=YES
$	GOTO NEXTARG
$BADARG:
$	QUITEXEQ == YES		! INDICATE TERMINATION OF C.P.
$	_:="   INVALID ARGUMENT: "'P'
$	SHOW SYM _		! PRINT BAD ARGUMENT
$	_:="   RE-ISSUE SSTEST COMMAND PROCEDURE"
$	SHOW SYM _		! PRINT COMMENT
$	GOTO CONT		! CONT THIS COMMAND PROCEDURE
$EXHELP:
$	IF HELP.EQ.NO THEN GOTO CONT
$	@SYSTST$RES:SSHELP	! DISPLAY HELP INFO
$CONT:
$	IF QUITEXEQ.EQ.YES THEN GOTO SKIPE ! IF TIME TO QUIT, SKIP EXEQ PHASE
$	@SYSTST$RES:GLOBALS2
$	PHASE == EXEQ
$	@SYSTST$RES:SCAN	! SCAN GROUP FLAGS & EXEC S.S.'S IF SET
$ SKIPE:
$	ASSIGN /PROCESS DUMMY: SYS$PRINT: ! FORCE SUBMITTED LOG 2B SAVED
$	DEFINE/GROUP SYSTST$ERRS 'ERRTOT ! LOG ERROR TOTAL IN LOGNAM TABLE
$	DEASSIGN/GROUP SYSTST$ABORT ! ABORT SIGNAL NO LONGER NEEDED
$	DEASSIGN/SYSTEM SYSTST$RES ! FINISHED WITH RESIDENCE LOG NAME
$	_:=""
$	SHOW SYM _		! DISPLAY A BLANK LINE
$	_:="   LOGICAL NAME SYSTST$ERRS CONTAINS TOTAL NUMBER OF
$	SHOW SYM _
$	_:="     FAILING TEST MODULES AND/OR DCL COMMANDS.
$	SHOW SYM _
$	_:=""
$	SHOW SYM _		! DISPLAY A BLANK LINE
$	SHOW TRANSLATION SYSTST$ERRS ! DISPLAY COUNT OF ERRORS
$	_:=""
$	SHOW SYM _		! DISPLAY A BLANK LINE
$	_:="   SSTEST PROCESSING "'DISP'
$	SHOW SYM _
$	SHOW DAYTIME		! DISPLAY TIME OF DAY
