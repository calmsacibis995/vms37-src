	.TITLE	SATSSF17 - SATS SYSTEM SERVICE TESTS  (FAILING S.C.)
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SATS SYSTEM SERVICE TESTS
;
; ABSTRACT:	The SATSSF17 module tests the execution of the following
; VMS system services, invoked in such a way as to expect failing
; status codes:
;		$INPUT
;		$OUTPUT
;		$QIO
;		$QIOW
;
;
; ENVIRONMENT:	User mode image; needs CMKRNL privilege,
;		dynamically acquires other privileges, as needed.
;
; AUTHOR: Larry D. Jones,		CREATION DATE: OCTOBER, 1979
;
; MODIFIED BY:
;
;	V03-001	LDJ0001		Larry D. Jones,		17-Sep-1980
;		Modified to conform to new build command procedures.
;**
;--
.PAGE
.SBTTL	DECLARATIONS
;
; MACRO LIBRARY CALLS
;
	$PRVDEF				; privilege definitions
	$UETPDEF			; UETP message definitions
	$SHR_MESSAGES UETP,116,<<TEXT,INFO>> ; UETP$_TEXT definition
	$PHDDEF				; process header definitions
	$PCBDEF				; PCB definitions
	$SSDEF				; SS definitions
	$STSDEF				; STS definitions
;
; Equated symbols
;
WARNING		= 0			; warning severity value for msgs
SUCCESS		= 1			; success     "      "    "   "
ERROR		= 2			; error       "      "    "   "
INFO		= 3			; information "      "    "   "
SEVERE		= 4			; fatal       "      "    "   "
PRVHND_SXV40	= 1			; page 0 address for SETEXV

.PAGE
	.SBTTL	OWN STORAGE
	.PSECT	RODATA,RD,NOWRT,NOEXE,LONG
;
TEST_MOD_NAME:
	.ASCIC	/SATSSF17/		; needed for SATSMS message
TEST_MOD_NAME_D:
	.ASCID	/SATSSF17/		; module name
TEST_MOD_BEGIN:
	.ASCIC	/begun/
TEST_MOD_SUCC:
	.ASCIC	/successful/
TEST_MOD_FAIL:
	.ASCIC	/failed/
INPUT:
	.ASCIC	/INPUT/
OUTPUT:
	.ASCIC	/OUTPUT/
QIO:
	.ASCIC	/QIO/
QIOW:
	.ASCIC	/QIOW/
INADR:
	.LONG	NOACCESS,NOACCESS	; page address of noaccess psect
PROT:
	.LONG	PRT$C_NA		; protection code for no access psect
PRVHND_SXV41:				; read only access location
CS1:
	.ASCID	\Test !AC service name !AC step !UL failed.\
CS2:
	.ASCID	\Expected !AS = !XL received !AS = !XL\
CS3:
	.ASCID	\Expected !AS!UB = !XL received !AS!UB = !XL\
EXP:
	.ASCID	\status\
MBNAM:
	.ASCID	\TT\
.PAGE
;
	.SBTTL	R/W PSECT
	.PSECT	RWDATA,RD,WRT,NOEXE,LONG
;
TPID:
	.LONG	0			; PID for this process
CURRENT_TC:
	.LONG	0			; ptr to current test case
	.ALIGN LONG
REG_SAVE_AREA:
	.BLKL	15			; register save area
MOD_MSG_CODE:
	.LONG	UETP$_SATSMS		; test module message code for putmsg
TMN_ADDR:
	.ADDRESS TEST_MOD_NAME
TMD_ADDR:
	.ADDRESS TEST_MOD_BEGIN
PRVPRT:
	.BYTE	0			; protection return byte for SETPRT
PRIVMASK:
	.QUAD	0			; priv. mask
CHM_CONT:
	.LONG	0			; change mode continue address
RETADR:
	.BLKL	2			; returned address's from SETPRT
INP:
	$INPUT	0,0,0			; INPUT parameter list
OUT:
	$OUTPUT	0,0,0			; OUTPUT parameter list
QIOP:
	$QIO	-1,0,IO$_READVBLK,0,0,0,MBNAM,0 ; QIO parameter list
QIOWP:
	$QIOW	-1,0,IO$_READVBLK,0,0,0,MBNAM,0 ; QIOW parameter list

REG:
	.ASCID	\register R\
REGNUM:
	.LONG	0			; register number
MSGL:
	.LONG	80			; buffer desc.
	.ADDRESS BUF
BUF:
	.BLKB	80
MESSAGEL:
	.LONG	0			; message desc.
	.ADDRESS	BUF
SERV_NAME:
	.LONG	0			; service name pointer
MBCHAN:
	.WORD	0			; channel location
	.PAGE
	.PSECT	SATS_ACCVIO_1,RD,WRT,NOEXE,PAGE
EMPTY:		.BLKB	512	; reserve a page of space
;
; + 
; ******************************************************************
; *                                                                *
; * 	THE ORDER OF STATEMENTS IN THIS PSECT IS CRITICAL.         *
; *	DO NOT RE-ARRANGE THE VARIABLES. CONSULT SATS              *
; *	FUNCTIONAL SPECIFICATION FOR A DESCRIPTION OF THE USE      *
; *	OF THE EMPTY PSECT (AND ITS COMPANION PSECT, NOACCESS).    *
; *                                                                *
; ******************************************************************
; -
;
PRVHND_SXV42	= . - 1		; prvhnd arg for SETEXV (last byte in the page)
		. = . - 13	; allow room for string descriptor
; type AAAAA_SSSX5 go here:
		.LONG	6	; string length (will cross psect boundary)
		.ADDRESS .+4	; string address
; type AAAAA_SSSX3 go here:
		.BLKB	1	; low-order byte of string length
; type AAAAA_SSSX2 go here:
		.BLKL	1	; string length
;
;
;
;
	.PSECT	SATS_ACCVIO_2,RD,WRT,NOEXE,PAGE
NOACCESS:	.BLKB	512	; reserve a page of space
		. = . - 512	; return loc ctr to beginning of psect
		.ADDRESS EMPTY	; address of accessible string
		.ADDRESS EMPTY/^X100 ; address of accessible string
;+
; *** NOTE -- DO NOT CHANGE LOCATION OR SEQUENCE OF ABOVE STATEMENTS!
; ***         THIS PSECT (NOACCESS) MUST APPEAR IN MEMORY IMMEDIATELY
; ***         FOLLOWING THE EMPTY PSECT. PSECT NAMES AND OPTIONS WILL BE 
; ***         CHOSEN TO FORCE THE DESIRED PSECT ORDERING.
;-
;
;
;
;
.PAGE
	.PSECT	SATSSF17,RD,WRT,EXE,LONG
	.SBTTL	SATSSF17
;++
; FUNCTIONAL DESCRIPTION:
;
;	After performing some initial housekeeping, such as
; printing the module begin message and acquiring needed privileges,
; the system services are tested in each of their failure conditions.
; Detected failures are identified and  an error message is printed
; on the terminal.  Upon completion of the test a success or fail
; message is printed on the terminal.
;
; CALLING SEQUENCE:
;
;	$ RUN SATSSF17  ...  (DCL COMMAND)
;
; INPUT PARAMETERS:
;
;	none
;
; IMPLICIT INPUTS:
;
;	none
;
; OUTPUT PARAMETERS:
;
;	none
;
; IMPLICIT OUTPUTS:
;
;	Messages to SYS$OUTPUT are the only output from SATSSF17.
;	They are of the form:
;
;		%UETP-S-SATSMS, TEST MODULE SATSSF17 BEGUN ... (BEGIN MSG)
;		%UETP-S-SATSMS, TEST MODULE SATSSF17 SUCCESSFUL ... (END MSG)
;		%UETP-E-SATSMS, TEST MODULE SATSSF17 FAILED ... (END MSG)
;		%UETP-I-TEXT, ... (VARIABLE INFORMATION ABOUT A TEST MODULE FAILURE)
;
; COMPLETION CODES:
;
;	The SATSSF17 routine terminates with a $EXIT to the
;	operating system with a status code defined by UETP$_SATSMS.
;
; SIDE EFFECTS:
;
;	none
;
;--



	TEST_START SATSSF17		; let the test begin
	$SETPRT_S INADR=W^INADR, RETADR=W^RETADR, -
		  PROT=W^PROT, PRVPRT=W^PRVPRT ; set noaccess psect
					   ; ... for no user access

	.SBTTL INPUT TESTS
;+
;
; $INPUT tests
;
; test for an EFN of -1
;
;-
	MOVAL	W^INPUT,W^SERV_NAME		; set the service name
	$CREMBX_S LOGNAM=W^MBNAM,-
		    CHAN=W^MBCHAN		; get a legal channel number
	$INPUT   CHAN = W^MBCHAN,-
		BUFFER= W^MBNAM,-
		LENGTH= #0,-
		  EFN = #-1			; try EFN = -1
	FAIL_CHECK SS$_ILLEFC			; check failure
;+
;
; test for an EFN of 500
;
;-
	NEXT_TEST
	$INPUT   CHAN = W^MBCHAN,-
		BUFFER= W^MBNAM,-
		LENGTH= #0,-
		   EFN= #500			; try illegal EFN = 500
	FAIL_CHECK SS$_ILLEFC			; check failure
;+
;
; test for an EFN of 123 without an associated cluster
;
;-
	NEXT_TEST
	$INPUT   CHAN = W^MBCHAN,-
		BUFFER= W^MBNAM,-
		LENGTH= #0,-
		  EFN = #123			; try EFN =123
	FAIL_CHECK SS$_UNASEFC			; check failure
;+
;
; test unaccessable IOSB parameter = page 0 access
;
;-
	NEXT_TEST
	$INPUT   CHAN = W^MBCHAN,-
		 IOSB = W^PRVHND_SXV40,-
		LENGTH= #0,-
		BUFFER= W^MBNAM			; try page 0 access
	FAIL_CHECK SS$_ACCVIO			; check failure
;+
;
; test unaccessable IOSB parameter = read-only PSECT
;
;-
	NEXT_TEST
	$INPUT   CHAN = W^MBCHAN,-
		 IOSB = W^PRVHND_SXV41,-
		LENGTH= #0,-
		BUFFER= W^MBNAM			; try read-only PSECT
	FAIL_CHECK SS$_ACCVIO			; check failure
;+
;
; test unaccessable IOSB parameter = noaccess protection
;
;-
	NEXT_TEST
	$INPUT   CHAN = W^MBCHAN,-
		 IOSB = W^PRVHND_SXV42,-
		LENGTH= #0,-
		BUFFER= W^MBNAM			; try noaccess BUFFER param.
	FAIL_CHECK SS$_ACCVIO			; check failure
;+
;
; test non-existent channel number
;
;-
	NEXT_TEST
	$DASSGN_S CHAN = W^MBCHAN		; deassign the channel
	$INPUT   CHAN = W^MBCHAN,-
		BUFFER= W^MBNAM,-
		LENGTH= #0			; try illegal channel
	FAIL_CHECK SS$_NOPRIV			; check the failure
;+
;
; test illegal channel number
;
;-
	NEXT_TEST
	CLRL	W^MBCHAN			; make an illegal channel number
	$INPUT   CHAN = W^MBCHAN,-
		BUFFER= W^MBNAM,-
		LENGTH= #0			; try illegal channel number
	FAIL_CHECK SS$_IVCHAN			; check failure
.PAGE
	.SBTTL OUTPUT TESTS
;+
;
; $OUTPUT tests
;
; test for an EFN of -1
;
;-
	NEXT_TEST
	MOVAL	W^OUTPUT,W^SERV_NAME		; set the service name
	$CREMBX_S LOGNAM=W^MBNAM,-
		    CHAN=W^MBCHAN		; get a legal channel number
	$OUTPUT   CHAN = W^MBCHAN,-
		BUFFER = W^MBNAM,-
		LENGTH = #0,-
		   EFN = #-1			; try EFN = -1
	FAIL_CHECK SS$_ILLEFC			; check failure
;+
;
; test for an EFN of 500
;
;-
	NEXT_TEST
	$OUTPUT   CHAN = W^MBCHAN,-
		BUFFER = W^MBNAM,-
		LENGTH = #0,-
		   EFN = #500			; try illegal EFN = 500
	FAIL_CHECK SS$_ILLEFC			; check failure
;+
;
; test for an EFN of 123 without an associated cluster
;
;-
	NEXT_TEST
	$OUTPUT   CHAN = W^MBCHAN,-
		BUFFER = W^MBNAM,-
		LENGTH = #0,-
		   EFN = #123			; try EFN =123
	FAIL_CHECK SS$_UNASEFC			; check failure
;+
;
; test unaccessable IOSB parameter = page 0 access
;
;-
	NEXT_TEST
	$OUTPUT   CHAN = W^MBCHAN,-
		  IOSB = W^PRVHND_SXV40,-
		LENGTH = #0,-
		BUFFER = W^MBNAM		; try page 0 access
	FAIL_CHECK SS$_ACCVIO			; check failure
;+
;
; test unaccessable IOSB parameter = read-only PSECT
;
;-
	NEXT_TEST
	$OUTPUT   CHAN = W^MBCHAN,-
		  IOSB = W^PRVHND_SXV41,-
		LENGTH = #0,-
		BUFFER = W^MBNAM		; try read-only PSECT
	FAIL_CHECK SS$_ACCVIO			; check failure
;+
;
; test unaccessable IOSB parameter = noaccess protection
;
;-
	NEXT_TEST
	$OUTPUT   CHAN = W^MBCHAN,-
		  IOSB = W^PRVHND_SXV42,-
		LENGTH = #0,-
		BUFFER = W^MBNAM		; try noaccess BUFFER param.
	FAIL_CHECK SS$_ACCVIO			; check failure
;+
;
; test non-existent channel number
;
;-
	NEXT_TEST
	$DASSGN_S CHAN = W^MBCHAN		; deassign the channel
	$OUTPUT   CHAN = W^MBCHAN,-
		BUFFER = W^MBNAM,-
		LENGTH = #0			; try illegal channel
	FAIL_CHECK SS$_NOPRIV			; check the failure
;+
;
; test illegal channel number
;
;-
	NEXT_TEST
	CLRL	W^MBCHAN			; make an illegal channel number
	$OUTPUT   CHAN = W^MBCHAN,-
		BUFFER = W^MBNAM,-
		LENGTH = #0			; try illegal channel number
	FAIL_CHECK SS$_IVCHAN			; check failure

.SBTTL QIO TESTS
;+
;
; $QIO tests
; test for an EFN of 0
;
;-
	MOVAL	W^QIO,W^SERV_NAME	; set service name
	$CREMBX_S LOGNAM=W^MBNAM,-
		    CHAN=W^MBCHAN	; get a legal channel number
	MOVZWL	W^MBCHAN,W^QIOP+QIO$_CHAN ; set the channel number
	$QIO_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		 EFN=#-1		; try EFN=0
	FAIL_CHECK SS$_ILLEFC		; check failure
	$QIO_G W^QIOP			; try _G
	FAIL_CHECK SS$_ILLEFC		; check failure
;+
;
; test for an EFN of 500
;
;-
	NEXT_TEST
	$QIO_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		 EFN=#500		; try EFN=500
	FAIL_CHECK SS$_ILLEFC		; check failure
	MOVL	#500,W^QIOP+QIO$_EFN	; set illegal EFN
	$QIO_G W^QIOP			; try _G
	FAIL_CHECK SS$_ILLEFC		; check failure
;+
;
; test for an EFN of 123 without an associated cluster
;
;-
	NEXT_TEST
	$QIO_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		 EFN=#123		; try _S
	FAIL_CHECK SS$_UNASEFC		; check failure
	MOVL	#123,W^QIOP+QIO$_EFN	; set illegal EFN
	$QIO_G W^QIOP			; try _G
	FAIL_CHECK SS$_UNASEFC		; check failure
	CLRL	W^QIOP+QIO$_EFN		; clean up illegal EFN
;+
;
; test unaccessable IOSB = page 0 access
;
;-
	NEXT_TEST
	$QIO_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		IOSB=W^PRVHND_SXV40	; try _S
	FAIL_CHECK SS$_ACCVIO		; check failure
	MOVAL	W^PRVHND_SXV40,W^QIOP+QIO$_IOSB ; set illegal address
	$QIO_G W^QIOP			; try _G
	FAIL_CHECK SS$_ACCVIO		; check the failure
;+
;
; test unaccessable IOSB = read-only PSECT
;
;-
	NEXT_TEST
	$QIO_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		IOSB=W^PRVHND_SXV41	; try _S
	FAIL_CHECK SS$_ACCVIO		; check failure
	MOVAL	W^PRVHND_SXV41,W^QIOP+QIO$_IOSB ; set IOSB adr
	$QIO_G W^QIOP			; try _G
	FAIL_CHECK SS$_ACCVIO		; check failure
;+
;
; test noaccess protection in IOSB
;
;-
	NEXT_TEST
	$QIO_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		IOSB=W^PRVHND_SXV42	; try _S
	FAIL_CHECK SS$_ACCVIO		; check failure
;+
;
; test non-existent channel number
;
;-
	NEXT_TEST
	$DASSGN_S CHAN=W^MBCHAN		; release the channel
	$QIO_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-		; try _S
		  P2=#0
	FAIL_CHECK SS$_NOPRIV		; check failure
	$QIO_G W^QIOP			; try _G
	FAIL_CHECK SS$_NOPRIV		; check failure
;+
;
; test illegal channel number
;
;-
	NEXT_TEST
	CLRL	W^MBCHAN		; set illegal channel number
	$QIO_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0
	FAIL_CHECK SS$_IVCHAN		; check failure
	CLRL	W^QIOP+QIO$_CHAN	; set illegal channel number
	$QIO_G W^QIOP			; try _G
	FAIL_CHECK SS$_IVCHAN		; check failure
.PAGE
.SBTTL QIOW TESTS
;+
;
; $QIOW tests
; test for an EFN of 0
;
;-
	NEXT_TEST
	MOVAL	W^QIOW,W^SERV_NAME	; set service name
	$CREMBX_S LOGNAM=W^MBNAM,-
		    CHAN=W^MBCHAN	; get a legal channel number
	MOVZWL	W^MBCHAN,W^QIOWP+QIOW$_CHAN ; set the channel number
	$QIOW_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		 EFN=#-1		; try EFN=0
	FAIL_CHECK SS$_ILLEFC		; check failure
	$QIOW_G W^QIOWP			; try _G
	FAIL_CHECK SS$_ILLEFC		; check failure
;+
;
; test for an EFN of 500
;
;-
	NEXT_TEST
	$QIOW_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		 EFN=#500		; try EFN=500
	FAIL_CHECK SS$_ILLEFC		; check failure
	MOVL	#500,W^QIOWP+QIOW$_EFN	; set illegal EFN
	$QIOW_G W^QIOWP			; try _G
	FAIL_CHECK SS$_ILLEFC		; check failure
;+
;
; test for an EFN of 123 without an associated cluster
;
;-
	NEXT_TEST
	$QIOW_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		 EFN=#123		; try _S
	FAIL_CHECK SS$_UNASEFC		; check failure
	MOVL	#123,W^QIOWP+QIOW$_EFN	; set illegal EFN
	$QIOW_G W^QIOWP			; try _G
	FAIL_CHECK SS$_UNASEFC		; check failure
	CLRL	W^QIOWP+QIOW$_EFN	; clean up illegal EFN
;+
;
; test unaccessable IOSB = page 0 access
;
;-
	NEXT_TEST
	$QIOW_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		IOSB=W^PRVHND_SXV40	; try _S
	FAIL_CHECK SS$_ACCVIO		; check failure
	MOVAL	W^PRVHND_SXV40,W^QIOWP+QIOW$_IOSB ; set illegal address
	$QIOW_G W^QIOWP			; try _G
	FAIL_CHECK SS$_ACCVIO		; check the failure
;+
;
; test unaccessable IOSB = read-only PSECT
;
;-
	NEXT_TEST
	$QIOW_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		IOSB=W^PRVHND_SXV41	; try _S
	FAIL_CHECK SS$_ACCVIO		; check failure
	MOVAL	W^PRVHND_SXV41,W^QIOWP+QIOW$_IOSB ; set IOSB adr
	$QIOW_G W^QIOWP			; try _G
	FAIL_CHECK SS$_ACCVIO		; check failure
;+
;
; test noaccess protection in IOSB
;
;-
	NEXT_TEST
	$QIOW_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0,-
		IOSB=W^PRVHND_SXV42	; try _S
	FAIL_CHECK SS$_ACCVIO		; check failure
;+
;
; test non-existent channel number
;
;-
	NEXT_TEST
	$DASSGN_S CHAN=W^MBCHAN		; release the channel
	$QIOW_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0
	FAIL_CHECK SS$_NOPRIV		; check failure
	$QIOW_G W^QIOWP			; try _G
	FAIL_CHECK SS$_NOPRIV		; check failure
;+
;
; test illegal channel number
;
;-
	NEXT_TEST
	CLRL	W^MBCHAN		; set illegal channel number
	$QIOW_S  CHAN=W^MBCHAN,-
		FUNC=#IO$_READVBLK,-
		  P1=W^MBNAM,-
		  P2=#0
	FAIL_CHECK SS$_IVCHAN		; check failure
	CLRL	W^QIOWP+QIOW$_CHAN	; set illegal channel number
	$QIOW_G W^QIOWP			; try _G
	FAIL_CHECK SS$_IVCHAN		; check failure
	TEST_END			; end the test
.PAGE
.SBTTL REG_SAVE
;++
; FUNCTIONAL DESCRIPTION:
;	Subroutine to save R2-R11 in the register save location.
;
; CALLING SEQUENCE:
;	PUSHL	#0		; save a dummy parameter
;	CALLS	#1,W^REG_SAVE	; save R2-R11
;
; INPUT PARAMETERS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
;--

REG_SAVE:
	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
	MOVC3	#4*10,^X14(FP),W^REG_SAVE_AREA	; save the registers in the program
	RET
	.SBTTL	REG_CHECK
;++
; FUNCTIONAL DESCRIPTION:
;	Subroutine to test R0 & R2-R11 for proper content after a service
;	execution. A snapshot is taken by the REG_SAVE routine at the
;	beginning of each step and this routine is executed after the
;	services have been executed.
;
; CALLING SEQUENCE:
;	PUSHL	#SS$_XXXXXX	; push expected R0 contents
;	CALLS	#1,W^REG_CHECK	; execute this routine
;
; INPUT PARAMETERS:
;	expected R0 contents on the stack
;
; OUTPUT PARAMETERS:
;	possible error messages printed using $PUTMSG
;
;--

REG_CHECK:
	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
	CMPL	4(AP),R0			; is this the right fail code?
	BEQL	10$				; br if yes
	PUSHL	R0				; push received data
	PUSHL	4(AP)				; push expected data
	PUSHAL	W^EXP				; push the string variable
	CALLS	#3,W^PRINT_FAIL			; print the error message
10$:
	CMPC3	#4*10,^X14(FP),W^REG_SAVE_AREA	; check all but R0
	BEQL	20$				; br if O.K.
	SUBL3	#REG_SAVE_AREA,R3,R6		; calculate the register number
	DIVL2	#4,R6
	ADDB3	#^X2,R6,W^REGNUM		; put it in the string
	BICL2	#3,R1				; backup to register boundry
	BICL2	#3,R3
	PUSHL	W^REGNUM			; push register number
	PUSHL	(R1)				; push received data
	PUSHL	(R3)				; push expected data
	PUSHAL	W^REG				; set string pntr param.
	CALLS	#4,W^PRINT_FAIL			; print the error message
20$:
	RET
	.SBTTL	PRINT_FAIL
;++
; FUNCTIONAL DESCRIPTION:
;	Subroutine to report failures using $PUTMSG
;
; CALLING SEQUENCE:
; Mode  #1	PUSHL EXPECTED	Mode    #2	PUSHL REG_NUMBER
;		PUSHL RECEIVED			PUSHL EXPECTED
;		PUSHAL STRING_VAR		PUSHL RECEIVED
;		CALLS #3,W^PRINT_FAIL		PUSHAL STRING_VAR
;						CALLS #4,W^PRINT_FAIL
;
; INPUT PARAMETERS:
;	listed above
;
; OUTPUT PARAMETERS:
;	an error message is printed using $PUTMSG
;
;--

PRINT_FAIL:
	.WORD	^M<R2,R3,R4,R5>
	$FAO_S	W^CS1,W^MESSAGEL,W^MSGL,#TEST_MOD_NAME,W^SERV_NAME,W^CURRENT_TC
	PUTMSG	<#UETP$_TEXT,#1,#MESSAGEL>	; print the message
	CMPB	(AP),#4				; is this a register message?
	BEQL	10$				; br if yes
	$FAO_S	W^CS2,W^MESSAGEL,W^MSGL,4(AP),8(AP),4(AP),12(AP)
	BRB	20$				; goto output message
10$:
	$FAO_S	W^CS3,W^MESSAGEL,W^MSGL,4(AP),16(AP),8(AP),4(AP),16(AP),12(AP)
20$:
	PUTMSG	<#UETP$_TEXT,#1,#MESSAGEL>	; print the message
	MOVAL	W^TEST_MOD_FAIL,W^TMD_ADDR	; set failure message address
	INSV	#ERROR,#0,#3,W^MOD_MSG_CODE	; set severity code
	RET
.PAGE
	.SBTTL	MOD_MSG_PRINT
MOD_MSG_PRINT:
;
;  *****************************************************************
;  *                                                               *
;  *  PRINTS THE TEST MODULE BEGUN/SUCCESSFUL/FAILED MESSAGES      *
;  *	   (USING THE PUTMSG MACRO).                               *
;  *                                                               *
;  *****************************************************************
;
	PUTMSG	<W^MOD_MSG_CODE,#2,W^TMN_ADDR,W^TMD_ADDR> ; PRINT MSG
	RSB				; ... AND RETURN TO CALLER
;
	.SBTTL	CHMRTN
CHMRTN:
; ******************************************************************
; *								   *
; *	CHANGE MODE ROUTINE. THIS ROUTINE GETS CONTROL WHENEVER    *
; *	A CMKRNL, CMEXEC, OR CMSUP SYSTEM SERVICE IS ISSUED	   *
; *	BY THE MODE MACRO ('TO' OPTION).  IT MERELY DOES 	   *
; *	A JUMP INDIRECT ON A FIELD SET UP BY MODE. IT HAS 	   *
; *	THE EFFECT OF RETURNING TO THE END OF THE MODE		   *
; *	MACRO EXPANSION.					   *
; *								   *
; ******************************************************************
;
	.WORD	0			; ENTRY MASK
	JMP	@CHM_CONT		; RETURN TO MODE MACRO IN NEW MODE
;
; *	RET INSTR WILL BE ISSUED IN EXPANSION OF 'MODE FROM, ....' MACRO
;
	.END	SATSSF17
