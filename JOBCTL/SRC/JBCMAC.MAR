;	.TITLE	JBCMAC	JOB_CONTROLLER MACRO FILE
;	.IDENT	'V03-000'
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************

;++
; FACILITY:	STARLET JOB CONTROLLER
;
; ABSTRACT:	ASSEMBLER PREFIX FILE
;
; ENVIRONMENT:
;
; AUTHOR:  W.H.BROWN, CREATION DATE:
;
; MODIFIED BY:
;
;	V02-010	MLJ0081		Martin L. Jack,	1-Mar-1982  20:04
;		Change maximum number of symbionts to match number of queues.
;
;	V02-009	GWF0142		Gary W. Fowler		10-Dec-1981
;		Added definitions for timer requests ID's used in enforcing
;		restricted log in hours.
;
;	V02-008	SPF00012	Steve Forgey		28-Jul-1981
;		Updated and moved accounting message definitions to SYSDEF.
;		Added work queues for new accounting message types (process
;		purge, image delete and purge, system function).
;
;	V02-007	GWF00074	Gary W. Fowler		27-Jul-1981
;		Added symbol defining length of job name.
;
;	V02-006	GWF0086		Gary W. Fowler		20-Jul-1981
;		change maximum number of symbionts to 32 and maximum number of
;		queues to 64.
;
;	V02-005	GWF0041		Gary W. Fowler		08-May-1981
;		Add current file size to symbiont control table
;
;	V02-004	GWF0018		Gary W. Fowler		24-Feb-1981
;		Expande size of JCD_T_LINBUF.  Add definition of max length
;		for queue names and log file names
;
;	V02-003	GWF0002		Gary W. Fowler		4-Jan-1980
;		Expanded size of JCD_T_LINBUF to 170 bytes so full length DJI
;		message will not overlay symbiont name table.  Remove symbiont
;		name from symbiont controll table because it was not needed.
;
;	V02-002	GWF0001		Gary W. Fowler		29-Nov-1979
;		Added items to support multiple symbiont types.
;
;
;--
;	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
;	[11,1]LIB.MLB/ML

;
; MACROS:
;
; MACRO TO SET ERROR STATUS
;
	.MACRO	SETSTAT	CODE,PLC=R0
	.IF IDN	<CODE><NORMAL>
	MOVL	#1,PLC
	.IFF
	_$$$_ = <JBC$_'CODE&^X7FFF/4>
	.IF LT	<_$$$_-64>
	MNEGL	#_$$$_,PLC
	.IFF
	.IF LT <_$$$_-128>
	CVTBL	#-_$$$_,PLC
	.IFF
	CVTWL	#-_$$$_,PLC
	.ENDC
	.ENDC
	.ENDC
	.ENDM
;
; MACRO TO TEST AND REPORT AN ERROR
;
	.MACRO	CHECK_ERROR	ERRNUM,?L1
	BLBS	R0,L1
	CHMU	S^#ERRNUM
L1:
	.ENDM
;
; MACRO TO REQUEST AN ERROR MESSAGE OUTPUT
;
	.MACRO	SHOW_ERROR	ERRNUM
	CHMU	S^#ERRNUM
	.ENDM
;
; MACRO TO PERFORM A COMAPIR OF 2 QUADWORDS
;
;	NOTE:	DISPLACEMENT MODE IS ASSUMED
	.MACRO	CMPQUAD	ARG1,ARG2,?L1
	CMPL	4+ARG1,4+ARG2
	BNEQ	L1
	CMPL	ARG1,ARG2
L1:
	.ENDM
 
;
; MACRO TO SET OR CLEAR A BIT BY BIT NUMBER
;
;CALL:
;	SETBIT	BITNUM,FLAGWORD
; OR:
;	CLRBIT	BITNUM,FLAGWORD
;
; WHERE:
;	BITNUM IS ANY VALID SOURCE OPERAND SECIFYING THE BIT
;		OFFSET FROM THE FLAG BASE TO SET/CLEAR
;
;	FLAGWORD IS ANY VALID DESTINATION OPERAND
;
	.MACRO	SETBIT	VAL,FLAG
	.NTYPE	_$$_	VAL
	.IF EQ	<_$$_-^X0EF>
	.IF NDF	VAL
	BBSS	S^#VAL,FLAG,.+1
	.IFF
	.IF LT <VAL-8>
	BISB	#<1@VAL>,FLAG
	.IFF
	BBSS	#VAL,FLAG,.+1
	.ENDC
	.ENDC
	.IFF
	BBSS	VAL,FLAG,.+1
	.ENDC
	.ENDM	SETBIT
;
	.MACRO	CLRBIT	VAL,FLAG
	.NTYPE	_$$_	VAL
	.IF EQ	<_$$_-^X0EF>
	.IF NDF	VAL
	BBCC	S^#VAL,FLAG,.+1
	.IFF
	.IF LT <VAL-8>
	BICB	#<1@VAL>,FLAG
	.IFF
	BBCC	#VAL,FLAG,.+1
	.ENDC
	.ENDC
	.IFF
	BBCC	VAL,FLAG,.+1
	.ENDC
	.ENDM	CLRBIT
 
;
; MACRO TO INVOKE FORMS CHECKING
;
	.MACRO	IF_FORM_SAME	PLC
	BSBW	SYM$CMPFORMS
	BLBS	R0,PLC
	.ENDM
 
	.MACRO	IF_FORM_DIF	PLC
	BSBW	SYM$CMPFORMS
	BLBC	R0,PLC
	.ENDM
;
; PROGRAM SECTION DEFINITION MACROS
;	ARGUMENTS ARE:
;		1) SECTION NAME (KEY WORD IS NAME)
;		2) ALIGNMENT    (KEY WORD IS ALIGN)
;
;	IN ALL CASE, ARGUMENTS ARE OPTIONAL
;
; MACRO TO GENERATE A PURE PROGRAM SECTION
;
	.MACRO	PURE_SECTION NAME=JBC_PURE,ALIGN=BYTE
 
	.PSECT	NAME	EXE,RD,NOWRT,ALIGN
 
	.ENDM	PURE_SECTION
;
; MACRO TO GENERATE IMPURE DATA SEGMENT
;
	.MACRO	IMPURE_DATA  NAME=JBC_RWDATA,ALIGN=LONG
 
	.PSECT	NAME	NOEXE,WRT,RD,ALIGN
 
	.ENDM	IMPURE_DATA
;
; MACRO TO GENERATE A STRING WITH DESCRIPTOR
;
;	STRING_DESC <SRTING> 
;
; WHERE:
;	<STRING> IS THE STRING TO BE USED
;
	.MACRO	STRING_DESC ST
 
	.NCHR	.$$.,<ST>
	.LONG	.$$.
	.LONG	.+4
	.ASCII	\ST\
 
	.ENDM

;
; MACRO TO GENERATE A COUNTED ASCII STRING
;
	.MACRO	CNTSTR	ST,?L1
	.BYTE	<L1-.-1>
	.ASCII	\ST\
L1:
	.ENDM
;
; MACRO TO INDIACTE THAT THE QUEUE HAS CHANGED
;
	.MACRO	SYM_QUECHANGE
	INCW	SQH$W_QUESEQ(R10)
	.ENDM
	.MACRO	JBCQUEDEF		; JOB_CONTROLLER QUEUE DEFINITIONS
	$SYMDEF				; SYMBIONT MANAGER RECORD
	$SJHDEF				; SYMBIONT MANAGER JOB HEADER
	$SQRDEF				; SYMBIONT MANAGER QUEUE RECORD
	$SQHDEF				; SYMBIONT MANAGER QUEUE HEADER
	$SMQDEF				; SYMBIONT QUEUE FILE HEADER FORMAT
;
; DEFINE MULTIPLY USED BITS
;
	SMQ$V_CREPND=SMQ$V_PAUSED	; CREATE PENDING FOR BATCH QUEUES IS
	SMQ$M_CREPND=SMQ$M_PAUSED	; THE SAME AS PAUSED FOR DEVICE QUEUES
 
	.ENDM
 
;
; DEFINE JOB COTROLLER PARAMETERS
;
	.MACRO	JBCPARDEF		;
 
	$JBCMSGDEF			; DEFINE JOB CONTROLLER MESSAGES
	JBC_K_SYNCFLAG	= 1		; EVENT TO SYNC AST AND MAINLINE
	JBC_K_PRTIMFLAG	= 2		; PRINT TIME CLOCK HAS RUN OUT
	JBC_K_HOURLY	= 3		; HOURLY CHECK JOB CLOCK HAS RUN OUT
	JBC_K_MINUTES	= 4		; MINUTE CHECK JOB CLOCK HAS RUN OUT
	JBC_K_UPDFLG	= 8		; EVENT FLAG FOR UPDATE SECTION
	JBC_K_MAXBUFR	= 4		; NUMBER OF MESSAGE BUFFERS
	SYM_K_QUENAM	= 15		; MAXIMUM LENGTH FOR A QUEUE NAME
	SYM_K_MAXDEVNAM	= 15		; MAXIMUM LENGHT FOR A DEVICE NAME
	SYM_K_MAXFILNAM	= 19		; LONGEST NAME POSIBLE
	SYM_K_PRTPRI	= 32		; NUMBER IF PRINT PRIORITIES
	SYM_K_MAXQLEN	= 96		; MAXIMUM LENGTH OF QUEUE NAME
	CURPDEV		=16		; OFFSET TO NO. OF DEVICES USING THIS SYMBIONT
	CURSYMB		=17		; OFFSET TO NO. OF THIS SYMBIONT TYPE
	SYM_K_ENTSIZ	=18		; LENGTH OF ENTRY IN SYMBIONT NAME TABLE
	SYM_K_NAMSIZ	=16		; MAX LENGTH OF SYMBIONT NAME
	JBC_NAM_LEN	=10		; MAX LENGTH OF JOB NAME
 
;***********************************************************************
 
;
; DEFINE CONFIGUARATION PARAMETERS
;
	SYM_K_NUMQUEUE	= 64		; MAXIMUM NUMBER OF QUEUES
	SYM_K_NUMRECORD	= 1024		; NUMBER OF RECORDS IN QUEUE
	SYM_K_MAXSYMB	= SYM_K_NUMQUEUE ; NUMBER OF SYMBIONT CONTROL TABLES
;
;***********************************************************************
;
; DEFINE THE JOB CONTROLLER DATA BLOCK
;  THIS CONTAINS ALL THE IMPURE WORK LOCATIONS COMMON TO
;  VARIOUS PARTS OF THE JOB CONTROLLER.
;
	$DEFINI	JCD
 
	.BLKB	-<8*6>			; SET NEGATIVE OFFSET FOR LISTS
	.BLKB	-8			; SET NEGATIVE OFFSET FOR TIME
	.BLKB	-4			; NEGATIVE OFFSET FOR PROCESS ID
 
$DEF	JCD_T_START			; DEFINE START OF BLOCK
$DEF	JCD_Q_TIME	.BLKL	2	; SPACE FOR CURRENT TIME
$DEF	JCD_L_SMBPID	.BLKL	1	; OUTSTANDING CREATE SYMBIONT ID
$DEF	JCD_G_WORKLIST			; START OF WORK LIST AREA
$DEF	JCD_Q_SYMBSRV	.BLKL	2	; PRINT SYMBIONT SERVICE QUEUE
$DEF	JCD_Q_SYMBMAN	.BLKL	2	; PENDING REQUEST FOR SYMBINT MANAGER
$DEF	JCD_Q_PROCDEL	.BLKL	2	; LIST OF DELETED PROCESSES TO PROCESS
$DEF	JCD_Q_ACNTFIL	.BLKL	2	; PENDING REQUESTS TO ACCOUNTING MANAGER
$DEF	JCD_Q_SYSFUNC	.BLKL	2	; LIST OF SYSTEM FUNCTIONS TO PROCESS
$DEF	JCD_G_WLEND			; END OF WORK LIST AREA
$DEF	JCD_Q_FREEBUFR	.BLKL	2	; LIST OF FREE MESSAGE BUFFERS
$DEF	JCD_Q_SYMBWAIT	.BLKL	2	; SYMBIONT WAITING FOR WORK
$DEF	JCD_G_LISTS			; END OF LISTS
$DEF	JCD_T_INDEX0			; POINT TO INDEX FROM
$DEF	JCD_W_TMPCHAN	.BLKW	1	; TEMPORARY CHANNEL FOR VARIOUS USES
$DEF	JCD_W_MBCHAN	.BLKW	1	; MAILBOX CHANNEL FOR INPUT DATA
$DEF	JCD_W_FLAGS	.BLKW	1	; VARIOUS FLAG BITS
	_VIELD	JBC,0,<-		; BITS FLAGS FOR JOB CONTROLLER
		<MBREADREQ,,M>,-	; MAILBOX READ REQUIRED
		<SYMCREREQ,,M>,-	; SYMBIONT CREATE REQUIRED
		<SYMINIREQ,,M>,-	; SYMBIONT INITIATE REQUIRED
		<CREJOBREQ,,M>,-	; CREATE A BATCH JOB IS REQUIRED
		<SRVTIMQUE,,M>,-	; SERVICE THE TIME QUEUE
		<SRVCKLOGIN,,M>,-	; SERVICE CHECK JOB TIMER
		<SRVMIDNITE,,M>,-	; SERVICE MIDNITE
		>
$DEF	JCD_W_JOBSEQ	.BLKW	1	; JOB SEQUENCE NUMBER
$DEF	JCD_Q_LBUFDES			; DESCRIPTOR FOR LOCAL LINE BUFFER
$DEF	JCD_W_LBUFCNT	.BLKW	1	; COUNT OF BYTES IN BUFFER
$DEF	JCD_W_LBUFSIZ	.BLKW	1	; MAXIMUM SIZE OF THE BUFFER
$DEF	JCD_A_LBUFADR	.BLKL	1	; ADDRESS OF THE LINE BUFFER
$DEF	JCD_Q_TMPDESC	.BLKL	2	; A WORK QUADWORD DESCRIPTOR
$DEF	JCD_A_QUEBASE	.BLKL	1	; BASE ADDRESS OF PRINT QUE
$DEF	JCD_B_CURSYMB	.BLKB	1	; CURRENT COUNT OF PRINT SYMBIONTS
$DEF	JCD_B_CURPDEV	.BLKB	1	; NUMBER OF PRINT DEVICES
$DEF	JCD_T_LINBUF	.BLKB	800	; THE LINE BUFFER
$DEF	JCD_T_LINBFE	.BLKL	1	; THIS LOCATION MARKS THE END OF THE LINE BUFFER
$DEF	JCD_T_TABADD	.BLKB	18	; START OF SYMBIONT NAME TABLE
$DEF	JCD_T_ENTR1	.BLKB	18
$DEF	JCD_T_ENTR2	.BLKB	18
$DEF	JCD_T_ENTR3	.BLKB	18
$DEF	JCD_T_ENTR4	.BLKB	18
$DEF	JCD_T_ENTR5	.BLKB	18
$DEF	JCD_T_ENTR6	.BLKB	18
$DEF	JCD_T_ENTR7	.BLKB	18
$DEF	JCD_T_ENTR8	.BLKB	18
$DEF	JCD_T_ENTR9	.BLKB	18
$DEF	JCD_T_ENTR10	.BLKB	18
$DEF	JCD_T_ENTR11	.BLKB	18
$DEF	JCD_T_ENTR12	.BLKB	18
$DEF	JCD_T_ENTR13	.BLKB	18
$DEF	JCD_T_ENTR14	.BLKB	18
$DEF	JCD_T_ENTR15	.BLKB	18
 
	JCD_K_SIZE = .-JCD_T_START	; FIND TOTAL SIZE OF STRUCTURE
 
	$DEFEND	JCD
;
; DEFINE LIMIT OF OPERATOR REQUESTS
;
;
;
	.DSABL	DBG
; DEFINE JOB CONTROLLER MESSAGE
;
	$DEFINI	JCM
 

$DEF	JCM_Q_LINKS	.BLKQ	1	; LIST LINK WORDS
$DEF	JCM_Q_IOSB	.BLKQ	1	; IOSTAT FROM MAIL BOX READ
$DEF	JCM_T_MSGDATA	.BLKL	200	; MESSAGE DATA AREA
$DEF	JCM_K_SIZE			; SIZE OF MESSAGE
 
	$DEFEND	JCM
 
	$DEFINI	SMR
 
	.=-4
 
$DEF	SMR$L_PROCID	.BLKL	1	; OFFSET TO PROCID IN IOSB
$DEF	SMR$W_TYPE	.BLKW	1	; MESSAGE GENERIC TYPE
$DEF	SMR$W_MAILBOX	.BLKW	1	; RESPONCE MAILBOX UNIT
$DEF	SMR$Q_PRVMSK	.BLKL	2	; CURRENT PRIVLEDGE MASK OF SENDER
$DEF	SMR$L_UIC	.BLKL	1	; UIC OF SENDER
$DEF	SMR$T_USERNAM	.BLKL	3	; USER NAME OF SENDER
$DEF	SMR$Q_ACNTNAM	.BLKL	2	; CHARGE ACCOUNT
$DEF	SMR$B_PROCPRI	.BLKB	1	; BASE PRIORITY OF PROCESS
			.BLKB	3	; 3 SPARE BYTES
$DEF	SMR$L_PID	.BLKL	1	; PROCESS ID
$DEF	SMR$L_STS	.BLKL	1	; PROCESS STATUS
$DEF	SMR$L_OWNER	.BLKL	1	; OWNER PROCESS ID (0 => NONE)
$DEF	SMR$T_TERMINAL	.BLKB	8	; TERMINAL NAME (COUNTED ASCII STRING)
$DEF	SMR$Q_SYSTIME	.BLKQ	1	; CURRENT SYSTEM TIME
$DEF	SMR$W_USERREQ	.BLKW	1	; USER REQUEST TYPE
$DEF	SMR$T_DATA	.BLKB	126	; BEGINNING OF DATA AREA
 
	$DEFEND	SMR
 
	.ENDM
 
;
; DEFINE SYMBIONT CONTROL TABLE AND STATE VALUES.
;
 
	.MACRO	JBCSCTDEF		;
	$DEFINI	SCT
 
$DEF	SCT_Q_LINKS	.BLKL	2	; LIST LINK WORDS
$DEF	SCT_B_STATE	.BLKB	1	; STATE OF ASSOCIATED SYMBIONT
$DEF	SCT_B_FLAGS	.BLKB	1	; SYMBIONT CONTROL FLAGS
$DEF	SCT_W_MBCHAN	.BLKW	1	; MAILBOX CHANNEL TO SYMBIONT
$DEF	SCT_L_PROCID	.BLKL	1	; PROCESS ID FOR SYMBIONT
$DEF	SCT_L_CURFIL	.BLKL	1	; ADDRESS IN QUEUE OF FILE
$DEF	SCT_W_QINDEX	.BLKW	1	; INDEX TO CURRENT DEVICE QUEUE
$DEF	SCT_W_JOBSTAT	.BLKW	1	; STATUS OF PRINT FROM THE SYMBIONT
$DEF	SCT_L_CPUTIM	.BLKL	1	; ACCUMULATED CPU TIME FOR JOB
$DEF	SCT_L_PAGCNT	.BLKL	1	; COUNT OF NUMBER OF PAGES PRINTED
$DEF	SCT_L_QIOCNT	.BLKL	1	; NUMBER OF QIO REQUESTS ISSUED
$DEF	SCT_L_GETCNT	.BLKL	1	; COUNT THE NUMBER OF "GETS" ISSUED
$DEF	SCT_L_PNMTAB	.BLKL	1	; POINTER TO SYMBIONT NAME TABLE
$DEF	SCT_L_FILESIZ	.BLKL	1	; SIZE OF FILE CURRENTLY PRINTING
 
$DEF	SCT_K_SIZE			; SIZE OF STRUCTURE
 
	$DEFEND	SCT

;
; DEFINE SYMBIONT STATE BYTE VALUES
;
	$EQULST	SCT_K_,,0,1,<-
		FREE,-			; SYMBIONT NON-EXISTANT
		INIT,-			; SYMBIONT HAS INITED
		DEQJOB,-		; SYMBIONT NEEDS AN ASSIGNMENT
		DEQFIL,-		; SYMBIONT MUST BE SENT FILE TO PRINT
		TRMJOB,-		; TIME TO TERMINATE THE CURRENT JOB
		ABOREQ,-		; ABORT HAS BEEN REQUESTED
		REQUE,-			; ABORT THEN REQUEUE THE FILE
		SUSPND,-		; SYMBIONT IS SUSPENDED
		MRKDEL,-		; SYMBIONT HAS BEEN SENT DELETE REQUEST
		>
 
	.ENDM
