$ VERIFY = 'F$VERIFY(0)
$ !
$ !	VECTORTST.COM - Tests the vector of VMSRTL.EXE to make
$ !	sure that none of the vector locations have changed.
$ !
$ !	Version 1.2 - Steven B. Lionel - 9-July-1981
$ !
$ ON ERROR THEN CONTINUE
$ ON CONTROL_Y THEN GOTO ABORT
$ ABORT=0
$ SEVERITY=1
$ ANLSYMB:=" "
$ MASSYMB:=" "
$ IF P1 .EQS. "" THEN P1 := "SYS$LIBRARY:VMSRTL.EXE"
$ IF P2 .EQS. "" THEN P2 := "COM$:VECTORTST.DAT"
$ IF "''F$LOG("SYS$PUTMSG")'" .EQS. "" THEN DEFINE SYS$PUTMSG "@"
$ DEFINE/USER SYS$OUTPUT NL:
$ DEFINE/USER SYS$ERROR ANLERR.TMP
$ ANALYZE/IMAGE/OUT=VMSRTL.ANL 'P1'
$ IF $STATUS THEN GOTO ANLOK
$ SEVERITY=$SEVERITY
$ TYPE ANLERR.TMP
$ GOTO QUIT2
$ANLOK:
$ ON ERROR THEN GOTO QUIT
$ OPEN/ERR=QUIT ANLFIL VMSRTL.ANL
$ OPEN MASTER 'P2'
$ ! Get file name and creation time
$ READ ANLFIL LINE
$ READ ANLFIL LINE
$ READ ANLFIL FNAME
$F10:
$ READ ANLFIL LINE
$ IF 'F$LOCATE("link date/time",LINE) .EQS. 'F$LENGTH(LINE) THEN GOTO F10
$ IF "''F$EXTRACT(2,9,LINE)'" .NES. "link date" THEN GOTO F10
$ TIME:="''F$EXTRACT(18,99,LINE)
$ WRITE SYS$OUTPUT "%RTL-I-IDENT, analyzing image ''FNAME', created ''TIME'"
$ RETURN:="10"
$ GOTO SORTANL
$10:
$ RETURN:="TOP"
$ GOTO GETMAS
$TOP:
$ IF "''ANLSYMB'" .NES. "''MASSYMB'" THEN GOTO 20
$ IF "''ANLADDR'" .EQS. "''MASADDR'" THEN GOTO GETBOTH
$ IF "''MASADDR'" .EQS. "*" THEN GOTO GETBOTH	! Non-vectored symbol
$ LINE:="%RTL-E-VECHASMOV, vector symbol ''MASSYMB' has moved from ''MASADDR' to ''ANLADDR'"
$ WRITE SYS$OUTPUT LINE
$ IF SEVERITY .LT. 2 THEN SEVERITY=2
$ GOTO GETBOTH
$20:
$ IF "''ANLSYMB'" .LTS. "''MASSYMB'" THEN GOTO 30
$ LINE:="%RTL-E-SYMDEL, vector symbol ''MASSYMB' has been deleted"
$ WRITE SYS$OUTPUT LINE
$ IF SEVERITY .LT. 2 THEN SEVERITY=2
$ RETURN:="TOP"
$ GOTO GETMAS
$30:
$ LINE:="%RTL-W-NEWSYM, master file does not contain symbol ''ANLSYMB', address ''ANLADDR'"
$ WRITE SYS$OUTPUT LINE
$ IF SEVERITY .LT. 2 THEN SEVERITY=0
$ RETURN:="TOP"
$ GOTO GETANL
$GETBOTH:
$ RETURN:="40"
$ GOTO GETMAS
$40:
$ RETURN:="TOP"
$ GOTO GETANL
$GETMAS:
$ READ/END=MASEND MASTER LINE
$ IF "''F$EXTRACT(0,1,LINE)'" .EQS. "!" THEN GOTO GETMAS
$ N='F$LOCATE("-",LINE)
$ IF N .EQ. 'F$LENGTH(LINE) THEN GOTO MASERR
$ MASSYMB:="''F$EXTRACT(0,N,LINE)'"
$ L='F$LENGTH(LINE) - N - 1
$ N=N+1
$ MASADDR:="''F$EXTRACT(N,L,LINE)'"
$ GOTO 'RETURN
$MASERR:
$ WRITE SYS$OUTPUT "%RTL-E-SYNERRMAS, syntax error in master file - line follows"
$ WRITE SYS$OUTPUT " ""''LINE'"""
$ IF SEVERITY .LT. 2 THEN SEVERITY=2
$ GOTO GETMAS
$MASEND:
$ MASSYMB:="~"
$ MASADDR:="0"
$ IF "''ANLSYMB'" .EQS. "!" THEN GOTO QUIT
$ GOTO 'RETURN
$GETANL:
$ READ/END=ANLEND ANLFIL LINE
$ N='F$LOCATE("-",LINE)
$ IF N .EQ. 'F$LENGTH(LINE) THEN GOTO ANLERR
$ ANLSYMB:="''F$EXTRACT(0,N,LINE)'"
$ L='F$LENGTH(LINE) - N - 1
$ N=N+1
$ ANLADDR:="''F$EXTRACT(N,L,LINE)'"
$ COUNT=COUNT+1
$ IF COUNT .LT. 100 THEN GOTO 'RETURN
$ SUM=SUM+COUNT
$ WRITE SYS$OUTPUT "%RTL-I-PROGRESS, ''SUM symbols processed"
$ COUNT=0
$ GOTO 'RETURN
$ANLERR:
$ WRITE SYS$OUTPUT "%RTL-E-SYNERRANL, syntax error in analyze file - line follows"
$ WRITE SYS$OUTPUT " ""''LINE'"""
$ IF SEVERITY .LT. 2 THEN SEVERITY=2
$ GOTO GETANL
$ANLEND:
$ ANLSYMB:="!"
$ ANLADDR:="0"
$ IF "''MASSYMB'" .EQS. "~" THEN GOTO QUIT
$ GOTO 'RETURN
$SORTANL:
$ WRITE SYS$OUTPUT "%RTL-I-STARTPASS, start of pass 1"
$ OPEN/WRITE SRTANL VMSRTL.TMP
$ COUNT=0
$ SUM=0
$S10:
$ READ/END=SRTEND ANLFIL LINE
$ IF "''F$EXTRACT(2,6,LINE)'" .NES. "value:" THEN GOTO S10
$ N = 'F$LOCATE("%X",LINE)+3
$ ANLADDR := "''F$EXTRACT(N,8,LINE)'"
$A10:
$ READ/END=SRTEND ANLFIL LINE
$ IF "''F$EXTRACT(2,7,LINE)'" .NES. "symbol:" THEN GOTO A10
$ ANLSYMB := "''F$EXTRACT(11,32,LINE)'"
$ N = 'F$LENGTH(ANLSYMB) - 1
$ ANLSYMB := "''F$EXTRACT(0,N,ANLSYMB)'"
$ WRITE SRTANL "''ANLSYMB'-''ANLADDR'"
$ COUNT=COUNT+1
$ IF COUNT .LT. 100 THEN GOTO S10
$ SUM=SUM+100
$ WRITE SYS$OUTPUT "%RTL-I-PROGRESS, ''SUM' symbols processed"
$ COUNT=0
$ GOTO S10
$SRTEND:
$ CLOSE SRTANL
$ CLOSE ANLFIL
$ SUM=SUM+COUNT
$ WRITE SYS$OUTPUT "%RTL-I-PROGRESS, ''SUM' symbols processed"
$ DEFINE/USER SYS$OUTPUT NL:
$ SORT/KEY=(POS:1,SIZE:31) VMSRTL.TMP VMSRTL.TMP
$ PURGE VMSRTL.TMP
$ OPEN ANLFIL VMSRTL.TMP
$ COUNT=0
$ SUM=0
$ WRITE SYS$OUTPUT "%RTL-I-STARTPASS, start of pass 2"
$ GOTO GETANL
$ABORT:
$ ABORT=1
$QUIT:
$ N=$SEVERITY
$QUIT2:
$ SET NOON
$ IF N .EQ. 1 THEN N=0
$ IF SEVERITY .LT. N THEN SEVERITY=N
$ IF "''F$LOG("MASTER")" .NES. "" THEN CLOSE MASTER
$ IF "''F$LOG("ANLFIL")" .NES. "" THEN CLOSE ANLFIL
$ IF "''F$LOG("SRTANL")" .NES. "" THEN CLOSE SRTANL
$ IF F$SEARCH("VMSRTL.ANL") .NES. "" THEN DELETE VMSRTL.ANL;*
$ IF F$SEARCH("VMSRTL.TMP") .NES. "" THEN DELETE VMSRTL.TMP;*
$ IF F$SEARCH("ANLERR.TMP") .NES. "" THEN DELETE ANLERR.TMP;*
$ IF SEVERITY .EQ. 0 THEN WITH := "completed with warnings"
$ IF SEVERITY .EQ. 1 THEN WITH := "completed successfully"
$ IF SEVERITY .GE. 2 THEN WITH := "completed with errors"
$ IF ABORT THEN WITH := "aborted with ^Y"
$ LET := "''F$EXTRACT(SEVERITY,1,"WSEIF???")
$ SEVERITY = SEVERITY + %X10000000
$ WRITE SYS$OUTPUT "%RTL-''LET'-DONE, vector analysis ''WITH'"
$ IF VERIFY THEN SET VERIFY
$ EXIT 'SEVERITY
