	.TITLE	MTH$CEXP	COMPLEX EXPONENTIATION
	.IDENT	/1-002/		; File: MTHCEXP.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	Perform complex exponentiation: e**(r,i)
;
;
;--
;
; VERSION: 0
;
; HISTORY:
;
; AUTHOR:
;	Jonathan M. Taylor, 19-JUL-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 0 of MTH$CEXP
;
; 1-001	- Update version number and copyright notice.  The last edit
;	   number for version 0 was 4.  JBS 16-NOV-78
; 1-002	- Add "_" to the PSECT directive.  JBS 21-DEC-78

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;	OERR.MAR

;
; EXTERNAL SYMBOLS:
	.GLOBL	MTH$SIN_R4
	.GLOBL	MTH$COS_R4
	.GLOBL	MTH$EXP_R4

;
; MACROS:
;	NONE

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
	argadr	=	4	; offset from AP of arg adr

;
; OWN STORAGE:
;	NONE

	.SBTTL	MTH$CEXP - perform COMPLEX exponentiation

;++
; FUNCTIONAL DESCRIPTION:
;
;	The result of the operation e ** (r, i) is computed
;	by:
;
;	result = (EXP(r) * COS(i), EXP(r) * SIN(i))
;
; CALLING SEQUENCE:
;	Exponential.wfc.v	= MTH$CEXP(arg.rfc.r)
;
;
; INPUT PARAMETERS:
;	The one input parameter is the address of COMPLEX number (r, i),
;	where r and i are both single-precision floating point values.
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	Signals:	MTH$_SINSIGLOS	if |i| > 2*PI*2**31.
;			Floating Overflow if r > 88.028
;
;--


	.ENTRY	MTH$CEXP,	^M<R2,R3,R4,R5,R6,R7>
	MTH$FLAG_JACKET			; resignal

	MOVL	@argadr(AP), R0		; R0 = r
	JSB	MTH$EXP_R4		; R0 = EXP(r)
	MOVL	R0, R5			; R5 = EXP(r)

	MOVL	argadr(AP), R6		; R6 -> (r, i)

	MOVL	4(R6), R0		; R0 = i
	JSB	MTH$SIN_R4		; R0 = SIN(i)
	MOVL	R0, R7			; R7 = SIN(i)

	MOVL	4(R6), R0		; R0 = i
	JSB	MTH$COS_R4		; R0 = COS(i)

	MULF	R5, R0			; R0 = COS(i) * EXP(r)
	MULF3	R7, R5, R1		; R1 = SIN(i) * EXP(r)
	RET


	.END

