MODULE LIB$CRC_TABLE (	! Library CRC table genarator 
	IDENT = '1-002', ! File: LIBCRCTAB.B32
	LINKAGE(FORTRAN)
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:  General Utility Library
!
! ABSTRACT:
!
!
! Given a polynomial, a table is constructed which can be
! used by the LIB$CRC module for generating the CRC value for an
! input stream.
!
! ENVIRONMENT:  User Mode - AST re-entrant
!
! AUTHOR:  Donald G. Petersen, CREATION DATE:  09-Jan-78
!
! MODIFIED BY:
!
! 	Donald G. Petersen, 09-Jan-78: VERSION 0
! 01	- original
! 0-02	- Change REQUIRE files for VAX system build.  DGP 28-Apr-78
! 0-03	- Change file name to LIBCRCTAB, and change the name of
!	   the require file similarly.  JBS 14-NOV-78
! 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
! 1-002 - Don't use arithmetic shift for right shift.  SBL 4-Dec-1979
!--

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
	LIB$CRC_TABLE:NOVALUE;	! CRC table build routine 

!
! INCLUDE FILES:
!
	REQUIRE 'RTLIN:RTLPSECT';	! Define DECLARE_PSECT macros
!
! MACROS:
!
!	NONE	
!
! EQUATED SYMBOLS:
!
!	NONE	
!
! PSECT DECLARATIONS:
!

	DECLARE_PSECTS (LIB);		! declare PSECTs for LIB$ facility

!
! OWN STORAGE:
!
!	NONE	
!
! EXTERNAL REFERENCES:
!
!	NONE	

GLOBAL ROUTINE LIB$CRC_TABLE  (	! Build a table for the CRC routine 
    POLY,			! Adr of CRC polynomial input
    TABLE)			! Adr of where table is to be built
	:NOVALUE =		! No value is returned
!++
! FUNCTIONAL DESCRIPTION:
!
! The input polynomial is used to produce a table with 16 longword entries
! which is the proper format to be used by the LIB$CRC routine for
! calculating the CRC for a data stream.
!
! FORMAL PARAMETERS:
!
!	poly.rl.r		input CRC polynomial
!	table.wl.ar		Adr of where table of 16 longwords

!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN
    MAP
	TABLE: REF VECTOR [16],
	POLY: REF VECTOR [1];
    LOCAL
	TMP,			! temporary
	X,			! used as a flag
	INDEX,			! index into the CRC table
	I;			! iteration index for making each entry
    INCR INDEX FROM 0 TO 15 DO
	BEGIN
	TMP = .INDEX;
	INCR I FROM 1 TO 4 DO
	    BEGIN
	    X = .TMP AND 1;
	    TMP = .TMP<1,31>;	! Equivalent to .TMP ^ -1
	    IF .X EQL 1 THEN
		TMP = .TMP XOR .POLY[0]
	    END;
	TABLE[.INDEX] = .TMP
	END;
    RETURN;
    END;
   END
   ELUDOM
