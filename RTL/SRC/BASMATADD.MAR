	.TITLE	BAS$MAT_ADD
	.IDENT	/1-006/			; File: BASMATADD.MAR Edit: PLL1006

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: BASIC code support
;
; ABSTRACT:
;
;	This module adds 2 arrays of any dtype and stores the result in a
;	third array.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: R. Will, CREATION DATE: 18-Jun-79
;
; MODIFIED BY:
;++
; 1-001	- Original
; 1-002 - Set IV flag in entry mask.  RW  2-Oct-79
; 1-003 - Changed external references to G^ RNH 25-Sep-81
; 1-004 - Substitute a macro for the calls to the array fetch and store
;	  routines.  This should speed things up.  PLL 6-Nov-81
; 1-005 - Correct a run-time expression in the FETCH and STORE macros.
;	  PLL 20-Jan-82
; 1-006 - Correct FETCH, STORE again.  PLL 23-Feb-82
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

	$DSCDEF					; define descriptor offsets
	$SFDEF					; use to get scale

;
; EXTERNAL DECLARATIONS:
;

	.DSABL	GBL				; Prevent undeclared
						;  symbols from being
						;  automatically global.
	.EXTRN	BAS$K_ARGDONMAT			; signalled if all 3 blocks
						;  not present in array desc
						;  or dimct = 0
	.EXTRN	BAS$K_DATTYPERR			; signalled if dtype of array
						; isn't word long float double
	.EXTRN	BAS$K_MATDIMERR			; signalled if # of dims on
						;  source arrays don't agree
	.EXTRN	BAS$K_ARRMUSSAM			; signalled if upper and lower
						;  bnds not same on src arrays
	.EXTRN	BAS$STO_FA_W_R8			; array element store for word
	.EXTRN	BAS$STO_FA_L_R8			; array element store for long
	.EXTRN	BAS$STO_FA_F_R8			; array element store - float
	.EXTRN	BAS$STO_FA_D_R8			; array element store - double
	.EXTRN	BAS$FET_FA_W_R8			; array element fetch - word
	.EXTRN	BAS$FET_FA_L_R8			; array element fetch - long
	.EXTRN	BAS$FET_FA_F_R8			; array element fetch - float
	.EXTRN	BAS$FET_FA_D_R8			; array element fetch - double
	.EXTRN	BAS$MAT_REDIM			; check if redimensioning of
						;  dest array is necessary, if
						;  so, do it
	.EXTRN	BAS$$SCALE_R1			; scale for double procision
	.EXTRN	BAS$$STOP			; signal fatal errors


;
; MACROS:
;

;	$BAS$MAT_ADD	add loop algorithm,  see next page
;	FETCH		fetch an element from an array
;	STORE		store an element into an array

;
; EQUATED SYMBOLS:
;

	lower_bnd2 = 0				; stack offset for temp
	lower_bnd1 = 4				; stack offset for temp
	upper_bnd1 = 8				; stack offset for temp
	save_src1 = 12				; stack offset for temp
	dsc$l_l1_1 = 24				; desc offset if 1 sub
	dsc$l_u1_1 = 28				; desc offset if 1 sub
	dsc$l_l1_2 = 28				; desc offset if 2 sub
	dsc$l_u1_2 = 32				; desc offset if 2 sub
	dsc$l_l2_2 = 36				; desc offset if 2 sub
	dsc$l_u2_2 = 40				; desc offset if 2 sub

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT _BAS$CODE PIC, USR, CON, REL, LCL, SHR, -
			EXE, RD, NOWRT, LONG

;+
; These macros are a substitute for calls to the array fetch and store
; routines.  They will call the BAS$ routines only if the array is a 
; virtual array.  Otherwise, they will calculate the linear index into
; the array via the INDEX instruction.  (Note that BASIC programs must
; be able to handle FORTRAN arrays, so the code must check for arrays
; stored by column.)  The INDEX instructions should provide a significant
; performance improvement over calling a routine for each element of
; the array.
;-

	.LIST	ME
	.MACRO	FETCH	array_dtype,?L1,?L2,?L3,?L11,?L21 ; fetch an array element

	CMPB	dsc$b_class(R0), #dsc$k_class_bfa  ;virtual array?
	BNEQ	L1				; no
	JSB	BAS$FET_FA_'array_dtype'_R8	; yes, use the fetch routine
	BRW	L3				; done
L1:	BBS	#5, 10(R0), L2			; br if array stored by cols
	CMPB	dsc$b_dimct(R0), #1		; 1 or 2 dims?
	BNEQ	L11				; 2 dims
	MOVZWL	dsc$w_length(R0), R4		; make length longword
	INDEX	R1, dsc$l_l1_1(R0), dsc$l_u1_1(R0), R4, #0, R3
	ADDL	dsc$a_a0(R0), R3		; add start addr to offset
	MOV'array_dtype'	(R3), R0	; return element in R0
	BRW	L3				; 1 dim done
L11:	INDEX	R1, dsc$l_l1_2(R0), dsc$l_u1_2(R0), dsc$l_m2(R0), #0, R3
						; I * M2  [Li's are zero]
	MOVZWL	dsc$w_length(R0), R4		; need longword length for INDEX
	INDEX	R2, dsc$l_l2_2(R0), dsc$l_u2_2(R0), R4, R3, R3
						; (J + (I * M2)) * length 
	ADDL	dsc$a_a0(R0), R3		; compute addr of element 
	MOV'array_dtype'	(R3), R0	; return element in R0
	BRW	L3				; done
L2:	CMPB	dsc$b_dimct(R0), #1		; 1 or 2 dims?
	BNEQ	L21				; 2 dims
	MOVZWL	dsc$w_length(R0), R4		; make length longword
	INDEX	R2, dsc$l_l1_1(R0), dsc$l_u1_1(R0), R4, #0, R3
	ADDL	dsc$a_a0(R0), R3		; add start addr to offset
	MOV'array_dtype'	(R3), R0	; return element in R0
	BRW	L3				; 1 dim done
L21:	INDEX	R2, dsc$l_l2_2(R0), dsc$l_u2_2(R0), dsc$l_m1(R0), #0, R3
						; J * M1  [Li's are zero]
	MOVZWL	dsc$w_length(R0), R4		; need longword length for INDEX
	INDEX	R1, dsc$l_l1_2(R0), dsc$l_u1_2(R0), R4, R3, R3
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R0), R3		; compute addr of element
	MOV'array_dtype'	(R3), R0	; return element in R0

L3:
	.ENDM



	.LIST	ME
	.MACRO	STORE	array_dtype,?L1,?L2,?L3,?L4,?L5,?L11,?L21,?L31,?L41

	.IF	IDN	'array_dtype', D	; array is double
	CMPB	dsc$b_class(R2), #dsc$k_class_bfa  ; virtual array?
	BNEQ	L1				; no
	JSB	BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L5				; done
L1:	BBS	#5, 10(R2), L2			; br if stored col-wise
	CMPB	dsc$b_dimct(R2), #1		; 1 or 2 dim?
	BNEQ	L11				; 2 dims
	MOVZWL	dsc$w_length(R2), R6		; make length a longword
	INDEX	R3, dsc$l_l1_1(R2), dsc$l_u1_1(R2), R6, #0, R5
	ADDL	dsc$a_a0(R2), R5		; add start addr to offset
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L5				; 1 dim done
L11:	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), dsc$l_m2(R2), #0, R5
						; I * M2
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), R6, R5, R5
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L5				; done
L2:	CMPB	dsc$b_dimct(R2), #1		; 1 or 2 dim?
	BNEQ	L21				; 2 dims
	MOVZWL	dsc$w_length(R2), R6		; make length a longword
	INDEX	R4, dsc$l_l1_1(R2), dsc$l_u1_1(R2), R6, #0, R5
	ADDL	dsc$a_a0(R2), R5		; add start addr to offset
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L5				; 1 dim done
L21:	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), dsc$l_m1(R2), #0, R5
						; J * M1
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), R6, R5, R5
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	.IFF					; array type other than double
	CMPB	dsc$b_class(R1), #dsc$k_class_bfa  ;virtual array?
	BNEQ	L3				; no
	JSB	BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L5				; done
L3:	BBS	#5, 10(R1), L4			; br if stored col-wise
	CMPB	dsc$b_dimct(R1), #1		; 1 or 2 dims?
	BNEQ	L31				; 2 dims
	MOVZWL	dsc$w_length(R1), R6		; make length a longword
	INDEX	R2, dsc$l_l1_1(R1), dsc$l_u1_1(R1), R6, #0, R4
	ADDL	dsc$a_a0(R1), R4		; add start addr to offset
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L5				; 1 dim done
L31:	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), dsc$l_m2(R1), #0, R4
						; I * M2
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), R5, R4, R4
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L5				; done
L4:	CMPB	dsc$b_dimct(R1), #1		; 1 or 2 dims?
	BNEQ	L41				; 2 dims
	MOVZWL	dsc$w_length(R1), R6		; make length a longword
	INDEX	R3, dsc$l_l1_1(R1), dsc$l_u1_1(R1), R6, #0, R4
	ADDL	dsc$a_a0(R1), R4		; add start addr to offset
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L5				; 1 dim done
L41:	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), dsc$l_m1(R1), #0, R4
						; J * M1
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), R5, R4, R4
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	.ENDC

L5:
	.ENDM



;+
; This macro contains the looping mechanism for accessing all elements of
; an array.  It also contains all the logic for all the combinations of data
; types and scaling.  A macro is used to make it easy to maintain the parallel
; code for all the different data types.
;-

	.LIST	ME
	.MACRO	$BAS$MAT_ADD src1_dtype, src2_dtype ; add algorithm

;+
; Loop through all the rows.  Row and column upper and lower bounds have been
; initialized on the stack.
;-

LOOP_1ST_SUB'src1_dtype'src2_dtype':
	MOVL	lower_bnd2(SP), R11		; R11 has 2nd lower bound

;+
; Loop through all the elements (columns) of the current row.  Column lower
; bound is initialized in R11.  Column upper bound is on the stack.
; Distinguish array by data type so that the correct fetch routine can 
; retrieve the data, the correct add can be done and the correct
; store routine can be called.
;-

LOOP_2ND_SUB'src1_dtype'src2_dtype':

;+
; Get the data from the first source array
;-

	MOVL	src1_matrix(AP), R0		; pointer to 1st src array
	MOVL	lower_bnd1(SP), R1		; current row
	MOVL	R11, R2				; current col
	FETCH	'src1_dtype'			; fetch data from src1 array
	MOV'src1_dtype'	R0, save_src1(SP)	; store the 1st array element

;+
; Get the data from the second source array
;-

	MOVL	src2_matrix(AP), R0		; pointer to 2nd src array
	MOVL	lower_bnd1(SP), R1		; current row
	MOVL	R11, R2				; current col
	FETCH	'src2_dtype'			; fetch data from src2 array

;+
; If the data types of the 2 source arrays is the same, do the arithmetic
; in that data type.  Else convert the data to a common type and add.
; If scaling is needed (ie if at least one but not both of the arrays is
; double) convert integer to double.  (Note that the integerize is not
; necessary because only integers (not float) can be converted to double,
; and the sum of 2 integers is guaranteed to be integer).
;-

	.IF	IDN	src1_dtype, src2_dtype	; src arrays are
						; same data type
	ADD'src1_dtype'2	save_src1(SP), R0 ; add the source elements
	BSBW	DEST_CASE_'src1_dtype'		; go to store in dest
	.IFF					; src arrays different dtype
	.IF	IDN	src1_dtype, D		; source 1 is double
	CVT'src2_dtype'D	R0, -(SP)	; cvt array2 to double & save
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	MULD2	(SP)+, R0			; scale 2nd element
						;  NOTE that integerize
						;  is not required after mul
						;  because data was already
						;  an integer (dbl & dbl not
						;  in this cond code and
						;  float & dbl not allowed)
	ADDD2	save_src1(SP), R0		; add 1st element & scaled 2nd
	BSBW	DEST_CASE_D			; cvrt double sum to dest type
	.IFF					; 1st array not double
	.IF	IDN	src2_dtype, D		; is 2nd src double
	CVT'src1_dtype'D	save_src1(SP), save_src1(SP)
						; yes, make src1 double & save
	MOVD	R0, -(SP)			; save the data
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	MULD2	save_src1+8(SP), R0		; scale, (+8 because src2 is
						;  double and saved on stack
						; NOTE that integerize
						;  is not required after mul
						;  because data was already
						;  an integer (dbl & dbl not
						;  in this cond code and
						;  float & dbl not allowed)
	ADDD2	(SP)+, R0			; compute the sum
	BSBW	DEST_CASE_D			; cvrt double sum to dest type
	.IFF					; no double operands try float
	.IF	IDN	src1_dtype, F		; is 1st element float
	CVT'src2_dtype'F	R0, R0		; make 2nd element float
	ADDF2	save_src1(SP), R0		; add
	BSBW	DEST_CASE_F			; cvrt float sum to dest type
	.IFF					; 1st array not float
	.IF	IDN	src2_dtype, F		; is 2nd array float
	CVT'src1_dtype'F	save_src1(SP), R1 ; yes-make 1st element float
	ADDF2	R1, R0				; add
	BSBW	DEST_CASE_F			; cvrt float sum to dest type
	.IFF					; no double or float, try long
	.IF	IDN	src1_dtype, L		; is 1st array long
	CVT'src2_dtype'L	R0, R0		; make 2nd element long
	ADDL2	save_src1(SP), R0		; add
	BSBW	DEST_CASE_L			; convrt long sum to dest type
	.IFF					; src2 must be long, because
						;  all that is left is word
						;  and word and that will be
						;  caught by 1st case
	CVT'src1_dtype'L	save_src1(SP), R1 ; cvt src1 to long (know its
						;  type word, but use argument
						;  in case ever add byte
	ADDL2	R1, R0				; add
	BSBW	DEST_CASE_L			; convrt long sum to dest type
	.ENDC
	.ENDC
	.ENDC
	.ENDC
	.ENDC
	.ENDC

;+
; Have stored that element.  Now see if it was the last column.  If not,
; continue with the next column.  Otherwise continue to next row.
;-

	INCL	R11				; get next column
	CMPL	R11, R9				; see if last column done
	BGTR	5$
	BRW	LOOP_2ND_SUB'src1_dtype'src2_dtype' ; no, continue inner loop

;+
; Have completed entire row.  See if it was the last row.  If not,
; continue with next row.
;-

5$:	INCL	lower_bnd1(SP)			; get next row
	CMPL	lower_bnd1(SP), upper_bnd1(SP)	; see if last row done
	BGTR	10$
	BRW	LOOP_1ST_SUB'src1_dtype'src2_dtype' ; no, continue outer loop

10$:	RET					; yes, finished

	.ENDM

	.SBTTL	BAS$MAT_ADD  - Add 2 arrays giving a third
;++
; FUNCTIONAL DESCRIPTION:
;
;	Add 2 arrays giving a third.  Signal an error if the 2 arrays to be
;	added do not have the same number of dimensions and the same
;	upper and lower bounds for those dimensions.  Redimension the output
;	array to have the same upper bounds as the input arrays.
;	Initialize all the necessary
;	looping	information on the stack.  Conversions may have to be done
;	so that the sources are the same data type, so divide
;	the looping portion according to the data types.  Conversion to the
;	correct destination data type will be done by a JSB to a routine,
;	instead of multiplying the number of possible combinations by 4.
;
; CALLING SEQUENCE:
;
;	CALL BAS$MAT_ADD (src1_array.rx.da, src2_array.rw.da, dest_matrix.wx.da)
;
; INPUT PARAMETERS:
;
	src1_matrix = 4
	src2_matrix = 8
;
; IMPLICIT INPUTS:
;
;	Scale from the callers frame to scale double precision.
;
; OUTPUT PARAMETERS:
;
	dest_matrix = 12
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	This routine calls the redimensioning routine and the array element
;	fetch and store routines and therefore may signal any of their errors.
;	It may also signal any of the errors listed in the externals section.
;	It may also cause the destination array to have different dimensions.
;
;--

	.ENTRY BAS$MAT_ADD, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,IV>

;+
;	REGISTER USAGE
;	R0 - R8 destroyed by store routines
;	R9	upper bound for 2nd subscript
;	R10	pointer to dest array descriptor
;	R11	current value of 2nd subscript
;-

;+
; Put routine arguments into registers for ease of use.
; If block 2 of array descriptor (multipliers) is not present then error.
;-

	MOVL	src1_matrix(AP), R2		; ptr to src1 array descr
	BBC	#DSC$V_FL_BOUNDS, DSC$B_AFLAGS(R2), ERR_ARGDONMAT
						; exit if block 3 not
						; present in descriptor
	MOVL	src2_matrix(AP), R3		; ptr to src2 array descr
	BBC	#DSC$V_FL_BOUNDS, DSC$B_AFLAGS(R3), ERR_ARGDONMAT
						; exit if block 3 not
						; present in descriptor
	MOVL	dest_matrix(AP), R10		; ptr to dest descriptor
	CLRD	-(SP)				; reserve space to save src1

;+
; Set up limits for looping through all elements
;-

	CMPB	DSC$B_DIMCT(R2), #1		; determine # of subscripts
	BEQLU	INIT_ONE_SUB			; 1 sub, go init
	BGTRU	INIT_TWO_SUBS			; >=2 subs, go init
						; 0 subs, fall into error proc

ERR_ARGDONMAT:
	PUSHL	#BAS$K_ARGDONMAT		; signal error, 0 for dimct
	CALLS	#1, G^BAS$$STOP			; or block 2 or 3 absent

;+
; There is only 1 subscript.  Redimension the destination array.
; Make both upper and lower bound for 2nd
; subscript a 1.  A second subscript will be passed to and ignored by the
; store routine.  Put bounds for 1st subscript on stack.
;-

INIT_ONE_SUB:
	CMPB	#1, DSC$B_DIMCT(R3)		; do src arrays have same
						;  number of dimensions
	BNEQU	ERR_MATDIMERR			; no, error
	CMPB	dsc$l_u1_1(R2), dsc$l_u1_1(R3)	; do src arrays have the same
						;  upper bounds
	BNEQU	ERR_ARRMUSSAM			; no, error
	CMPB	dsc$l_l1_1(R2), dsc$l_l1_1(R3)	; do src arrays have the same
						;  lower bounds
	BNEQU	ERR_ARRMUSSAM			; no, error
	PUSHL	dsc$l_u1_1(R3)			; get bound for redim
	PUSHL	R10				; pointer to dest array desc
	CALLS	#2, BAS$MAT_REDIM		; redimension the dest
	PUSHL	dsc$l_u1_1(R3)			; 1st upper bound
	PUSHL	dsc$l_l1_1(R3)			; 1st lower bound
	BGTR	1$				; not 0 or neg, do 2nd sub
	MOVL	#1, (SP)			; don't alter col 0
1$:	PUSHL	#1				; dummy 2nd upper bound
	MOVL	#1, R9				; dummy 2nd lower bound
	BRB	SEPARATE_DTYPES			; go loop

ERR_MATDIMERR:
	PUSHL	#BAS$K_MATDIMERR		; Signal error, src arrays
	CALLS	#1, G^BAS$$STOP			;  don't have same # dimensns

ERR_ARRMUSSAM:
	PUSHL	#BAS$K_ARRMUSSAM		; Signal error, src arrays
	CALLS	#1, G^BAS$$STOP			;  same bounds

;+
; There are 2 subscripts.  Check and redimension the destination array if
; necessary.  Put the upper bound for both subscripts on the
; stack and make sure that the lower bound for both subscripts will start
; at 1 (do not alter row or col 0)
;-

INIT_TWO_SUBS:
	CMPB	#2, DSC$B_DIMCT(R3)		; do src arrays have same
						;  number of dimensions
	BNEQU	ERR_MATDIMERR			; no, error
	CMPB	dsc$l_u1_2(R2), dsc$l_u1_2(R3)	; do src arrays have the same
						;  1st upper bounds
	BNEQU	ERR_ARRMUSSAM			; no, error
	CMPB	dsc$l_l1_2(R2), dsc$l_l1_2(R3)	; do src arrays have the same
						;  1st lower bounds
	BNEQU	ERR_MATDIMERR			; no, error
	CMPB	dsc$l_u2_2(R2), dsc$l_u2_2(R3)	; do src arrays have the same
						;  2nd upper bounds
	BNEQU	ERR_ARRMUSSAM			; no, error
	CMPB	dsc$l_l2_2(R2), dsc$l_l2_2(R3)	; do src arrays have the same
						;  2nd lower bounds
	BNEQU	ERR_ARRMUSSAM			; no, error
	PUSHL	dsc$l_u2_2(R3)			; 2nd upper bound
	PUSHL	dsc$l_u1_2(R3)			; 1st upper bound
	PUSHL	R10				; dest array pointer
	CALLS	#3, BAS$MAT_REDIM		; redimension destination
	PUSHL	dsc$l_u1_2(R3)			; 1st upper bound
	PUSHL	dsc$l_l1_2(R3)			; 1st lower bound
	BGTR	1$				; not row 0 or neg, do cols
	MOVL	#1, (SP)			; start with row 1
1$:	MOVL	dsc$l_u2_2(R3), R9		; 2nd upper bound
	PUSHL	dsc$l_l2_2(R3)			; 2nd lower bound
	BGTR	SEPARATE_DTYPES			; not col 0 or neg, go loop
	MOVL	#1, (SP)			; start with col 1

;+
; Algorithm now differs according to data types
;-

SEPARATE_DTYPES:
	CASEB	DSC$B_DTYPE(R2), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
2$:	.WORD	WORD-2$				; code for word dtype
	.WORD	LONG-2$				; code for long dtype
	.WORD	ERR_DATTYPERR-2$		; quad not supported
	.WORD	FLOAT-2$			; code for float dtype
	.WORD	DOUBLE-2$			; code for double dtype

ERR_DATTYPERR:
	PUSHL	#BAS$K_DATTYPERR		; Signal error, unsupported
	CALLS	#1, G^BAS$$STOP			; dtype in array desc

	.PAGE
;+
; Source array is a word array.  Now differentiate on the destination type.
;-

WORD:	CASEB	DSC$B_DTYPE(R3), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	WORD_TO_WORD-1$			; code for word dtype
	.WORD	WORD_TO_LONG-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	WORD_TO_FLOAT-1$		; code for float dtype
	.WORD	WORD_TO_DOUBLE-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
WORD_TO_WORD:	$BAS$MAT_ADD	W, W

	.PAGE
WORD_TO_LONG:	$BAS$MAT_ADD	W, L

	.PAGE
WORD_TO_FLOAT:	$BAS$MAT_ADD	W, F

	.PAGE
WORD_TO_DOUBLE:	$BAS$MAT_ADD	W, D

	.PAGE
;+
; Source array is a longword array.  Now differentiate on the destination type
;-

LONG:	CASEB	DSC$B_DTYPE(R3), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	LONG_TO_WORD-1$			; code for word dtype
	.WORD	LONG_TO_LONG-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	LONG_TO_FLOAT-1$		; code for float dtype
	.WORD	LONG_TO_DOUBLE-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
LONG_TO_WORD:	$BAS$MAT_ADD	L, W

	.PAGE
LONG_TO_LONG:	$BAS$MAT_ADD	L, L

	.PAGE
LONG_TO_FLOAT:	$BAS$MAT_ADD	L, F

	.PAGE
LONG_TO_DOUBLE:	$BAS$MAT_ADD	L, D

	.PAGE
;+
; Source array is a floating array.  Now differentiate on the destination type
;-

FLOAT:	CASEB	DSC$B_DTYPE(R3), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	FLOAT_TO_WORD-1$		; code for word dtype
	.WORD	FLOAT_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	FLOAT_TO_FLOAT-1$		; code for float dtype
	.WORD	FLOAT_TO_DOUBLE-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
FLOAT_TO_WORD:	$BAS$MAT_ADD	F, W

	.PAGE
FLOAT_TO_LONG:	$BAS$MAT_ADD	F, L

	.PAGE
FLOAT_TO_FLOAT:	$BAS$MAT_ADD	F, F

	.PAGE
FLOAT_TO_DOUBLE: $BAS$MAT_ADD	F, D

	.PAGE
;+
; Source array is a double array.  Now differentiate on the destination type.
;-

DOUBLE:	CASEB	DSC$B_DTYPE(R3), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	DOUBLE_TO_WORD-1$		; code for word dtype
	.WORD	DOUBLE_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	DOUBLE_TO_FLOAT-1$		; code for float dtype
	.WORD	DOUBLE_TO_DOUBL-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
DOUBLE_TO_WORD:	$BAS$MAT_ADD	D, W

	.PAGE
DOUBLE_TO_LONG:	$BAS$MAT_ADD	D, L

	.PAGE
DOUBLE_TO_FLOAT: $BAS$MAT_ADD	D, F

	.PAGE
DOUBLE_TO_DOUBL: $BAS$MAT_ADD	D, D

;+
; Add has been in word.  Determine destination type to convert to dest.
;-

DEST_CASE_W:
	CASEB	DSC$B_DTYPE(R10), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	STORE_WORD-1$			; no conversion needed
	.WORD	DEST_W_TO_L-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	DEST_W_TO_F-1$			; code for float dtype
	.WORD	DEST_W_TO_D-1$			; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Add has been in long.  Determine destination type to convert to dest.
;-

DEST_CASE_L:
	CASEB	DSC$B_DTYPE(R10), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	DEST_L_TO_W-1$			; code for word dtype
	.WORD	STORE_LONG-1$			; no conversion needed
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	DEST_L_TO_F-1$			; code for float dtype
	.WORD	DEST_L_TO_D-1$			; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Add has been in float.  Determine destination type to convert to dest.
;-

DEST_CASE_F:
	CASEB	DSC$B_DTYPE(R10), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	DEST_F_TO_W-1$			; code for word dtype
	.WORD	DEST_F_TO_L-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	STORE_FLOAT-1$			; no conversion needed
	.WORD	ERR_DATTYPERR-1$		; double unsupported w/ float
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Add has been in double.  Determine destination type to convert to dest.
;-

DEST_CASE_D:
	CASEB	DSC$B_DTYPE(R10), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	DEST_D_TO_W-1$			; code for word dtype
	.WORD	DEST_D_TO_L-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	ERR_DATTYPERR-1$		; float unsupported w/ double
	.WORD	STORE_DOUBLE-1$			; no conversion needed
	BRW	ERR_DATTYPERR			; unsupported dtype

DEST_L_TO_W:
	CVTLW	R0, R0				; convert
	BRB	STORE_WORD			; go store

DEST_F_TO_W:
	CVTFW	R0, R0				; convert
	BRB	STORE_WORD			; go store

DEST_D_TO_W:
	MOVD	R0, -(SP)			; save double
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
	DIVD3	R0, (SP)+, R0			; descale for dest
	CVTDW	R0, R0				; convert to word
						; fall in to word store

STORE_WORD:
	MOVL	R10, R1				; pointer to dest descriptor
	MOVL	lower_bnd1+4(SP), R2		; current row (extra longword
						;  on top of stack for jsb)
	MOVL	R11, R3				; current column
	STORE	W				; store
	RSB					; go continue loop

DEST_W_TO_L:
	CVTWL	R0, R0				; convert
	BRB	STORE_LONG			; go store

DEST_F_TO_L:
	CVTFL	R0, R0				; convert
	BRB	STORE_LONG			; go store

DEST_D_TO_L:
	MOVD	R0, -(SP)			; save double
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
	DIVD3	R0, (SP)+, R0			; descale for dest
	CVTDL	R0, R0				; convert
						; fall into long store

STORE_LONG:
	MOVL	R10, R1				; pointer to dest descriptor
	MOVL	lower_bnd1+4(SP), R2		; current row (extra longword
						;  on stack for jsb)
	MOVL	R11, R3				; current column
	STORE	L				; store
	RSB					; go continue loop

DEST_W_TO_F:
	CVTWF	R0, R0				; convert
	BRB	STORE_FLOAT			; go store

DEST_L_TO_F:
	CVTLF	R0, R0				; convert
						; fall into store float

STORE_FLOAT:
	MOVL	R10, R1				; pointer to dest descriptor
	MOVL	lower_bnd1+4(SP), R2		; current row (extra longword
						; on stack for jsb)
	MOVL	R11, R3				; current column
	STORE	F				; store
	RSB					; go continue loop
						; fall into store float
DEST_W_TO_D:
	CVTWD	R0, -(SP)			; save double
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
	MULD2	(SP)+, R0			; scale for dest
	BRB	STORE_DOUBLE			; go store

DEST_L_TO_D:
	CVTLD	R0, -(SP)			; save double
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
	MULD2	(SP)+, R0			; scale for dest
						; fall into store double

STORE_DOUBLE:
	MOVL	R10, R2				; pointer to dest descriptor
	MOVL	lower_bnd1+4(SP), R3		; current row (extra longword
						;  on stack for jsb)
	MOVL	R11, R4				; current column
	STORE	D				; store
	RSB					; go continue loop

	.END
