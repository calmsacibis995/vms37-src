	.TITLE	LIB$CRC - Calculate cyclic redundancy check 
	.IDENT	/1-006/		; File: LIBCRC.MAR Edit: RKR1006

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: General Utility Library
;
; ABSTRACT:
;
;	Calculate the CRC of a data stream
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: Donald G. Petersen, CREATION DATE: 30-Dec-77
;
; MODIFIED BY:
;
;	DGP,30-Dec-77 : VERSION 00
; 01	- Original 
;
; 1-001	- Updated version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Add "_" to PSECT directive.  JBS 21-DEC-78
; 1-003	- Add R2 and R3 to the entrymask since they are clobbered by
;	   the CRC instruction.  JBS 21-DEC-78
; 1-004 - Enhance to recognize additional classes of string descriptors
;	  by invoking LIB$ANALYZE_SDESC_R3 to extract length and 
;	  address of 1st data byte.  RKR 26-MAY-1981.
; 1-005 - Add special-case code to process classes of descriptors that
;	  "read" like fixed strings more efficiently.  RKR 7-OCT-1981
; 1-006 - Redirect jsb from LIB$ANALYZE_SDESC_R3 to
;	  LIB$ANALYZE_SDESC_R2.  RKR 18-NOV-1981.

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES: NONE
;

; EXTERNAL SYMBOLS
	.DSABL	GBL			; Only explicit externals
	.EXTRN	LIB$ANALYZE_SDESC_R2	; Extract length and address of
					; 1st data byte of string.
;
; MACROS:
;
	$DSCDEF				; symbols for parts of a
					; descriptor

;
; EQUATED SYMBOLS: NONE
;

;
; OWN STORAGE: NONE
;

;
; PSECT DECLARATIONS:
;
	.PSECT _LIB$CODE PIC, SHR, LONG, EXE, NOWRT


	.SBTTL	LIB$CRC  - Calculate cyclic redundancy check 
;++
; FUNCTIONAL DESCRIPTION:
;
;	The CRC of the data stream specified is calculated and returned.
;
; CALLING SEQUENCE:
;
;	crc.wl.v = LIB$CRC (table.rl.ra, inicrc.rl.r, stream.rt.dx)
;
;
; INPUT PARAMETERS:
;
	TABLE = 4		; Adr. of table
	INICRC = 8		; Adr. of adr. of initial CRC longword
	STREAM = 12		; Adr. of data stream desc.
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
;
;	crc.wl.v 
;
; SIDE EFFECTS:
;
;	NONE
;
;--

	.ENTRY LIB$CRC, ^M<R2,R3>	; Entry point
	MOVL	STREAM(AP), R0		; Address of STREAM descriptor
	CMPB	DSC$B_CLASS(R0), #DSC$K_CLASS_D ; read like fixed ?
	BGTRU	1$			; used general path

	MOVQ	@STREAM(AP), R0		; length ->R0, addr -> R1
	CRC	@TABLE(AP), @INICRC(AP), R0, (R1)	; calc crc
	RET				; return result in R0

1$:	JSB	G^LIB$ANALYZE_SDESC_R2	; Extract: length->R1, addr->R2
	CRC	@TABLE(AP), @INICRC(AP), R1, (R2) ; Calculate CRC
	RET				; return result in R0
	.END
