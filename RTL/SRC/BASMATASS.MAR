	.TITLE	BAS$MAT_ASSIGN
	.IDENT	/1-005/			; File: BASMATASS.MAR Edit: PLL1005

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: BASIC code support
;
; ABSTRACT:
;
;	This module copies one matrix to another.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: R. Will, CREATION DATE: 29-May-79
;
; MODIFIED BY:
;++
; 1-001	- Original 
; 1-002 - Change external references to G^ RNH 25-Sep-81
; 1-003 - Substitute a macro for the calls to the array fetch and store
;	  routines.  This should speed things up.  PLL 6-Nov-81
; 1-004 - Correct a run-time expression FETCH and STORE macros.  PLL 20-Jan-82
; 1-005 - Correct FETCH, STORE again.  PLL 23-Feb-82
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

	$DSCDEF					; define descriptor offsets
	$SFDEF					; use to get scale

;
; EXTERNAL DECLARATIONS:
;

	.DSABL	GBL				; Prevent undeclared
						;  symbols from being
						;  automatically global.
	.EXTRN	BAS$K_ARGDONMAT			; signalled if all 3 blocks
						;  not present in array desc
						;  or dimct = 0
	.EXTRN	BAS$K_DATTYPERR			; signalled if dtype of array
						; isn't word long float double
						; or if not string to string
	.EXTRN	BAS$STO_FA_W_R8			; array element store for word
	.EXTRN	BAS$STO_FA_L_R8			; array element store for long
	.EXTRN	BAS$STO_FA_F_R8			; array element store - float
	.EXTRN	BAS$STO_FA_D_R8			; array element store - double
	.EXTRN	BAS$FET_FA_W_R8			; array element fetch - word
	.EXTRN	BAS$FET_FA_L_R8			; array element fetch - long
	.EXTRN	BAS$FET_FA_F_R8			; array element fetch - float
	.EXTRN	BAS$FET_FA_D_R8			; array element fetch - double
	.EXTRN	BAS$STORE_BFA			; array element store any type
	.EXTRN	BAS$FETCH_BFA			; array element fetch any type
	.EXTRN	BAS$MAT_REDIM			; check if redimensioning of
						;  dest array is necessary, if
						;  so, do it
	.EXTRN	BAS$$SCALE_R1			; scale the double procision
	.EXTRN	STR$FREE1_DX_R4			; free a string
	.EXTRN	BAS$$STOP			; signal fatal errors


;
; MACROS:
;

;	$BAS$MAT_ASS	copy loop algorithm,  see next page
;	FETCH		fetch an element from an array
;	STORE		store an element into an array

;
; EQUATED SYMBOLS:
;

	first_arg = 4				; arg offset for str copy
	second_arg = 8				; arg offset for str copy
	index1 = 12				; stack offset for str copy
	index2 = 16				; stack offset for str copy
	temp_desc = 20				; stack offset for str copy
	lower_bnd2 = 0				; stack offset for temp
	lower_bnd1 = 4				; stack offset for temp
	upper_bnd1 = 8				; stack offset for temp
	dsc$l_l1_1 = 24				; desc offset if 1 sub
	dsc$l_u1_1 = 28				; desc offset if 1 sub
	dsc$l_l1_2 = 28				; desc offset if 2 sub
	dsc$l_u1_2 = 32				; desc offset if 2 sub
	dsc$l_l2_2 = 36				; desc offset if 2 sub
	dsc$l_u2_2 = 40				; desc offset if 2 sub

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT _BAS$CODE PIC, USR, CON, REL, LCL, SHR, -
			EXE, RD, NOWRT, LONG

;+
; These macros are a substitute for calls to the array fetch and store
; routines.  They will call the BAS$ routines only if the array is a 
; virtual array.  Otherwise, they will calculate the linear index into
; the array via the INDEX instruction.  (Note that BASIC programs must
; be able to handle FORTRAN arrays, so the code must check for arrays
; stored by column.)  The INDEX instructions should provide a significant
; performance improvement over calling a routine for each element of
; the array.
;-

	.LIST	ME
	.MACRO	FETCH	array_dtype,?L1,?L2,?L3,?L11,?L21 ; fetch an array element

	CMPB	dsc$b_class(R0), #dsc$k_class_bfa  ;virtual array?
	BNEQ	L1				; no
	JSB	BAS$FET_FA_'array_dtype'_R8	; yes, use the fetch routine
	BRW	L3				; done
L1:	BBS	#5, 10(R0), L2			; br if array stored by cols
	CMPB	dsc$b_dimct(R0), #1		; 1 or 2 dims?
	BNEQ	L11				; 2 dims
	MOVZWL	dsc$w_length(R0), R4		; make length longword
	INDEX	R1, dsc$l_l1_1(R0), dsc$l_u1_1(R0), R4, #0, R3
	ADDL	dsc$a_a0(R0), R3		; add start addr to offset
	MOV'array_dtype'	(R3), R0	; return element in R0
	BRW	L3				; 1 dim done
L11:	INDEX	R1, dsc$l_l1_2(R0), dsc$l_u1_2(R0), dsc$l_m2(R0), #0, R3
						; I * M2  [Li's are zero]
	MOVZWL	dsc$w_length(R0), R4		; need longword length for INDEX
	INDEX	R2, dsc$l_l2_2(R0), dsc$l_u2_2(R0), R4, R3, R3
						; (J + (I * M2)) * length 
	ADDL	dsc$a_a0(R0), R3		; compute addr of element 
	MOV'array_dtype'	(R3), R0	; return element in R0
	BRW	L3				; done
L2:	CMPB	dsc$b_dimct(R0), #1		; 1 or 2 dims?
	BNEQ	L21				; 2 dims
	MOVZWL	dsc$w_length(R0), R4		; make length longword
	INDEX	R2, dsc$l_l1_1(R0), dsc$l_u1_1(R0), R4, #0, R3
	ADDL	dsc$a_a0(R0), R3		; add start addr to offset
	MOV'array_dtype'	(R3), R0	; return element in R0
	BRW	L3				; 1 dim done
L21:	INDEX	R2, dsc$l_l2_2(R0), dsc$l_u2_2(R0), dsc$l_m1(R0), #0, R3
						; J * M1  [Li's are zero]
	MOVZWL	dsc$w_length(R0), R4		; need longword length for INDEX
	INDEX	R1, dsc$l_l1_2(R0), dsc$l_u1_2(R0), R4, R3, R3
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R0), R3		; compute addr of element
	MOV'array_dtype'	(R3), R0	; return element in R0

L3:
	.ENDM



	.LIST	ME
	.MACRO	STORE	array_dtype,?L1,?L2,?L3,?L4,?L5,?L11,?L21,?L31,?L41

	.IF	IDN	'array_dtype', D	; array is double
	CMPB	dsc$b_class(R2), #dsc$k_class_bfa  ; virtual array?
	BNEQ	L1				; no
	JSB	BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L5				; done
L1:	BBS	#5, 10(R2), L2			; br if stored col-wise
	CMPB	dsc$b_dimct(R2), #1		; 1 or 2 dim?
	BNEQ	L11				; 2 dims
	MOVZWL	dsc$w_length(R2), R6		; make length a longword
	INDEX	R3, dsc$l_l1_1(R2), dsc$l_u1_1(R2), R6, #0, R5
	ADDL	dsc$a_a0(R2), R5		; add start addr to offset
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L5				; 1 dim done
L11:	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), dsc$l_m2(R2), #0, R5
						; I * M2
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), R6, R5, R5
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L5				; done
L2:	CMPB	dsc$b_dimct(R2), #1		; 1 or 2 dim?
	BNEQ	L21				; 2 dims
	MOVZWL	dsc$w_length(R2), R6		; make length a longword
	INDEX	R4, dsc$l_l1_1(R2), dsc$l_u1_1(R2), R6, #0, R5
	ADDL	dsc$a_a0(R2), R5		; add start addr to offset
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L5				; 1 dim done
L21:	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), dsc$l_m1(R2), #0, R5
						; J * M1
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), R6, R5, R5
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	.IFF					; array type other than double
	CMPB	dsc$b_class(R1), #dsc$k_class_bfa  ;virtual array?
	BNEQ	L3				; no
	JSB	BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L5				; done
L3:	BBS	#5, 10(R1), L4			; br if stored col-wise
	CMPB	dsc$b_dimct(R1), #1		; 1 or 2 dims?
	BNEQ	L31				; 2 dims
	MOVZWL	dsc$w_length(R1), R6		; make length a longword
	INDEX	R2, dsc$l_l1_1(R1), dsc$l_u1_1(R1), R6, #0, R4
	ADDL	dsc$a_a0(R1), R4		; add start addr to offset
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L5				; 1 dim done
L31:	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), dsc$l_m2(R1), #0, R4
						; I * M2
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), R5, R4, R4
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L5				; done
L4:	CMPB	dsc$b_dimct(R1), #1		; 1 or 2 dims?
	BNEQ	L41				; 2 dims
	MOVZWL	dsc$w_length(R1), R6		; make length a longword
	INDEX	R3, dsc$l_l1_1(R1), dsc$l_u1_1(R1), R6, #0, R4
	ADDL	dsc$a_a0(R1), R4		; add start addr to offset
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L5				; 1 dim done
L41:	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), dsc$l_m1(R1), #0, R4
						; J * M1
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), R5, R4, R4
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	.ENDC

L5:
	.ENDM



;+
; This macro contains the looping mechanism for accessing all elements of
; an array.  It also contains all the logic for all the combinations of data
; types and scaling.  A macro is used to make it easy to maintain the parallel
; code for all the different data types.
;-

	.LIST	ME
	.MACRO	$BAS$MAT_ASS src_dtype, dest_dtype ; copy algorithm

;+
; Loop through all the rows.  Row and column upper and lower bounds have been
; initialized on the stack.
;-

LOOP_1ST_SUB'src_dtype'dest_dtype':
	MOVL	lower_bnd2(SP), R11		; R11 has 2nd lower bound

;+
; Loop through all the elements (columns) of the current row.  Column lower
; bound is initialized in R11.  Column upper bound is on the stack.
; Distinguish array by data type so that the correct fetch routine can 
; retrieve the data, the correct conversion can be done and the correct
; store routine can be called.
;-

LOOP_2ND_SUB'src_dtype'dest_dtype':

;+
; Get the data from source array
;-

	MOVL	R10, R0				; pointer to array dest
	MOVL	lower_bnd1(SP), R1		; current row
	MOVL	R11, R2				; current col
	FETCH	'src_dtype'			; fetch data from src array

;+
; If the data types of the source and destination arrays is different,
; convert the data to the destination type.  If scaling is needed (ie if
; at least one but not both of the arrays is double) scale the data.
;-

	.IF	DIF	src_dtype, dest_dtype	; src and dest arrays are not
						; save data type
	.IF	IDN	src_dtype, D		; source is double
	MOVD	R0, -(SP)			; save the data
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	DIVD3	R0, (SP)+, R0			; scale
	.ENDC
	CVT'src_dtype'dest_dtype'	R0, R0	; convert data from R0 into R0
						;  R0 & R1 for double
	.IF	IDN	dest_dtype, D		; dest is double
	MOVD	R0, -(SP)			; save the data
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	MULD2	(SP)+, R0			; scale, NOTE that integerize
						;  is not required after mul
						;  because data was already
						;  an integer (dbl to dbl not
						;  in this cond code and
						;  float to dbl not allowed)
	.ENDC
	.ENDC

;+
; Now store the data in the destination array.
; Code is same for all dtypes except double, since double passed by value
; takes 2 words and all other supported dtypes by value takes 1 longword
;-

	.IF	IDN	dest_dtype, D		; see if dtype is double
	.IFF					; no, word, long, float
	MOVL	dest_matrix(AP), R1		; pointer to array desc
	MOVL	lower_bnd1(SP), R2		; current row
	MOVL	R11, R3				; current column
	.IFT					; type is double
	MOVL	dest_matrix(AP), R2		; pointer to array desc
	MOVL	lower_bnd1(SP), R3		; current row
	MOVL	R11, R4				; current column
	.ENDC					; code now same for all dtypes
	STORE	'dest_dtype'			; store in array
	INCL	R11				; get next column
	CMPL	R11, R9				; see if last column done
	BGTR	5$
	BRW	LOOP_2ND_SUB'src_dtype'dest_dtype' ; no, continue inner loop

;+
; Have completed entire row.  See if it was the last row.  If not,
; continue with next row.
;-

5$:	INCL	lower_bnd1(SP)			; get next row
	CMPL	lower_bnd1(SP), upper_bnd1(SP)	; see if last row done
	BGTR	7$
	BRW	LOOP_1ST_SUB'src_dtype'dest_dtype' ; no, continue outer loop

7$:	RET					; yes, finished

	.ENDM

	.SBTTL	BAS$MAT_ASSIGN  - Copy one matrix to another
;++
; FUNCTIONAL DESCRIPTION:
;
;	Copy one matrix to another.  Redimension the output matrix to have the
;	same dimensions as the input matrix.  Initialize all the necessary
;	looping	information on the stack.  Conversions will have to be done
;	from the source data type to the destination data type, so divide
;	the looping portion according to the data types.  String arrays
;	may only be copied to string arrays so no conversion is necessary
;	there.
;
; CALLING SEQUENCE:
;
;	CALL BASMAT_ASSIGN (src_matrix.rx.da, dest_matrix.wx.da)
;
; INPUT PARAMETERS:
;
	src_matrix = 4
;
; IMPLICIT INPUTS:
;
;	Scale from the callers frame to scale double precision.
;
; OUTPUT PARAMETERS:
;
	dest_matrix = 8
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	This routine calls the redimensioning routine and the array element
;	fetch and store routines and therefore may signal any of their errors.
;	It may also signal any of the errors listed in the externals section.
;	It may also cause the destination array to have different dimensions.
;
;--

	.ENTRY BAS$MAT_ASSIGN, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,IV>

;+
;	REGISTER USAGE
;	R0 - R8 destroyed by store routines
;	R9	upper bound for 2nd subscript
;	R10	pointer to array descriptor
;	R11	current value of 2nd subscript
;-

;+
; Put routine arguments into registers for ease of use.
; If block 2 of array descriptor (multipliers) is not present then error.
;-

	MOVL	src_matrix(AP), R10		; ptr to array descr in R10
	BBC	#DSC$V_FL_BOUNDS, DSC$B_AFLAGS(R10), ERR_ARGDONMAT
						; exit if block 3 not
						; present in descriptor
	CMPB	DSC$B_DTYPE(R10), #DSC$K_DTYPE_T ; see if not numeric
	BGEQU	STRING				; no, must be string 

;+
; Set up limits for looping through all elements
;-

	CMPB	DSC$B_DIMCT(R10), #1		; determine # of subscripts
	BEQLU	INIT_ONE_SUB			; 1 sub, go init
	BGTRU	INIT_TWO_SUBS			; >=2 subs, go init
						; 0 subs, fall into error proc

ERR_ARGDONMAT:
	PUSHL	#BAS$K_ARGDONMAT		; signal error, 0 for dimct
	CALLS	#1, G^BAS$$STOP			; or block 2 or 3 absent

;+
; There is only 1 subscript.  Redimension the destination array.
; Make both upper and lower bound for 2nd
; subscript a 1.  A second subscript will be passed to and ignored by the
; store routine.  Put bounds for 1st subscript on stack.
;-

INIT_ONE_SUB:
	PUSHL	dsc$l_u1_1(R10)			; get bound for redim
	PUSHL	dest_matrix(AP)			; pointer to dest array desc
	CALLS	#2, BAS$MAT_REDIM		; redimension the dest
	PUSHL	dsc$l_u1_1(R10)			; 1st upper bound
	PUSHL	dsc$l_l1_1(R10)			; 1st lower bound
	BGTR	1$				; not 0 or neg, do 2nd sub
	MOVL	#1, (SP)			; don't alter col 0
1$:	PUSHL	#1				; dummy 2nd upper bound
	MOVL	#1, R9				; dummy 2nd lower bound
	BRB	SEPARATE_DTYPES			; go loop

;+
; There are 2 subscripts.  Check and redimension the destination array if
; necessary.  Put the upper bound for both subscripts on the
; stack and make sure that the lower bound for both subscripts will start
; at 1 (do not alter row or col 0)
;-

INIT_TWO_SUBS:
	PUSHL	dsc$l_u2_2(R10)			; 2nd upper bound
	PUSHL	dsc$l_u1_2(R10)			; 1st upper bound
	PUSHL	dest_matrix(AP)			; dest array pointer
	CALLS	#3, BAS$MAT_REDIM		; redimension destination
	PUSHL	dsc$l_u1_2(R10)			; 1st upper bound
	PUSHL	dsc$l_l1_2(R10)			; 1st lower bound
	BGTR	1$				; not row 0 or neg, do cols
	MOVL	#1, (SP)			; start with row 1
1$:	MOVL	dsc$l_u2_2(R10), R9		; 2nd upper bound
	PUSHL	dsc$l_l2_2(R10)			; 2nd lower bound
	BGTR	SEPARATE_DTYPES			; not col 0 or neg, go loop
	MOVL	#1, (SP)			; start with col 1

;+
; Algorithm now differs according to data types
;-

SEPARATE_DTYPES:
	CASEB	DSC$B_DTYPE(R10), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
2$:	.WORD	WORD-2$				; code for word dtype
	.WORD	LONG-2$				; code for long dtype
	.WORD	ERR_DATTYPERR-2$		; quad not supported
	.WORD	FLOAT-2$			; code for float dtype
	.WORD	DOUBLE-2$			; code for double dtype

ERR_DATTYPERR:
	PUSHL	#BAS$K_DATTYPERR		; Signal error, unsupported
	CALLS	#1, G^BAS$$STOP			; dtype in array desc

	.PAGE
;+
; Source array is a string array.
;-

STRING:

;+
;	REGISTER USAGE
;	R2	current bound for 1st subscript
;	R3	upper bound for 1st subscript
;	R4	lower bound for 2nd subscript
;	R5	upper bound for 2nd subscript
;	R6	current value of 2nd subscript
;	R9	pointer to destination array descriptor
;	R10	pointer to source array descriptor
;-

;+
; Initialize stack for CALLG to element store routine.  Then divide algorithm
; based on number of subscripts.  Since the dtype in the temporary descriptor
; is text the array fetch and store routines will signal an error if
; the dtype of the array is not text of array of descriptors of type text
; and there is no need to do a check for dtype here.
;-

	MOVL	dest_matrix(AP), R9
	CLRL	-(SP)				; pointer of descriptor
	PUSHL	#<<DSC$K_CLASS_D @ 24> + <DSC$K_DTYPE_T @ 16>> ; 1st desc word
						; class, type and 0 length
	CLRQ	-(SP)				; space for indices
	PUSHL	R10				; ptr to dest desc for call
	PUSHAL	12(SP)				; pointer to NULL descriptor
	CMPB	DSC$B_DIMCT(R10), #1		; determine # of subscripts
	BEQLU	INIT_ONE_SUB_S			; 1 sub, go init
	BGTRU	INIT_TWO_SUBS_S			; >=2 subs, go init
	BRW	ERR_ARGDONMAT			; 0 subs, error

;+
; There is only 1 subscript.  Make both upper and lower bound for 2nd
; subscript a 1.  A second subscript will be passed to and ignored by the
; store routine because the argcount in the arglist of the CALL will not
; include the 2nd subscript.  Put bounds for 1st subscript into registers.
;-

INIT_ONE_SUB_S:
	PUSHL	#3				; 3 arguments to store routine
						;  ignores 2nd index
	PUSHL	dsc$l_u1_1(R10)			; get bound for redim
	PUSHL	R9				; pointer to dest array desc
	CALLS	#2, BAS$MAT_REDIM		; redimension the dest array
	MOVL	dsc$l_u1_1(R10), R3		; 1st upper bound
	MOVL	dsc$l_l1_1(R10), R2		; 1st lower bound
	BGTR	1$				; not 0 or neg, init 2nd bound
	MOVL	#1, R2				; don't alter row 0
1$:	MOVL	#1, R4				; set 2nd lower bnd to 1
	MOVL	#1, R5				; set 2nd upper bnd to 1
	BRB	LOOP_1ST_SUB_S			; go loop

;+
; There are 2 subscripts.  Put the upper bound for both subscripts in
; registers and make sure that the lower bound for both subscripts will start
; at 1 (do not alter row or col 0)
;-

INIT_TWO_SUBS_S:
	PUSHL	#4				; 4 arguments to store routine
	PUSHL	dsc$l_u2_2(R10)			; 2nd upper bound for redim
	PUSHL	dsc$l_u1_2(R10)			; 1st upper bound for redim
	PUSHL	R9				; ptr to dest array to redim
	CALLS	#3, BAS$MAT_REDIM		; redim the dest array
	MOVL	dsc$l_u2_2(R10), R5		; 2nd upper bound
	MOVL	dsc$l_l2_2(R10), R4		; 2nd lower bound
	BGTR	1$				; not col 0 or neg, do cols
	MOVL	#1, R4				; start with col 1
1$:	MOVL	dsc$l_u1_2(R10), R3		; 1st upper bound
	MOVL	dsc$l_l1_2(R10), R2		; 1st lower bound
	BGTR	LOOP_1ST_SUB_S			; not row 0 or neg, go loop
	MOVL	#1, R2				; start with row 1

;+
; Loop through all the rows.  Row and column upper and lower bounds have been
; initialized in registers.
;-

LOOP_1ST_SUB_S:
	MOVL	R4, R6				; R6 has 1st lower bound

;+
; Loop through all the elements (columns) of the current row.  Column lower
; bound is initialized in R6.  Column upper bound is in R5.
;-

LOOP_2ND_SUB_S:

;+
; Fetch the string from the source array.
;-

	MOVL	R6, index2(SP)			; current column
	MOVL	R2, index1(SP)			; current row
	MOVL	R10, first_arg(SP)		; source array desc pointer
	MOVAQ	temp_desc(SP), second_arg(SP)	; desc to copy fetch
	CALLG	(SP), BAS$FETCH_BFA		; copy element to temp_desc

;+
; Store the string in the destination array.  Note that the indices are
; already set from the fetch.
;-

	MOVAQ	temp_desc(SP), first_arg(SP)	; string to copy to dest
	MOVL	R9, second_arg(SP)		; dest array desc pointer
	CALLG	(SP), BAS$STORE_BFA		; store in array
	INCL	R6				; get next column
	CMPL	R6, R5				; see if last column done
	BLEQ	LOOP_2ND_SUB_S			; no, continue inner loop

;+
; Have completed entire row.  See if it was the last row.  If not,
; continue with next row.
;-

	INCL	R2				; get next row
	CMPL	R2, R3				; see if last row done
	BLEQ	LOOP_1ST_SUB_S			; no, continue outer loop

	MOVAQ	temp_desc(SP), R0		; yes, free the temp string
	JSB	G^STR$FREE1_DX_R4
	RET					; finished

	.PAGE
;+
; Source array is a word array.  Now differentiate on the destination type.
;-

WORD:	MOVL	dest_matrix(AP), R11		; point to dest descriptor
	CASEB	DSC$B_DTYPE(R11), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	WORD_TO_WORD-1$			; code for word dtype
	.WORD	WORD_TO_LONG-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	WORD_TO_FLOAT-1$		; code for float dtype
	.WORD	WORD_TO_DOUBLE-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
WORD_TO_WORD:	$BAS$MAT_ASS	W, W

	.PAGE
WORD_TO_LONG:	$BAS$MAT_ASS	W, L

	.PAGE
WORD_TO_FLOAT:	$BAS$MAT_ASS	W, F

	.PAGE
WORD_TO_DOUBLE:	$BAS$MAT_ASS	W, D

	.PAGE
;+
; Source array is a longword array.  Now differentiate on the destination type
;-

LONG:	MOVL	dest_matrix(AP), R11		; point to dest descriptor
	CASEB	DSC$B_DTYPE(R11), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	LONG_TO_WORD-1$			; code for word dtype
	.WORD	LONG_TO_LONG-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	LONG_TO_FLOAT-1$		; code for float dtype
	.WORD	LONG_TO_DOUBLE-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
LONG_TO_WORD:	$BAS$MAT_ASS	L, W

	.PAGE
LONG_TO_LONG:	$BAS$MAT_ASS	L, L

	.PAGE
LONG_TO_FLOAT:	$BAS$MAT_ASS	L, F

	.PAGE
LONG_TO_DOUBLE:	$BAS$MAT_ASS	L, D

	.PAGE
;+
; Source array is a floating array.  Now differentiate on the destination type
;-

FLOAT:	MOVL	dest_matrix(AP), R11		; point to dest descriptor
	CASEB	DSC$B_DTYPE(R11), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	FLOAT_TO_WORD-1$		; code for word dtype
	.WORD	FLOAT_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	FLOAT_TO_FLOAT-1$		; code for float dtype
	.WORD	ERR_DATTYPERR-1$		; float unsupported w/ double
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
FLOAT_TO_WORD:	$BAS$MAT_ASS	F, W

	.PAGE
FLOAT_TO_LONG:	$BAS$MAT_ASS	F, L

	.PAGE
FLOAT_TO_FLOAT:	$BAS$MAT_ASS	F, F

	.PAGE
;+
; Source array is a double array.  Now differentiate on the destination type.
;-

DOUBLE:	MOVL	dest_matrix(AP), R11		; point to dest descriptor
	CASEB	DSC$B_DTYPE(R11), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	DOUBLE_TO_WORD-1$		; code for word dtype
	.WORD	DOUBLE_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	ERR_DATTYPERR-1$		; float unsupported w/ double
	.WORD	DOUBLE_TO_DOUBL-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
DOUBLE_TO_WORD:	$BAS$MAT_ASS	D, W

	.PAGE
DOUBLE_TO_LONG:	$BAS$MAT_ASS	D, L

	.PAGE
DOUBLE_TO_DOUBL: $BAS$MAT_ASS	D, D

	.END					; end of BAS$MAT_ASSIGN
