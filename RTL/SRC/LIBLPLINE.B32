MODULE LIB$LP_LINES (				! Fetch number of lines per page
		IDENT = '1-002'			! File: LIBLPLINE.B32
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY: General Purpose Library
!
! ABSTRACT:
!
!	Contains a routine to return the system-wide default Line Printer length,
!	as determined by the logical name SYS$LP_LINES.
!
! ENVIRONMENT: User mode - AST reentrant
!
! AUTHOR: Richard Grove and Steven Lionel, CREATION DATE: 10-Sep-1979
!
! EDIT HISTORY:
!
! 1-001 - Original.
! 1-002	- Improve comments and remove OTSMAC.  JBS 31-OCT-1979
!<BLF/PAGE>

!
! SWITCHES:
!

SWITCHES ADDRESSING_MODE (EXTERNAL = GENERAL, NONEXTERNAL = WORD_RELATIVE);

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    LIB$LP_LINES;				! Get default Line Printer length

!
! INCLUDE FILES:
!

LIBRARY 'RTLSTARLE';

REQUIRE 'RTLIN:RTLPSECT';

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! PSECT DEFINITIONS:
!
DECLARE_PSECTS (LIB);				! Declare psects for LIB$ facility
!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
    OTS$CVT_TI_L;				! Convert ASCII text string to longword integer


GLOBAL ROUTINE LIB$LP_LINES = 			! Fetch number of lines per page

!++
! FUNCTIONAL DESCRIPTION:
!
!	LIB$LP_LINES   computes  the   default  number  of  lines  on   a
!	line-printer page.  This procedure (or an equivalent computation)
!	should be used by all native-mode VAX/VMS utilities that  produce
!	"listing" files and do pagination.
!
!	U.S.  standard paper stock permits 66  lines  of  printing  on  a
!	physical page.  From this value, the utility should deduct:
!
!	     1.  3 lines for top margin
!
!	     2.  3 lines for bottom margin
!
!	     3.  3 lines for listing heading information, consisting of:
!
!	         1.  Language-processor identification line
!
!	         2.  Source-program identification line
!
!	         3.  One blank line
!
!	The algorithm used by LIB$LP_LINES is:
!
!	     1.  Translate the logical name SYS$LP_LINES
!
!	     2.  Convert the ASCII value obtained to a binary integer
!
!	     3.  Verify that the resulting value is in the range [30:99].
!
!	     4.  If any of the steps above fail, return the default  U.S.
!	         paper size of 66 lines.
!
! CALLING SEQUENCE:
!
!	LP_length.wl.v = LIB$LP_LINES ( )
!
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	System-wide (or user-defined) logical name SYS$LP_LINES
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	Default number of lines on a Physical Line Printer page.
!	If the logical name translation or conversion to binary
!	fails, a default value of 66 is returned.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	LOGDES : BLOCK [8, BYTE],		! String descriptor for logical name
	NUMBER,					! Accumulator for integer conversion
	NUMDES : BLOCK [8, BYTE],		! String descriptor for translated string
	NUMSTR : VECTOR [8, BYTE];		! Buffer for translated string

!+
! Initialize descriptors for logical name and translated string
!-
    LOGDES [DSC$B_CLASS] = DSC$K_CLASS_S;
    LOGDES [DSC$B_DTYPE] = DSC$K_DTYPE_T;
    LOGDES [DSC$W_LENGTH] = 12;
    LOGDES [DSC$A_POINTER] = UPLIT BYTE('SYS$LP_LINES');
    NUMDES [DSC$B_CLASS] = DSC$K_CLASS_S;
    NUMDES [DSC$B_DTYPE] = DSC$K_DTYPE_T;
    NUMDES [DSC$W_LENGTH] = 8;
    NUMDES [DSC$A_POINTER] = NUMSTR [0];
!+
! Translate and convert the logical name SYS$LP_LINES to determine
! default number of lines on LP listing page.
!-

    IF $TRNLOG (LOGNAM = LOGDES, RSLBUF = NUMDES, RSLLEN = NUMDES [DSC$W_LENGTH])
    THEN

	IF OTS$CVT_TI_L (NUMDES, NUMBER)
	THEN

	    IF .NUMBER GEQ 30 AND .NUMBER LEQ 99 THEN RETURN .NUMBER;

!+
! The default value for U.S. standard paper stock is 66.
!-
    RETURN 66;
    END;					! of routine LIB$LP_LINES

END						!End of module LIB$LP_LINES

ELUDOM
