MODULE COB$ACC_DAY (
		IDENT = '1-006' ! file: COBACCDAY.B32 Edit:RKR1006
		) =
BEGIN

!
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	COBOL SUPPORT
!
! ABSTRACT
!
!
!
! ENVIRONMENT:	Vax-11 User Mode
!
! AUTHOR: MLJ , CREATION DATE: 16-JAN-1979
!
! MODIFIED BY:
!
! 1-001 - Original. MLJ 16-JAN-1979
! 1-002 - Added boilerplate and comments. RKR 18-JULY-1979
! 1-003 - Declare psects via library macro. RKR 23-AUG-1979
! 1-004 - Change symbolic name of LIBRARY.  RKR 1-OCT-79
! 1-005 - Cosmetic changes.  RKR 18-OCT-79
! 1-006 - Fix computation of number of days since 01-JAN so that
!	  computation does not fail when this delta is 0.  (i.e.
!	  on 01-JAN-xxxx).	RKR 11-JAN-81
!
!--

!<BLF/PAGE>

!
! SWITCHES
!

SWITCHES ADDRESSING_MODE (EXTERNAL = GENERAL, NONEXTERNAL = WORD_RELATIVE);

!
! LINKAGES
!
!	NONE
!
! TABLE OF CONTENTS:
!
FORWARD ROUTINE

	COB$ACC_DAY : NOVALUE ;

! INCLUDE FILES
!
REQUIRE 'RTLIN:RTLPSECT' ;		! Macros for declaring psects
LIBRARY 'RTLSTARLE';

!
! MACROS
!
!	NONE
!
! EQUATED SYMBOLS
!
!	NONE
!
! PSECT DECLARATIONS:
!
DECLARE_PSECTS (COB) ;			! Psects for COB$ facility


GLOBAL ROUTINE COB$ACC_DAY(DST): NOVALUE=
!
!++	FUNCTIONAL DESCRIPTION
!
!	Returns date as YYDDD
!
! FORMAL PARAMETERS:
!
!	DST.wt.ds	Address of string descriptor to receive date
!
! IMPLICIT INPUTS:
!
!	Date as supplied by $ASCTIM
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--
!
    BEGIN
    MAP
	DST:		REF BLOCK[,BYTE];	! Pointer to destination descriptor
    LOCAL
	SYSTIM:		VECTOR[2],		! Buffer for $GETTIM
	JAN1TIM:	VECTOR[2],		! Buffer for $BINTIM
	TIMBUF:		BLOCK[23,BYTE],		! Buffer for $ASCTIM
	BUFFER:		BLOCK[5,BYTE],		! Buffer for assembling output
	TIMDESC:	VECTOR[2],		! Descriptor for TIMBUF
	CTLDESC:	VECTOR[2],		! Descriptor for FAO control
	BUFDESC:	VECTOR[2],		! Descriptor for FAO output
	N;					! Number of days since 01-Jan


    ! Get system date and time.
    !
    $GETTIM(TIMADR=SYSTIM);			! Get time in 64-bit format


    ! Convert the system date and time to ASCII.
    !
    TIMDESC[0] = 23;				! Set up descriptor
    TIMDESC[1] = TIMBUF;			!
    $ASCTIM(TIMBUF=TIMDESC, TIMADR=SYSTIM);	! Get ASCII representation


    ! Save the year of century.
    !
    BUFFER[0,0,16,0] = .TIMBUF[9,0,16,0];


    ! Replace the system date and time by 01-Jan of the same year and
    ! reconvert to 64-bit format.
    !
    TIMBUF[0,0,32,0] = '01-J';			! Change to 01-Jan
    TIMBUF[4,0,16,0] = 'AN';			!     ...
    $BINTIM(TIMBUF=TIMDESC, TIMADR=JAN1TIM);	! Reconvert to 64-bit format


    ! Compute the difference JAN1TIM - SYSTIM.  This yields a delta-time
    ! value for the number of days since 01-Jan.
    !
    IF .JAN1TIM[0] LSSU .SYSTIM[0] THEN JAN1TIM[1] = .JAN1TIM[1] - 1;
    JAN1TIM[0] = .JAN1TIM[0] - .SYSTIM[0];
    JAN1TIM[1] = .JAN1TIM[1] - .SYSTIM[1];

    ! If we're dealing with a date of 01-JAN-xx, the above sequence of
    ! conversions will leave us with a value of zero.  Unfortunately,
    ! zero is positive and the following conversion will attempt to
    ! deal with it as a date, rather than a delta time.  To correct for
    ! this, if the high-order part of the computed JAN1TIM is postive
    ! we force it negative to insure interpretation as a delta time.

    IF .JAN1TIM[1] GEQ 0 THEN JAN1TIM[1] = -1 ;

    ! Convert the delta-time value to ASCII.  The number of days is then
    ! one less than the desired Julian date.
    !
    $ASCTIM(TIMBUF=TIMDESC, TIMADR=JAN1TIM);


    ! Convert the number of days to binary.
    !
    N = 0;
    INCR I FROM 0 TO 3 DO
	BEGIN
	IF .TIMBUF[.I,0,8,0] NEQ %C' '
	THEN
	    N = .N * 10 + .TIMBUF[.I,0,8,0] - %C'0';
	END;


    ! Convert this value plus one into the output string.
    !
    CTLDESC[0] = 4;
    CTLDESC[1] = UPLIT('!3ZL');
    BUFDESC[0] = 3;
    BUFDESC[1] = BUFFER[2,0,0,0];
    $FAO(CTLDESC, 0, BUFDESC, .N + 1);
    CH$COPY(5, BUFFER, %C' ', .DST[DSC$W_LENGTH], .DST[DSC$A_POINTER]);
    END;

END ELUDOM
