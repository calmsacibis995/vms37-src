	.TITLE	MTH$AINT - FLOATING TRUNCATION
	.IDENT	/1-006/		; File: MTHAINT.MAR  Edit: JAW1006

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	This module contains routine MTH$AINT:
;	truncate a floating-point number.
;
;
;--
;
; VERSION: 0
;
; HISTORY:
;
; AUTHOR:
;	Jonathan M. Taylor, 30-Jul-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 0 of MTH$AINT
;
; 0-3	- Remove MTH$FLAG_JACKET.  TNH 5-July-78
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Add "_" to the PSECT directive.  JBS 21-DEC-78
; 1-003	- Add a JSB entry point.  JBS 16-AUG-1979
; 1-004 - Make the primary JSB entry MTH$AINT_R2 so that it can
;	  disable and restore IV.  SBL 26-Sept-1979
; 1-005	- Remove MTH$AINT_R1, since the BASIC compiler has converted to
;	   the _R2 entry point.  JBS 26-NOV-1979
; 1-006 - Mask all bits except IV when restoring PSW.  JAW 14-Jul-1981
;

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;
;	NONE
;
; EXTERNAL SYMBOLS:
;
;	NONE
;
;
; MACROS:
;
	$PSLDEF				; PSL Macros
;

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
;
;	NONE
;

;
; OWN STORAGE:
;	
;	NONE
;

	.SBTTL	MTH$AINT	Real to Real truncation

;++
; FUNCTIONAL DESCRIPTION:
;
;	Return the arguments with zeroes to the right of the decimal
;	point.
;
; CALLING SEQUENCE:
;
;	Truncation.wf.v = MTH$AINT (arg.rf.r)
;
; INPUT PARAMETERS:
;
;	The one argument is a single-precision floating-point value
;	and is call-by-reference.
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	Reserved Operand and Floating Underflow exceptions can occur.
;	
;--
	.ENTRY	MTH$AINT,	^M<>
	MOVF	@4(AP), R0		; R0 = arg
	EMODF	R0, #0, #1, R1, R1	; R1 = fraction_part(R0)
	SUBF	R1, R0			; R0 = integer_part(R0)
	RET


	.SBTTL	MTH$AINT_R2	JSB entry point

;++
; FUNCTIONAL DESCRIPTION:
;
;	Return the arguments with zeroes to the right of the decimal
;	point.
;
; CALLING SEQUENCE:
;
;	Truncation.wf.v = JSB MTH$AINT_R2 (arg.rf.v)
;
; INPUT PARAMETERS:
;
;	The one argument is a single-precision floating-point value
;	and is call-by-value.
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	Reserved Operand and Floating Underflow exceptions can occur.
;	
;--


MTH$AINT_R2::				; R0 = arg
	MOVPSL	R2			; Save current PSL
	BICPSW	#PSL$M_IV		; Disable integer overflow
	EMODF	R0, #0, #1, R1, R1	; R1 = fraction_part(R0)
	SUBF	R1, R0			; R0 = integer_part(R0)
	BICW	#^C<PSL$M_IV>, R2	; Clear all but IV
	BISPSW	R2			; Restore previous IV
	RSB				; Return to caller

	.END
