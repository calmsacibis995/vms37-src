	.TITLE	MTH$FLOOR - Greatest integer floating routine
	.IDENT	/1-006/			; File: MTHFLOOR.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: Math Library
;
; ABSTRACT:
;
;	This routine finds the largest integer less than the input
;	value, i.e. it truncates toward negative infinity
;	for type float.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR:R. Will,	 CREATION DATE: 1-Dec-78
;
; MODIFIED BY:
;
; VERSION 00
; 1-001	- Original 
; 1-002	- Add "_" to the PSECT directive.  JBS 22-DEC-78
; 1-003 - Put MTH$AINT code in line.  RW 26-Mar-79
; 1-004 - Correct bug for -1 < input < 0.  RW  11-Jul-79
; 1-005	- Add a JSB entry point.  JBS 25-JUL-1979
; 1-006	- Change name to MTH$FLOOR.  JBS 27-JUL-1979

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

;
; EXTERNAL DECLARATIONS:
;
	.DSABL	GBL				; Prevent undeclared
						; symbols from being
						; automatically global.
;
; MACROS:
;

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT _MTH$CODE PIC, USR, CON, REL, LCL, SHR, -
			EXE, RD, NOWRT, LONG


	.SBTTL	MTH$FLOOR  - greatest integer floating routine
;++
; FUNCTIONAL DESCRIPTION:
;
;	This routine finds the floor by truncating, and then if the
;	input value is negative and not an integer subtracting 1.
;
; CALLING SEQUENCE:
;
;	CALL result_int.wf.v = MTH$FLOOR (input.rf.r)
;
; INPUT PARAMETERS:
;
	input_addr = 4
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	the floating value of the greatest integer
;
; SIDE EFFECTS:
;
;	NONE
;
;--

	.ENTRY	MTH$FLOOR, ^M<>		; entry point

	MOVF	@input_addr(AP), R0		; R0 = arg
	EMODF	R0, #0, #1, R1, R1		; R1 = fraction_part(R0)
	SUBF	R1, R0

	BGTR	40$				; if > 0, have correct answer

	TSTF	R1				; look at fraction part
	BGEQ	40$				; if > 0, 0 < input < 1 and
						;  we have the correct answer
						; if = 0, input was integer and
						;  we have the correct answer

	SUBF2	#1,R0				; subtract 1 from truncated
						; negative non-integer

40$:	RET

	.SBTTL	MTH$FLOOR_R1	- JSB entry point
;++
; FUNCTIONAL DESCRIPTION:
;
;	This is the JSB entry point to MTH$FLOOR.
;
; CALLING SEQUENCE:
;
;	JSB result_int.wf.v = MTH$FLOOR_R1 (input.rf.v)
;
; INPUT PARAMETERS:
;
;	R0 contains the input value
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	the floating value of the greatest integer
;
; SIDE EFFECTS:
;
;	NONE
;
;--

MTH$FLOOR_R1::					; entry point

	EMODF	R0, #0, #1, R1, R1		; R1 = fraction_part(R0)
	SUBF	R1, R0

	BGTR	40$				; if > 0, have correct answer

	TSTF	R1				; look at fraction part
	BGEQ	40$				; if > 0, 0 < input < 1 and
						;  we have the correct answer
						; if = 0, input was integer and
						;  we have the correct answer

	SUBF2	#1,R0				; subtract 1 from truncated
						; negative non-integer

40$:	RSB

	.END
