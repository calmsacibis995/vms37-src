	.TITLE	MTH$MIN1	IMIN1, JMIN1, and AMIN1 functions
	.IDENT	/1-002/		; File: MTHMIN1.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	This module contains the minimum value functions which take
;	floating-point arguments.
;
;
;--
;
; VERSION: 0
;
; HISTORY:
;
; AUTHOR:
;	Jonathan M. Taylor, 12-JUL-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 0 of MTHMIN1
;
; 0-4	- Remove MTH$FLAG_JACKET.  TNH 26-July-78
; 0-5	- Fix access violation.  TNH 16-Aug-78
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Add "_" to the PSECT directive.  JBS 22-DEC-78

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;	oerr.mar
;

;
; EXTERNAL SYMBOLS:
;	NONE
;

;
; MACROS:
;	NONE
;

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
;	NONE
;

;
; OWN STORAGE:
;	NONE
;

	.SBTTL	MTH$IMIN1, MTH$JMIN1, MTH$AMIN1

;++
; FUNCTIONAL DESCRIPTION:
;	Use routine MINF to compute the minimum of n arguments,
;	n is greater than or equal to 1.  Return the result converted
;	to the proper type.
;
;
; CALLING SEQUENCE:
;	Minimum.ww.v = MTH$IMIN1 ({arg.rf.r})
;	Minimum.wl.v = MTH$JMIN1 ({arg.rf.r})
;	Minimum.wf.v = MTH$AMIN1 ({arg.rf.r})
;	
;
;
; INPUT PARAMETERS:
;	The n parameters are single-precision floating-point values
;	and are call-by-reference.
;	
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	Reserved Operand and Integer Overflow exceptions can occur.
;	
;
;--




	.ENTRY	MTH$IMIN1,	^M<IV>
	BSB	MINF		; R0 = min arg
	CVTFW	R0, R0		; INTEGER*2 it
	RET

	.ENTRY	MTH$JMIN1, 	^M<IV>
	BSB	MINF		; R0 = min arg
	CVTFL	R0, R0		; INTEGER*4 it
	RET

	.ENTRY	MTH$AMIN1,	^M<>
	BSB	MINF		; R0 = min arg
	RET

;+
; MINF returns smallest of the REAL*4 args.
;-

MINF:
	MOVZBL	(AP), R1		; R1 = number of args
	TSTL	(AP)+			; AP -> first arg adr
1$:	MOVF	@(AP)+, R0		; get trial min
	BRB	3$			; check arg count
2$:	CMPF	@0(AP),R0		; if this arg is less then trial min
	BLSS	1$			; then it becomes trial min
	TSTL	(AP)+			; else ignore it
3$:	SOBGTR	R1, 2$			; check arg count
	RSB


	.END
