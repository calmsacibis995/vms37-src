	.TITLE	MTH$JMIN0	JMIN0 and AJMIN0 functions
	.IDENT	/1-002/		; File: MTHJMIN0.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	This module contains the minimum value functions for
;	longword arguments.
;
;
;--
;
; VERSION: 0
;
; HISTORY:
;
; AUTHOR:
;	Jonathan M. Taylor, 12-JUL-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 0 of MTHJMIN0
;
; 0-3	- Fix access violation.  TNH 16-Aug-78
; 1-001	- Update version number and copyright notice.  JBS 16-NOv-78
; 1-002	- Add "_" to the PSECT directive.  JBS 22-DEC-78

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;	oerr.mar
;

;
; EXTERNAL SYMBOLS:
;	NONE
;

;
; MACROS:
;	NONE
;

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
;	NONE
;

;
; OWN STORAGE:
;	NONE
;

	.SBTTL	MTH$JMIN0, MTH$AJMIN0

;++
; FUNCTIONAL DESCRIPTION:
;	Call routine MINL to compute the minimum of n arguments, n is
;	greater than or equal to 1.
;	Return the result converted to the proper type.
;
;
; CALLING SEQUENCE:
;	Minimum.wl.v =MTH$JMIN0 ({arg.rl.r})
;	Minimum.wf.v =MTH$AJMIN0 ({arg.rl.r})
;	
;
;
; INPUT PARAMETERS:
;	The n parameters are longword values and are call-by-reference.
;	
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	NONE
;
;--




	.ENTRY	MTH$JMIN0,	^M<>
	BSB	MINL		; R0 = min arg
	RET

	.ENTRY	MTH$AJMIN0,	^M<>
	BSB	MINL		; R0 = min arg
	CVTLF	R0, R0		; REAL*4 it
	RET

;+
; MINL returns the smallest of the INTEGER*4 args.
;-

MINL:
	MOVZBL	(AP), R1		; R1 = number of args
	TSTL	(AP)+			; AP -> first arg adr
1$:	MOVL	@(AP)+, R0		; get trial min
	BRB	3$			; check arg count
2$:	CMPL	@0(AP),R0		; if this arg is less then trial min
	BLSS	1$			; then it becomes trial min
	TSTL	(AP)+			; else ignore it
3$:	SOBGTR	R1, 2$			; check arg count
	RSB


	.END
