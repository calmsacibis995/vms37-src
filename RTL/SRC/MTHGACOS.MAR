	.TITLE	MTH$GACOS 	; G Floating Point Arc-cosine routine
				; (GACOS,GACOSD)
	.IDENT /1-006/		; File: MTHGACOS.MAR  EDIT:  RNH1006
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;
; MTH$GACOS is a function which returns the G floating point arc-cosine 
; in radians of its G floating point argument. The call is standard 
; call-by-reference.
;
; MTH$GACOSD is a function which returns the G floating point arc-cosine 
; in degrees of its G floating point argument. The call is standard 
; call-by-reference.
;
;--
;
; VERSION: 1
;
; HISTORY:
; AUTHOR:
;	Steven B. Lionel, 15-Jan-79: Version 1
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY	; Detailed Current Edit History


; ALGORITHMIC DIFFERENCES FROM FP-11/C ROUTINE:	none
;
; Edit History for Version 1 of MTH$GACOS 
;
; 1-001 - Adapted from MTH$DACOS version 1-001.  SBL 15-Jan-79
; 1-002 - Change JSB entry to MTH$GACOS_R7.  RBG 28-Sept-1979
; 1-003 - Added degree entry points.  RNH 22-MAR-1981
; 1-004 - Modified computation of 1 - X^2 to avoid loss of significance
;           for |X|>=1/2.  RNH 02-Sept-1981
; 1-005 - Changed shared external references to G^. RNH 02-Oct-81
; 1-006 - Eliminated symbolic short literals.  RNH 15-Oct-81

	.SBTTL	DECLARATIONS	; Declarative Part of Module

;
; INCLUDE FILES:	OTSPARAMS.MAR
;
; EXTERNAL SYMBOLS:
;

	.DSABL	GBL			; force undefineds to error
	.EXTRN	MTH$GATAN_R7		; arctangent routine
	.EXTRN	MTH$GATAND_R7		; arctangent routine
	.EXTRN	MTH$GSQRT_R5		; square root routine
	.EXTRN	MTH$$SIGNAL		; math signal routine
	.EXTRN	MTH$K_INVARGMAT		; Error code

; EQUATED SYMBOLS:

	value	= 4			; value.rg.r

;
; MACROS:	none
;
; PSECT DECLARATIONS:

	.PSECT	_MTH$CODE	PIC,SHR,LONG,EXE,NOWRT
					; program section for math routines
;
; OWN STORAGE:	none
;
;
; CONSTANTS:
;

G_PI_OVER_2:
	.WORD	^O040031, ^O020773, ^O052104, ^O026430
					; PI/2
G_PI:
	.WORD	^O040051, ^O020773, ^O052104, ^O026430
					; PI
G_90:
	.LONG	^X80004076, ^X0		; 90
G_180:
	.LONG	^X80004086, ^X0		; 180

	.SBTTL	MTH$GACOS  - Standard G Floating GACOS


;++
; FUNCTIONAL DESCRIPTION:
;
; GACOS  - G floating point function
;
; GACOS(X) is computed as:
;
;	If X = 0, then GACOS(X) = PI/2.
;	If X = 1, then GACOS(X) = 0.
;	If X = -1, then GACOS(X) = PI.
;	If 0 < X < 1/2, then GACOS(X) = ATAN(SQRT(1-X**2)/X).
;	If 1/2 =< X < 1, then GACOS(X) = ATAN(SQRT((1-X)*(1+x))/X).
;	If -1/2 < X < 0, then GACOS(X) = ATAN(SQRT(1-X**2)/X) + PI.
;	If -1 < X =< -1/2, then GACOS(X) = ATAN(SQRT((1-X)*(1+x))/X) + PI
;	If 1 < |X|, error.
;
; CALLING SEQUENCE:
;
;	gacos.wg.v = MTH$GACOS(x.rg.r)
;
; INPUT PARAMETERS:

	LONG = 4			; define longword multiplier
	x = 1 * LONG			; Contents of x is the argument

; IMPLICIT INPUTS:	none
;
; OUTPUT PARAMETERS:
;
;	VALUE:	G floating arc-cosine of the argument
;
; IMPLICIT OUTPUTS:	none
;
; COMPLETION CODES:	none
;
; SIDE EFFECTS:
;
; Signals: MTH$_INVARGMAT if |X| > 1 with reserved operand in R0/R1 (copied
; to the signal mechanism vector CHF$L_MCH_R0/R1 by LIB$SIGNAL).
; Associated message is: "INVALID ARGUMENT". Result is reserved operand -0.0
; unless a user supplied (or any) error handler changes CHF$L_MCH_R0/R1.
;
; NOTE: This procedure disables floating point underflow, enables integer
; overflow.
;
;---


	.ENTRY	MTH$GACOS, ^M<IV, R2, R3, R4, R5, R6, R7>
					; standard call-by-reference entry
					; disable DV (and FU), enable IV
	MTH$FLAG_JACKET			; flag that this is a jacket procedure in
					; case of an error in routine
					; If an error, convert signal to user PC
					; and resignal
	MOVG	@value(AP), R0		; R0/R1 = |X| = @value(AP)
	BSBB	MTH$GACOS_R7		; call special GACOS routine
	RET				; return with result in R0/R1


	.SBTTL	MTH$GACOS_R7 - Special GACOS routine

; Special GACOS - used by the standard routine and direct JSB call.
;
; CALLING SEQUENCE:
;	save anything needed in R0:R7
;	MOVG	..., R0			; input in R0/R1
;	JSB	MTH$GACOS_R7
;	RSB				; return with result in R0/R1
;

MTH$GACOS_R7::				; special GACOS routine
MTH$GACOS_R9::				; Release 1 name
	MOVG	R0, R6			; save X in R6/R7
	BNEQ	TEST_FOR_1.0		; branch if |X| > 0

;
; X = 0
;

	MOVQ	G_PI_OVER_2, R0		; R0/R1 = PI/2
	RSB				; return PI/2 if |X| = 0

;
; 0 < |X|
;

TEST_FOR_1.0:
	BICW	#^X8000, R0		; R0/R1 = |X|
	CMPG	R0, #1			; compare |X| with 1.0
	BGEQ	GEQ_TO_1.0		; branch if |X| >= 1.0

;
; 0 < |X| < 1.0
;

	CMPW	#^X4000, R0		; Check for possible loss of 
	BGTR	1$			;   significance
	SUBG3	R0, #1, R2		; R2/R3 = 1 - X
	ADDG2	#1, R0			; R0/R1 = 1 + X
	MULG2	R2, R0			; R0/R1 = 1 - X^2
	BRB	2$			; Join main flow
1$:	MULG2	R0, R0			; R0/R1 = X**2
	SUBG3	R0, #1, R0		; R0/R1 = 1.0 - X**2
2$:	JSB	G^MTH$GSQRT_R5		; R0/R1 = GSQRT(1-X**2)
	DIVG2	R6, R0			; R0/R1 = GSQRT(1-X**2)/X
	PUSHL	R6			; save sign of X for sign test
	JSB	G^MTH$GATAN_R7		; R0/R1 = GATAN(GSQRT(1-X**2)/X)
	MOVL	(SP)+, R6		; restore sign of X
	BRB	TEST_SIGN		; branch to TEST_SIGN

;
; 1 =< |X| 
;

GEQ_TO_1.0:
	BGTR	ERROR			; branch to ERROR if |X| > 1.0


;
; |X| = 1.0
;

	CLRQ	R0			; R0/R1 = 0


;
; Test the sign of X in order to decide if add PI to the result
;

TEST_SIGN:
	TSTG	R6			; test the sign of X
	BGEQ	10$			; branch if X > 0
	ADDG2	G_PI, R0		; add PI to R0/R1 if X < 0
10$:	RSB				; return with result in R0/R1

;
; 1 < |X|, error
;

ERROR:	PUSHL	(SP)			; return PC from JSB routine
	MOVZBL	#MTH$K_INVARGMAT, -(SP)	; condition value
	ASHQ	#15, #1, R0		; R0 = result = reserved operand -0.0
					; goes to signal mechanism vector
					; (CHF$L_MCH_R0/R1) so error handler
					; can modify the result.
	CALLS	#2, G^MTH$$SIGNAL	; signal error and use real user's PC
					; independent of CALL vs JSB
	RSB				; return - R0 restored from CHF$L_MCH_R0/R1


	.SBTTL	MTH$GACOSD  - Standard G Floating GACOSD


;++
; FUNCTIONAL DESCRIPTION:
;
; GACOSD  - G floating point function
;
; GACOSD(X) is computed as:
;
;	If X = 0, then GACOSD(X) = 90.
;	If X = 1, then GACOSD(X) = 0.
;	If X = -1, then GACOSD(X) = 180.
;	If 0 < X < 1, then GACOSD(X) = GATAND(SQRT(1-X**2)/X).
;	If 1/2 =< X < 1, then GACOS(X) = GATAND(SQRT((1-X)*(1+x))/X).
;	If -1 < X < 0, then GACOSD(X) = GATAND(SQRT(1-X**2)/X) + 180.
;	If -1 < X =< -1/2, then GACOS(X) = GATAND(SQRT((1-X)*(1+x))/X) + 180.
;	If 1 < |X|, error.
;
; CALLING SEQUENCE:
;
;	GACOSD.wg.v = MTH$GACOSD(x.rg.r)
;
; INPUT PARAMETERS:

	LONG = 4			; define longword multiplier
	x = 1 * LONG			; Contents of x is the argument

; IMPLICIT INPUTS:	none
;
; OUTPUT PARAMETERS:
;
;	VALUE:	G floating arc-cosine of the argument
;
; IMPLICIT OUTPUTS:	none
;
; COMPLETION CODES:	none
;
; SIDE EFFECTS:
;
; Signals: MTH$_INVARGMAT if |X| > 1 with reserved operand in R0/R1 (co180ed
; to the signal mechanism vector CHF$L_MCH_R0/R1 by LIB$SIGNAL).
; Associated message is: "INVALID ARGUMENT". Result is reserved operand -0.0
; unless a user supplied (or any) error handler changes CHF$L_MCH_R0/R1.
;
; NOTE: This procedure disables floating point underflow, enables integer
; overflow.
;
;---


	.ENTRY	MTH$GACOSD, ^M<IV, R2, R3, R4, R5, R6, R7>
					; standard call-by-reference entry
					; disable DV (and FU), enable IV
	MTH$FLAG_JACKET			; flag that this is a jacket procedure in
					; case of an error in routine
					; If an error, convert signal to user PC
					; and resignal
	MOVG	@value(AP), R0		; R0/R1 = |X| = @value(AP)
	BSBB	MTH$GACOSD_R7		; call special GACOSD routine
	RET				; return with result in R0/R1


	.SBTTL	MTH$GACOSD_R7 - Special GACOSD routine

; Special GACOSD - used by the standard routine and direct JSB call.
;
; CALLING SEQUENCE:
;	save anything needed in R0:R7
;	MOVG	..., R0			; input in R0/R1
;	JSB	MTH$GACOSD_R7
;	RSB				; return with result in R0/R1
;

MTH$GACOSD_R7::				; special GACOSD routine
	MOVG	R0, R6			; save X in R6/R7
	BNEQ	D_TEST_FOR_1.0		; branch if |X| > 0

;
; X = 0
;

	MOVQ	G_90, R0		; R0/R1 = 90
	RSB				; return 90 if |X| = 0

;
; 0 < |X|
;

D_TEST_FOR_1.0:
	BICW	#^X8000, R0		; R0/R1 = |X|
	CMPG	R0, #1			; compare |X| with 1.0
	BGEQ	D_GEQ_TO_1.0		; branch if |X| >= 1.0

;
; 0 < |X| < 1.0
;

	CMPW	#^X4000, R0		; Check for possible loss of 
	BGTR	1$			;   significance
	SUBG3	R0, #1, R2		; R2/R3 = 1 - X
	ADDG2	#1, R0			; R0/R1 = 1 + X
	MULG2	R2, R0			; R0/R1 = 1 - X^2
	BRB	2$			; Join main flow
1$:	MULG2	R0, R0			; R0/R1 = X**2
	SUBG3	R0, #1, R0		; R0/R1 = 1.0 - X**2
2$:	JSB	G^MTH$GSQRT_R5		; R0/R1 = GSQRT(1-X**2)
	DIVG2	R6, R0			; R0/R1 = GSQRT(1-X**2)/X
	PUSHL	R6			; save sign of X for sign test
	JSB	G^MTH$GATAND_R7		; R0/R1 = GATAND(GSQRT(1-X**2)/X)
	MOVL	(SP)+, R6		; restore sign of X
	BRB	D_TEST_SIGN		; branch to D_TEST_SIGN

;
; 1 =< |X| 
;

D_GEQ_TO_1.0:
	BEQL	10$
	BRW	ERROR			; branch to ERROR if |X| > 1.0


;
; |X| = 1.0
;

10$:	CLRQ	R0			; R0/R1 = 0


;
; Test the sign of X in order to decide if add 180 to the result
;

D_TEST_SIGN:
	TSTG	R6			; test the sign of X
	BGEQ	10$			; branch if X > 0
	ADDG2	G_180, R0		; add 180 to R0/R1 if X < 0
10$:	RSB				; return with result in R0/R1




	.END
