	.TITLE	OTS$POWRR - REAL ** REAL power routine
	.IDENT	/1-005/		; File: OTSPOWRR.MAR  Edit: SBL1005

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: Language support library - user callable
;++
; ABSTRACT:
;
;	REAL base to REAL power.
;
;	Floating overflow can occur
;	Undefined exponentiation can occur if:
;		1)  Negative base
;		2)  0 base and power is 0 or negative.
;
;
;
;--
;
; VERSION: 0
;
; HISTORY:
;
; AUTHOR:
;	Jonathan Taylor, 28-Jun-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed current edit history


; Edit history for Version 0 of OTS$POWRR
;
; version 0-2 Removed W^ from MTH$$ERROR.
; version 0-3 changed MTH$$ERROR to MTH$$SIGNAL - JMT 5-OCT-77
; 0-7	- Change FOR$FLAG_JACKET to MTH$FLAG_JACKET.  TNH 17-July-78
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Change MTH__UNDEXP to MTH$K_UNDEXP.  JBS 07-DEC-78
; 1-003	- Add "_" to the PSECT directive.  JBS 22-DEC-78
; 1-004 - Declare externals.  SBL 17-May-1979
; 1-005 - Use general mode addressing.  SBL 30-Nov-1981

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;

;
; EXTERNAL SYMBOLS:
;
	.DSABL	GBL
	.EXTRN	MTH$K_UNDEXP
	.EXTRN	MTH$$SIGNAL		; Math error routine
	.EXTRN	MTH$ALOG_R5		; floating logarithm
	.EXTRN	MTH$EXP_R4		; floating exponental

;
; MACROS:
;

;
; EQUATED SYMBOLS:
;
	base	= 4			; base input formal - by-value
	exp	= 8			; exponent input formal - by-value

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT	_OTS$CODE	PIC,SHR,LONG,EXE,NOWRT
					; program section for OTS$ code

	.SBTTL	OTS$POWRR - REAL to REAL giving REAL result

;++
; FUNCTIONAL DESCRIPTION:
;
;	OTS$POWRR - REAL result = REAL base ** REAL exponent
;
;	The REAL result is given by:
;
;	base	exponent	result
;	----	--------	------
;
;	= 0	> 0		0.0
;	= 0	= 0		Undefined Exponentiation
;	= 0	< 0		Undefined Exponentiation
;
;	< 0	any		Undefined Exponentiation
;
;	> 0	> 0		EXP(exp * LOG(base))
;	> 0	= 0		1.0
;	> 0	< 0		EXP(exp * LOG(base))
;
;
;	Floating Overflow can occur.
;	Undefined Exponentiation can occur if:
;		1)  base is 0 and exponent is 0 or negative
;		2)  base is negative
;
; CALLING SEQUENCE:
;
;	power.wf.v = OTS$POWRR (base.rf.v, exponent.rf.v)
;
; INPUT PARAMETERS:
;	base and exponent parameters are call by value
;
; IMPLICIT INPUTS:
;	none
;
; OUTPUT PARAMETERS:
;	none
;
; IMPLICIT OUTPUTS:
;	none
;
; FUNCTIONAL VALUE:
;	OTS$POWRR - REAL base ** REAL power
;
; SIDE EFFECTS:
;
;	SIGNALs SS$_ARITH with floating overflow hardware code if
;	floating overflow.
;	SIGNALs MTH$_UNDEXP (82 = ' UNDEFINED EXPONENTIATION') if
;		1)  base is 0 and exponent is 0 or negative
;		2)  base is negative
;
;
;--

	.ENTRY	OTS$POWRR, ^M<IV,R2,R3,R4,R5,R6,R7> ; enable integer overflow
	MTH$FLAG_JACKET			; establish math error handler
	MOVF	exp(AP), R1		; R1 = REAL exponent
	MOVF	base(AP), R0		; R0 = REAL base
	BEQL	ZERO			; special case 0 ** n
	BLSS	ERR			; negative base is an error
	TSTF	R1			; test the exponent
	BEQL	ONE			; special case n ** 0
	MOVF	R1, -(SP)		; save exponent (R1) across call
	JSB	G^MTH$ALOG_R5		; R0 = LOG(base)
	MULF	(SP)+, R0		; R0 = exp * LOG(base)
	JSB	G^MTH$EXP_R4		; R0 = EXP(exp * LOG(base))
	RET				; function value returned in R0

ONE:
	MOVF	#1, R0			; answer is one
	RET				; returned in R0/R1

ZERO:
	CLRF	R0			; answer is zero
	TSTF	R1			; test the exponent (R1)
	BGTR	RETURN			; return if positive exponent
ERR:
	ASHL	#15, #1, R0		; R0 = undefined operand
	MOVZBL	#MTH$K_UNDEXP, -(SP)	; FORTRAN error number
	CALLS	#1, G^MTH$$SIGNAL	; convert to 32-bit condition code
					; and SIGNAL MTH$UNDEXP
					; Note: 2nd arg not needed since
					; no JSB OTS$POWRR is possible.
RETURN:
	RET				; return function value in R0/R1

	.END
