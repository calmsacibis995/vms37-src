	.TITLE	MTH$DACOS 	; Double Precision Floating Point Arc-cosine routine
				; (DACOS,DACOSD)
	.IDENT /1-007/		; File: MTHDACOS.MAR  Edit: RNH1007
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;
; MTH$DACOS is a function which returns the double precision floating point
; arc-cosine in radians of its double precision floating point argument. The 
; call is standard call-by-reference.
;
; MTH$DACOSD is a function which returns the double precision floating point
; arc-cosine in degrees of its double precision floating point argument. The 
; call is standard call-by-reference.
;
;--
;
; VERSION: 01
;
; HISTORY:
; AUTHOR:
;	Peter Yuo, 29-Jun-77: Version 01
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY	; Detailed Current Edit History


; ALGORITHMIC DIFFERENCES FROM FP-11/C ROUTINE:	none
;
; Edit History for Version 01 of MTH$DACOS 
;
; 0-2	MTH$$ERROR changed to MTH$$SIGNAL.
;	MTH$_... changed to MTH__....
;	Changed error handling mechanism. Put error result in R0:R1 before
;	calling MTH$$SIGNAL in order to allow user modify error result.
; 0-3	- Changed MOVD #0, to CLRD.  TNH 10-Nov-77
; 1-001	- Updated version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Change MTH__INVARG to MTH$K_INVARGMAT.  JBS 07-DEC-78
; 1-003	- Add "_" to PSECT directive.  JBS 22-DEC-78
; 1-004 - Declare externals.  SBL 17-May-1979
; 1-005 - Change JSB entry to MTH$DACOS_R7.  RBG 28-Sept-1979
; 1-006 - Added degree entry points. RNH 22-MAR-1981
; 1-007 - Modified computation of 1 - X^2 to avoid loss of significance for
;           |X|>=1/2  RNH 02-Sept-1981

	.SBTTL	DECLARATIONS	; Declarative Part of Module

;
; INCLUDE FILES:
;

;
; EXTERNAL SYMBOLS:
;
	.DSABL	GBL
	.EXTRN	MTH$DSQRT_R5
	.EXTRN	MTH$DATAN_R7
	.EXTRN	MTH$DATAND_R7
	.EXTRN	MTH$K_INVARGMAT
	.EXTRN	MTH$$SIGNAL

;
; EQUATED SYMBOLS:

	SD_1.0	= ^F1.0			; 1.0
	value	= 4			; value.rd.r

;
; MACROS:	none
;
; PSECT DECLARATIONS:

	.PSECT	_MTH$CODE	PIC,SHR,LONG,EXE,NOWRT
					; program section for math routines
;
; OWN STORAGE:	none
;
;
; CONSTANTS:
;

D_PI_OVER_2:
	.WORD	^O040311, ^O007732, ^O121041, ^O064302
					; PI/2
D_PI:
	.WORD	^O040511, ^O007732, ^O121041, ^O064302
					; PI
D_90:
	.LONG	^X000043B4, ^X0		; 90
D_180:
	.LONG	^X00004434, ^X0		; 180

	.SBTTL	MTH$DACOS  - Standard Double Precision Floating DACOS


;++
; FUNCTIONAL DESCRIPTION:
;
; DACOS  - double precision floating point function
;
; DACOS(X) is computed as:
;
;	If X = 0, then DACOS(X) = PI/2.
;	If X = 1, then DACOS(X) = 0.
;	If X = -1, then DACOS(X) = PI.
;	If 0 < X < 1/2, then DACOS(X) = ATAN(SQRT(1-X**2)/X).
;	If 1/2 < X < 1, then DACOS(X) = ATAN(SQRT((1-X)*(1+x))/X).
;	If -1/2 < X < 0, then DACOS(X) = ATAN(SQRT(1-X**2)/X) + PI.
;	If -1 < X =< -1/2, then DACOS(X) = ATAN(SQRT((1-X)*(1+x))/X) + PI.
;	If 1 < |X|, error.
;
; CALLING SEQUENCE:
;
;	DACOS.wd.v = MTH$DACOS(x.rd.r)
;
; INPUT PARAMETERS:

	LONG = 4			; define longword multiplier
	x = 1 * LONG			; Contents of x is the argument

; IMPLICIT INPUTS:	none
;
; OUTPUT PARAMETERS:
;
;	VALUE:	double precision floating arc-cosine of the argument
;
; IMPLICIT OUTPUTS:	none
;
; COMPLETION CODES:	none
;
; SIDE EFFECTS:
;
; Signals: MTH$_INVARG if |X| > 1 with reserved operand in R0/R1 (copied
; to the signal mechanism vector CHF$L_MCH_R0/R1 by LIB$SIGNAL).
; Associated message is: "INVALID ARGUMENT". Result is reserved operand -0.0
; unless a user supplied (or any) error handler changes CHF$L_MCH_R0/R1.
;
; NOTE: This procedure disables floating point underflow, enables integer
; overflow.
;
;---


	.ENTRY	MTH$DACOS, ^M<IV, R2, R3, R4, R5, R6, R7>
					; standard call-by-reference entry
					; disable DV (and FU), enable IV
	MTH$FLAG_JACKET			; flag that this is a jacket procedure in
					; case of an error in routine
					; If an error, convert signal to user PC
					; and resignal
	MOVD	@value(AP), R0		; R0/R1 = |X| = @value(AP)
	BSBB	MTH$DACOS_R7		; call special DACOS routine
	RET				; return with result in R0/R1


	.SBTTL	MTH$DACOS_R7 - Special DACOS routine

; Special DACOS - used by the standard routine and direct JSB call.
;
; CALLING SEQUENCE:
;	save anything needed in R0:R7
;	MOVD	..., R0			; input in R0/R1
;	JSB	MTH$DACOS_R7
;	RSB				; return with result in R0/R1
;

MTH$DACOS_R7::				; special DACOS routine
MTH$DACOS_R9::				; Release 1 name
	MOVD	R0, R6			; save X in R6/R7
	BNEQ	TEST_FOR_1.0		; branch if |X| > 0

;
; X = 0
;

	MOVD	D_PI_OVER_2, R0		; R0/R1 = PI/2
	RSB				; return PI/2 if |X| = 0

;
; 0 < |X|
;

TEST_FOR_1.0:
	BICW	#^X8000, R0		; R0/R1 = |X|
	CMPD	R0, S^#SD_1.0		; compare |X| with 1.0
	BGEQ	GEQ_TO_1.0		; branch if |X| >= 1.0

;
; 0 < |X| < 1.0
;

	CMPW	#4000, R0		; Check for possible loss of 
	BGTR	1$			;   significance
	SUBD3	R0, #1, R2		; R2/R3 = 1 - X
	ADDD	#1, R0			; R0/R1 = 1 + X
	MULD	R2, R0			; R0/R1 = 1 - X^2
	BRB	2$			; Join main flow
1$:	MULD2	R0, R0			; R0/R1 = X**2
	SUBD3	R0, S^#SD_1.0, R0	; R0/R1 = 1.0 - X**2
2$:	JSB	MTH$DSQRT_R5		; R0/R1 = DSQRT(1-X**2)
	DIVD	R6, R0			; R0/R1 = DSQRT(1-X**2)/X
	PUSHL	R6			; save sign of X for sign test
	JSB	MTH$DATAN_R7		; R0/R1 = DATAN(DSQRT(1-X**2)/X)
	MOVL	(SP)+, R6		; restore sign of X
	BRB	TEST_SIGN		; branch to TEST_SIGN

;
; 1 =< |X| 
;

GEQ_TO_1.0:
	BGTR	ERROR			; branch to ERROR if |X| > 1.0


;
; |X| = 1.0
;

	CLRD	R0			; R0/R1 = 0


;
; Test the sign of X in order to decide if add PI to the result
;

TEST_SIGN:
	TSTD	R6			; test the sign of X
	BGEQ	10$			; branch if X > 0
	ADDD	D_PI, R0		; add PI to R0/R1 if X < 0
10$:	RSB				; return with result in R0/R1

;
; 1 < |X|, error
;

ERROR:	PUSHL	(SP)			; return PC from JSB routine
	MOVZBL	#MTH$K_INVARGMAT, -(SP)	; condition value
	ASHQ	#15, #1, R0		; R0 = result = reserved operand -0.0
					; goes to signal mechanism vector
					; (CHF$L_MCH_R0/R1) so error handler
					; can modify the result.
	CALLS	#2, G^MTH$$SIGNAL	; signal error and use real user's PC
					; independent of CALL vs JSB
	RSB				; return - R0 restored from CHF$L_MCH_R0/R1



	.SBTTL	MTH$DACOSD  - Standard Double Precision Floating DACOSD


;++
; FUNCTIONAL DESCRIPTION:
;
; DACOSD  - double precision floating point function
;
; DACOSD(X) is computed as:
;
;	If X = 0, then DACOSD(X) = 90.
;	If X = 1, then DACOSD(X) = 0.
;	If X = -1, then DACOSD(X) = 180.
;	If 0 < X < 1, then DACOSD(X) = ATAN(SQRT(1-X**2)/X).
;	If 1/2 < X < 1, then DACOS(X) = ATAN(SQRT((1-X)*(1+x))/X).
;	If -1/2 < X < 0, then DACOSD(X) = ATAN(SQRT(1-X**2)/X) + 180.
;	If -1 < X =< -1/2, then DACOS(X) = ATAN(SQRT((1-X)*(1+x))/X) + 180.
;	If 1 < |X|, error.
;
; CALLING SEQUENCE:
;
;	DACOSD.wd.v = MTH$DACOSD(x.rd.r)
;
; INPUT PARAMETERS:

	LONG = 4			; define longword multiplier
	x = 1 * LONG			; Contents of x is the argument

; IMPLICIT INPUTS:	none
;
; OUTPUT PARAMETERS:
;
;	VALUE:	double precision floating arc-cosine of the argument
;
; IMPLICIT OUTPUTS:	none
;
; COMPLETION CODES:	none
;
; SIDE EFFECTS:
;
; Signals: MTH$_INVARG if |X| > 1 with reserved operand in R0/R1 (co180ed
; to the signal mechanism vector CHF$L_MCH_R0/R1 by LIB$SIGNAL).
; Associated message is: "INVALID ARGUMENT". Result is reserved operand -0.0
; unless a user supplied (or any) error handler changes CHF$L_MCH_R0/R1.
;
; NOTE: This procedure disables floating point underflow, enables integer
; overflow.
;
;---


	.ENTRY	MTH$DACOSD, ^M<IV, R2, R3, R4, R5, R6, R7>
					; standard call-by-reference entry
					; disable DV (and FU), enable IV
	MTH$FLAG_JACKET			; flag that this is a jacket procedure in
					; case of an error in routine
					; If an error, convert signal to user PC
					; and resignal
	MOVD	@value(AP), R0		; R0/R1 = |X| = @value(AP)
	BSBB	MTH$DACOSD_R7		; call special DACOSD routine
	RET				; return with result in R0/R1


	.SBTTL	MTH$DACOSD_R7 - Special DACOSD routine

; Special DACOSD - used by the standard routine and direct JSB call.
;
; CALLING SEQUENCE:
;	save anything needed in R0:R7
;	MOVD	..., R0			; input in R0/R1
;	JSB	MTH$DACOSD_R7
;	RSB				; return with result in R0/R1
;

MTH$DACOSD_R7::				; special DACOSD routine
	MOVD	R0, R6			; save X in R6/R7
	BNEQ	D_TEST_FOR_1.0		; branch if |X| > 0

;
; X = 0
;

	MOVD	D_90, R0		; R0/R1 = 90
	RSB				; return 90 if |X| = 0

;
; 0 < |X|
;

D_TEST_FOR_1.0:
	BICW	#^X8000, R0		; R0/R1 = |X|
	CMPD	R0, S^#SD_1.0		; compare |X| with 1.0
	BGEQ	D_GEQ_TO_1.0		; branch if |X| >= 1.0

;
; 0 < |X| < 1.0
;

	CMPW	#4000, R0		; Check for possible loss of 
	BGTR	1$			;   significance
	SUBD3	R0, #1, R2		; R2/R3 = 1 - X
	ADDD	#1, R0			; R0/R1 = 1 + X
	MULD	R2, R0			; R0/R1 = 1 - X^2
	BRB	2$			; Join main flow
1$:	MULD2	R0, R0			; R0/R1 = X**2
	SUBD3	R0, S^#SD_1.0, R0	; R0/R1 = 1.0 - X**2
2$:	JSB	MTH$DSQRT_R5		; R0/R1 = DSQRT(1-X**2)
	DIVD	R6, R0			; R0/R1 = DSQRT(1-X**2)/X
	PUSHL	R6			; save sign of X for sign test
	JSB	MTH$DATAND_R7		; R0/R1 = DATAND(DSQRT(1-X**2)/X)
	MOVL	(SP)+, R6		; restore sign of X
	BRB	D_TEST_SIGN		; branch to D_TEST_SIGN

;
; 1 =< |X| 
;

D_GEQ_TO_1.0:
	BEQL	10$
	BRW	ERROR			; branch to ERROR if |X| > 1.0


;
; |X| = 1.0
;

10$:	CLRD	R0			; R0/R1 = 0


;
; Test the sign of X in order to decide if add 180 to the result
;

D_TEST_SIGN:
	TSTD	R6			; test the sign of X
	BGEQ	10$			; branch if X > 0
	ADDD	D_180, R0		; add 180 to R0/R1 if X < 0
10$:	RSB				; return with result in R0/R1


	.END
