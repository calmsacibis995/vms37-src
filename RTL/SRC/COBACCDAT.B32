MODULE COB$ACC_DATE (				!
		IDENT = '1-005'			! file: COBACCDAT.B32
		) =
BEGIN
!
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!
!++
! FACILITY:	COBOL SUPPORT
!
! ABSTRACT
!
!
!
! ENVIRONMENT:	Vax-11 User Mode
!
! AUTHOR: MLJ , CREATION DATE: 16-JAN-1979
!
! MODIFIED BY:
!
! 1-001 - Original. MLJ 16-JAN-1979
! 1-002 - Added boilerplate and comments. RKR 18-JULY-1979
! 1-003 - Declare psects via library macro.  RKR 23-AUG-1979
! 1-004 - Change symbolic name of LIBRARY file. RKR 1-OCT-79
! 1-005 - Cosmetic changes.  RKR 18-OCT-79
!
!--

!<BLF/PAGE>

!
! SWITCHES
!

SWITCHES ADDRESSING_MODE (EXTERNAL = GENERAL, NONEXTERNAL = WORD_RELATIVE);

!
! LINKAGES
!
!	NONE
!
! TABLE OF CONTENTS:
!
FORWARD ROUTINE
!
	COB$ACC_DATE : NOVALUE ;
!
! INCLUDE FILES
!

REQUIRE 'RTLIN:RTLPSECT' ;	! Macros for psect declarations
LIBRARY 'RTLSTARLE';

!
! MACROS
!
!	NONE
!
! EQUATED SYMBOLS
!
!	NONE
!
! PSECT DECLARATIONS:
!
DECLARE_PSECTS (COB) ;		! Psects for COB$ facility


GLOBAL ROUTINE COB$ACC_DATE(DST): NOVALUE=
!
!++	FUNCTIONAL DESCRIPTION
!
!	Returns date as YYMMDD
!
!
! FORMAL PARAMETERS:
!
!	DST.wt.ds	Address of string descriptor to receive date
!
! IMPLICIT INPUTS:
!
!	Date, as supplied by $ASCTIM call
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--
!
    BEGIN
    MAP
	DST:		REF BLOCK[,BYTE];	! Pointer to destination descriptor
    BIND
	MONTHNAME = UPLIT(
	    'JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC ')
	: BLOCK[,LONG],

	MONTHNUM = UPLIT(
	    '010203040506070809101112')
	: BLOCK[,WORD];
    LOCAL
	TIMBUF:		BLOCK[11,BYTE],		! Buffer for $ASCTIM
	BUFFER:		BLOCK[6,BYTE],		! Buffer for assembling output
	TIMDESC:	VECTOR[2];		! Descriptor for TIMBUF


    TIMDESC[0] = 11;				! Set up descriptor
    TIMDESC[1] = TIMBUF;			!
    $ASCTIM(TIMBUF=TIMDESC);			! Get date
    BUFFER[0,0,16,0] = .TIMBUF[9,0,16,0];	! Copy year of century
    INCR I FROM 0 TO 11 DO
	BEGIN
	IF .MONTHNAME[.I,0,24,0] EQL .TIMBUF[3,0,24,0]
	THEN
	    BEGIN				! Copy month of year
	    BUFFER[2,0,16,0] = .MONTHNUM[.I,0,16,0];
	    EXITLOOP;
	    END;
	END;
    BUFFER[4,0,16,0] = .TIMBUF[0,0,16,0];	! Copy day of month
    IF .BUFFER[4,0,8,0] EQL %C' ' THEN BUFFER[4,0,8,0] = %C'0';
    CH$COPY(6, BUFFER, %C' ', .DST[DSC$W_LENGTH], .DST[DSC$A_POINTER]);
    END;

END ELUDOM
