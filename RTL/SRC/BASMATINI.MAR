	.TITLE	BAS$MAT_INIT
	.IDENT	/1-006/			; File: BASMATINI.MAR Edit: PLL1006

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: BASIC code support
;
; ABSTRACT:
;
;	This module initializes each element of a matrix to the input
;	constant.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: R. Will, CREATION DATE: 23-May-79
;
; MODIFIED BY:
;++
; 1-001	- Original 
; 1-002 - Make references to bounds signed.  RW 7-Jun-79
; 1-003 - Change shared external references to G^ RNH 25-Sep-81
; 1-004 - Substitute a macro for the calls to the store routines.
;	  This should speed things up.  PLL 6-Nov-81
; 1-005 - Correct a run-time expression in the FETCH and STORE macros.
;	  PLL 20-Jan-82
; 1-006 - Correct another bug in the STORE macro.  Does not compute
;	  linear index for one dimensional arrays properly.  PLL 23-Feb-82
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

	$DSCDEF
	$SFDEF

;
; EXTERNAL DECLARATIONS:
;
	.DSABL	GBL				; Prevent undeclared
						;  symbols from being
						;  automatically global.
	.EXTRN	BAS$K_ARGDONMAT			; signalled if all 3 blocks
						;  not present in array desc
						;  or dimct = 0
	.EXTRN	BAS$K_DATTYPERR			; signalled if dtype of array
						; isn't word long float double
	.EXTRN	BAS$STO_FA_W_R8			; array element store for word
	.EXTRN	BAS$STO_FA_L_R8			; array element store for long
	.EXTRN	BAS$STO_FA_F_R8			; array element store - float
	.EXTRN	BAS$STO_FA_D_R8			; array element store - double
	.EXTRN	BAS$$SCALE_R1			; get the scale for double
	.EXTRN	BAS$$STOP			; signal fatal errors


;
; MACROS:
;

;	$BAS$MAT_INIT	see below, defines entire initialization algorithm
;	STORE		store an element into an array

;
; EQUATED SYMBOLS:
;

	lower_bnd2 = 0				; stack offset for temp
	lower_bnd1 = 4				; stack offset for temp
	upper_bnd1 = 8				; stack offset for temp
	constant_cvt = 12			; stack offset,converted const
	dsc$l_l1_1 = 24				; desc offset if 1 sub
	dsc$l_u1_1 = 28				; desc offset if 1 sub
	dsc$l_l1_2 = 28				; desc offset if 2 sub
	dsc$l_u1_2 = 32				; desc offset if 2 sub
	dsc$l_l2_2 = 36				; desc offset if 2 sub
	dsc$l_u2_2 = 40				; desc offset if 2 sub

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT _BAS$CODE PIC, USR, CON, REL, LCL, SHR, -
			EXE, RD, NOWRT, LONG


	.SBTTL	BAS$MAT_INIT  - Initialize a  matrix
;++
; FUNCTIONAL DESCRIPTION:
;
;	This routine initializes each element of a matrix to the
;	input constant.  The algorithm is the same for the 4 supported
;	BASIC data types.  In order to keep the code for the 4 data types
;	the same and to simplify the reading, the code has been done as
;	a macro, which the four data types use varying only the letters
;	(W, L, F, D) in converting the constant, in passing the constant
;	and calling the array store routines.
;
; CALLING SEQUENCE:
;
;	CALL BASMAT_INIT (matrix.wx.da, constant.rl.v)
;
; INPUT PARAMETERS:
;
	constant = 8
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
	matrix = 4
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	This routine calls the BASIC matrix store routines, and may cause
;	any of their errors to be signalled.  It also may signal any of the
;	errors listed in the externals area.
;
;--


;+
; This macro is a substitute for calls to the array store
; routines.  It will call the BAS$ routines only if the array is a 
; virtual array.  Otherwise, it will calculate the linear index into
; the array via the INDEX instruction.  (Note that BASIC programs must
; be able to handle FORTRAN arrays, so the code must check for arrays
; stored by column.)  The INDEX instructions should provide a significant
; performance improvement over calling a routine for each element of
; the array.
;-


	.LIST	ME
	.MACRO	STORE	array_dtype,?L1,?L2,?L3,?L4,?L5,?L11,?L31

	.IF	IDN	'array_dtype', D	; array is double
	CMPB	dsc$b_class(R2), #dsc$k_class_bfa  ; virtual array?
	BNEQ	L11				; no
	JSB	BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L5				; done
L11:	CMPB	dsc$b_dimct(R2), #1		; 1 or 2 dim?
	BNEQ	L1				; 2 dims
	MOVZWL	dsc$w_length(R2), R6		; make length a longword
	INDEX	R3, dsc$l_l1_1(R2), dsc$l_u1_1(R2), R6, #0, R5
	ADDL	dsc$a_a0(R2), R5		; add start addr to offset
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L5				; 1 dim done
L1:	BBS	#5, 10(R2), L2			; br if stored col-wise
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), dsc$l_m2(R2), #0, R5
						; I * M2
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), R6, R5, R5
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L5				; done
L2:	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), dsc$l_m1(R2), #0, R5
						; J * M1
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), R6, R5, R5
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	.IFF					; array type other than double
	CMPB	dsc$b_class(R1), #dsc$k_class_bfa  ;virtual array?
	BNEQ	L31				; no
	JSB	BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L5				; done
L31:	CMPB	dsc$b_dimct(R1), #1		; 1 or 2 dims?
	BNEQ	L3				; 2 dims
	MOVZWL	dsc$w_length(R1), R6		; make length a longword
	INDEX	R2, dsc$l_l1_1(R1), dsc$l_u1_1(R1), R6, #0, R4
	ADDL	dsc$a_a0(R1), R4		; add start addr to offset
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L5				; 1 dim done
L3:	BBS	#5, 10(R1), L4			; br if stored col-wise
	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), dsc$l_m2(R1), #0, R4
						; I * M2
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), R5, R4, R4
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L5				; done
L4:	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), dsc$l_m1(R1), #0, R4
						; J * M1
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), R5, R4, R4
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	.ENDC

L5:
	.ENDM



	.LIST	ME
	.MACRO	$BAS$MAT_INIT dtype		; initialize algorithm

;+
;	REGISTER USAGE
;	R0 - R8 destroyed by store routines
;	R9	upper bound for 2nd subscript
;	R10	pointer to array descriptor
;	R11	current value of 2nd subscript
;-

;+
; Set up limits for looping through all elements
;-

	.IF	IDN	dtype, L
	.IFT					; data type is long
	MOVL	constant(AP), -(SP)		; move constant
	.IFF					; data type is not long
	CVTL'dtype'	constant(AP), -(SP)	; make constant same datatype
						;  as array, save on stack
	.ENDC
	.IF	IDN	dtype, D		; if array is double
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	MULD2	R0, (SP)			; scale
	.ENDC
	CMPB	DSC$B_DIMCT(R10), #1		; determine # of subscripts
	BEQLU	INIT_ONE_SUB'dtype'		; 1 sub, go init
	BGTRU	INIT_TWO_SUBS'dtype'		; >=2 subs, go init
	BRW	ERR_ARGDONMAT			; 0 subs, error

;+
; There is only 1 subscript.  Make both upper and lower bound for 2nd
; subscript a 1.  The second subscript will be passed to and ignored by the
; store routine.
;-

INIT_ONE_SUB'dtype':
	PUSHL	dsc$l_u1_1(R10)			; 1st upper bound
	PUSHL	dsc$l_l1_1(R10)			; 1st lower bound
	BGTR	1$				; not 0 or neg, do 2nd sub
	MOVL	#1, (SP)			; don't alter col 0
1$:	MOVL	#1, R9				; dummy 2nd lower bound
	PUSHL	#1				; dummy 2nd upper bound
	BRB	LOOP_2ND_SUB'dtype'		; go loop

;+
; There are 2 subscripts.  Put the upper bound for both subscripts on the
; stack and make sure that the lower bound for both subscripts will start
; at 1 (do not alter row or col 0)
;-

INIT_TWO_SUBS'dtype':
	PUSHL	dsc$l_u1_2(R10)			; 1st upper bound
	PUSHL	dsc$l_l1_2(R10)			; 1st lower bound
	BGTR	1$				; not row 0 or neg, do cols
	MOVL	#1, (SP)			; start with row 1
1$:	MOVL	dsc$l_u2_2(R10), R9		; 2nd upper bound
	PUSHL	dsc$l_l2_2(R10)			; 2nd lower bound
	BGTR	LOOP_1ST_SUB'dtype'		; not col 0, go loop
	MOVL	#1, (SP)			; start with col 1

;+
; Loop through all the rows.  Row and column upper and lower bounds have been
; initialized on the stack.
;-

LOOP_1ST_SUB'dtype':
	MOVL	lower_bnd2(SP), R11		; R11 has 2nd lower bound

;+
; Loop through all the elements (columns) of the current row.  Column lower
; bound is initialized in R11.  Column upper bound is on the stack.
; Distinguish array by data type so that the correct store routine can be
; called and the constant can be converted to the correct type.
;-

LOOP_2ND_SUB'dtype':

	MOV'dtype'	constant_cvt(SP), R0	; put constant into R0
						;  R0 & R1 for double
;+
; Code is same for all datatypes except double, since double passed by value
; takes 2 words and all other supported datatypes by value takes 1 longword
;-

	.IF	IDN	dtype, D		; see if datatype is double
	.IFF					; no, word, long, float
	MOVL	R10, R1				; pointer to array desc
	MOVL	lower_bnd1(SP), R2		; current row
	MOVL	R11, R3				; current column
	.IFT					; type is double
	MOVL	R10, R2				; pointer to array desc
	MOVL	lower_bnd1(SP), R3		; current row
	MOVL	R11, R4				; current column
	.ENDC					; code now same for all types
	STORE	'dtype'				; store in array
	INCL	R11				; get next column
	CMPL	R11, R9				; see if last column done
	BGTR	2$
	BRW	LOOP_2ND_SUB'dtype'		; no, continue inner loop

;+
; Have completed entire row.  See if it was the last row.  If not,
; continue with next row.
;-

2$:	INCL	lower_bnd1(SP)			; get next row
	CMPL	lower_bnd1(SP), upper_bnd1(SP)	; see if last row done
	BGTR	3$
	BRW	LOOP_1ST_SUB'dtype'		; no, continue outer loop

3$:	RET					; yes, finished
	.ENDM

	.ENTRY BAS$MAT_INIT , ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,IV>

;+
; Put routine arguments into registers for ease of use.
; If block 2 of array descriptor (multipliers) is not present then error.
;-

	MOVL	matrix(AP), R10			; ptr to array descr in R10
	BBC	#DSC$V_FL_BOUNDS, DSC$B_AFLAGS(R10), ERR_ARGDONMAT
						; exit if block 3 not
						; present in descriptor

;+
; Algorithm now differs according to data types
;-

	CASEB	DSC$B_DTYPE(R10), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	WORD-1$				; code for word dtype
	.WORD	LONG-1$				; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	FLOAT-1$			; code for float dtype
	.WORD	DOUBLE-1$			; code for double dtype

ERR_DATTYPERR:
	PUSHL	#BAS$K_DATTYPERR		; Signal error, unsupported
	CALLS	#1, G^BAS$$STOP			; dtype in array desc

ERR_ARGDONMAT:
	PUSHL	#BAS$K_ARGDONMAT		; signal error, 0 for dimct
	CALLS	#1, G^BAS$$STOP			; or block 2 or 3 absent

	.PAGE
WORD:	$BAS$MAT_INIT W				; expand to word operations
	.PAGE
LONG:	$BAS$MAT_INIT L				; expand to long operations
	.PAGE
FLOAT:	$BAS$MAT_INIT F				; expand to float operations
	.PAGE
DOUBLE:	$BAS$MAT_INIT D				; expand to double operations

	.END					; end of BAS$MAT_INIT
