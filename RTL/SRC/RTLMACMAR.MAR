;+
; This file, RTLMACMAR.MAR, defines macros from the RTL for STARLET.MLB.
; Edit: SBL1003
;-
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; Edit History:
;
; 1-001	- Original, with just $LIB_KEY TABLE.  JBS 19-DEC-1979
; 1-002 - Add copyright notice.  SBL 11-Mar-1980
; 1-003 - Reset location counter to proper place at end of $LIB_KEY_TABLE.
;	  SBL 10-Sep-1981
;--
;+
; $LIB_KEY_TABLE builds the data structure for LIB$LOOKUP_KEY.
; It is called with a single argument, consisting of a list of
; pairs.  The pairs consist of the keyword string followed
; by the (32-bit) value associated with the keyword.  For example:
;
;	$LIB_KEY_TABLE <<A,0>,<B,1>,<C,2>>
;
; associates A with 0, B with 1 and C with 2.
;-
;
; This macro produces one entry in the key table.  It is called from
; $LIB_KEY_TABLE with LIB$$A_STRLOC pointing to the place to put the keyword
; string.  LIB$$A_STRLOC is updated.
;
	.MACRO		$$LIB_KEY_ENTRY STRING, VALUE
	.ADDRESS	LIB$$A_STRLOC
	.LONG		VALUE
LIB$$A_HERE=.
.=LIB$$A_STRLOC
	.ASCIC		\STRING\
LIB$$A_STRLOC=.
.=LIB$$A_HERE
	.ENDM		$$LIB_KEY_ENTRY
;
; This macro produces the key table for LIB$LOOKUP_KEY.
;
	.MACRO		$LIB_KEY_TABLE PAIRS
;+
; Count the number of argument pairs.
;-
LIB$$K_NPAIRS=0
	.IRP		ENTRY, <PAIRS>
LIB$$K_NPAIRS=LIB$$K_NPAIRS+1
	.ENDR
;+
; Emit the count of following longwords.
;-
	.LONG		LIB$$K_NPAIRS*2
;+
; Compute where the strings will start.
;-
LIB$$A_STRLOC=.+<8*LIB$$K_NPAIRS>
;+
; Emit each table entry, with its string.
;-
	.IRP 		ENTRY, <PAIRS>
	$$LIB_KEY_ENTRY	ENTRY
	.ENDR
;+
; Reset location counter to just past the last string.
;-
.=LIB$$A_STRLOC
	.ENDM		$LIB_KEY_TABLE
;
;	End of macro $LIB_KEY_TABLE
;
;	End of file RTLMACMAR.MAR
