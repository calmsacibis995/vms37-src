	.TITLE	LIB$ICHAR	; Return first char of string as value
	.IDENT	/1-006/		; File: LIBICHAR.MAR Edit:RKR1006

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: General Utility Library
;++
; ABSTRACT:
;
;	Return first character of a string as INTEGER*4 ASCII value.
;
;--
;
; VERSION: 1-001
;
; HISTORY:
;
; AUTHOR:
;	Thomas N. Hastings, 6-Aug-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 0
;

; 1-001	- Update copyright notice and version number.  The last edit
;	   number on version 0 was 3.  JBS 16-NOV-78
; 1-002	- Add "_" to PSECT directive.  JBS 21-DEC-78
; 1-003 - Add support for 0 length dynamic strings.  RW  16-AUG-79
; 1-004 - Enhance to allow recognition of additional classes of
;	  string descriptors by using LIB$ANALYZE_SDESC_R3 to extract
;	  length and address of 1st byte of data from descritpor.
;	  RKR 20-MAY-1981
; 1-005 - Add special-case code to process string descriptors that
;	  "read" like fix string descriptors.  RKR 7-OCT-1981.
; 1-006 - Redirect jsb's from LIB$ANALYZE_SDESC_R3 to 
;	  LIB$ANALYZE_SDESC_R2.  Drop R3 from entry mask.
;	  RKR 18-NOV-1981.

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES: NONE
;

;
; EXTERNAL SYMBOLS:
;
	.DSABL	GBL			; no default externals
	.EXTRN	LIB$ANALYZE_SDESC_R2	; extract length and address

;
; MACROS:
;
	$DSCDEF			; for fields within a descriptor

;
; PSECT DECLARATIONS:
;
	.PSECT	_LIB$CODE PIC,SHR,LONG,EXE,NOWRT
;
; EQUATED SYMBOLS: NONE
;

;
; OWN STORAGE: NONE
;


	.SBTTL	LIB$ICHAR - Return first char of string as INTEGER*4 value

;++
; FUNCTIONAL DESCRIPTION:
;
;	Return first character of string as INTEGER*4 8-bit ASCII value.
;	If the string is a null string, ie. the length is 0, return a 
;	null.
;
; CALLING SEQUENCE:
;
;	First_char_val.wlu.v = LIB$ICHAR (string.rt.dx)

	string = 4			; Adr. of string descriptor

;
; INPUT PARAMETERS:
;	NONE
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; ROUTINE VALUE
;	FIRST_CHAR_VAL.wlu.v	value of 1st char as a INTEGER*4
;
; SIDE EFFECTS:
;	NONE
;
;--



	.ENTRY	LIB$ICHAR, ^M<R2>	; entry mask
	MOVL	STRING(AP), R0		; address of string descriptor
	CMPB	DSC$B_CLASS(R0), #DSC$K_CLASS_D	; read like fixed ?
	BGTRU	1$			; no
	MOVQ	@STRING(AP), R0		; length->R0, address->R1
	TSTW	R0			;
	BLEQU	10$			; if negative
	MOVZBL	(R1), R0		; value of 1st character
	RET

1$:	JSB	G^LIB$ANALYZE_SDESC_R2	; extract: length->R1, addr->R2
	BLBC	R0, 10$			; if not success, quit
	TSTW	R1			; length 0 ?
	BLEQU	10$			; if so, return a null
	MOVZBL	(R2), R0		; value of 1st character
	RET				; return from LIB$ICHAR

10$:	CLRL	R0			; return a null
	RET

	.END				; End of module LIB$ICHAR
