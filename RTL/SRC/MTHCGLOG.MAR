	.TITLE	MTH$CGLOG	G COMPLEX*16 Natural Logarithm
	.IDENT	/1-004/		; File: MTHCGLOG.MAR Edit: SBL1004

;
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;
;	This module contains routine MTH$CGLOG - perform G COMPLEX*16 log
;
;--
;
; VERSION: 1
;
; HISTORY:
;
; AUTHOR:
;	Steven B. Lionel, 20-July-1979
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History
;
; 1-001 - Adapted from MTH$CLOG version 1-002.  SBL 20-July-1979
; 1-002 - Change PUSHAB to PUSHAG.  SBL 23-Aug-1979
; 1-003 - Change shared external references to G^ RNH 25-Sep-81
; 1-004 - Use general mode addressing.  SBL 30-Nov-1981

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;

;
; EXTERNAL SYMBOLS:
	.DSABL	GBL
	.EXTRN	MTH$GATAN2
	.EXTRN	MTH$CGABS
	.EXTRN	MTH$GLOG_R8

;
; MACROS:
;	NONE

;
; PSECT DECLARATIONS:
;
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:

;
; OWN STORAGE:
;	NONE

	.SBTTL	MTH$CGLOG

;++
; FUNCTIONAL DESCRIPTION:
;
;	The Natural Logaritm of a complex number (r, i) is
;	computed as follows:
;
;		CLOG(arg) = (LOG(CABS(arg)), ATAN2(arg))
;
; CALLING SEQUENCE:
;	CALL MTH$CGLOG (result.wgc.r, arg.rgc.r)
;
;
; INPUT PARAMETERS:
	arg = 8			; Argument by reference, a G COMPLEX*16
				; value.
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;
	result = 4		; Result by reference, a G COMPLEX*16
				; value.
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	Signals	Reserved Operand if the input parameter is bad (-0.0)
;
;--


	.ENTRY	MTH$CGLOG,	^M<R2,R3,R4,R5,R6,R7,R8>
	MTH$FLAG_JACKET			; set up error handler
	MOVL	arg(AP), R0		; R0 -> (arg)
	PUSHAG	(R0)+			; push addr(r)
	PUSHL	R0			; push addr(i)
	CALLS	#2, G^MTH$GATAN2	; R0-R1 = GATAN2(arg)
	MOVQ	R0, -(SP)		; Save ATAN2(arg)
	PUSHL	arg(AP)			; Get CABS(arg)
	CALLS	#1, G^MTH$CGABS		; Get CABS(arg)
	JSB	G^MTH$GLOG_R8		; R0-R1 gets LOG(CABS(arg))
	MOVL	result(AP), R2		; Address of result
	MOVQ	R0, (R2)+		; Real part of result
	MOVQ	(SP)+, (R2)		; Imaginary part of result
	RET


	.END
