	.TITLE	FOR$DATE_T_DS	; return system date to FORTRAN
				; character string
	.IDENT	/1-002/		; File: FORDATEDS.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: FORTRAN SYSTEM ROUTINE - FORTRAN LIBRARY
;++
; ABSTRACT:
;	Return system date in the form DD-MMM-YY to a FORTRAN (fixed-len)
;	string.
;
;--
;
; VERSION: 0
;
; HISTORY:
;
; AUTHOR:
;	Jonathan M. Taylor, 23-Aug-77: Version 0
;
; MODIFIED BY:
;
; 0-1	original  JMT 23-Aug-77
; 0-2	squeezed a byte out.  JMT 29-Jan-78
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 0 of FOR$DATE_T_DS
;

; 0-3	- Remove FOR$FLAG_JACKET.  TNH 11-July-78
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Add "_" to the PSECT directive.  JBS 22-DEC-78

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;	oerr.mar

;
; EXTERNAL SYMBOLS:
;	none

;
; MACROS:
;	none

;
; PSECT DECLARATIONS:
	.PSECT	_FOR$CODE	LONG, EXE, NOWRT, PIC, SHR

;
; EQUATED SYMBOLS:
;	none

;
; OWN STORAGE:
;	none


	.SBTTL	FOR$DATE_T_DS - return system date to FORTRAN string

;++
; FUNCTIONAL DESCRIPTION:
;	Use system service ASCTIM to move system date to a local (stack)
;	buffer.  Move the last 2 digits of date on top of 1st 2 -
;	"1977" -> "7777".  Return this date to user string.
;
; CALLING SEQUENCE:
;	CALL	FOR$DATE_T_DS (date_string.wt.ds)
;
;
; INPUT PARAMETERS:
;	date_string is the address of a fixed-len string.
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	NONE
;
;--



	.ENTRY	FOR$DATE_T_DS,	^M<R2,R3,R4,R5>
	SUBL	#12, SP			; make room for date string
	MOVL	SP, R2			; R2 -> room
	MOVL	R2, -(SP)		; build a string descriptor for room
	MOVL	#^X10E000B, -(SP)	; len = 11
	MOVL	SP, R0			; R0 -> src descriptor
	$ASCTIM_S	, (R0)		; R2 -> date string
	MOVW	9(R2), 7(R2)		; mung up year
	MOVQ	@4(AP), R0		; R0/R1 = string descriptor
	MOVC5	#9, (R2), #^A/ /, R0, (R1)
					; move into dest buffer
	RET


	.END
