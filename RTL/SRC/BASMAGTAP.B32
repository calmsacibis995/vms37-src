MODULE BAS$MAGTAPE (				!
		IDENT = '1-003'			! File: BASMAGTAP.B32
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:  BASIC-PLUS-2 Miscellaneous I/O
!
! ABSTRACT:
!
!	This module contains the BASIC MAGTAPE function,
!	Which manipulates non file-structured mag tape files.
!
! ENVIRONMENT:  VAX-11 User Mode
!
! AUTHOR: John Sauter, CREATION DATE: 20-APR-1979
!
! MODIFIED BY:
!
! 1-001	- Original.
! 1-002	- Add a call to BAS$$CB_POP.  JBS 10-JUL-1979
! 1-003	- Set up ISB$A_USER_FP.  JBS 25-JUL-1979
!--

!<BLF/PAGE>

!
! SWITCHES:
!

SWITCHES ADDRESSING_MODE (EXTERNAL = GENERAL, NONEXTERNAL = WORD_RELATIVE);

!
! LINKAGES:
!

REQUIRE 'RTLIN:OTSLNK';				! Define linkages

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    BAS$MAGTAPE;				! Manipulate a mag tape file

!
! INCLUDE FILES:
!

REQUIRE 'RTLML:OTSLUB';				! Get LUB definitions

REQUIRE 'RTLML:OTSISB';				! Get ISB definitions

REQUIRE 'RTLIN:BASIOERR';			! I/O error codes

REQUIRE 'RTLIN:RTLPSECT';			! Macros for defining psects

LIBRARY 'RTLSTARLE';				! System symbols

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!

LITERAL
    BAS$K_REWOFL = 1,				! Rewind and go off-line
    BAS$K_WEOF = 2,				! Write EOF
    BAS$K_REWIND = 3,				! Rewind
    BAS$K_SKIPREC = 4,				! Skip record
    BAS$K_BACKREC = 5,				! Backspace record
    BAS$K_SETDENPAR = 6,			! Set density and parity
    BAS$K_STATUS = 7,				! Tape status
    BAS$K_FILECHAR = 8,				! File characteristics
    BAS$K_REWCLOSE = 9;				! Rewind on CLOSE

!
! PSECTS:
!
DECLARE_PSECTS (BAS);				! Declare psects for BAS$ facility
!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
    BAS$$CB_PUSH : JSB_CB_PUSH NOVALUE,		! Load register CCB
    BAS$$CB_POP : JSB_CB_POP NOVALUE,		! Done with register CCB
    BAS$$STOP : NOVALUE,			! Signal fatal error
    BAS$$STOP_IO : NOVALUE;			! Signal fatal I/O error

!+
! The following are the error codes used in this module.
!-

EXTERNAL LITERAL
    BAS$K_IO_CHANOT : UNSIGNED (8),		! Channel not open.
    BAS$K_ILLMAGUSA : UNSIGNED (8);		! Illegal MAGTAPE() Usage


GLOBAL ROUTINE BAS$MAGTAPE (			! Do a MAGTAPE function
	FUNCTION, 				! Function code, 1 to 9
	PARAM, 					! Additional parameter
	CHAN					! Channel
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Manipulate a non-file structured mag tape.  Nine functions
!	are defined, coded as follows:
!
!	CODE	MEANING
!	----	-------
!
!	1	Rewind and go offline
!
!	2	Write and end-of-file (EOF) mark.
!
!	3	Rewind
!
!	4	Skip record.  The additional argument is the number
!		of records to skip.  The value returned is the number
!		of records not skipped.
!
!	5	Backspace record.  The additional argument is the
!		number of records to backspace.  The value returned
!		is the number of records not backspaced.
!
!	6	Set density and parity.  The additional argument encodes
!		the desired density and parity, see below.
!
!	7	Return magtape status.  The value returned is the
!		status.
!
!	8	Return file characteristics.  The value returned is
!		the file characteristics.
!
!	9	Rewind on close.  When the next CLOSE is done on this
!		channel, the tape will be rewound.
!
! FORMAL PARAMETERS:
!
!	FUNCTION.rl.v	The function to perform, see above.
!	PARAM.rl.v	An additional parameter to some functions,
!			see above.
!	CHAN.rl.v	The channel to do this function to.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	Depends on the function, see above.  Where the result is not
!	specified, 0 is returned.
!
! SIDE EFFECTS:
!
!	Signals if an error is encountered.
!	BAS$$CB_PUSH will signal if the channel number is invalid.
!	We signal BAS$K_IO_CHANOT if the channel is not open.
!
!--

    BEGIN

    BUILTIN
	FP;

    GLOBAL REGISTER
	CCB = K_CCB_REG : REF BLOCK [, BYTE];

    LOCAL
	FMP : REF BLOCK [, BYTE],
	RESULT;

    FMP = .FP;
!+
! Note: this function is provided as a transition aid from BASIC-PLUS.
! It is not intended to be used for new development in BASIC-PLUS-2.
! Therefore, it has been organized as a separate, self-contained
! module, which can be omitted in situations where BASIC-PLUS
! compatability is not a goal.
!-
!+
! Load register CCB from the channel specified.  It had better be
! open.  Channel 0 is not permitted.
!-

    IF (.CHAN EQL 0) THEN BAS$$STOP (BAS$K_ILLMAGUSA);

    BAS$$CB_PUSH (.CHAN, LUB$K_LUN_MIN);
    CCB [ISB$A_USER_FP] = .FMP [SF$L_SAVE_FP];

    IF ( NOT .CCB [LUB$V_OPENED]) THEN BAS$$STOP_IO (BAS$K_IO_CHANOT);

!+
! If no result is specified below, return zero.
!-
    RESULT = 0;
!+
! Dispatch on the function code.
!-

    CASE .FUNCTION FROM BAS$K_REWOFL TO BAS$K_REWCLOSE OF
	SET

	[BAS$K_REWOFL] :
!+
! This function is not implemented.  In VMS, unloading a mag tape
! is the responsibility of the command language interpreter, using
! the DISMOUNT command.
!-
	    BAS$$STOP_IO (BAS$K_ILLMAGUSA);

	[BAS$K_WEOF] :
!+
! This function is not implemented.  To get its effect, CLOSE and
! then OPEN the channel.  Be sure to specify that no rewinding is
! to be done when you OPEN it.
!-
	    BAS$$STOP_IO (BAS$K_ILLMAGUSA);

	[BAS$K_REWIND] :
!+
! This function repositions the file to its first record.  It can
! be used on any file (except unit record and terminal), not just
! mag tape.
!-
	    BEGIN

	    LOCAL
		REWIND_STATUS;

	    REWIND_STATUS = $REWIND (RAB = .CCB);

	    IF ( NOT .REWIND_STATUS)
	    THEN
		BEGIN

		WHILE (.CCB [RAB$L_STS] EQL RMS$_RSA) DO
		    BEGIN
		    $WAIT (RAB = .CCB);
		    $REWIND (RAB = .CCB);
		    END;

		IF (.CCB [RAB$L_STS] NEQ RMS$_BOF) THEN BAS$$STOP_IO (BAS$K_IOERR_REC);

		END;

	    END;

	[BAS$K_SKIPREC] :
!+
! This function is not implemented.  To get its effect, issue
! GETs and ignore the data transfered.
!-
	    BAS$$STOP_IO (BAS$K_ILLMAGUSA);

	[BAS$K_BACKREC] :
!+
! This function is not implemented.  To get its effect, issue the
! REWIND function and then skip forward until reaching the desired
! record.
!-
	    BAS$$STOP_IO (BAS$K_ILLMAGUSA);

	[BAS$K_SETDENPAR] :
!+
! This function is not implemented.  The responsibility for setting
! density and parity in VMS belongs to the command language, which
! provides the MOUNT command for this purpose.
!-
	    BAS$$STOP_IO (BAS$K_ILLMAGUSA);

	[BAS$K_STATUS] :
!+
! This function is not implemented, since the SET function is not.
!-
	    BAS$$STOP_IO (BAS$K_ILLMAGUSA);

	[BAS$K_FILECHAR] :
!+
! This function is not implemented, since the FSP$ function effectively
! replaces it, and, in the absence of most of the other functions
! it is not very useful.
!-
	    BAS$$STOP_IO (BAS$K_ILLMAGUSA);

	[BAS$K_REWCLOSE] :
!+
! This function is not implemented.  To get its effect, after
! CLOSEing the channel, OPEN it again for input, do a REWIND, and CLOSE
! it.  Better, make use of the OPEN options to get this effect.
!-
	    BAS$$STOP_IO (BAS$K_ILLMAGUSA);
	TES;

!+
! We are done with register CCB
!-
    BAS$$CB_POP ();
!+
! We have stored in RESULT any non-zero value to be returned.
!-
    RETURN (.RESULT);
    END;					! of routine BAS$MAGTAPE

END						! of module BAS$MAGTAPE

ELUDOM
