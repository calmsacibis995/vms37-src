	.TITLE	FOR$DATE	; return system date in 9-byte array
	.IDENT	/1-002/		; File: FORDATE.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: FORTRAN SUPPORT LIBRARY
;++
; ABSTRACT:
;
;	Call system service $ASCTIM to return system date to user
;	area.
;
;--
;
; VERSION: 0
;
; HISTORY:
;
; AUTHOR:
;	Jonathan M. Taylor, 18-Aug-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 0 of FOR$DATE
;

; 0-3	- Remove FOR$FLAG_JACKET.  TNH 11-July-78
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Add "_" to the PSECT directive.  JBS 22-DEC-78

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;	oerr.mar

;
; EXTERNAL SYMBOLS:
	.GLOBL	SYS$ASCTIM

;
; MACROS:
;	NONE

;
; PSECT DECLARATIONS:
	.PSECT	_FOR$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
;	NONE

;
; OWN STORAGE:
;	NONE


	.SBTTL	FOR$DATE - return system date as an 9 byte string

;++
; FUNCTIONAL DESCRIPTION:
;	Returns system date as an 9 byte string in the form
;	DD-MMM-YY. Build a work space on the stack to con-
;	tain the date returned by the system (DD-MMM-YYYY).
;	Do a $ASCTIM to get date into this area.  Move the string
;	into the user area, artfully avoiding the first two YY's.
;
; CALLING SEQUENCE:
;	CALL	FOR$DATE (9_byte_array.wb.ra)
;
;
; INPUT PARAMETERS:
;	9_byte_array		the addr of 9 bytes into which the
;				string is to be placed
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	NONE
;
;--


	.ENTRY	FOR$DATE,	^M<R2,R3,R4,R5>
	SUBL	#12, SP			; make space for date string
	MOVL	SP, R2			; R2 -> space
	MOVL	R2, -(SP)		; build a string descriptor
	MOVL	#^X10E000B, -(SP)	; len=11
	MOVL	SP, R0			; R0 -> string descriptor
	$ASCTIM_S	,(R0)		; R2 -> date string
	MOVC3	#7, (R2), @4(AP)	; move in "dd-mmm-"
	CMPB	(R1)+, (R1)+		; skip "19"
	MOVC3	#2, (R1), (R3)		; move in rest of year
	RET				; return to user


	.END
