	.TITLE	MTH$GCOSH 	; G Floating Hyperbolic Cosine routine
				; (GCOSH)
	.IDENT /1-005/		; File: MTHGCOSH.MAR	EDIT: RNH1005
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;
; MTH$GCOSH is a function  which  returns the G floating hyperbolic cosine
; of its G floating point argument. The call is standard
; call-by-reference.
;
;--
;
; VERSION: 1
;
; HISTORY:
; AUTHOR:
;	Steven B. Lionel, 26-Jan-79: Version 1
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY	; Detailed Current Edit History

; Edit History for Version 1 of MTH$GCOSH 
;
; 1-001 - Adapted from MTH$DCOSH.  SBL
; 1-002 - Use MTH$GEXP_R6.  SBL 27-Sept-1979
; 1-003 - Eliminated second call to EXP for input values between .25 and
;	  27*ln2 by computing COSH(x) = (Z + 1/Z)/2, with Z = EXP(|x|).
;	- Eliminated second call to EXP for input values between 27*ln2
;	  and 1023*ln2.
;	- Changed all final floating point divisions by 2 to interger 
;	  subrtracts of 1 from the exponent field.
;	- Changed entry mask to excluded R7 - no longer needed.
;	- Extended maximum range to 1024*ln2.
;	- Chaneged logic for computing EXP(|x|-ln2) to reduce error.
;	- RNH 10-FEB-81
; 1-004 - Changed W^ to G^ in call to MTH$$SIGNAL RNH 09-Sept-1981
; 1-005 - Eliminated symbolic short literals.  RNH 15-Oct-81

	.SBTTL	DECLARATIONS	; Declarative Part of Module

;
; INCLUDE FILES:
;
; EXTERNAL SYMBOLS:
	.DSABL	GBL
	.EXTRN	MTH$$SIGNAL
	.EXTRN	MTH$K_FLOOVEMAT
	.EXTRN	MTH$GEXP_R6
;
; EQUATED SYMBOLS:

	value	= 4			; value.rg.r

;
; MACROS:	none
;


;
; PSECT DECLARATIONS:

	.PSECT	_MTH$CODE	PIC,SHR,LONG,EXE,NOWRT
					; program section for math routines
;
; OWN STORAGE:	none
;
;
; CONSTANTS:
;

G_1023_LOG_2:
	.QUAD	^X7B606E3A28B740A6	; 1023*ln2
G_1024_LOG_2:
	.QUAD	^X39EEFEFA2E4240A6	; 1024*ln2
G_LOG_2_HI:
	.QUAD	^X3C00FEFA2E424006	; (high 43 bits of ln2)+2**-43
G_LOG_2_LO:
	.QUAD	^X4C67361C8654BD50	; ln2 - G_LOG_2_HI
G_2_POWER_27:
	.QUAD	^X00000000000041C0	; 2**27

GCOSHTAB:
	.WORD	^O037262,^O102412
	.WORD	^O063735,^O115572	; DECIMAL: 0.2759648863787355D-06
	.WORD	^O037432,^O000636
	.WORD	^O032545,^O014512	; DECIMAL: 0.2480155975461668D-04
	.WORD	^O037566,^O140554
	.WORD	^O013400,^O037701	; DECIMAL: 0.1388888889781712D-02
	.WORD	^O037705,^O052525
	.WORD	^O052525,^O046771	; DECIMAL: 0.4166666666665359D-01
	.WORD	^O040000,0
	.WORD	0,^O000001			; DECIMAL: 0.5000000000000000D0
	.WORD	^O040020,0
	.WORD	0,0			; DECIMAL: 0.1000000000000000D+01
GCOSHLEN	= .- GCOSHTAB/8


	.SBTTL	MTH$GCOSH  - G Double Precision Floating GCOSH


;++
; FUNCTIONAL DESCRIPTION:
;
; GCOSH  - double precision G floating point function
;
; GCOSH(X) is computed as:
;
;	If |X| < 2**-27, GCOSH(X) = 1.
;	IF 2**-27 =< |X| < 0.25, GCOSH(X) = Chebyshev series
;	If 0.25 =< |X| < 27*ln2, let Z = GEXP(|X|) and set GCOSH(X) = (Z+1/Z)/2
;	If 27*ln2 =< |X| 1023*ln2, then GCOSH(X) = GEXP(|X|)/2.
;	If 1023*ln2 =< |X| < 1024*ln2, then GCOSH(X) = GEXP(|X|-ln2).
;	If 1024*ln2 =< |X|, then overflow.
;
; CALLING SEQUENCE:
;
;	GCOSH.wg.v = MTH$GCOSH(x.rg.r)
;
; INPUT PARAMETERS:

	LONG = 4			; define longword multiplier
	x = 1 * LONG			; Contents of x is the argument

; IMPLICIT INPUTS:	none
;
; OUTPUT PARAMETERS:
;
;	VALUE:	G floating hyperbolic cosine of the argument
;
; IMPLICIT OUTPUTS:	none
;
; COMPLETION CODES:	none
;
; SIDE EFFECTS:
;
; Signal: MTH$_FLOOVEMAT if 1024*ln2 =< |X| with reserved operand in R0/R1
; (copied to the signal mechanism vector CHF$L_MCH_R0/R1 by LIB$SIGNAL).
; Associated message is: "FLOATING OVERFLOW IN MATH LIBRARY". Result is reserved
; operand -0.0 unless a user supplied (or any) error handler changes CHF$L_MCH_R0/R1.
;
; NOTE: This procedure disables floating point underflow, enables integer
; overflow.
;
;---


	.ENTRY	MTH$GCOSH, ^M<IV, R2, R3, R4, R5, R6 >
					; standard call-by-reference entry
					; disable DV (and FU), enable IV
	MTH$FLAG_JACKET			; flag that this is a jacket procedure in
					; case of an error in routine
					; If an error, convert signal to user PC
					; and resignal
	MOVG	@value(AP), R0		; R0/R1 = |X| = @value(AP)
	BICW2	#^X8000, R0		; R0/R1 = |X|
	CMPW	R0, #^X3FF0		; compare |X| with 0.25
	BGEQ	GEQ_TO_0.25		; branch if |X| >= 0.25

;
; |X| < 0.25
;

	CMPW	R0, #^X3E60 		; compare |X| with 2**-27
	BGEQ	GEQ_TO_2M27		; branch if |X| >= 2**-27

;
; |X| < 2**-27
;

	MOVG	#1, R0			; R0/R1 = 1.0
	RET				; return with result = 1.0

;
; 2**-27 =< |X| < 0.25
;

GEQ_TO_2M27:
	MULG2	R0,R0			;Get ARG**2 for POLYG.
	POLYG	R0, #GCOSHLEN-1, GCOSHTAB
					; R0/R1 = SUM(Ci*X**i)
	RET				; return with result in R0

;
; 0.25 =< |X|
;

GEQ_TO_0.25:
	CMPG	R0, G_1023_LOG_2		; compare |X| with 1023*ln2
	BGTR	GTR_THAN_1023_LOG_2		; branch if |X| > 1023*ln2

;
; 0.25 =< |X| =< 1023*ln2
;

	JSB	MTH$GEXP_R6		; R0/R1 = GEXP(|X|)
	CMPG	R0, G_2_POWER_27	; Compare GEXP(|X|) with 2**27, if
	BGTR	ONE_TERM_ONLY		; larger, only one term is needed.
	DIVG3	R0, #1, R2		; R2/R3 = GEXP(-|X|)
	ADDG2	R2, R0			; R0/R1 = GEXP(X) + GEXP(-X)
ONE_TERM_ONLY:
	SUBW	#^X0010, R0		; R0/R1 = (GEXP(X) + GEXP(-X))/2
	RET				; return with result in R0/R1

;
; 1023*ln2 < |X|
;

GTR_THAN_1023_LOG_2:
	CMPG	R0, G_1024_LOG_2	; compare |X| with 1024*ln2
	BGEQ	ERROR			; branch to ERROR if 1024*ln2 =<|X| 


;
; 1023*ln2 =< |X| < 1024*ln2 
;

	SUBG2	G_LOG_2_HI, R0		; R0/R1=|X|-(high 43 bits of ln2+2**-43)
	JSB	MTH$GEXP_R6		; R0/R1=GEXP(|X|-G_LOG_2_HI)
	MULG3	G_LOG_2_LO, R0, R2	; R2/R3=G_LOG_2_LO*GEXP(|X|-G_LOG_2_LO)
	ADDG2	R2, R0			; R0/R1=GEXP(|X|-ln2) 
	RET				; return with result in R0/R1

;
; 1023*ln2 + LOG(2) =< |X|, error
;

ERROR:	MOVZBL	#MTH$K_FLOOVEMAT, -(SP)	; condition value
	ASHQ	#15, #1, R0		; R0 = result = reserved operand -0.0
					; goes to signal mechanism vector
					; (CHF$L_MCH_R0/R1) so error handler
					; can modify the result.
	CALLS	#1, G^MTH$$SIGNAL	; signal error and use real user's PC
					; independent of CALL vs JSB
	RET				; return - R0/R1 restored from CHF$L_MCH_R0/R1



	.END
