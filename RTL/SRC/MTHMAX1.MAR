	.TITLE	MTH$MAX1	IMAX1 JMAX1, and AMAX1 functions
	.IDENT	/1-002/		; File: MTHMAX1

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	This module contains maximum value functions which take
;	floating-point values as arguments.
;
;
;--
;
; VERSION: 0
;
; HISTORY:
;
; AUTHOR:
;	Jonathan M. Taylor, 12-JUL-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 0 of MTHMAX1
;
; 0-4	- Remove MTH$FLAG_JACKET.  TNH 26-July-78
; 0-5	- Fix access violation.  TNH 16-Aug-78
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Add "_" to the PSECT directive.  JBS 22-DEC-78

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;	oerr.mar
;

;
; EXTERNAL SYMBOLS:
;	NONE
;

;
; MACROS:
;	NONE
;

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
;	NONE
;

;
; OWN STORAGE:
;	NONE
;

	.SBTTL	MTH$IMAX1, MTH$JMAX1, MTH$AMAX1

;++
; FUNCTIONAL DESCRIPTION:
;	Call routine MAXF to compute the maximum of n arguments, n is
;	greater than or equal to 1.
;	Return result converted to the proper type.

;
;
; CALLING SEQUENCE:
;	Maximum.ww.v =MTH$IMAX1 ({arg.rf.r})
;	Maximum.wl.v =MTH$JMAX1 ({arg.rf.r})
;	Maximum.wf.v =MTH$AMAX1 ({arg.rf.r})
;	
;
;
; INPUT PARAMETERS:
;	The n parameters are single-precision floating-point values
;	and are call-by-reference.
;	
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	Reserved Operand and Integer Overflow exceptions can occur.
;	
;
;--




	.ENTRY	MTH$IMAX1,	^M<IV>
	BSB	MAXF		; R0 = max arg
	CVTFW	R0, R0		; INTEGER*2 it
	RET

	.ENTRY	MTH$JMAX1,	^M<IV>
	BSB	MAXF		; R0 = max arg
	CVTFL	R0, R0		; INTEGER*4 it
	RET

	.ENTRY	MTH$AMAX1,	^M<>
	BSB	MAXF		; R0 = max arg
	RET

;+
; MAXF returns the largest of the REAL*4 args.
;-

MAXF:
	MOVZBL	(AP), R1		; R1 = number of args
	TSTL	(AP)+			; Ap -> first arg adr
1$:	MOVF	@(AP)+, R0		; get trial max
	BRB	3$			; check arg count
2$:	CMPF	@0(AP),R0		; if this arg is greater then trial max
	BGTR	1$			; then it becomes trial max
	TSTL	(AP)+			; else ignore it
3$:	SOBGTR	R1, 2$			; check arg count
	RSB


	.END
