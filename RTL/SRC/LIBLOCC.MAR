	.TITLE	LIB$LOCC - locate a character in a character string 
	.IDENT	/1-005/		; File: LIBLOCC.MAR Edit:RKR1005

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: General Utility Library
;
; ABSTRACT:
;
;	return the relative position of the matching character or zero.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: Donald G. Petersen, CREATION DATE: 03-Jan-78
;
; MODIFIED BY:
;
;	DGP, 03-Jan-78 : VERSION 00
; 01	- Original 
; 00-02	- DGP 06-Jan-78 - Relative position not found properly
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Add "_" to the PSECT directive.  JBS 21-DEC-78
; 1-003 - Enhance to recognize additional classes of string descriptors
;	  by invoking LIB$AMNALYZE_SDESC_R3 to extract length and
;	  address of 1st data byte of string.  RKR 22-MAY-1981
; 1-004 - Add special-case code to process string descriptors that
;	  "read" like fixed string descriptors.  RKR 7-OCT-1981.
; 1-005 - Redirect jsb's from LIB$ANALYZE_SDESC_R3 to
;	  LIB$ANALYZE_SDESC_R2.  Reorganize register usage so that
;	  R5 is no longer needed and can be dropped from entry mask.
;	  RKR 18-NOV-1981

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES: NONE
;

; EXTERNAL SYMBOLS
	.DSABL	GBL			; Only explicit externals
	.EXTRN	LIB$ANALYZE_SDESC_R2	; Extract length and address of
					; 1st byte of data
;
; MACROS:
;
	$DSCDEF			; fields in a descriptor

;
; EQUATED SYMBOLS: NONE
;

;
; OWN STORAGE: NONE
;

;
; PSECT DECLARATIONS:
;
	.PSECT _LIB$CODE PIC, SHR, LONG, EXE, NOWRT


	.SBTTL	LIB$LOCC  - locate a character 
;++
; FUNCTIONAL DESCRIPTION:
;
;	The character is compared with successive bytes of the string 
;	specified by the string descriptor until an equality is found 
;	or the string is exhausted.
;	The relative position of the unequal byte, or zero is returned.
;	If the string has a zero length then a zero is returned.
;
; CALLING SEQUENCE:
;
;	index.wlu.v = LIB$LOCC (char.rt.dx, string.rt.dx)
;

; INPUT PARAMETERS:
;
	CHAR = 4				; Adr of character desc
	STRING = 8				; Adr of string desc
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
;
;	index.wlu.v
;
; SIDE EFFECTS:
;
;	NONE
;
;--

	.ENTRY LIB$LOCC , ^M<R2, R3, R4>	; Entry point
	MOVL	CHAR(AP), R0		; Address of char string descr.
	CMPB	DSC$B_CLASS(R0), #DSC$K_CLASS_D	; read like fixed ?
	BGTRU	1$			; no
	MOVL	DSC$A_POINTER(R0), R3	; address of char
	BRB	2$			; join common flow

1$:	JSB	G^LIB$ANALYZE_SDESC_R2	; Extract: length->R1, addr->R2
	MOVL	R2, R3			; save addr, length is assumed
					; to be one

2$:	MOVL	STRING(AP), R0		; Address of string descriptor
	CMPB	DSC$B_CLASS(R0), #DSC$K_CLASS_D	; read like fixed ?
	BGTRU	3$			; no
	MOVZWL	DSC$W_LENGTH(R0), R4	; length of string
	MOVL	DSC$A_POINTER(R0), R2	; address of string
	BRB	4$			; join common flow

3$:	JSB	G^LIB$ANALYZE_SDESC_R2	; Extract: length->R1, addr->R2
	MOVZWL	R1, R4			; save length of string

4$:	LOCC	(R3), R4, (R2)		; locate a character
				; State of regs after a LOCC instr.
				; R0 = If found, number of bytes
				;      remaining in string (including
				;      located one),
				;      else 0.
				; R1 = if found, address of byte found,
				;      else address of 1 byte beyond
				;      string.
				;

	BEQL	10$			; branch if none found
	SUBW3	R0, R4, R0		; get relative position
	INCL	R0			; one origin
10$:	RET
	.END
