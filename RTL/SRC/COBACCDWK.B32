MODULE COB$ACC_DAYWEEK (
		IDENT = '1-006'		! file: COBACCDWK.B32 EDIT:MLJ1006
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	COBOL SUPPORT
!
! ABSTRACT
!
!
!
! ENVIRONMENT:	Vax-11 User Mode
!
! AUTHOR: MLJ , CREATION DATE: 16-JAN-1979
!
! MODIFIED BY:
!
! 1-001 - Original. MLJ 16-JAN-1979
! 1-002 - Added boilerplate and comments. RKR 18-JULY-1979
! 1-003 - Declare psects via library macro.  RKR 23-AUG-1979
! 1-004 - Change symbolic name of LIBRARY. RKR 1-OCT-79
! 1-005 - Cosmetic changes.   RKR 18-OCT-79
! 1-006 - Rewrite to use simplified algorithm.  MLJ 02-Aug-81
!
!--

!<BLF/PAGE>

!
! SWITCHES
!

SWITCHES ADDRESSING_MODE (EXTERNAL = GENERAL, NONEXTERNAL = WORD_RELATIVE);

!
! LINKAGES
!
!	NONE
!
! TABLE OF CONTENTS:
!
FORWARD ROUTINE

	COB$ACC_DAYWEEK : NOVALUE;

! INCLUDE FILES
!

REQUIRE 'RTLIN:RTLPSECT' ;		! Macros for declaring psects
LIBRARY 'RTLSTARLE';

!
! MACROS
!
!	NONE
!
! EQUATED SYMBOLS
!
!	NONE
!
! PSECT DECLARATIONS:
!
DECLARE_PSECTS (COB) ;			! Psects for COB$ facility


GLOBAL ROUTINE COB$ACC_DAYWEEK(DST): NOVALUE=
!
!++	FUNCTIONAL DESCRIPTION
!
!	Returns day_of_week as 1 - 7 corresponding to Mon. - Sun.
!
! FORMAL PARAMETERS:
!
!	DST.wt.ds	Address of descriptor of string to receive ans.
!
! IMPLICIT INPUTS:
!
!	Date as supplied by $GETTIM.
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--
!
    BEGIN
    MAP
	DST:		REF BLOCK[,BYTE];	! Pointer to destination descriptor
    LOCAL
	SYSTIM:		VECTOR[2],		! Buffer for $GETTIM
	QUOTIENT,				! Quotient from division
	REMAINDER,				! Remainder (discarded)
	BUFFER;					! Buffer for output character
    BUILTIN
	EDIV;


    !+
    ! Get the system date and time.  Divide by the number of least significant
    ! bits in a day (864 x 10**9) to get the number of days since 17-Nov-1858.
    ! Bias this result to account for day 0 being a Wednesday.  Then, take the
    ! result modulo 7 to get the day of the week such that Monday results in 0.
    ! Finally, bias the result by ASCII '1' to get the answer as desired and
    ! return it.
    !-
    $GETTIM(TIMADR=SYSTIM);
    EDIV(%REF(1000000000), SYSTIM, QUOTIENT, REMAINDER);	! Div by 10**9
    QUOTIENT = .QUOTIENT / 864;					! Finish
    BUFFER = ((.QUOTIENT + 2) MOD 7) + %C'1';
    CH$COPY(1, BUFFER, %C' ', .DST[DSC$W_LENGTH], .DST[DSC$A_POINTER]);
    END;

END ELUDOM
