	.TITLE	MTH$GMOD
	.IDENT	/1-002/		; File: MTHDMOD.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	This module contains routine MTH$GMOD:
;	Returns the value:
;		arg1 - (int(arg1/arg2))*arg2
;
;
;--
;
; AUTHOR:
;	Steven B. Lionel, 5-Feb-79: Version 1
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for MTH$GMOD
;
; 1-001 - Adapted from MTH$DMOD version 1-002.  SBL 5-Feb-1979
; 1-002 - Add check of quotient exponent for loss of significance.
;	  SPR 11-23605  SBL 18-Sept-1979

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;
;

;
; EXTERNAL SYMBOLS:
;
	.DSABL	GBL
	.EXTRN	MTH$K_SIGLOSMAT		; Significance lost error
	.EXTRN	MTH$$SIGNAL		; Signal Math error
;
; MACROS:
;	NONE
;

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
;
	K_BIAS		= 1024		; Exponent bias for G_floating
	K_PRECISION	= 53		; Fraction precision in bits
;
; OWN STORAGE:
;	NONE
;

	.SBTTL	MTH$GMOD - G REAL*8 remainder

;++
; FUNCTIONAL DESCRIPTION:
;	Return the remainder of arg1/arg2 in floating format
;	Remainder = arg1 - (int(arg1/arg2))*arg2
;
;
; CALLING SEQUENCE:
;	Remainder.wg.v =MTH$GMOD (dividend.rg.r, divisor.rg.r)
;	
;
;
; INPUT PARAMETERS:
;
	dividend	= 4		; Dividend argument
	divisor		= 8		; Divisor argument
;	
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	MTH$_SIGLOSMAT - Significance loss in math library.  If the
;	quotient of arg1/arg2 is greater in magnitude than the
;	precision of the datatype. (2**52 for G_floating)
;	If this occurs, the default result is the reserved operand.
;	SS$_FLTOVF - Floating overflow
;	SS$_ROPRAND - Reserved operand
;	
;--



	.ENTRY	MTH$GMOD,	^M<R2,R3>
	DIVG3	@divisor(AP), @dividend(AP), R0	; R0-R1 = arg1/arg2
;
;   Check the quotient to see if we have lost any significant bits.
;   If we have, signal error MTH$_SIGLOSMAT.
;
	CMPZV	#4, #11, R0, #<K_BIAS+K_PRECISION>
	BGEQ	ERROR			; If so, its an error
	EMODG	#1, #0, R0, R2, R2	; R2-R3 = fract(arg1/arg2)
	SUBG2	R2, R0			; R0-R1 = int(arg1/arg2)			
	MULG2	@divisor(AP), R0	; R0-R1 = int(arg1/arg2)*arg2
	SUBG3	R0, @dividend(AP), R0	; R0-R1 = arg1 - int(arg1/arg2)*arg2
	RET

;+
; Come here if significance has been lost.
;-
ERROR:
	ASHQ	#1, #15, R0		; R0-R1 = reserved operand
	MOVZBL	#MTH$K_SIGLOSMAT, -(SP)	; Error code
	CALLS	#1, G^MTH$$SIGNAL	; Signal the error
	RET				; Return to caller


	.END
