	.TITLE	OTS$POWGG - G REAL*8 ** G REAL*8 power routine
	.IDENT	/1-005/		; File: OTSPOWGG.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: Language support library - user callable
;++
; ABSTRACT:
;
;	  G double base to G double power.
;
;	Floating overflow can occur
;	Undefined exponentiation can occur if:
;		1)  Negative base
;		2)  0 base and power is 0 or negative.
;
;
;
;--
;
; VERSION: 1
;
; HISTORY:
;
; AUTHOR:
;	Steven B. Lionel, 06-Feb-79: Version 1
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed current edit history


; Edit history for Version 1 of OTS$POWGG
;
; 1-001 - Adapted from OTS$POWDD version 1-001.  SBL 06-Feb-79
; 1-002	- Declare externals.  SBL 17-MAY-1979
; 1-003	- Use 32-bits to reference externals.  JBS 20-JUN-1979
; 1-004 - Use MTH$GEXP_R6.  SBL 27-Sept-1979
; 1-005 - Changed shared external references to G^ RNH 25-Sep-81

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;

;
; EXTERNAL SYMBOLS:
;

	.DSABL	GBL
	.EXTRN	MTH$K_UNDEXP
	.EXTRN	MTH$$SIGNAL		; Math error routine
	.EXTRN	MTH$GLOG_R8		; Double logarithm
	.EXTRN	MTH$GEXP_R6		; Double exponential

;
; MACROS:
;

;
; EQUATED SYMBOLS:
;
	base	= 4			; base input formal - by-value
	exp	= 12			; exponent input formal - by-value
	fexp	= 8			; exponent when base is floating
					; by-value

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT	_OTS$CODE	PIC,SHR,LONG,EXE,NOWRT
					; program section for OTS$ code

	.SBTTL	OTS$POWGG - G REAL*8 to G REAL*8 giving G REAL*8 result

;++
; FUNCTIONAL DESCRIPTION:
;
;	OTS$POWGG - G REAL*8 result = G REAL*8 base ** G REAL*8 exponent
;
;	The G REAL*8 result is given by:
;
;	base	exponent	result
;	----	--------	------
;
;	= 0	> 0		0.0
;	= 0	= 0		Undefined Exponentiation
;	= 0	< 0		Undefined Exponentiation
;
;	< 0	any		Undefined Exponentiation
;
;	> 0	> 0		EXP(exp * LOG(base))
;	> 0	= 0		1.0
;	> 0	< 0		EXP(exp * LOG(base))
;
;
;	Floating Overflow can occur.
;	Undefined Exponentiation can occur if:
;		1)  base is 0 and exponent is 0 or negative
;		2)  base is negative
;
; CALLING SEQUENCE:
;
;	power.wg.v = OTS$POWGG (base.rg.v, exponent.rg.v)
;
; INPUT PARAMETERS:
;	base and exponent parameters are call by value
;
; IMPLICIT INPUTS:
;	none
;
; OUTPUT PARAMETERS:
;	none
;
; IMPLICIT OUTPUTS:
;	none
;
; FUNCTIONAL VALUE:
;	OTS$POWGG - G REAL*8 base ** G REAL*8 power
;
; SIDE EFFECTS:
;
;	SIGNALs SS$_ARITH with floating overflow hardware code if
;	floating overflow.
;	SIGNALs MTH$_UNDEXP (82 = ' UNDEFINED EXPONENTIATION') if
;		1)  base is 0 and exponent is 0 or negative
;		2)  base is negative
;
;
;--

	.ENTRY	OTS$POWGG, ^M<IV,R2,R3,R4,R5,R6,R7,R8> ; enable integer overflow
	MTH$FLAG_JACKET			; establish math error handler
	MOVG	exp(AP), R2		; R2/R3 = G exponent
	MOVG	base(AP), R0		; R0/R1 = G base
	BEQL	ZERO			; special case 0 ** n
	BLSS	ERR			; negative base is an error
	TSTG	R2			; test the exponent
	BEQL	ONE			; special case n ** 0
	MOVQ	R2, -(SP)		; save exponent (R2/R3) across call
	JSB	G^MTH$GLOG_R8		; R0/R1 = LOG(base)
	MULG2	(SP)+, R0		; R0/R1 = exp * LOG(base)
	JSB	G^MTH$GEXP_R6		; R0/R1 = EXP(exp * LOG(base))
	RET				; function value returned in R0/R1

ONE:
	MOVG	#1, R0			; answer is one
	RET				; returned in R0/R1

ZERO:
	CLRQ	R0			; answer is zero
	TSTG	R2			; test the exponent (R2/R3)
	BGTR	RETURN			; return if positive exponent
ERR:
	ASHQ	#15, #1, R0		; R0/R1 = undefined operand
	MOVZBL	#MTH$K_UNDEXP, -(SP)	; Math library error number
	CALLS	#1, G^MTH$$SIGNAL	; convert to 32-bit condition code
					; and SIGNAL MTH$UNDEXP
RETURN:
	RET				; return function value in R0/R1

	.END
