	.TITLE	MTH$CGABS	G COMPLEX*16 Absolute value
	.IDENT	/1-001/		; File: MTHCGABS.MAR

;
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	Return the absolute value of the G COMPLEX*16 value.
;
;
;--
;
; VERSION: 1
;
; HISTORY:
;
; AUTHOR:
;	Steven B. Lionel, 20-July-1979
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History
;
; 1-001 - Adapted from MTH$CABS version 1-002.  SBL 20-July-1979

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;

;
; EXTERNAL SYMBOLS:
;
	.DSABL	GBL
	.EXTRN	MTH$GSQRT_R5

;
; MACROS:
;

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;	NONE

	.SBTTL	MTH$CGABS - G COMPLEX*16 Absolute Value

;++
; FUNCTIONAL DESCRIPTION:
;
;	MTH$CGABS computes the absolute value of a COMPLEX number (r, i)
;	as follows:
;
;	result = ABS(MAX*SQRT((MIN/MAX)**2 + 1))
;
; CALLING SEQUENCE:
;	result.wd.v = MTH$CGABS (arg.rgc.r)
;
;
; INPUT PARAMETERS:
;
	arg = 4			; The address of the G COMPLEX*16 argument.
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
;
; FUNCTION VALUE:
;
;	The G floating absolute value is returned in R0-R1.
;
; SIDE EFFECTS:
;	Signals:	Invalid Operand if r or i are undefined (-0.0).
;			Floating overflow if r and i are both large.
;--


	.ENTRY	MTH$CGABS,	^M<R2,R3,R4,R5>
	MTH$FLAG_JACKET			; resignal
	MOVL	arg(AP), R4		; Get address of argument
	MOVQ	(R4)+, R0		; Get real part
	MOVQ	(R4), R2		; Get imaginary part
	EXTZV	#4, #11, R0, R5		; Get exponent of real part
	CMPZV	#4, #11, R2, R5		; Is imaginary part bigger?
	BGEQ	REALLO			; Yes, that is correct
	MOVQ	R0, R4			; Swap values
	MOVQ	R2, R0
	MOVQ	R4, R2

REALLO:

; at this point R0-R1 contains MIN (the smaller of |r| and |i|), and
; R2-R3 contains MAX (the larger of |r| and |i|).
;
	TSTG	R2			; is divisor zero?
	BEQL	ZERO			; yes, answer is zero
	DIVG2	R2, R0			; R0-R1 = MIN/MAX
	MULG2	R0, R0			; R0-R1 = (MIN/MAX)**2
	ADDG2	#1, R0			; R0-R1 = (MIN/MAX)**2 + 1
	MOVQ	R2, -(SP)		; Save maximum
	JSB	MTH$GSQRT_R5		; R0-R1 = SQRT((MIN/MAX)**2+1)
	MULG2	(SP), R0		; R0-R1= MAX*SQRT((MIN/MAX)**2+1)
					; Floating overflow could happen
	BICW	#^X8000, R0		; R0-R1= ABS(....)
	RET				; With result in R0-R1

ZERO:
	CLRQ	R0			; result is zero
	RET



	.END
