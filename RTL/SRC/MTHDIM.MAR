	.TITLE	MTH$DIM		possitive difference functions
	.IDENT	/1-002/		; File: MTHDIM.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	This module contains the positive difference routines.
;	Positive difference (a,b) is defined to be MAX(a-b,0)
;
;
;--
;
; VERSION: 1
;
; HISTORY:
;
; AUTHOR:
;	Jonathan M. Taylor, 14-JUL-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 1 of MTH$DIM
;
; 0-4	- Remove MTH$FLAG_JACKET.  TNH 5-July-78
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002 - Add MTH$GDIM, MTH$HDIM.  SBL 18-Jan-79

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;	NONE
;

;
; EXTERNAL SYMBOLS:
;	NONE
;

;
; MACROS:
;	NONE
;

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
;	NONE
;

;
; OWN STORAGE:
;	NONE
;

	.SBTTL	MTH$IIDIM

;++
; FUNCTIONAL DESCRIPTION:
;	Returns, as routine value, the positive difference of two
;	INTEGER*2 arguments.
;
;
; CALLING SEQUENCE:
;	Positive_difference.ww.v = MTH$IIDIM (a1.rw.r, a2.rw.r)
;	
;
;
; INPUT PARAMETERS:
;	a1          adr of INTEGER*2 value
;	a2          adr of INTEGER*2 value
;	
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	SIGNALS Integer Overflow if  a1-a2  overflows.
;	
;
;--




	.ENTRY	MTH$IIDIM,	^M<IV>	; enable integer overflow
	SUBW3	@8(AP), @4(AP), R0	; R0 = difference of args
	BGEQ	1$			; return if non-negative
	CLRW	R0			; else return 0
1$:	RET

	.SBTTL	MTH$JIDIM

;++
; FUNCTIONAL DESCRIPTION:
;	Returns, as routine value, the positive difference of two
;	INTEGER*4 arguments.
;
;
; CALLING SEQUENCE:
;	Positive_difference.wl.v = MTH$JIDIM (a1.rl.r, a2.rl.r)
;	
;
;
; INPUT PARAMETERS:
;	a1          adr of INTEGER*4 value
;	a2          adr of INTEGER*4 value
;	
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	SIGNALS Integer Overflow if  a1-a2  overflows.
;	
;
;--


	.ENTRY	MTH$JIDIM,	^M<IV>	; enable integer overflow
	SUBL3	@8(AP), @4(AP), R0	; R0 = difference of args
	BGEQ	1$			; return if non-negative
	CLRL	R0			; else return 0
1$:	RET

	.SBTTL	MTH$DIM

;++
; FUNCTIONAL DESCRIPTION:
;	Returns, as routine value, the positive difference of two
;	REAL*4 arguments.
;
;
; CALLING SEQUENCE:
;	Positive_difference.wf.v = MTH$DIM (a1.rf.r, a2.rf.r)
;	
;
;
; INPUT PARAMETERS:
;	a1          adr of a single-precision floating-point value
;	a2          adr of a single-precision floating-point value
;	
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	Floating Overflow, Floating Underflow, and Reserved Operand
;	exceptions can occur.
;	
;
;--


	.ENTRY	MTH$DIM,	^M<>
	SUBF3	@8(AP), @4(AP), R0	; R0 = difference of args
	BGEQ	1$			; return if non-negative
	CLRL	R0			; else return 0
1$:	RET

	.SBTTL	MTH$DDIM

;++
; FUNCTIONAL DESCRIPTION:
;	Returns as routine value, the positive difference of two
;	REAL*8 numbers.
;
;
; CALLING SEQUENCE:
;	Positive_difference.wd.v = MTH$DDIM (a1.rd.r, a2.rd.r)
;	
;
;
; INPUT PARAMETERS:
;	a1          adr of a double-precision floating-point value
;	a2          adr of a double-precision floating-point value
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	Floating Overflow, Floating Underflow, and Reserved Operand
;	exceptions can occur.
;	
;
;--

	.ENTRY	MTH$DDIM, 	^M<>
	SUBD3	@8(AP), @4(AP), R0	; R0 = difference of args
	BGEQ	1$			; return if non-negative
	CLRQ	R0			; else return 0
1$:	RET



	.SBTTL	MTH$GDIM

;++
; FUNCTIONAL DESCRIPTION:
;	Returns as routine value, the positive difference of two
;	G floating numbers.
;
;
; CALLING SEQUENCE:
;
;	Positive_difference.wg.v = MTH$GDIM (a1.rg.r, a2.rg.r)
;
;
; INPUT PARAMETERS:
;
;	a1	- address of a G floating value
;	a2	- address of a G floating value
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	Floating Overflow, Floating Underflow and Reserved Operand
;	exceptions can occur.
;
;
;--

	.ENTRY	MTH$GDIM, ^M<>
	SUBG3	@8(AP), @4(AP), R0	; R0 = difference of args
	BGEQ	1$			; return if non-negative
	CLRQ	R0
1$:	RET				; return

	.SBTTL	MTH$HDIM

;++
; FUNCTIONAL DESCRIPTION:
;	Returns the positive difference of two H floating numbers.
;	Because an H floating number can not be represented in
;	64 bits, it is returned to the first argument; the input
;	parameters being shifted to the second and third arguments,
;	as per system convention.
;
;
; CALLING SEQUENCE:
;
;	CALL MTH$HDIM (Positive_difference.wh.r, a1.rh.r, a2.rh.r)
;
;
; INPUT PARAMETERS:
;
;	a1	- address of a H floating value
;	a2	- address of a H floating value
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	Positive_difference	- H floating result by reference
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	Floating Overflow, Floating Underflow and Reserved Operand
;	exceptions can occur.
;
;
;--

	.ENTRY	MTH$HDIM, ^M<>
	SUBH3	@12(AP), @8(AP), @4(AP)	; @4(AP) = difference of args
	BGEQ	1$			; return if non-negative
	CLRH	@4(AP)
1$:	RET				; return


	.END
