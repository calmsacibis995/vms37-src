	.TITLE	MTH$DASIN 	; Double Precision Floating Point Sine routine
				; (DASIN,DASIND)
	.IDENT /1-006/		; File: MTHDASIN.MAR  Edit: RNH1007
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;
; MTH$DASIN is a function which returns the double precision floating point
; arcsine in radians of its double precision floating point argument. The call 
; is standard call-by-reference.
;
; MTH$DASIND is a function which returns the double precision floating point
; arcsine in radians of its double precision floating point argument. The call 
; is standard call-by-reference.
;
;--
;
; VERSION: 01
;
; HISTORY:
; AUTHOR:
;	Peter Yuo, 29-Jun-77: Version 01
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY	; Detailed Current Edit History


; ALGORITHMIC DIFFERENCES FROM FP-11/C ROUTINE:	none
;
; Edit History for Version 01 of MTH$DASIN 
;
; 0-2	MTH$$ERROR changed to MTH$$SIGNAL.
;	MTH$_... changed to MTH__....
;	Changed error handling mechanism. Put error result in R0:R1 before
;	calling MTH$$SIGNAL in order to allow user modify error result.
; 0-3	Fix bug involving wrong addressing mode.  JMT 9-Nov-77
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Change MTH__INVARG to MTH$K_INVARGMAT.  JBS 07-DEC-78
; 1-003	- Add "_" to the PSECT directive.  JBS 26-DEC-78
; 1-004 - Declare externals.  SBL 17-May-1979
; 1-005 - Change JSB entry to MTH$DASIN_R7.  RBG 28-Sept-1979
; 1-006 - Added degree entry points.  RNH 29-MAR-1981

	.SBTTL	DECLARATIONS	; Declarative Part of Module

;
; INCLUDE FILES:	MTHJACKET.MAR
;

;
; EXTERNAL SYMBOLS:
;
	.DSABL	GBL
	.EXTRN	MTH$DSQRT_R5
	.EXTRN	MTH$DATAN_R7
	.EXTRN	MTH$DATAND_R7
	.EXTRN	MTH$K_INVARGMAT
	.EXTRN	MTH$$SIGNAL

;
; EQUATED SYMBOLS:

	SD_1.0	= ^F1.0			; 1.0
	value	= 4			; value.rd.r

;
; MACROS:	none
;
; PSECT DECLARATIONS:

	.PSECT	_MTH$CODE	PIC,SHR,LONG,EXE,NOWRT
					; program section for math routines
;
; OWN STORAGE:	none
;
;
; CONSTANTS:
;

D_PI_OVER_2:
	.WORD	^O040311, ^O007732, ^O121041, ^O064302
					; PI/2
D_PI:
	.WORD	^O040511, ^O007732, ^O121041, ^O064302
					; PI
D_90:
	.LONG	^X00043B4, ^X0		; 90
D_180:
	.LONG	^X0004434, ^X0		; 180


	.SBTTL	MTH$DASIN  - Standard Double Precision Floating DASIN


;++
; FUNCTIONAL DESCRIPTION:
;
; DASIN  - double precision floating point function
;
; DASIN(X) is computed as:
;
;	If X = 0, then DASIN(X) = 0.
;	If X = 1, then DASIN(X) = PI/2.
;	If X = -1, then DASIN(X) = -PI/2.
;	If 0 < |X| < 1, then DASIN(X) = ATAN(X/SQRT(1-X**2)).
;	If 1 < |X|, error.
;
; CALLING SEQUENCE:
;
;	DASIN.wd.v = MTH$DASIN(x.rd.r)
;
; INPUT PARAMETERS:

	LONG = 4			; define longword multiplier
	x = 1 * LONG			; Contents of x is the argument

; IMPLICIT INPUTS:	none
;
; OUTPUT PARAMETERS:
;
;	VALUE:	double precision floating arcsine of the argument
;
; IMPLICIT OUTPUTS:	none
;
; COMPLETION CODES:	none
;
; SIDE EFFECTS:
;
; Signals: MTH$_INVARG if |X| > 1 with reserved operand in R0/R1
; (copied to the signal mechanism vector CHF$L_MCH_R0/R1 by LIB$SIGNAL).
; Associated message is: "INVALID ARGUMENT". Result is reserved
; operand -0.0 unless a user supplied (or any) error handler changes CHF$L_MCH_R0/R1.
;
; NOTE: This procedure disables floating point underflow, enables integer
; overflow.
;
;---


	.ENTRY	MTH$DASIN, ^M<IV, R2, R3, R4, R5, R6, R7>
					; standard call-by-reference entry
					; disable DV (and FU), enable IV
	MTH$FLAG_JACKET			; flag that this is a jacket procedure in
					; case of an error in routine
					; If an error, convert signal to user PC
					; and resignal
	MOVD	@value(AP), R0		; R0/R1 = |X| = @value(AP)
	BSBB	MTH$DASIN_R7		; call special DASIN routine
	RET				; return with result in R0/R1


	.SBTTL	MTH$DASIN_R7 - Special DASIN routine

; Special DASIN - used by the standard routine and direct JSB call.
;
; CALLING SEQUENCE:
;	save anything needed in R0:R7
;	MOVD	..., R0			; input in R0/R1
;	JSB	MTH$DASIN_R7
;	RSB				; return with result in R0/R1
;

MTH$DASIN_R7::				; special DASIN routine
MTH$DASIN_R9::				; Release 1 name
	MOVD	R0, R6			; save X in R6/R7
	BEQL	RETURN			; return DASIN(0) = 0

;
; 0 < |X|
;

	BICW	#^X8000, R0		; R0/R1 = |X|
	CMPD	R0, S^#SD_1.0		; compare |X| with 1.0
	BGEQ	GEQ_TO_1.0		; branch if |X| >= 1.0

;
; 0 < |X| < 1.0
;

	MULD2	R0, R0			; R0/R1 = X**2
	SUBD3	R0, S^#SD_1.0, R0	; R0/R1 = 1.0 - X**2
	JSB	MTH$DSQRT_R5		; R0/R1 = DSQRT(1-X**2)
	DIVD3	R0, R6, R0		; R0/R1 = X/DSQRT(1-X**2)
	JMP	MTH$DATAN_R7		; R0/R1 = DATAN(X/DSQRT(1-X**2))


;
; 1 =< |X| 
;

GEQ_TO_1.0:
	BGTR	ERROR			; branch to ERROR if |X| > 1.0


;
; |X| = 1.0
;

	MOVD	D_PI_OVER_2, R0		; R0/R1 = PI/2
	TSTD	R6			; test the sign of X
	BGEQ	RETURN			; branch if X > 0
	MNEGD	R0, R0			; R0/R1 = -PI/2
RETURN:	RSB				; return with result in R0/R1

;
; 1 < |X|, error
;

ERROR:	PUSHL	(SP)			; return PC from JSB routine
	MOVZBL	#MTH$K_INVARGMAT, -(SP)	; condition value
	ASHQ	#15, #1, R0		; R0 = result = reserved operand -0.0
					; goes to signal mechanism vector
					; (CHF$L_MCH_R0/R1) so error handler
					; can modify the result.
	CALLS	#2, G^MTH$$SIGNAL	; signal error and use real user's PC
					; independent of CALL vs JSB
	RSB				; return - R0 restored from CHF$L_MCH_R0/R1



	.SBTTL	MTH$DASIND  - Standard Double Precision Floating DASIND


;++
; FUNCTIONAL DESCRIPTION:
;
; DASIND  - double precision floating point function
;
; DASIND(X) is computed as:
;
;	If X = 0, then DASIND(X) = 0.
;	If X = 1, then DASIND(X) = 90.
;	If X = -1, then DASIND(X) = -90.
;	If 0 < |X| < 1, then DASIND(X) = ATAND(X/SQRT(1-X**2)).
;	If 1 < |X|, error.
;
; CALLING SEQUENCE:
;
;	dasind.wd.v = MTH$DASIND(x.rd.r)
;
; INPUT PARAMETERS:

	LONG = 4			; define longword multiplier
	x = 1 * LONG			; Contents of x is the argument

; IMPLICIT INPUTS:	none
;
; OUTPUT PARAMETERS:
;
;	VALUE:	double precision floating arcsine of the argument
;
; IMPLICIT OUTPUTS:	none
;
; COMPLETION CODES:	none
;
; SIDE EFFECTS:
;
; Signals: MTH$_INVARG if |X| > 1 with reserved operand in R0/R1
; (co180ed to the signal mechanism vector CHF$L_MCH_R0/R1 by LIB$SIGNAL).
; Associated message is: "INVALID ARGUMENT". Result is reserved
; operand -0.0 unless a user supplied (or any) error handler changes CHF$L_MCH_R0/R1.
;
; NOTE: This procedure disables floating point underflow, enables integer
; overflow.
;
;---


	.ENTRY	MTH$DASIND, ^M<IV, R2, R3, R4, R5, R6, R7>
					; standard call-by-reference entry
					; disable DV (and FU), enable IV
	MTH$FLAG_JACKET			; flag that this is a jacket procedure in
					; case of an error in routine
					; If an error, convert signal to user PC
					; and resignal
	MOVD	@value(AP), R0		; R0/R1 = |X| = @value(AP)
	BSBB	MTH$DASIND_R7		; call special DASIND routine
	RET				; Return with result in R0/R1


	.SBTTL	MTH$DASIND_R7 - Special DASIND routine

; Special DASIND - used by the standard routine and direct JSB call.
;
; CALLING SEQUENCE:
;	save anything needed in R0:R7
;	MOVD	..., R0			; input in R0/R1
;	JSB	MTH$DASIND_R7
;	RSB				; Return with result in R0/R1
;

MTH$DASIND_R7::				; special DASIND routine
	MOVD	R0, R6			; save X in R6/R7
	BEQL	D_RETURN		; Return DASIND(0) = 0

;
; 0 < |X|
;

	BICW	#^X8000, R0		; R0/R1 = |X|
	CMPD	R0, S^#SD_1.0		; compare |X| with 1.0
	BGEQ	D_GEQ_TO_1.0		; branch if |X| >= 1.0

;
; 0 < |X| < 1.0
;

	MULD2	R0, R0			; R0/R1 = X**2
	SUBD3	R0, S^#SD_1.0, R0	; R0/R1 = 1.0 - X**2
	JSB	MTH$DSQRT_R5		; R0/R1 = DSQRT(1-X**2)
	DIVD3	R0, R6, R0		; R0/R1 = X/DSQRT(1-X**2)
	JMP	MTH$DATAND_R7		; R0/R1 = DATAND(X/DSQRT(1-X**2))


;
; 1 =< |X| 
;

D_GEQ_TO_1.0:
	BGTR	ERROR			; branch to ERROR if |X| > 1.0


;
; |X| = 1.0
;

	MOVD	D_90, R0		; R0/R1 = 90
	TSTD	R6			; test the sign of X
	BGEQ	D_RETURN		; branch if X > 0
	MNEGD	R0, R0			; R0/R1 = -90
D_RETURN:
	RSB				; Return with result in R0/R1


	.END
