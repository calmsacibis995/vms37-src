MODULE FOR$$FMT_INTRP (			! Fortran Format Statement Interpreter
		IDENT = '2-035'		! File: FORFMTINT.B32  Edit: JAW2035
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
! FACILITY: FORTRAN
!
! ABSTRACT:
!
!	This module interprets FORTRAN format statements
!	which have been pre-compiled into an encoded form by either the
!	FORTRAN compiler or the run-time format compiler,
!	FOR$$FMT_COMPIL.  It is independent of READ and WRITE semantics
!	and is executed at both the READ Formatted and WRITE Formatted
!	User Data Formatters (UDF) level of abstraction.
!
! AUTHOR: Peter Yuo, CREATION DATE: 17-Feb-77
!
! MODIFIED BY:
!	Peter Yuo, 25-Feb-77, Version 1
! 01	- 	Original
!
!	Richard Grove, 19-Aug-77, Version 2
! 2-01	-	Restructure decision logic to use attributes table and CASE
! 2-05	- I elem size > 4, use 4 defaults - TNH
! 2-07	- Use FOR$K_abcmnoxyz as EXTERNAL LITERALs.  TNH 27-Oct-77
! 2-08	- Use FERR.  TNH 16-Dec-77
! 2-09	- Take out external reference to FOR$SIGNAL.  JMT 14-Jan-78
! 2-10	- Global register CCB.  JMT 8-Apr-78
! 2-11	- Change REQUIRE files for VAX system build.  DGP 28-Apr-78
! 2-12	- Make JSB linkages.  TNH 21-May-78
! 2-13	- Init ISB$W_FMT_REP, ISB$B_FMT_P.  TNH 22-May-78
! 2-14	- Format stack as word array, VFE error check.  TNH 2-Jun-78
! 2-17	- PIC dispatch tables.  TNH 6-June-78.
!
! 2-18	- Move format codes to FFMT.B32.  TNH 6-June-78
! 2-19	- Change the file name to FORFMTINT.B32, and change the
!	   names of the REQUIRE files similarly.  For example,
!	   FFMT.B32 is changed to FORFMT.  JBS 14-NOV-78.
! 2-020	- Update the copyright notice.  JBS 16-NOV-78
! 2-021	- Change the REQUIRE file names from FOR... to OTS...  JBS 06-DEC-78
! 2-022 - Add actions for new FORTRAN-77 format codes.  SBL 09-Feb-1979
! 2-023	- Correct a typographical error in edit 022.  JBS 09-FEB-1979
! 2-024 - Change FI_ACT to a word so that the new format codes will
!	  work, plus add room for expansion.  SBL 12-Mar-1979
! 2-025 - Add check for data transmitter seen to help FOR$$UDF_WF1 fix
!	  VFE bug reported in SPR 21588.  Change LUB$V_BLANKS to
!	  LUB$V_NULLBLNK.  SBL 12-Apr-79
! 2-026 - Add FI_STOP to FI_ACT table, for help with 2-025.  SBL 17-Apr-79
! 2-027 - Add forgotten x42.33E3 for REAL*16.  SBL 28-Jun-1979
! 2-028 - FORMAT_ADR no longer passed.  FMT_BEG set up by FOR$$IO_BEG.  SBL 5-Dec-1979
! 2-029 - Speed optimizations.  Do T, TR, TL here instead of UDF.  SBL 13-Dec-1979
! 2-030 - Fix select problem with default floating formats.  SBL 18-Dec-1979
! 2-031 - Code improvements.  Use more reasonable default O and Z sizes.
!	  SBL 4-Jan-1980
! 2-032 - Add defaults for O and Z format width when value is not 1, 2, 4, 8 or
!	  16 bytes.  SBL 29-Dec-1980
! 2-033- Improved fix for 2-032, courtesy of Joel CLinkenbeard.  SBL 8-Jan-1981
! 2-034 - Convert FOR$$FMT_INTRP1 to JSB linkage for better performance.
!	  JAW 29-Jul-1981
! 2-035 - Miscellaneous performance enhancements:  JAW 29-Jul-1981
!	  Check for certain specific one-byte format codes at the outset
!	    and special-case them.
!	  For all format codes, if optional second byte is not present,
!	    bypass checks for VFEs and for optional forms of RC and W.
!	  Break FI_ACT into two tables, each having 1-byte entries,
!	    placing the special action in FI_ACT_2 and indicating the
!	    need for special action with the low-order bit of FI_ACT.
!	    Select a special action only if this bit is set.
!	  For codes _DF through _DD, check for element size of 4 first.
!	  Narrow the scope of ACT, FMT_REPR and P, which are not needed
!	    in the outermost block, to conserve registers.
!	  Replace CASE on V_RC_TYPE with IF ... THEN to avoid an EXTZV.
!-

!
! SWITCHES:
!

SWITCHES ADDRESSING_MODE (EXTERNAL = GENERAL, NONEXTERNAL = WORD_RELATIVE) ,
    ZIP;					! Optimize for speed

!
! LINKAGES:
!

REQUIRE 'RTLIN:OTSLNK';				! define all LINKAGES

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    FOR$$FMT_INTRP0 : JSB_FMT0 NOVALUE,		! initialization
    FOR$$FMT_INTRP1 : JSB_FMT1 NOVALUE;		! Interpret until a data format code

!
! INCLUDE FILES:
!

REQUIRE 'RTLML:FORERR';				! FORTRAN error number definitions

REQUIRE 'RTLML:OTSISB';				! I/O statement block (ISB)

REQUIRE 'RTLML:OTSLUB';				! needed only to get LUB length!

! definitions

REQUIRE 'RTLIN:FORFMT';				! Format statement codes (used in select labels)

REQUIRE 'RTLIN:OTSMAC';				! Macros

REQUIRE 'RTLIN:RTLPSECT';			! Define DECLARE_PSECTS macro

REQUIRE 'RTLIN:RTLODBG';			! debugging aids

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! PSECT DECLARATIONS:
!
DECLARE_PSECTS (FOR);				! declare PSECTs for FOR$ facility
!
! OWN STORAGE:
!
!	NONE
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
    FOR$$SIGNAL_STO : NOVALUE,			! Signal_stop FOR$_abcmnoxyz, given
    						! (short) Fortran error number (FOR$K_abcmnoxyz)
    						! as a parameter
    FOR$$SIGNAL : NOVALUE;			! Signal FOR$_abcmnoxyz, given (short)

						! FORTRAN error number (FOR$K_abcmnoxyz)
						! as a parameter.


GLOBAL ROUTINE FOR$$FMT_INTRP0 			! Format interpreter initialization
    : JSB_FMT0 NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	Initializes the format interpreter
!
! IMPLICIT INPUTS:
!
!	CCB			Contains adr. of current LUB/ISB/RAB.
!
! IMPLICIT OUTPUTS:
!
!	CCB [ISB$W_FMT_REP]	Set repeat count to 0 to indicate no repeat
!				for this statement yet.
!	CCB [ISB$B_FMT_P]	Set P scale factor to 0 for this statement
!	CCB [ISB$A_FMT_PTR]	Initializes format pointer to
!				beginning
!	CCB [ISB$W_FMT_REVER]	Offset of current format reversion
!				point
!	CCB [ISB$B_FMT_DEP]	Depth of repeat group pushdown stack
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

	EXTERNAL REGISTER
	    CCB : REF BLOCK [, BYTE];

    !+
    ! Print input formals and implicit inputs, if debugging
    !-

    INPUT_FORMALS (FOR$$FMT_INTRP0, '.FORMAT_ADR', .FORMAT_ADR);
    IMPLICIT_INPUTS (FOR$$FMT_INTRP0, '.OTS$$A_CUR_LUB', .OTS$$A_CUR_LUB);

    !+
    ! Set repeat count to 0 to indicate no repeat for this statement.
    !-

    CCB [ISB$W_FMT_REP] = 0;

    !+
    ! Set P scale factor to 0 for this statement (no scaling).
    !-

    CCB [ISB$B_FMT_P] = 0;

    !+
    ! Set format flags to zero for this statement.
    !-

    CCB [ISB$W_FMT_FLAGS] = 0;

    !+
    ! Set BN flag if LUB$V_NULLBLNK is set
    !-

    CCB [ISB$V_BN] = .CCB [LUB$V_NULLBLNK];

    !+
    ! Set current format position to beginning of format.
    !-

    CCB [ISB$A_FMT_PTR] = .CCB [ISB$A_FMT_BEG];

    !+
    ! Initialize format reversion point to beginning of format
    ! byte array. The reversion point is used when there are
    ! more user data elements than data format codes.
    ! Since it is a 16-bit offset with respect to ISB$A_FMT_BEG, set to 0.
    !-

    CCB [ISB$W_FMT_REVER] = 0;

    !+
    ! Initialize format repeat group push down stack depth
    ! to empty (-1).  0 = 1 item, 1 = 2 items in stack, etc.
    !-

    CCB [ISB$B_FMT_DEP] = -1;

    !+
    ! Initialize ISB$B_FMT_CODE to zero, which will tell
    ! FOR$$UDF_WF9 not to call FOR$$UDF_WF1 unless there were no
    ! items in the I/O list.
    !-

    CCB [ISB$B_FMT_CODE] = 0;

    !+
    ! All other ISB locations and flags have already been
    ! initialized to 0 or a specified value by the I/O statement
    ! initialization for this I/O statement.
    !-

    !+
    ! Print function value, output formals, and implicit outputs,
    ! if debugging
    !-

    FUNCTION_VALUE (FOR$$FMT_INTRP0);
    OUTPUT_FORMALS (FOR$$FMT_INTRP0, '.FORMAT_ADR', .FORMAT_ADR);
    IMPLICIT_OUTPUT (FOR$$FMT_INTRP0, 'CCB[ISB$A_FMT_PTR]', .CCB [ISB$A_FMT_PTR], '.CCB[ISB$W_FMT_REVER]',
	.CCB [ISB$W_FMT_REVER], '.CCB[ISB$B_FMT_DEP]', .CCB [ISB$B_FMT_DEP]);

    RETURN;
    END;				! End of routine FOR$$FMT_INTRP0

GLOBAL ROUTINE FOR$$FMT_INTRP1		! Format interpret until data code
!	(EL_SIZE, 			! Size in addressable units of data elements
					! This argument is passed in
					! external register EL_SIZE.
!	DT_SEEN)			! 1 if data transmitter seen
					! This argument is passed in
					! external register DT_SEEN.
    : JSB_FMT1 NOVALUE = 		! Value is format code
					! Returned in external register
					! FMT_CODE.

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	FOR$$FMT_INTRP1 interprets FORTRAN format statements
!	which have been precompiled into an encoded form by either
!	the FORTRAN compiler or the run-time format compiler,
!	FOR$FMT_COMPIL. Only FOR$$FMT_INTRP1 understands the structure
!	and encoding of compiled format statements. Furthermore,
!	it is independent of READ and WRITE semantics.
!	Each call to FOR$$FMT_INTRP1 processes as many format
!	codes as possible until it encounters one which
!	needs to access user program data, needs to access the
!	data buffer, or depend on whether read or write. This
!	block is independent of whether a READ or WRITE is
!	being performed. It is invoked in both the formatted
!	READ and WRITE user data formatter routines (UDF).
!
!	Note: being compatible with -11 OTS, there is a
!	difference between nf and n(f) in that VFE's are evaluated only
!	once for the former and each time for the latter. The former
!	is termed, repeating a format code while the latter is
!	termed a repeat group.
!
! IMPLICIT INPUTS:
!
!	EL_SIZE			Size in addressable units of user
!				data element. Used to set default
!				widths (W) for default format.
!				A value of 0 indicates that this is
!				the end of the I/O list call and there is
!				no user I/O list element to be transmitted.
!
!	DT_SEEN			1 if a data transmitter has been seen by
!				the current call to the UDF level, 0
!				otherwise.  If it is set, we don't evaluate
!				format items for data transmitters.
!
!	OTS$$A_CUR_LUB		Adr. of current logical unit block
!				(LUB) Used to setup base (ISB) to
!				I/O statement block.
!
!    The following locations are set only by previous calls to
!    FOR$$FMT_INTRP1, i.e., are effectively OWN:
!
!	CCB [ISB$A_FMT_PTR]	Adr. of next byte in compiled
!				format byte array. A value of 0
!				indicates that this is the end of the
!				I/O list call and there is no user
!				I/O list element to be transmitted.
!	CCB [ISB$V_USER_ELEM]	0 until a user element format
!				code seen. Infinite loop preventer
!	CCB [ISB$W_FMT_REP]	Current format code repeat count (n)
!				or 0 if not repeating a single
!				format code. Note: the repeat
!				count for a repeat group is kept
!				in the top of the format stack, not here.
!
!	CCB [ISB$A_FMT_BEG]	Adr. of beginning of format statement
!	CCB [ISB$B_FMT_DEP]	Depth of repeat group format pushdown stack.
!	CCB [ISB$W_FMT_STKP]	Stack of offsets to beginning of repeat groups
!	CCB [ISB$W_FMT_STKR]	Stack of group repeat counts
!	CCB [ISB$W_FMT_REVER]	Offset of current format reversion
!				point to revert to when end of format
!				statement is encountered with more data
!				elements to be transmitted.
!	CCB [ISB$V_USER_ELEM]	Flag: 1 if seen a user data element format code,
!				0 if not.  Used to check for infinite format loop
!				in which no user data element format codes are present
!
! IMPLICIT OUTPUTS:
!
!    The following are outputs only to a successive call to
!    FOR$$FMT_INTRP{0,1}, i.e., are effectively OWN.
!
!	CCB [ISB$V_USER_ELEM]	0 if no user data element format
!				code seen this repeat group, 1
!				if one or more
!	CCB [ISB$W_FMT_REP]	Current format code repeat count (n)
!				or 0 if not repeating a single
!				format code. Note: the repeat
!				count for a repeat group is kept
!				in the top of the format stack, not here.
!	CCB [ISB$B_FMT_DEP]	Depth of repeat group format pushdown stack.
!	CCB [ISB$W_FMT_STKP]	Stack of offsets to beginning of repeat groups
!	CCB [ISB$W_FMT_STKR]	Stack of group repeat counts
!	CCB [ISB$W_FMT_REVER]	Offset of current format reversion
!				point to revert to when end of format
!				statement is encountered with more data
!				elements to be transmitted.
!
!    The following are output to available to the caller (read
!    or write user data formatter):
!
!	CCB [ISB$A_FMT_PTR]	Adr. of next byte to be read from
!				the compiled format statement byte array
!				are pushed as a pair.
!	CCB [ISB$B_FMT_P]	Signed scale factor (P)
!	CCB [ISB$W_FMT_W]	Unsigned width of field (W)
!	CCB [ISB$B_FMT_D]	Unsigned number of digits in fraction (D)
!	CCB [ISB$B_FMT_E]	Unsigned number of characters
!				in exponent (E).
!	CCB [ISB$V_USER_ELEM]	Flag: 1 if seen a user data element format code,
!				0 if not.  Used to check for infinite format loop
!				in which no user data element format codes are present
!
! SIDE EFFECTS:
!
!	SIGNAL_STOPs FOR$_SYNERRFOR (62='SYNTAX ERROR IN FORMAT")
!	SIGNAL_STOPs FOR$_INFFORLOP (60="INFINITE FORMAT LOOP")
!	SIGNAL_STOPs FOR$_VFEVALERR (68="VFE VALUE ERROR")
!--

!
    BEGIN

    EXTERNAL REGISTER
	CCB : REF BLOCK [, BYTE],	! Pointer to Common Control Block
	EL_SIZE,			! Element size (1st argument)
	DT_SEEN,			! Data transmitter seen (2nd argument)
	FMT_CODE : BLOCK [1, LONG];	! Format code (return value)

    BUILTIN
	TESTBITSC;

    MACRO				! Field definitions for action table
	FI_STOP = 0,6,1,0 %,		! Stop if DT_SEEN
	FI_GETW = 0,5,1,0 %,		! Get w value for format
	FI_GETD = 0,4,1,0 %,		! Get d value for format
	FI_GETE = 0,3,1,0 %,		! Get e value for format
	FI_USER = 0,2,1,0 %,		! Format code involves user data element
	FI_EXIT = 0,1,1,0 %,		! Exit from format interpreter loop
	FI_ACTION = 0,0,1,0 %;		! Code-specific action required;
					!  see FI_ACT_2 for action

					! MAINTENANCE NOTE: An optimization
					! below assumes knowledge of certain
					! bit settings in FI_ACT.  See comments.

    MACRO
	FI_ALL =			! Enumerate all format codes
!		  W D E U E S
!		        S X T
!		        E I O
!		        R T P
	  FI_PACK(0,0,0,0,1,1, 0),	! ER	= 0,	! 00	! Format syntax error
	  FI_PACK(0,0,0,0,0,0, 2),	! LP	= 1,	! 01	! ( - Format reversiïn point
	  FI_PACK(0,0,0,0,0,0, 3),	! NLP	= 2,	! 02	! n( - Left paren of repeat group
	  FI_PACK(0,0,0,0,0,0, 4),	! RP	= 3,	! 03	! ) - Right paren of repeat group
	  FI_PACK(0,0,0,0,1,1, 5),	! EOF	= 4,	! 04	! ) - End of format
	  FI_PACK(0,0,0,0,1,0, 1),	! SLS	= 5,	! 05	! / - Record separator
	  FI_PACK(0,0,0,0,1,0, 1),	! DLR	= 6,	! 06	! $ - Dollar sign: terminal I/O
	  FI_PACK(0,0,0,0,1,1, 1),	! CLN	= 7,	! 07	! : - Colon: terminate if end of list
			0,			! UNUSED  8
	  FI_PACK(0,0,0,0,0,0, 7),	! _S	= 9,	! 09	! S - Make + optional
	  FI_PACK(0,0,0,0,0,0, 8),	! SP	= 10,	! 0A	! SP - Force optional +
	  FI_PACK(0,0,0,0,0,0, 7),	! SS	= 11	! 0B	! SS - Leave out optional +
	  FI_PACK(1,0,0,0,0,0, 6),	! _P	= 12,	! 0C	! sP - signed scale factor
	  FI_PACK(1,0,0,0,0,0,11),	! _T	= 13,	! 0D	! Tn - Tab Set
	  FI_PACK(1,0,0,0,1,0, 1),	! _X	= 14,	! 0E	! old nX
	  FI_PACK(1,0,0,0,1,0, 1),	! _H	= 15,	! 0F	! nHcccc - Hollerith
	  FI_PACK(0,0,0,0,0,0, 9),	! BN	= 16	! 10	! BN = Blanks are nulls
	  FI_PACK(0,0,0,0,0,0,10),	! BZ	= 17	! 11	! BZ = Blanks are zeroes
	  FI_PACK(1,0,0,0,0,0,12),	! TL	= 18	! 12	! TLn = Tab left n columns
	  FI_PACK(1,0,0,0,0,0,13),	! TR	= 19	! 13	! TRn (new nX) = Tab right n columns
	  FI_PACK(0,0,0,1,1,1, 1),	! _Q	= 20,	! 14	! Q
	  FI_PACK(1,0,0,1,1,1, 1),	! _A	= 21,	! 15	! nAw - Alpha numeric
	  FI_PACK(1,0,0,1,1,1, 1),	! _L	= 22,	! 16	! nLw - Logical
	  FI_PACK(1,0,0,1,1,1, 1),	! _O	= 23,	! 17	! nOw - Octal
	  FI_PACK(1,0,0,1,1,1, 1),	! _I	= 24,	! 18	! nIw - Integer
	  FI_PACK(1,0,0,1,1,1, 1),	! _Z	= 25,	! 19	! nZw - Hexadecimal
	  FI_PACK(1,1,0,1,1,1, 1),	! XO	= 26,	! 1A	! Ow.m Octal
	  FI_PACK(1,1,0,1,1,1, 1),	! XI	= 27,	! 1B	! Iw.m Integer
	  FI_PACK(1,1,0,1,1,1, 1),	! XZ	= 28	! 1C	! Zw.m Hexadecimal
			0,			! UNUSED  29
	  FI_PACK(1,1,0,1,1,1, 1),	! _F	= 30,	! 1E	! nFw.d - Fixed format
	  FI_PACK(1,1,0,1,1,1, 1),	! _E	= 31,	! 1F	! nEw.d - Scientific notation format
	  FI_PACK(1,1,0,1,1,1, 1),	! _G	= 32,	! 20	! nGw.d - General format
	  FI_PACK(1,1,0,1,1,1, 1),	! _D	= 33,	! 21	! nDw.d - Double Precision format
	  FI_PACK(1,1,1,1,1,1, 1),	! XE	= 34,	! 22	! nEw.dEe
	  FI_PACK(1,1,1,1,1,1, 1),	! XG	= 35,	! 23	! nGw.dEe
			0,0,0,0,0,		! UNUSED  36:40
	  FI_PACK(0,0,0,1,1,1, 1),	! _DA	= 41,	! 29	! nA - default A
	  FI_PACK(0,0,0,1,1,1, 1),	! _DL	= 42,	! 2A	! nL - default L
	  FI_PACK(0,0,0,1,1,1, 1),	! _DO	= 43,	! 2B	! nO - default O
	  FI_PACK(0,0,0,1,1,1, 1),	! _DI	= 44,	! 2C	! nI - default I
	  FI_PACK(0,0,0,1,1,1, 1),	! _DZ	= 45,	! 2D	! nZ - default Z
			0,0,0,0,		! UNUSED  46:49
	  FI_PACK(0,0,0,1,1,1, 1),	! _DF	= 50,	! 32	! nF - default F
	  FI_PACK(0,0,0,1,1,1, 1),	! _DE	= 51,	! 33	! nE - default E
	  FI_PACK(0,0,0,1,1,1, 1),	! _DG	= 52,	! 34	! nG - default G
	  FI_PACK(0,0,0,1,1,1, 1)	! _DD	= 53,	! 35	! nD - default D
    %;					! End of macro FI_ALL

!+
! Define FI_PACK for use in constructing FI_ACT
!-

    MACRO 				! Attributes-packing macro for attributes table
	FI_PACK (W, D, E, U, X, S, NDX) =
	    (S^6 + W^5 + D^4 + E^3 + U^2 + X^1 + 
		%IF %IDENTICAL (NDX, 1) %THEN 0 %ELSE 1 %FI) %;

    BIND
	FI_ACT = 			! First action table
	    UPLIT BYTE ( FI_ALL ) : VECTOR [54, BYTE];

!+
! Redefine FI_PACK for use in constructing FI_ACT_2
!-

    UNDECLARE %QUOTE FI_PACK;

    MACRO
	FI_PACK (W, D, E, U, X, S, NDX) =
	    NDX %;

    BIND
	FI_ACT_2 = 			! Second action table
	    UPLIT BYTE ( FI_ALL ) : VECTOR [54, BYTE];

!<BLF/PAGE>

!
    !+
    ! Print input formals and implicit inputs, if debugging
    !-

    INPUT_FORMALS (FOR$$FMT_INTRP1, '.EL_SIZE', .EL_SIZE);
    IMPLICIT_INPUTS (FOR$$FMT_INTRP1, '.OTS$$A_CUR_LUB', .OTS$$A_CUR_LUB, '.CCB[ISB$A_FMT_PTR]',
	.CCB [ISB$A_FMT_PTR], '.CCB[ISB$V_USER_ELEM]', .CCB [ISB$V_USER_ELEM], '.CCB[ISB$W_FMT_REP]',
	.CCB [ISB$W_FMT_REP], '.CCB[ISB$W_FMT_REVER]', .CCB [ISB$W_FMT_REVER], '.CCB[ISB$B_FMT_DEP]',
	.CCB [ISB$B_FMT_DEP], '.CCB[ISB$L_FMT_STK]', .(.ISB + ISB$S_FMT_STK), '.CCB[ISB$L_FMT_STK]+%UPVAL',
	.(.ISB + ISB$S_FMT_STK + %UPVAL), '.CCB[ISB$L_FMT_STK]+2*%UPVAL',
	.(.ISB + ISB$S_FMT_STK + 2*%UPVAL), '.CCB[ISB$L_FMT_STK]+3*%UPVAL',
	.(.ISB + ISB$S_FMT_STK + 3*%UPVAL), '.CCB[ISB$V_USER_ELEM]', .CCB [ISB$V_USER_ELEM]);

    !+
    ! (NXTITM+1)
    ! Assume that a format code is being repeated.- nf not n(f).
    ! (as distinguished from a repeat group which is n(...))
    ! Decrement format repeat count (ISB$W_FMT_REP). Test
    ! if still more to repeat - if yes, skip usual format code
    ! dispatching by skipping loop altogether, redo defaults if
    ! default format codes and RETURN
    !-

    IF .CCB [ISB$W_FMT_REP] GTR 1
    THEN
	BEGIN

	LOCAL
	    ACT : BLOCK [1, LONG];	! Action table entry for format code

	FMT_CODE = .CCB [ISB$B_FMT_CODE];
	ACT = .FI_ACT [.FMT_CODE];
	IF .DT_SEEN AND .ACT [FI_STOP]
	THEN
	    BEGIN
	    FMT_CODE = 0;
	    RETURN;
	    END;
	CCB [ISB$W_FMT_REP] = .CCB [ISB$W_FMT_REP] - 1;
	END
    ELSE

    !+
    ! (FINTRP)
    ! Not in format code repeat - start format interpret loop
    ! Loop until encounter a format code which needs to access
    ! data (ER or explicit or default Q, A, L, O, I, Z, F, E, G, or D),
    !  needs to access the data buffer (X, H, Q), or
    ! depends on whether read or write (), /, $, :, T).
    !-

	BEGIN

	REGISTER
	    P,				! Pointer to format byte stream
	    ACT : BLOCK [1, LONG];	! Action table entry for format code

	P = .CCB [ISB$A_FMT_PTR];

	DO
	    BEGIN

	    !+
	    ! Pickup next format code byte from compiled format:
	    ! If optional representation byte
	    ! is present (V_FMT_REPRE=1), mask out flag bit
	    ! in format code and copy next byte to BITVECTOR
	    ! to indicate larger (less frequent) sizes of the
	    ! code representation or Variable Field Expressions (VFE).
	    !-

	    FMT_CODE = CH$RCHAR (.P);
	    FMT_CODE [V_FMT_REPRE] = 0;		! Clear bit for search
	    ACT = .FI_ACT [.FMT_CODE];

	    !+
	    ! If DT_SEEN is set and this format code needs a data transmitter
	    ! then return a format code of
	    ! zero to signal the fact.  This will be differentiated from
	    ! an error by the UDF level by checking DT_SEEN.
	    !-

	    IF .DT_SEEN AND .ACT [FI_STOP]
	    THEN
		BEGIN
		CCB [ISB$A_FMT_PTR] = .P;
		FMT_CODE = 0;
		RETURN;
		END;

	    FMT_CODE = CH$RCHAR_A (P);		! Re-read and increment pointer

	    !+
	    ! Optimization:
	    !
	    ! Check for certain easily-handled (and frequent) cases:
	    ! 1.  A/L/O/I/Z (codes 21-25) with no RC and byte-length W;
	    ! 2.  O/I/Z/F/E/D/G (codes 26-28 and 30-33) with no RC and
	    !     byte-length W, D;
	    ! 3.  E/G (codes 34-35) with no RC and byte-length W, D, E;
	    ! If found, handle directly and bypass the tests for VFE's,
	    ! word-length RC and W, and special action.  Note that
	    ! anything with V_FMT_REPRE set falls under OUTRANGE.
	    !
	    ! This optimization assumes knowledge of flag bit settings
	    ! in FI_ACT, and must be reconsidered if FI_ACT changes.
	    !-

	    IF NOT (CASE .FMT_CODE FROM _A TO XG OF
		SET
		    [_A TO _Z] :
			BEGIN
			CCB [ISB$W_FMT_W] = RBYTE_A (P);
			CCB [ISB$W_FMT_REP] = 1;
			CCB [ISB$V_USER_ELEM] = 1;
			1		! Indicate special case found
			END;
		    [XO TO XZ, _F TO _D] :
			BEGIN
			CCB [ISB$W_FMT_W] = RBYTE_A (P);
			CCB [ISB$B_FMT_D] = RBYTE_A (P);
			CCB [ISB$B_FMT_E] = 2;
			CCB [ISB$W_FMT_REP] = 1;
			CCB [ISB$V_USER_ELEM] = 1;
			1		! Indicate special case found
			END;
		    [XE TO XG] :
			BEGIN
			CCB [ISB$W_FMT_W] = RBYTE_A (P);
			CCB [ISB$B_FMT_D] = RBYTE_A (P);
			CCB [ISB$B_FMT_E] = RBYTE_A (P);
			CCB [ISB$W_FMT_REP] = 1;
			CCB [ISB$V_USER_ELEM] = 1;
			1		! Indicate special case found
			END;
		    [29, OUTRANGE] :
			0;		! Indicate special case not found
		TES)
	    THEN
		BEGIN

		!+
		! Get RC, W, D and E in the traditional, fully general
		! way, including check for VFE's and alternate forms of
		! W and RC.
		!
		! Optimization:
		!
		! If optional second byte is not present, bypass check
		! for VFE's and alternate forms of W and RC.
		!-

		IF NOT TESTBITSC (FMT_CODE [V_FMT_REPRE])
		THEN
		    BEGIN			! Begin short form
		    CCB [ISB$W_FMT_REP] = 1;
		    IF .ACT [FI_GETW]
		    THEN
			BEGIN
			CCB [ISB$W_FMT_W] = RBYTE_A (P);
			IF .ACT [FI_GETD]
			THEN
			    BEGIN
			    CCB [ISB$B_FMT_D] = RBYTE_A (P);
			    IF .ACT [FI_GETE]
			    THEN
				CCB [ISB$B_FMT_E] = RBYTE_A (P);
			    END;
			END;
		    END				! End short form
		ELSE
		    BEGIN			! Begin long form

		    LOCAL
			FMT_REPR : BLOCK [1, LONG];

		    FMT_REPR = RBYTE_A (P);

		    !+
		    ! Get repeat count (RC) from format and save in ISB$W_FMT_REP.
		    ! If repeat count is a VFE (FMT_REPR[V_RC_VFE]=1), get VFE and
		    ! check for out of range (1:32767).
		    ! If explicitly represented, get byte or word value.
		    ! Else set repeat count to 1. Possible for left paren
		    ! of a repeat group (NLP) or A, L, O, Z, I, F, E, G, D
		    ! or default A, L, O, Z, I, F, E, G, D.
		    !-

		    CCB [ISB$W_FMT_REP] = (IF .FMT_REPR [V_RC_VFE]
			THEN
			    BEGIN		! Process RC VFE

			    STACKLOCAL
				T;

			    T = CALL_VFE (P);

			    IF .T GEQU 32768 OR .T EQL 0
			    THEN
				BEGIN
				FOR$$SIGNAL (FOR$K_VFEVALERR);
				1		! Force repeat count to 1 on error
				END
			    ELSE
				.T		! Use user supplied value
			    END			! End of RC VFE processing

			ELSE

			    ! The following assumes that RC is either 00
			    ! (absent), 01 (byte) or 10 (word), and that
			    ! it cannot be 11.

			    IF .FMT_REPR [V_RC_TYPE_BYTE]
			    THEN
				RBYTE_A (P)		! RC is a byte
			    ELSE
				IF .FMT_REPR [V_RC_TYPE_WORD]
				THEN
				    RWORD_A (P)		! RC is a word
				ELSE
				    1);			! RC is absent

		    !+
		    ! P, T, X, H, A ,L, O, I, Z, F, E, G, D:
		    ! Get field width (w) from format and
		    ! set ISB$W_FMT_W. If width field is a
		    ! VFE (V_W_VFE=1), get VFE value and check range;
		    ! if P scale -128 to 127, else (field width w) 0 to 32767.
		    ! If width of field is a byte (V_W_WORD=0), get byte
		    ! else get word.  ISB$W_FMT_W is set as a
		    ! zero extended word.
		    !-

		    IF .ACT [FI_GETW]
		    THEN
			BEGIN
			CCB [ISB$W_FMT_W] = (IF .FMT_REPR [V_W_VFE] THEN
			    BEGIN

			    STACKLOCAL
				T;

			    T = CALL_VFE (P);

			    IF .FMT_CODE EQL _P
			    THEN
				BEGIN		    ! P scale

				IF .T GEQU 256
				THEN
				    BEGIN
				    FOR$$SIGNAL (FOR$K_VFEVALERR);
				    T = 0		    ! Force P scale to 0
				    END

				END
			    ELSE
				BEGIN			    ! Else w width of field

				IF .T LEQ 0
				THEN
				    BEGIN
				    FOR$$SIGNAL (FOR$K_VFEVALERR);
				    T = 1
				    END

				END;

			    .T			    ! return VFE value
			    END
			ELSE IF .FMT_REPR [V_W_WORD] THEN RWORD_A (P) ELSE RBYTE_A (P));

			!+
			! Get decimal part (d) from format and set
			! ISB$B_FMT_D. If decimal part is a VFE
			! (V_D_VFE=1) get VFE and check range (0:32767).
			! Else get byte from format
			!
			! Set default exponent width to 2.
			!-

			IF .ACT [FI_GETD]
			THEN
			    BEGIN
			    CCB [ISB$B_FMT_D] = (IF .FMT_REPR [V_D_VFE] THEN
				BEGIN		    ! VFE

				STACKLOCAL
				    T;

				T = CALL_VFE (P);

				IF .T GEQU 32768
				THEN
				    BEGIN
				    FOR$$SIGNAL (FOR$K_VFEVALERR);
				    1
				    END
				ELSE
				    .T

				END
			    ELSE RBYTE_A (P));
			    CCB [ISB$B_FMT_E] = 2;

			    !+
			    ! Get exponent width (e) from format and set
			    ! ISB$B_FMT_E.  If exponent width is a VFE, check
			    ! range (0:255).  Else get byte from format.
			    !-

			    IF .ACT [FI_GETE]
			    THEN
				BEGIN
				CCB [ISB$B_FMT_E] = (IF .FMT_REPR [V_E_VFE] THEN
				    BEGIN		    ! VFE

				    STACKLOCAL
					T;

				    T = CALL_VFE (P);

				    IF .T GEQU 256
				    THEN
					BEGIN
					FOR$$SIGNAL (FOR$K_VFEVALERR);
					1
					END
				    ELSE
					.T

				    END
				ELSE RBYTE_A (P));
				END;

			    END;

			END;

		    END;			! End long form

		!+
		! For all user data element format codes
		! (explicit and default Q, A, L, O, I, Z, F, E, G, D):
		! Set user data element format code
		! seen in this group, because not in an
		! infinite format loop invoking for a user
		! data element format code which doesn't exist.
		!-

		IF .ACT [FI_USER] THEN CCB [ISB$V_USER_ELEM] = 1;

		!+
		! Dispatch on format code and select appropriate actions:
		!-

		IF .ACT [FI_ACTION]
		THEN
		    CASE .FI_ACT_2 [.FMT_CODE] FROM 0 TO 13 OF
		    SET

		    [0] :

			!+
			! ER or undefined format code
			! Bad format: Signal_stop SYNTAX ERROR IN FORMAT (FOR$_SYNERRFOR)
			!-

			BEGIN
			FOR$$SIGNAL_STO (FOR$K_SYNERRFOR);
			FMT_CODE = 0;
			RETURN;
			END;

		    [1] :

			!+
			! No special actions required.
			!-

			;

		    [2] :

			!+
			! LP  Format reversion point: left paren of
			! second outer-most pair. Remeber current format
			! offset (ISB$W_FMT_REVER) in case more data
			! element in I/O list than data format
			! codes in format. Reset push down stack to
			! empty (-1) since this is start of
			! first group repeat. Clear user data element
			! seen flag (ISB$V_USER_ELEM) as a defense
			! against infinite loop with no data
			! transmit format code
			! Note: format text pointer already advanced to next byte
			!-

			BEGIN
			CCB [ISB$B_FMT_DEP] = -1;
			CCB [ISB$W_FMT_REVER] = .P - .CCB [ISB$A_FMT_BEG];
			CCB [ISB$V_USER_ELEM] = 0;
			END;			! End LP

		    [3] :

			!+
			! NLP   Left paren of a repeat group: Push repeat
			! count (ISB$W_FMT_REP) and current (ISB$A_FMT_PTR)
			! onto format stacks
			!-

			BEGIN
			CCB [ISB$B_FMT_DEP] = .CCB [ISB$B_FMT_DEP] + 1;
			VECTOR [CCB [ISB$W_FMT_STKR], .CCB [ISB$B_FMT_DEP];, WORD, UNSIGNED]	!
			= .CCB [ISB$W_FMT_REP];
			VECTOR [CCB [ISB$W_FMT_STKP], .CCB [ISB$B_FMT_DEP];, WORD, UNSIGNED]	!
			= .P - .CCB [ISB$A_FMT_BEG];
			CCB [ISB$W_FMT_REP] = 1;
			END;			! End NLP

		    [4] :

			!+
			! RP   Right paren of repeat group: Decrement
			! current group repeat count (on top of
			! stack)  If current group count still greater
			! than 0, set current format pointer back to
			! beginning of repeat group. Else pop off
			! beginning of group pointer and group repeat count
			!-

			IF (VECTOR [CCB [ISB$W_FMT_STKR], .CCB [ISB$B_FMT_DEP];, WORD, UNSIGNED]	!
			    = .VECTOR [CCB [ISB$W_FMT_STKR], .CCB [ISB$B_FMT_DEP];, WORD, UNSIGNED] - 1) GTR 0
			THEN
			! reset pointer to address of repeat group
			!
			    P = .CCB [ISB$A_FMT_BEG]	!
			    + .VECTOR [CCB [ISB$W_FMT_STKP], .CCB [ISB$B_FMT_DEP];, WORD, UNSIGNED]
			ELSE
			! pop off pointer and repeat count
			!
			    CCB [ISB$B_FMT_DEP] = .CCB [ISB$B_FMT_DEP] - 1;

		    [5] :

			!+
			! EOF   End of format:
			! If not end of user I/O list (EL_SIZE=0)
			! and no user data element format code
			! (ISB$V_USER_ELEM=0), then Signal_stop. INFINITE
			! FORMAT LOOP (FOR$_INFFORLOP).
			! Reset current format pointer to reversion point
			! (ISB$W_FMT_REVER). Initialize format stack depth.
			!-

			BEGIN
			P = .CCB [ISB$A_FMT_BEG] + .CCB [ISB$W_FMT_REVER];
			CCB [ISB$B_FMT_DEP] = -1;

			IF .EL_SIZE GTRU 0 AND NOT .CCB [ISB$V_USER_ELEM]
			THEN
			    BEGIN
			    FOR$$SIGNAL_STO (FOR$K_INFFORLOO);
			    FMT_CODE = 0;
			    RETURN;
			    END;

			END;

		    [6] :

			!+
			! P   Scale factor (sP): -128 =< s =< 127
			! Convert unsigned word width (w) (ISB$W_FMT_W)
			! to signed byte ('s) and save in ISB$B_FMT_P.
			!-

			BEGIN
			CCB [ISB$B_FMT_P] = .CCB [ISB$W_FMT_W];
			END;

		    [7] :

			!+
			! S, SS   Restore option of + to processor.
			!-

			BEGIN
			CCB [ISB$V_SP] = 0;
			END;

		    [8] :

			!+
			! SP   Force optional + to appear
			!-

			BEGIN
			CCB [ISB$V_SP] = 1;
			END;

		    [9] :

			!+
			! BN   Treat blanks as nulls on numeric input.
			!-

			BEGIN
			CCB [ISB$V_BN] = 1;
			END;

		    [10] :

			!+
			! BZ   Treat blanks as zeroes on numeric input.
			!-

			BEGIN
			CCB [ISB$V_BN] = 0;
			END;


		    [11] :

			!+
			! Tn   Move buffer pointer to position n
			!-

			BEGIN
			CCB [LUB$A_BUF_PTR] = .CCB [LUB$A_BUF_BEG] + (.CCB [ISB$W_FMT_W] - 1);
			END;

		    [12] :

			!+
			! TLn  Move buffer pointer left n positions
			!-

			BEGIN
			CCB [LUB$A_BUF_PTR] = .CCB [LUB$A_BUF_PTR] - .CCB [ISB$W_FMT_W];

			IF .CCB [LUB$A_BUF_PTR] LSSA .CCB [LUB$A_BUF_BEG]
			THEN
			    CCB [LUB$A_BUF_PTR] = .CCB [LUB$A_BUF_BEG];

			END;

		    [13] :

			!+
			! TRn  Move buffer pointer right n spaces.
			!	   Note: as of VMS Release 2, the format nX
			!	   is equivalent to TRn.  The old nX code
			!	   is no longer generated but is supported
			!	   for compatibility.
			!-

			BEGIN
			CCB [LUB$A_BUF_PTR] = .CCB [LUB$A_BUF_PTR] + .CCB [ISB$W_FMT_W];
			END;
		    TES;

		!+
		! End of loop - continue if just format control
		! ( (, n(, )) or not dependent on read/write
		! and doesn't access data buffer (P)
		!
		! EXITLOOP for format codes which access user data.
		! (ER or explicit or  default A, L, O, I, Z, F, E, G or D),
		! EXITLOOP for format codes which access data
		! buffer (X, H, Q)  EXITLOOP for format codes
		! which depend on whether read or write (end
		! of format, /, $, :, T).
		!-

		END
	    END
	UNTIL .ACT [FI_EXIT];

	!+
	! Reset format code and updated format text pointer in ISB.
	!-

	CCB [ISB$B_FMT_CODE] = .FMT_CODE;
	CCB [ISB$A_FMT_PTR] = .P;
	END;


    !+
    ! Default data format codes - set defaults based on size of
    ! each user data element even if inside a format code repeat
    ! since the size could be different for each user data element
    !-

    IF .FMT_CODE GEQU _DA
    THEN
	BEGIN

	CASE .FMT_CODE FROM _DA TO _DD OF
	    SET

	    [_DA] :

		!+
		! Default A: set w field (ISB$W_FMT_W) from
		! size of user data element
		!-

		CCB [ISB$W_FMT_W] = .EL_SIZE;

	    [_DL] :

		!+
		! Default L:  set w field (ISB$W_FMT_W) to 2
		!-

		CCB [ISB$W_FMT_W] = 2;

	    [_DI] :

		!+
		! Default I: Set w field to 7 if element is smaller than
		! 4 bytes else set it to 12.
		!-

		IF .EL_SIZE LSSU 4 THEN CCB [ISB$W_FMT_W] = 7 ELSE CCB [ISB$W_FMT_W] = 12;

	    [_DO, _DZ] :

		!+
		! Default O, Z.  Set to the width that would allow O
		! format plus a space.  \\ Note:  For compatibility with
		! previous releases, the sizes for 1, 2 and 4 bytes must
		! be 7, 7 and 12 respectively. \\
		!-

		CCB [ISB$W_FMT_W] = MAX (7, MIN (65535, (((8*.EL_SIZE)+2)/3)+1));
	    [_DF, _DE, _DG, _DD] :

		!+
		! Default F, E, G, D: Set w and e fields as is appropriate
		! to the element size.  Note that anything that is not
		! 8 (REAL*8) or 16 (REAL*16) is assumed to be 4 (REAL*4),
		! but check for 4 first.
		!-

		BEGIN

		SELECTONE .EL_SIZE OF
		    SET

		    [4] :
			BEGIN
			CCB [ISB$B_FMT_E] = 2;
			CCB [ISB$W_FMT_W] = 15;
			CCB [ISB$B_FMT_D] = 7;
			END;


		    [8] :
			BEGIN
			CCB [ISB$B_FMT_E] = 2;
			CCB [ISB$W_FMT_W] = 25;
			CCB [ISB$B_FMT_D] = 16;
			END;

		    [16] :
			BEGIN
			CCB [ISB$B_FMT_E] = 3;
			CCB [ISB$W_FMT_W] = 42;
			CCB [ISB$B_FMT_D] = 33;
			END;

		    [OTHERWISE] :
			BEGIN
			CCB [ISB$B_FMT_E] = 2;
			CCB [ISB$W_FMT_W] = 15;
			CCB [ISB$B_FMT_D] = 7;
			END;
		    TES;

		END;

	    [INRANGE] :
	    ;
	    TES;

	!+
	! Translate default format code to corresponding explicit code.
	!-

	FMT_CODE = .FMT_CODE - (_DA - _A);
	END;

    !+
    ! Print output formals,  implicit outputs, and function value if debugging
    !-

    FUNCTION_VALUE (FOR$$FMT_INTRP1, '.FMT_CODE', .FMT_CODE);
    OUTPUT_FORMALS (FOR$$FMT_INTRP1);
    IMPLICIT_OUTPUT (FOR$$FMT_INTRP1, '.CCB[ISB$A_FMT_PTR]', .CCB [ISB$A_FMT_PTR], '.CCB[ISB$B_FMT_DEP]',
	.CCB [ISB$B_FMT_DEP], '.CCB[ISB$W_FMT_REP]', .CCB [ISB$W_FMT_REP], '.CCB[ISB$L_FMT_STK]',
	.(.ISB + ISB$S_FMT_STK), '.CCB[ISB$L_FMT_STK]+%UPVAL', .(.ISB + ISB$S_FMT_STK + %UPVAL),
	'.CCB[ISB$L_FMT_STK]+2*%UPVAL', .(.ISB + ISB$S_FMT_STK + 2*%UPVAL), '.CCB[ISB$L_FMT_STK]+3*%UPVAL',
	.(.ISB + ISB$S_FMT_STK + 3*%UPVAL), '.CCB[ISB$B_FMT_P]', .CCB [ISB$B_FMT_P], '.CCB[ISB$W_FMT_W]',
	.CCB [ISB$W_FMT_W], '.CCB[ISB$B_FMT_D]', .CCB [ISB$B_FMT_D], '.CCB[ISB$B_FMT_E]',
	.CCB [ISB$B_FMT_E], '.CCB[ISB$V_USER_ELEM]', .CCB [ISB$V_USER_ELEM]);
    !+
    ! Return to read, write User Data Formatter (UDF). If default
    ! format code, return corresponding explicit format code
    ! to UDF. Else return the actual format code
    !-

    RETURN;

    END;				! End of routine FOR$$FMT_INTRP1

END					! End of module FOR$$FMT_INTRP

ELUDOM
