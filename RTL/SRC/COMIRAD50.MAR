	.TITLE	COM$IRAD50	; FORTRAN COMPATIBILITY - ASCII to RAD50 conversion
	.IDENT	/1-004/		; File: COMIRAD50.MAR  Edit: JAW1004

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: FORTRAN COMPATABILITY LIBRARY
;++
; ABSTRACT:
;
;	FORTRAN COMPATABILITY LIBRARY routine IRAD50 converts a stream
;	of ASCII characters to RAD50 words.
;
;--
;
; VERSION: 1-002
;
; HISTORY:
;
; AUTHOR:
;	Peter Yuo, 12-Sep-77: Version 0
;
; MODIFIED BY:
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History

; Edit History for Version 01 of ASCR5O
;
; 00-06	- Define formal for RAD50 so no access vio.  TNH 5-Jan-78
; 00-07	- Make PSECT be F4PCOMPAT$CODE.  TNH 5-Jan-78
; 0-8	- Bug fix for RAD50.  JMT 5-Jan-78
; 0-9	- Another bug fix for RAD50.  JMT 9-Jan-77
; 1-1	- Break module COM$ASCR50 into 3 modules:
;		COM$RAD50 - routine RAD50
;		COM$IRAD50 - routine IRAD50
;		COM$$R50WD - common ASCII to RAD50 conversion routine
; 1-002	- Update copyright notice.  JBS 16-NOV-78
; 1-003	- Add "_" to PSECT directive.  JBS 21-DEC-78
; 1-004 - Allow second argument to be passed either by descriptor or by
;	  reference.  SPR 11-35539.  JAW 04-Feb-1981

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;

;
; EXTERNAL SYMBOLS:
;
	.DSABL	GBL
	.EXTRN	COM$$R50WD_R6

;
; MACROS:
;
	$DSCDEF				; Define descriptor symbols.

;
; PSECT DECLARATIONS:
;
	.PSECT	_F4PCOMPAT$CODE	PIC,USR,CON,REL,LCL,SHR,EXE,RD,NOWRT

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;

	.SBTTL	IRAD50 - CONVERT HOLLERITH STRINGS TO RADIX-50 REPRESATATION

;++
; FUNCTIONAL DESCRIPTION:
;
; 	Algorithmic steps:
;	1) Initialization
;	   CHARS_REM = max_char_cnt.rbu.ra
;	   NEXT_INPUT_POSITION = char_array.rbu.ra
;	   NEXT_OUTPUT_POSITION = radix50_array.rbu.ra
;	   ACTUAL_CHAR_COUNT = 0
;	2) Call COM$$R50WD_R6 to convert one word at a time.
;	   If CHARS_REM =< 0 then return with function_value = ACTUAL_CHAR_COUNT,
;	NOTE: Three characters of ASCII input are packed into each word
;	of output in radix-50 format. The number of output word modified
;	is computed by the expression (in integer mode) (ICNT+2)/3.
;  

; CALLING SEQUENCE:
;
;	[no_char_conv.ww.v] = IRAD50 (max_char_cnt.rw.r, char_array.rbu.ra,
;				radix50_array.wbu.ra)

	max_char_cnt	= 4		; max_char_cnt.rw.r
	char_array	= 8		; char_array.rbu.ra
	radix50_array	= 12		; radix50_array.rbu.ra
;

;
; INPUT PARAMETERS:

;	max_char_cnt.rw.r		; maximum number of chars to convert
;	char_array.rbu.ra		; ascii string to be converted
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;
;	radix50_array.wbu.ra		; output location for the result
;					; of the conversion
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	NONE
;
;--



	.ENTRY	IRAD50, ^M<R2, R3, R4, R5, R6>
					; standard call-by-reference entry

;
; Initialization
;

	MOVZWL	@max_char_cnt(AP), R5	; R5 = maximum number of chars
					; to be converted
	MOVL	char_array(AP), R2	; R2 = address of input string
					;  or descriptor
	CMPW	DSC$W_LENGTH(R2), #255	; Is length <= 255?
	BGTRU	5$			; If not, assume by reference.
	CMPB	DSC$B_DTYPE(R2), #DSC$K_DTYPE_T	; Is data type T?
	BNEQU	5$			; If not, assume by reference.
	CMPB	DSC$B_CLASS(R2), #DSC$K_CLASS_S	; Is class S?
	BNEQU	5$			; If not, assume by reference.
	MOVL	DSC$A_POINTER(R2), R2	; Use address in descriptor.
5$:	MOVL	radix50_array(AP), R4	; R4 = address of the output location
	CLRL	R0			; R0 = ACTUAL_CHAR_COUNT = 0

;
; If CHARS_REM =< 0 then return with function_value equal to ACTUAL_CHAR_COUNT
; else call R50WD_R5 to convert one word at a time.
;

10$:
	JSB	COM$$R50WD_R6		; convert one word at a time
	MOVW	R1, (R4)+		; output one word at a time
	TSTL	R5			; any more?
	BGTR	10$			; branch if so
	RET				; return with R0 = ACTUAL_CHAR_COUNT


	.END
