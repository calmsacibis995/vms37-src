	.TITLE	LIB$SCANC - Scan for character 
	.IDENT	/1-006/		; File: LIBSCANC.MAR Edit: RKR1006

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: General Utility Library
;
; ABSTRACT:
;
;	Return an index into the source string of the satisfying 
;	character or zero
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: Donald G. Petersen, CREATION DATE: 03-Jan-78
;
; MODIFIED BY:
;
;	DGP, 03-Jan-78 : VERSION 00
; 01	- Original 
; 00-02	- DGP 05-Jan-78 - table.rbu.ra for table.rt.dx
; 00-03	- DGP 18-Jan-78 - remove one instruction
; 1-001	- Updated version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Add "_" to the PSECT directive.  JBS 21-DEC-78
; 1-003 - Fix entry mask to save R2 and R3 which get clobbered.
;	  SBL 02-Feb-79
; 1-004 - Enhance to recognize addiitonal classes of string descriptors
;	  by invoking LIB$ANALYZE_SDESC_R3 to extract length and address
;	  of 1st data byte from descriptor.  RKR 22-MAY-1981
; 1-005 - Add special-case code to process string descriptors that
;	  "read" like fixed string descriptors.  RKR 7-OCT-1981.
; 1-006 - Redirect jsb's from LIB$ANALYZE_SDESC_R3 to
;	  LIB$ANALYZE_SDESC_R2.  RKR 18-NOV-1981.

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES: NONE
;

; EXTERNAL SYMBOLS:
	.DSABL	GBL			; Only explicit external
	.EXTRN	LIB$ANALYZE_SDESC_R2	; Extract length and address of
					; 1st data byte
;
; MACROS:
;
	$DSCDEF		; field in a descriptor

;
; EQUATED SYMBOLS: NONE
;

;
; OWN STORAGE: NONE
;

;
; PSECT DECLARATIONS:
;
	.PSECT _LIB$CODE PIC, SHR, LONG, EXE, NOWRT


	.SBTTL	LIB$SCANC  - Scan characters 
;++
; FUNCTIONAL DESCRIPTION:
;
;	The bytes of the string specified by the source descriptor are 
;	successively used to index into a the table.
;	The byte selected from the table is ANDed with the mask byte 
;	specified by the mask descriptor.  The operation continues 
;	until the result of the AND is non-zero.  The relative position 
;	of the character in the source string which terminated the 
;	operation is returned if one is found.  Otherwise, zero
;	is returned.  If the source striing has a length of zero then 
;	a zero is returned.
;
; CALLING SEQUENCE:
;
;	index.wlu.v = LIB$SCANC (src.rt.dx, table.rbu.ra, mask.rbu.r)
;

; INPUT PARAMETERS:
;
	SOURCE = 4			; Adr of source string desc
	TABLE = 8			; Adr of base of table
	MASK = 12			; adr of mask byte
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
;
;	INDEX.wlu.v		; the resulting position
;
; SIDE EFFECTS:
;
;	NONE
;
;--

	.ENTRY LIB$SCANC , ^M<R2, R3>		; Entry point
	MOVL	SOURCE(AP), R0		; address of SOURCE descriptor
	CMPB	DSC$B_CLASS(R0), #DSC$K_CLASS_D	; read like fixed ?
	BGTRU	1$			; no
	MOVQ	@SOURCE(AP), R1		; length->R1, address->R2
	BRB	2$			; join common flow

1$:	JSB	G^LIB$ANALYZE_SDESC_R2	; Extract: length->R1, addr->R2
2$:	MOVZWL	R1, R4			; save length of SOURCE

	SCANC	R4, (R2), @TABLE(AP), @MASK(AP)	; Scan character
				; State of regs after a SCANC instr.
				; R0 = number of bytes remaining in
				;      source string (including the byte
				;      which produced the nonzero AND)
				; R1 = If found, address of byte which 
				;      produced non zero AND,
				;      else address of 1 byte beyond
				;      source string
				; R2 = 0
				; R3 = address of table
				;
	BEQL	10$			; branch if no character found
	SUBW3	R0, R4, R0		; get position of character
	INCL	R0			; one origin
10$:	RET
	.END
