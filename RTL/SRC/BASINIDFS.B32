MODULE BAS$INIT_DFS (				! Initialize DEF* frame
		IDENT = '1-005'			! File: BASINIDFS.B32
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!

!++
! FACILITY:  BASIC-PLUS-2 Frame Support
!
! ABSTRACT:
!
!	These routines set up and tear down frames for BASIC-PLUS-2.
!	Frames are used for main routines, external functions,
!	external subroutines, internal functions (both DEFs and DEF*s)
!	internal subroutines (GOSUBs) and condition handlers.
!
! ENVIRONMENT:  VAX-11 user mode
!
! AUTHOR: John Sauter, CREATION DATE: 10-Oct-78
!
! MODIFIED BY:
!
! 	, : VERSION
! 1-001	- Original.  Just a skeleton.
! 1-002	- Change LIB$S and OTS$S to STR$.  This routine is still not
!	   implemented.  JBS 21-MAY-1979
! 1-003	- Finally, code this routine, based on BAS$INIT_DEF.
!	   JBS 03-AUG-1979
! 1-004	- Remove BAS$K_WROMATPAC, not used.  JBS 19-SEP-1979
! 1-005	- Fix a comment.  JBS 07-NOV-1979
!--

!
!<BLF/PAGE>

!
! SWITCHES:
!

SWITCHES ADDRESSING_MODE (EXTERNAL = GENERAL, NONEXTERNAL = WORD_RELATIVE);

!
! LINKAGES:
!

LINKAGE
    BAS$INIT_LINK = JSB (REGISTER = 0, REGISTER = 1, REGISTER = 2) : 	!
    GLOBAL (BSF$A_MAJOR_STG = 11, BSF$A_MINOR_STG = 10, BSF$A_TEMP_STG = 9)	!
    NOPRESERVE (8, 7, 6, 5, 4, 3, 2, 1, 0);

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    BAS$INIT_DFS_R8 : NOVALUE BAS$INIT_LINK;	! start DEF*

!
! INCLUDE FILES:
!

REQUIRE 'RTLIN:RTLPSECT';			! macros for defing psects

REQUIRE 'RTLIN:BASFRAME';			! Define frame structure

REQUIRE 'RTLIN:BASINARG';			! Define argument list

LIBRARY 'RTLSTARLE';				! System symbols

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! PSECTS:
!
DECLARE_PSECTS (BAS);				! declare psects for BAS$ facility
!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
    BAS$$SIGNAL : NOVALUE,			! signals error
    STR$FREE1_DX,				! deallocates one string
    BAS$HANDLER;				! handles signals

!+
! The following are the error codes used in this module.
!-

EXTERNAL LITERAL
    BAS$K_TOOFEWARG : UNSIGNED (8),		! Too few arguments
    BAS$K_TOOMANARG : UNSIGNED (8),		! Too many arguments
    BAS$K_SCAFACINT : UNSIGNED (8),		! Scale factor interlock
    BAS$K_PROLOSSOR : UNSIGNED (8),		! Program lost, sorry
    BAS$K_ARGDONMAT : UNSIGNED (8),		! Arguments don't match
    BAS$K_NOTIMP : UNSIGNED (8);		! Not implemented


GLOBAL ROUTINE BAS$INIT_DFS_R8 (		! start DEF*
	ARGLIST, 				! frame parameters
	DATA_RELOC				! start of data
    ) : NOVALUE BAS$INIT_LINK =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Set up a frame for a BASIC-PLUS-2 DEF*.  The frame is allocated
!	on the stack, and R10 and R9 are set up to point to it.
!	The argument tells how to do the allocation.
!
! FORMAL PARAMETERS:
!
!	ARGLIST.rl.v	List of information needed to set up the
!			frame.  See BASIC-PLUS-2/VAX Description
!			of Generated Code for details.
!	DATA_RELOC.ra.v	Address of the major procedure's contribution
!			to the data PSECT.  This is needed so that the
!			argument list can be PIC.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	The values of R10 and R9, which point to the automatic
!	storage and the temporary storage, respectively.
!
! ROUTINE VALUE:
!
!	NONE
!
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	Leaves lots of things on the stack for use by the compiled
!	BASIC-PLUS-2 code.  These things will be removed by
!	BAS$END_DFS_R8.
!
!--

    BEGIN

    EXTERNAL REGISTER
	BSF$A_MAJOR_STG : REF BLOCK [0, BYTE] FIELD (BSF$MAJOR_FRAME),
	BSF$A_MINOR_STG : REF BLOCK [0, BYTE] FIELD (BSF$MINOR_FRAME),
	BSF$A_TEMP_STG;

    BUILTIN
	AP,
	FP,
	SP;

    MAP
	ARGLIST : REF BLOCK [0, BYTE] FIELD (BAS$INIT_ARGS),	! arg list
	AP : REF VECTOR;			! caller's arg list

!+
! Define local variables as registers.  We cannot have any stack locals
! since we manipulate the stack pointer in this routine.
!-

    REGISTER
	RETURN_ADDRESS,				! address to return to
	FMP : REF BLOCK [0, BYTE] FIELD (BSF$FCD),	! pointer to FCD
	ARRAY_DESC : REF BLOCK [0, BYTE],	! pointer to build array descriptors
	ARRAY_INDEX;				! index for array modification

!+
! Save return address because we are going to fool with the stack
!-
    RETURN_ADDRESS = ..SP;
!+
! Make sure we are passed an argument list we understand.
!-

    IF (.ARGLIST [BAS$B_IN_V_FCD] NEQ BAS$K_IN_V_FCD) THEN BAS$$SIGNAL (BAS$K_NOTIMP);

!+
! Allocate frame control data.
!-
    FMP = .FP;
    SP = .FMP - BSF$K_LENFCDDFS + %UPVAL;
!+
! LOAD Rn (R10)
!-
    BSF$A_MINOR_STG = .SP - 127;
!+
! Initialize parts of the frame control data.
!-
    FMP [BSF$A_MARK] = 0;
    FMP [BSF$A_BASE_R11] = .BSF$A_MAJOR_STG;
    FMP [BSF$A_BASE_R10] = .BSF$A_MINOR_STG;
    FMP [BSF$B_LEN_FCD] = BSF$K_LENFCDDFS;
    FMP [BSF$B_PROC_CODE] = .ARGLIST [BAS$B_IN_PROC_C];
    FMP [BSF$W_FCD_FLAGS] = .ARGLIST [BAS$W_IN_FLAGS];
    FMP [BSF$A_PROC_ID] = .ARGLIST [BAS$L_IN_PROC_I] + .DATA_RELOC;
    FMP [BSF$A_INIT_ARG] = .ARGLIST;
    FMP [BSF$L_INIT_REL] = .DATA_RELOC;
!+
! Allocate numeric scalars.  They are all initialized to zero.
!-

    INCR COUNTER FROM 1 TO .ARGLIST [BAS$L_IN_LEN_SC] DO
	BEGIN
	SP = .SP - %UPVAL;
	.SP = 0;
	END;

!+
! Copy formals.
!-

    DECR COUNTER FROM MIN (.ARGLIST [BAS$B_IN_NO_FML], ((.AP [0]) AND 255)) TO 1 DO
	BEGIN
	SP = .SP - %UPVAL;
	.SP = .AP [.COUNTER];
	END;

!+
! Allocate and initialize descriptors.
!-
    SP = .SP - .ARGLIST [BAS$L_IN_LEN_DT];
!+
! Set ARRAY_DESC to point to the space allocated.
!-
    ARRAY_DESC = .SP;
!+
! Load the space from the template and then modify it based
! on the modification table.
!-

    INCR COUNTER FROM 0 TO ((.ARGLIST [BAS$L_IN_LEN_DT]^-2) - 1) DO
	BEGIN
	ARRAY_DESC [.COUNTER*%UPVAL, 0, %BPVAL, 0] = .((.ARGLIST [BAS$L_IN_DT_TMT]) + 	!
	.DATA_RELOC + (.COUNTER*%UPVAL));
	END;

!+
! Now modify the descriptors.  These are usually array descriptors.
!-

    INCR COUNTER FROM 0 TO (.ARGLIST [BAS$L_IN_LEN_DM] - 1) DO
	BEGIN
	ARRAY_INDEX = .((.ARGLIST [BAS$L_IN_DT_MOD]) + .DATA_RELOC + (.COUNTER*%UPVAL));
	BSF$A_MINOR_STG [.ARRAY_INDEX, 0, %BPVAL, 0]	!
	= .BSF$A_MINOR_STG [.ARRAY_INDEX, 0, %BPVAL, 0] + .BSF$A_MINOR_STG;
	END;

!+
! Allocate dynamic string descriptors.
!-

    INCR COUNTER FROM 1 TO .ARGLIST [BAS$W_IN_NO_DST] DO
	BEGIN
	SP = .SP - %UPVAL;
	.SP = 0;				! Pointer 0 implies not allocated.
	SP = .SP - %UPVAL;
	BLOCK [.SP, DSC$B_CLASS; 0, BYTE] = DSC$K_CLASS_D;	! dynamic
	BLOCK [.SP, DSC$B_DTYPE; 0, BYTE] = DSC$K_DTYPE_T;	! text
	BLOCK [.SP, DSC$W_LENGTH; 0, BYTE] = 0;	! length = 0
	END;

    FMP [BSF$A_STR_DESC] = .SP;
!+
! Allocate fixed string templates.
!-

    INCR COUNTER FROM 1 TO .ARGLIST [BAS$W_IN_NO_FST] DO
	BEGIN
	SP = .SP - %UPVAL;
	.SP = 0;				! Pointer 0 implies not allocated.
	SP = .SP - %UPVAL;
	BLOCK [.SP, DSC$B_CLASS; 0, BYTE] = DSC$K_CLASS_S;	! fixed
	BLOCK [.SP, DSC$B_DTYPE; 0, BYTE] = DSC$K_DTYPE_T;	! text
	BLOCK [.SP, DSC$W_LENGTH; 0, BYTE] = 0;	! length = 0
	END;

!+
! Allocate numeric array elements.  They are all initialized to zero.
!-

    INCR COUNTER FROM 1 TO (.ARGLIST [BAS$L_IN_LEN_NA]^-2) DO
	BEGIN
	SP = .SP - %UPVAL;
	.SP = 0;
	END;

!+
! Allocate temporary cells.
!-

    IF ((.ARGLIST [BAS$L_IN_NO_TST] NEQ 0) OR (.ARGLIST [BAS$L_IN_NO_NMT] NEQ 0))
    THEN
	BEGIN
!+
! We must set up R9.  First allocate string temporaries.
!-

	INCR COUNTER FROM 1 TO .ARGLIST [BAS$L_IN_NO_TST] DO
	    BEGIN
	    SP = .SP - %UPVAL;
	    .SP = 0;				! Pointer 0 implies not allocated.
	    SP = .SP - %UPVAL;
	    BLOCK [.SP, DSC$B_CLASS; 0, BYTE] = DSC$K_CLASS_D;	! dynamic
	    BLOCK [.SP, DSC$B_DTYPE; 0, BYTE] = DSC$K_DTYPE_T;	! text
	    BLOCK [.SP, DSC$W_LENGTH; 0, BYTE] = 0;	! length = 0
	    END;

!+
! Point R9 to the last string descriptor allocated.
!-
	BSF$A_TEMP_STG = .SP;
!+
! Now allocate numeric temporaries.
!-
	SP = .SP - .ARGLIST [BAS$L_IN_NO_NMT];
	END;

!+
! Store R9 in the stack frame for setting up I/O lists.
!-
    FMP [BSF$A_BASE_R9] = .BSF$A_TEMP_STG;
!+
! Complete frame.
!-
    FMP [BSF$A_BASE_SP] = .SP;
    FMP [BSF$A_HANDLER] = BAS$HANDLER;
!+
! First consistency checks.
!-

    IF (((.AP [0]) AND 255) NEQ .ARGLIST [BAS$B_IN_NO_FML])
    THEN
!+
! The number of arguments is incorrect.
!-
	BEGIN

	IF (((.AP [0]) AND 255) GTRU .ARGLIST [BAS$B_IN_NO_FML])
	THEN
	    BAS$$SIGNAL (BAS$K_TOOMANARG)
	ELSE
	    BAS$$SIGNAL (BAS$K_TOOFEWARG);

	END;

    IF (((.FMP [BSF$W_FCD_FLAGS]) AND (BSF$M_FCD_RSTR)) NEQ 0)
    THEN
	BEGIN

	LOCAL
	    STR_DESC_ADDR : REF BLOCK [8, BYTE];

!+
! This procedure has been marked by the compiler as returning a
! string result.  Be sure that there is at least one formal, and
! that it is a dynamic string descriptor.  If so, null its value.
!-

	IF (.ARGLIST [BAS$B_IN_NO_FML] LSSU 1) THEN BAS$$SIGNAL (BAS$K_TOOFEWARG);

	STR_DESC_ADDR = AP [1];
	STR_DESC_ADDR = ..STR_DESC_ADDR;

	IF ((.STR_DESC_ADDR [DSC$B_CLASS] NEQU DSC$K_CLASS_D) OR 	!
	    (.STR_DESC_ADDR [DSC$B_DTYPE] NEQU DSC$K_DTYPE_T))
	THEN
	    BAS$$SIGNAL (BAS$K_ARGDONMAT);

!+
! Null the string.  This insures that, if the procedure does not reference
! the string, the function will have the value of the null string.
!-
	STR$FREE1_DX (.STR_DESC_ADDR);
	END;

!+
! Put the return address back on the stack so we can return to the
! caller.
!-
    SP = .SP - %UPVAL;
    .SP = .RETURN_ADDRESS;
    RETURN;
    END;					! of BAS$INIT_DFS_R8

END

ELUDOM
