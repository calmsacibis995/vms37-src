	.TITLE	MTH$CABS	COMPLEX ABSOLUTE VALUE
	.IDENT	/1-003/		; File: MTHCABS.MAR

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	Return the absolute value of the COMPLEX number.
;
;
;--
;
; VERSION: 0
;
; HISTORY:
;
; AUTHOR:
;	Jonathan M. Taylor, 19-JUL-77: Version 0
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 0 of MTH$CABS
;
; 0-4	- Add comment about floating overflow.  TNH 13-June-78
; 1-001	- Update version number and copyright notice.  JBS 16-NOV-78
; 1-002	- Add "_" to the PSECT directive.  JBS 21-DEC-78
; 1-003 - Use MTH$SQRT_R3.  SBL 27-Sept=1979

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;	OERR.MAR

;
; EXTERNAL SYMBOLS:
	.GLOBL	MTH$SQRT_R3

;
; MACROS:
;	NONE

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
	argadr	=	4	; offset from AP of arg addr

;
; OWN STORAGE:
;	NONE

	.SBTTL	MTH$CABS - COMPLEX absolute value

;++
; FUNCTIONAL DESCRIPTION:
;
;	MTH$CABS computes the absolute value of a COMPLEX number (r, i)
;	as follows:
;
;	result = ABS(MAX*SQRT((MIN/MAX)**2 + 1))
;
; CALLING SEQUENCE:
;	Absolute_value.wf.v	= MTH$CABS (arg.rfc.r)
;
;
; INPUT PARAMETERS:
;	The one input parameter is the address of a COMPLEX number (r, i), 
;	where r and i are both single-precision floating values.
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	SS$_ROPRAND	- Reserved operand
;	SS$_FLTOVF(_F)	- Floating overflow trap (fault)
;--


	.ENTRY	MTH$CABS,	^M<R2,R3,R4>
	MTH$FLAG_JACKET			; resignal
	MOVQ	@argadr(AP), R0		; R0/R1 = (r, i)
	BICW3	#^X8000, R0, R2		; R2 = ABS(r)
	BICW3	#^X8000, R1, R3		; R3 = ABS(i)
	CMPW	R2, R3			; compare the magnitudes of r and i
	BLSS	REALLO			; and branch if r is smaller
	TSTF	R2			; quit if r = 0
	BEQL	ZERO
	MOVL	R1, R2			; else swap so R0 is smaller
	MOVL	R0, R1			; R1 = larger part (r)
	MOVL	R2, R0			; R0 = smaller part (i)

REALLO:

; at this point R0 contains MIN (the smaller of |r| and |i|), and
; R1 contains MAX (the larger of |r| and |i|).
;
	DIVF	R1, R0			; R0 = MIN/MAX
	MULF	R0, R0			; R0 = (MIN/MAX)**2
	ADDF	#1.0, R0		; R0 = (MIN/MAX)**2 + 1
	MOVL	R1, R4			; R4 = MAX
	JSB	MTH$SQRT_R3		; R0 = SQRT((MIN/MAX)**2 + 1)
	MULF	R4, R0			; R0 = MAX*SQRT((MIN/MAX)**2 + 1)
					; Floating overflow could happen
	BICW	#^X8000, R0		; R0 = ABS(....)
	RET

ZERO:
	CLRL	R0			; answer is 0
	RET



	.END
