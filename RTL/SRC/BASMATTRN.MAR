	.TITLE	BAS$MAT_TRN
	.IDENT	/1-005/			; File: BASMATTRN.MAR	Edit: PLL1005

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: BASIC code support
;
; ABSTRACT:
;
;	This module writes the transpose of a matrix into a second matrix.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: R. Will, CREATION DATE: 10-Jul-79
;
; MODIFIED BY:
;++
; 1-001	- Original 
; 1-002 - Fix test for 'same array' for virtual.  RW  15-Feb-1980
; 1-003 - Change shared external references to G^ RNH 25-Sep-81
; 1-004 - Substitute a macro for the calls to the array fetch and store
;	  routines.  This should speed things up.  PLL 9-Nov-81
; 1-005 - Correct a run-time expression in the FETCH and STORE macros.
;	  PLL 20-Jan-82
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

	$DSCDEF					; define descriptor offsets
	$SFDEF					; use to get scale

;
; EXTERNAL DECLARATIONS:
;

	.DSABL	GBL				; Prevent undeclared
						;  symbols from being
						;  automatically global.
	.EXTRN	BAS$K_ARGDONMAT			; signalled if all 3 blocks
						;  not present in array desc
						;  or dimct = 0
	.EXTRN	BAS$K_DATTYPERR			; signalled if dtype of array
						; isn't word long float double
	.EXTRN	BAS$K_MATDIMERR			; signalled if src matrix has
						;  only 1 dimension
	.EXTRN	BAS$K_ILLOPE			; signalled if DSC$A_POINTER is
						;  same in src and dest matrices
	.EXTRN	BAS$STO_FA_W_R8			; array element store for word
	.EXTRN	BAS$STO_FA_L_R8			; array element store for long
	.EXTRN	BAS$STO_FA_F_R8			; array element store - float
	.EXTRN	BAS$STO_FA_D_R8			; array element store - double
	.EXTRN	BAS$FET_FA_W_R8			; array element fetch - word
	.EXTRN	BAS$FET_FA_L_R8			; array element fetch - long
	.EXTRN	BAS$FET_FA_F_R8			; array element fetch - float
	.EXTRN	BAS$FET_FA_D_R8			; array element fetch - double
	.EXTRN	BAS$MAT_REDIM			; check if redimensioning of
						;  dest array is necessary, if
						;  so, do it
	.EXTRN	BAS$$SCALE_R1			; scale the double procision
	.EXTRN	BAS$$STOP			; signal fatal errors

;
; MACROS:
;

;	$BAS$MAT_TRN	transpose loop algorithm,  see next page
;	FETCH		fetch an element from an array
;	STORE		store an element into an array

;
; EQUATED SYMBOLS:
;

	first_arg = 4				; arg offset for str copy
	second_arg = 8				; arg offset for str copy
	index1 = 12				; stack offset for str copy
	index2 = 16				; stack offset for str copy
	temp_desc = 20				; stack offset for str copy
	lower_bnd2 = 0				; stack offset for temp
	lower_bnd1 = 4				; stack offset for temp
	upper_bnd1 = 8				; stack offset for temp
	dsc$l_l1_1 = 24				; desc offset if 1 sub
	dsc$l_u1_1 = 28				; desc offset if 1 sub
	dsc$l_l1_2 = 28				; desc offset if 2 sub
	dsc$l_u1_2 = 32				; desc offset if 2 sub
	dsc$l_l2_2 = 36				; desc offset if 2 sub
	dsc$l_u2_2 = 40				; desc offset if 2 sub

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT _BAS$CODE PIC, USR, CON, REL, LCL, SHR, -
			EXE, RD, NOWRT, LONG


;+
; These macros are a substitute for calls to the array fetch and store
; routines.  They will call the BAS$ routines only if the array is a 
; virtual array.  Otherwise, they will calculate the linear index into
; the array via the INDEX instruction.  (Note that BASIC programs must
; be able to handle FORTRAN arrays, so the code must check for arrays
; stored by column.)  The INDEX instructions should provide a significant
; performance improvement over calling a routine for each element of
; the array.
;-

	.LIST	ME
	.MACRO	FETCH	array_dtype,?L1,?L2,?L3	; fetch an array element

	CMPB	dsc$b_class(R0), #dsc$k_class_bfa  ;virtual array?
	BNEQ	L1				; no
	JSB	BAS$FET_FA_'array_dtype'_R8	; yes, use the fetch routine
	BRW	L3				; done
L1:	BBS	#5, 10(R0), L2		; br if array stored by cols
	INDEX	R1, dsc$l_l1_2(R0), dsc$l_u1_2(R0), dsc$l_m2(R0), #0, R3
						; I * M2  [Li's are zero]
	MOVZWL	dsc$w_length(R0), R4		; need longword length for INDEX
	INDEX	R2, dsc$l_l2_2(R0), dsc$l_u2_2(R0), R4, R3, R3
						; (J + (I * M2)) * length 
	ADDL	dsc$a_a0(R0), R3		; compute addr of element 
	MOV'array_dtype'	(R3), R0	; return element in R0
	BRW	L3				; done
L2:	INDEX	R2, dsc$l_l2_2(R0), dsc$l_u2_2(R0), dsc$l_m1(R0), #0, R3
						; J * M1  [Li's are zero]
	MOVZWL	dsc$w_length(R0), R4		; need longword length for INDEX
	INDEX	R1, dsc$l_l1_2(R0), dsc$l_u1_2(R0), R4, R3, R3
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R0), R3		; compute addr of element
	MOV'array_dtype'	(R3), R0	; return element in R0

L3:
	.ENDM

	.LIST	ME
	.MACRO	STORE	array_dtype,?L1,?L2,?L3,?L4,?L5

	.IF	IDN	'array_dtype', D	; array is double
	CMPB	dsc$b_class(R2), #dsc$k_class_bfa  ; virtual array?
	BNEQ	L1				; no
	JSB	BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L5				; done
L1:	BBS	#5, 10(R2), L2			; br if stored col-wise
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), dsc$l_m2(R2), #0, R5
						; I * M2
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), R6, R5, R5
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L5				; done
L2:	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), dsc$l_m1(R2), #0, R5
						; J * M1
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), R6, R5, R5
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	.IFF					; array type other than double
	CMPB	dsc$b_class(R1), #dsc$k_class_bfa  ;virtual array?
	BNEQ	L3				; no
	JSB	BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L5				; done
L3:	BBS	#5, 10(R1), L4			; br if stored col-wise
	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), dsc$l_m2(R1), #0, R4
						; I * M2
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), R5, R4, R4
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L5				; done
L4:	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), dsc$l_m1(R1), #0, R4
						; J * M1
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), R5, R4, R4
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	.ENDC

L5:
	.ENDM

;+
; This macro contains the looping mechanism for accessing all elements of
; an array.  It also contains all the logic for all the combinations of data
; types and scaling.  A macro is used to make it easy to maintain the parallel
; code for all the different data types.
;-

	.LIST	ME
	.MACRO	$BAS$MAT_TRN src_dtype, dest_dtype ; transpose algorithm

;+
; Loop through all the rows.  Row and column upper and lower bounds have been
; initialized on the stack.
;-

LOOP_1ST_SUB'src_dtype'dest_dtype':
	MOVL	lower_bnd2(SP), R11		; R11 has 2nd lower bound

;+
; Loop through all the elements (columns) of the current row.  Column lower
; bound is initialized in R11.  Column upper bound is on the stack.
; Distinguish array by data type so that the correct fetch routine can 
; retrieve the data, the correct conversion can be done and the correct
; store routine can be called.
;-

LOOP_2ND_SUB'src_dtype'dest_dtype':

;+
; Get the data from source array
;-

	MOVL	R10, R0				; pointer to array dest
	MOVL	lower_bnd1(SP), R1		; current row
	MOVL	R11, R2				; current col
	FETCH	'src_dtype'			; fetch data from src array

;+
; If the data types of the source and destination arrays is different,
; convert the data to the destination type.  If scaling is needed (ie if
; at least one but not both of the arrays is double) scale the data.
;-

	.IF	DIF	src_dtype, dest_dtype	; src and dest arrays are not
						; save data type
	.IF	IDN	src_dtype, D		; source is double
	MOVD	R0, -(SP)			; save the data
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	DIVD3	R0, (SP)+, R0			; scale
	.ENDC
	CVT'src_dtype'dest_dtype'	R0, R0	; convert data from R0 into R0
						;  R0 & R1 for double
	.IF	IDN	dest_dtype, D		; dest is double
	MOVD	R0, -(SP)			; save the data
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	MULD2	(SP)+, R0			; scale, NOTE that integerize
						;  is not required after mul
						;  because data was already
						;  an integer (dbl to dbl not
						;  in this cond code and
						;  float to dbl not allowed)
	.ENDC
	.ENDC

;+
; Now store the data in the destination array.
; Code is same for all dtypes except double, since double passed by value
; takes 2 words and all other supported dtypes by value takes 1 longword
;-

	.IF	IDN	dest_dtype, D		; see if dtype is double
	.IFF					; no, word, long, float
	MOVL	dest_matrix(AP), R1		; pointer to array desc
	MOVL	lower_bnd1(SP), R3		; current row, put in col
	MOVL	R11, R2				; current column, put in row
	.IFT					; type is double
	MOVL	dest_matrix(AP), R2		; pointer to array desc
	MOVL	lower_bnd1(SP), R4		; current row, put in col
	MOVL	R11, R3				; current column, put in row
	.ENDC					; code now same for all dtypes
	STORE	'dest_dtype'			; store in array
	INCL	R11				; get next column
	CMPL	R11, R9				; see if last column done
	BGTR	3$
	BRW	LOOP_2ND_SUB'src_dtype'dest_dtype' ; no, continue inner loop

;+
; Have completed entire row.  See if it was the last row.  If not,
; continue with next row.
;-

3$:	INCL	lower_bnd1(SP)			; get next row
	CMPL	lower_bnd1(SP), upper_bnd1(SP)	; see if last row done
	BGTR	5$
	BRW	LOOP_1ST_SUB'src_dtype'dest_dtype' ; no, continue outer loop

5$:	RET					; yes, finished

	.ENDM

	.SBTTL	BAS$MAT_TRN  - Transpose one matrix into another
;++
; FUNCTIONAL DESCRIPTION:
;
;	Transpose one matrix into another.  If the src matrix has 2 dimensions,
;	redimension the output matrix to have the number of rows that the src
;	has columns and the number of columns that the src has rows.  (thereby
;	ensuring that the dest matrix also has 2 dimensions).  Initialize all
;	the necessary looping information on the stack.  Conversions will have
;	to be done from the source data type to the destination data type, so
;	divide the looping portion according to the data types.
;
; CALLING SEQUENCE:
;
;	CALL BASMAT_TRN (src_matrix.rx.da, dest_matrix.wx.da)
;
; INPUT PARAMETERS:
;
	src_matrix = 4
;
; IMPLICIT INPUTS:
;
;	Scale from the callers frame to scale double precision.
;
; OUTPUT PARAMETERS:
;
	dest_matrix = 8
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	This routine calls the redimensioning routine and the array element
;	fetch and store routines and therefore may signal any of their errors.
;	It may also signal any of the errors listed in the externals section.
;	It may also cause the destination array to have different dimensions.
;
;--

	.ENTRY BAS$MAT_TRN, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,IV>

;+
;	REGISTER USAGE
;	R0 - R8 destroyed by fetch and store routines
;	R9	upper bound for 2nd subscript
;	R10	pointer to array descriptor
;	R11	current value of 2nd subscript
;-

;+
; Put routine arguments into registers for ease of use.
; If block 3 of array descriptor (bounds) is not present then error.
;-

	MOVL	src_matrix(AP), R10		; ptr to array descr in R10
	BBC	#DSC$V_FL_BOUNDS, DSC$B_AFLAGS(R10), ERR_ARGDONMAT
						; exit if block 3 not
						; present in descriptor

;+
; Set up limits for looping through all elements
;-

	CMPB	DSC$B_DIMCT(R10), #2		; determine # of subscripts
	BEQLU	INIT_SUBS			; 2 subs, initialize loop
						; not 2 subs, fall into error

ERR_MATDIMERR:
	PUSHL	#BAS$K_MATDIMERR		; signal error, not 2 dims
	CALLS	#1, G^BAS$$STOP			;  for src matrix

ERR_ARGDONMAT:
	PUSHL	#BAS$K_ARGDONMAT		; signal error,
	CALLS	#1, G^BAS$$STOP			;  block 2 or 3 absent

ERR_ILLOPE:
	PUSHL	#BAS$K_ILLOPE			; signal error, DSC$A_POINTER
	CALLS	#1, G^BAS$$STOP			;  same for src & dest matrices

;+
; There are 2 subscripts.  Make sure the source and dest matrices are not the
; same.  Check and redimension the destination array if
; necessary.  Put the upper bound for both subscripts on the
; stack and make sure that the lower bound for both subscripts will start
; at 1 (do not alter row or col 0)
;-

INIT_SUBS:
	MOVL	dest_matrix(AP), R11		; get pointer to dest matrix
	CMPB	DSC$B_CLASS(R10), #DSC$K_CLASS_A ; is src virtual
	BNEQU	VIRTUAL				; src is virtual go process
	CMPB	DSC$B_CLASS(R11), #DSC$K_CLASS_A ; is dest virtual?
	BNEQU	INIT_SUBS_2			; yes, cant be same array
	CMPL	DSC$A_POINTER(R10), DSC$A_POINTER(R11) ; are matrices the same?
	BEQLU	ERR_ILLOPE			; yes, error
	BRB	INIT_SUBS_2
VIRTUAL:
	CMPB	DSC$B_CLASS(R11), #DSC$K_CLASS_A ; is dest virtual
	BEQLU	INIT_SUBS_2			; no, cant be same array
	CMPL	DSC$L_LOGUNIT(R10), DSC$L_LOGUNIT(R11) ; are matrices same
	BNEQ	INIT_SUBS_2			; no
	CMPL	DSC$L_BYTEOFF(R10), DSC$L_BYTEOFF(R11) ; are the matrices
						; the same, (test for same by
						;  same dsc$l_byteoff, dsc$l_logunit)
	BEQL	ERR_ILLOPE			; yes, error
INIT_SUBS_2: 
	PUSHL	dsc$l_u1_2(R10)			; 2nd upr bnd, make 1st in dest
	PUSHL	dsc$l_u2_2(R10)			; 1st upr bnd, make 2nd in dest
	PUSHL	R11				; dest array pointer
	CALLS	#3, BAS$MAT_REDIM		; redimension destination
	PUSHL	dsc$l_u1_2(R10)			; 1st upper bound
	PUSHL	dsc$l_l1_2(R10)			; 1st lower bound
	BGTR	1$				; not row 0 or neg, do cols
	MOVL	#1, (SP)			; start with row 1
1$:	MOVL	dsc$l_u2_2(R10), R9		; 2nd upper bound
	PUSHL	dsc$l_l2_2(R10)			; 2nd lower bound
	BGTR	SEPARATE_DTYPES			; not col 0 or neg, go loop
	MOVL	#1, (SP)			; start with col 1

;+
; Algorithm now differs according to data types
;-

SEPARATE_DTYPES:
	CASEB	DSC$B_DTYPE(R10), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
2$:	.WORD	WORD-2$				; code for word dtype
	.WORD	LONG-2$				; code for long dtype
	.WORD	ERR_DATTYPERR-2$		; quad not supported
	.WORD	FLOAT-2$			; code for float dtype
	.WORD	DOUBLE-2$			; code for double dtype

ERR_DATTYPERR:
	PUSHL	#BAS$K_DATTYPERR		; Signal error, unsupported
	CALLS	#1, G^BAS$$STOP			; dtype in array desc

	.PAGE
;+
; Source array is a word array.  Now differentiate on the destination type.
;-

WORD:	CASEB	DSC$B_DTYPE(R11), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	WORD_TO_WORD-1$			; code for word dtype
	.WORD	WORD_TO_LONG-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	WORD_TO_FLOAT-1$		; code for float dtype
	.WORD	WORD_TO_DOUBLE-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
WORD_TO_WORD:	$BAS$MAT_TRN	W, W

	.PAGE
WORD_TO_LONG:	$BAS$MAT_TRN	W, L

	.PAGE
WORD_TO_FLOAT:	$BAS$MAT_TRN	W, F

	.PAGE
WORD_TO_DOUBLE:	$BAS$MAT_TRN	W, D

	.PAGE
;+
; Source array is a longword array.  Now differentiate on the destination type
;-

LONG:	CASEB	DSC$B_DTYPE(R11), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	LONG_TO_WORD-1$			; code for word dtype
	.WORD	LONG_TO_LONG-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	LONG_TO_FLOAT-1$		; code for float dtype
	.WORD	LONG_TO_DOUBLE-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
LONG_TO_WORD:	$BAS$MAT_TRN	L, W

	.PAGE
LONG_TO_LONG:	$BAS$MAT_TRN	L, L

	.PAGE
LONG_TO_FLOAT:	$BAS$MAT_TRN	L, F

	.PAGE
LONG_TO_DOUBLE:	$BAS$MAT_TRN	L, D

	.PAGE
;+
; Source array is a floating array.  Now differentiate on the destination type
;-

FLOAT:	CASEB	DSC$B_DTYPE(R11), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	FLOAT_TO_WORD-1$		; code for word dtype
	.WORD	FLOAT_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	FLOAT_TO_FLOAT-1$		; code for float dtype
	.WORD	ERR_DATTYPERR-1$		; float unsupported w/ double
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
FLOAT_TO_WORD:	$BAS$MAT_TRN	F, W

	.PAGE
FLOAT_TO_LONG:	$BAS$MAT_TRN	F, L

	.PAGE
FLOAT_TO_FLOAT:	$BAS$MAT_TRN	F, F

	.PAGE
;+
; Source array is a double array.  Now differentiate on the destination type.
;-

DOUBLE:	CASEB	DSC$B_DTYPE(R11), #DSC$K_DTYPE_W, #<DSC$K_DTYPE_D - DSC$K_DTYPE_W>
1$:	.WORD	DOUBLE_TO_WORD-1$		; code for word dtype
	.WORD	DOUBLE_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	ERR_DATTYPERR-1$		; float unsupported w/ double
	.WORD	DOUBLE_TO_DOUBL-1$		; code for double dtype
	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
DOUBLE_TO_WORD:	$BAS$MAT_TRN	D, W

	.PAGE
DOUBLE_TO_LONG:	$BAS$MAT_TRN	D, L

	.PAGE
DOUBLE_TO_DOUBL: $BAS$MAT_TRN	D, D

	.END					; end of BAS$MAT_TRN
