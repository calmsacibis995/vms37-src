	.TITLE	MTH$CDEXP	D COMPLEX*16 Exponential
	.IDENT	/1-002/		; File: MTHCDEXP.MAR Edit:RNH1002

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY: MATH LIBRARY
;++
; ABSTRACT:
;	Perform D COMPLEX*16 exponentiation: e**(r,i)
;
;
;--
;
; VERSION: 1
;
; HISTORY:
;
; AUTHOR:
;	Steven B. Lionel, 20-July-1979
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History
;
; 1-001 - Adapted from MTH$CEXP version 1-002.  SBL 20-July-1979
; 1-002 - Changed shared external references to G^ RNH 25-Sep-81

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;

;
; EXTERNAL SYMBOLS:
	.DSABL	GBL
	.EXTRN	MTH$DSIN_R7
	.EXTRN	MTH$DCOS_R7
	.EXTRN	MTH$DEXP_R7

;
; MACROS:
;	NONE

;
; PSECT DECLARATIONS:
	.PSECT	_MTH$CODE	PIC, SHR, LONG, EXE, NOWRT

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;	NONE

	.SBTTL	MTH$CDEXP - perform D COMPLEX*16 exponentiation

;++
; FUNCTIONAL DESCRIPTION:
;
;	The result of the operation e ** (r, i) is computed
;	by:
;
;	result = (EXP(r) * COS(i), EXP(r) * SIN(i))
;
; CALLING SEQUENCE:
;	CALL MTH$CDEXP (result.wdc.r, arg.rdc.r)
;
;
; INPUT PARAMETERS:
	arg	= 8		; D COMPLEX*16 argument by reference
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
	result	= 4		; D COMPLEX*16 result by reference
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	Signals:	MTH$_SINSIGLOS	if |i| > 2*PI*2**31.
;			Floating Overflow if r > 88.028
;
;--


	.ENTRY	MTH$CDEXP,	^M<R2,R3,R4,R5,R6,R7>
	MTH$FLAG_JACKET			; resignal

	MOVQ	@arg(AP), R0		; R0-R1 = real part
	JSB	G^MTH$DEXP_R7		; R0-R1 = EXP(r)
	MOVQ	R0, -(SP)		; Save it on the stack

	MOVL	arg(AP), R0		; R0 is address of arg

	MOVQ	8(R0), R0		; R0-R1 = imaginary part
	MOVQ	R0,-(SP)		; Save imaginary part
	JSB	G^MTH$DCOS_R7		; R0-R1 = COS(i)
	MULD3	R0, 8(SP), @result(AP)	; Store real part
	MOVQ	(SP)+, R0		; Get imaginary part again
	JSB	G^MTH$DSIN_R7		; R0-R1 = SIN(i)
	MOVL	result(AP), R2		; Address of result
	MULD3	R0, (SP)+, 8(R2)	; Store imaginary part
	RET


	.END

