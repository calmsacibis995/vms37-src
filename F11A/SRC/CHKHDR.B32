MODULE CHKHDR (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 1
!
! ABSTRACT:
!
!	This routine verifies that the block given it is in fact a
!	file header. If file number and/or file seqence number are also
!	supplied, they are checked as well.
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  13-Dec-1976  16:11
!
! MODIFIED BY:
!
!	A0101	ACG0146		Andrew C. Goldstein,	22-Feb-1980  21:42
!		Change file sequence number check to no such file
!
!	A0100	ACG00001	Andrew C. Goldstein,  10-Oct-1978  20:02
!	Previous revision history moved to F11A.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';

GLOBAL ROUTINE CHECK_HEADER (HEADER, FILE_ID) =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine verifies that the block given it is in fact a
!	file header. If file number and/or file seqence number are also
!	supplied, they are checked as well.
!
! CALLING SEQUENCE:
!	CHECK_HEADER (ARG1, ARG2)
!
! INPUT PARAMETERS:
!	ARG1: address of header image
!	ARG2: address of file ID
!
! IMPLICIT INPUTS:
!	NONE
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	USER_STATUS contains code if not valid
!
! ROUTINE VALUE:
!	0 if garbage
!	1 if valid and correct file header
!	2 if deleted file header
!
! SIDE EFFECTS:
!	NONE
!
!--

BEGIN

MAP
	HEADER		: REF BBLOCK,	! file header arg
	FILE_ID		: REF BBLOCK;	! file ID arg

LOCAL
	MAP_AREA	: REF BBLOCK;	! pointer to header map area

EXTERNAL ROUTINE
	CHECKSUM;			! compute file header checksum


! First check the structure level.
!

IF .HEADER[FH1$W_STRUCLEV] NEQ FH1$C_LEVEL1
THEN (ERR_STATUS (SS$_FILESTRUCT); RETURN 0);

! Now point to the map area and make sure that the extension
! RVN is zero (no multi-volume supported yet.)
! Also check the retrieval pointer format data.
!

MAP_AREA = .HEADER + .HEADER[FH1$B_MPOFFSET]*2;

IF .MAP_AREA[FM1$B_EX_RVN] NEQ 0
OR .MAP_AREA[FM1$B_COUNTSIZE] NEQ 1
OR .MAP_AREA[FM1$B_LBNSIZE] NEQ 3
THEN (ERR_STATUS (SS$_FILESTRUCT); RETURN 0);

! Check the retrieval pointer counts for consistency with the
! available space.
!

IF .MAP_AREA[FM1$B_INUSE] GTRU .MAP_AREA[FM1$B_AVAIL]
OR .MAP_AREA[FM1$B_AVAIL] GTRU
    255 - (.MAP_AREA + FM1$C_POINTERS - .HEADER) / 2
THEN (ERR_STATUS (SS$_BADFILEHDR); RETURN 0);

! At this point, we have verified that the block at least once was a
! valid file header.
!
! Look at the file number in the header. If zero, this is a 
! deleted header.
!

IF .HEADER[FH1$W_FID_NUM] EQL 0
THEN (ERR_STATUS (SS$_NOSUCHFILE); RETURN 2);

! Now compute the header checksum.
!

IF NOT CHECKSUM (.HEADER)
THEN (ERR_STATUS (SS$_BADCHKSUM); RETURN 2);

! Check file number and file sequence number.
!

IF .HEADER[FH1$W_FID_NUM] NEQ .FILE_ID[FID$W_NUM]
THEN (ERR_STATUS (SS$_FILENUMCHK); RETURN 2);

IF .HEADER[FH1$W_FID_SEQ] NEQ .FILE_ID[FID$W_SEQ]
THEN (ERR_STATUS (SS$_NOSUCHFILE); RETURN 2);

! Header is ok.
!

RETURN 1;

END;					! end of routine CHECK_HEADER

END
ELUDOM
