MODULE CLENUP (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 1
!
! ABSTRACT:
!
!	This module performs the necessary cleanup after an operation.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  6-Jan-1977  23:53
!
! MODIFIED BY:
!
!	V02-008	LMP0005		L. Mark Pilant,		29-Dec-1981  14:00
!		Properly cleanup any Cathedral windows.
!
!	V02-007	ACG0246		Andrew C. Goldstein,	22-Dec-1981  20:44
!		Make /NOCACHE flush all caches
!
!	V02-006	ACG0245		Andrew C. Goldstein,	18-Dec-1981  18:29
!		Send spool file to print during cleanup
!
!	V02-005	ACG0244		Andrew C. Goldstein,	18-Dec-1981  17:42
!		Do buffer flush before deallocating control blocks
!
!	V02-004	ACG0167		Andrew C. Goldstein,	7-May-1980  18:47
!		Previous revision history moved to F11A.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';


FORWARD ROUTINE
	CLEANUP,			! normal cleanup
	ZERO_WINDOWS,			! invalidate all windows of file
	ERR_CLEANUP,			! cleanup after error
	MAKE_DEACCESS,			! deaccess the file
	DEL_EXTFCB,			! deallocate extension FCB's
	ZERO_CHANNEL;			! zero user channel pointer

GLOBAL ROUTINE CLEANUP =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine performs the cleanup needed after a successfully
!	completed file operation.
!
! CALLING SEQUENCE:
!	CLEANUP ()
!
! INPUT PARAMETERS:
!	NONE
!
! IMPLICIT INPUTS:
!	CLEANUP_FLAGS: indicate specific actions to do
!	PRIMARY_FCB: FCB of file
!	CURRENT_WINDOW: window of file
!	DIR_FCB: FCB of directory
!	DIR_WINDOW: window of directory
!	CURRENT_VCB: VCB of volume in process
!	IO_PACKET: I/O packet of request
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	FCB's and windows deleted when appropriate
!	header written
!	FCB updated
!
!--

BEGIN

LOCAL
	HEADER		: REF BBLOCK;	! file header

EXTERNAL
	CONTEXT_START,			! start of cleanup context area
	CONTEXT_SAVE,			! start of context save area
	CLEANUP_FLAGS	: BITVECTOR,	! cleanup action flags
	FILE_HEADER	: REF BBLOCK,	! address of last file header read
	CURRENT_FIB	: REF BBLOCK,	! address of current FIB in use
	PRIMARY_FCB	: REF BBLOCK,	! FCB of file
	CURRENT_WINDOW	: REF BBLOCK,	! window of file
	DIR_FCB		: REF BBLOCK,	! FCB of directory
	DIR_WINDOW	: REF BBLOCK,	! window of directory
	CURRENT_VCB	: REF BBLOCK,	! VCB of volume
	IO_PACKET	: REF BBLOCK;	! I/O packet in process

EXTERNAL LITERAL
	CONTEXT_SIZE;			! length of context area

EXTERNAL ROUTINE
	FLUSH_BUFFERS,			! write all dirty buffers
	FLUSH_FID,			! flush a file from buffer pool
	READ_HEADER,			! read file header
	INIT_FCB,			! initialize FCB
	DEALLOCATE;			! deallocate dynamic memory


! Switch back to the primary context area if necessary (no normal cleanup
! is ever necessary on secondary context).
!

IF .CONTEXT_SAVE NEQ 0
THEN
    BEGIN
    CH$MOVE (CONTEXT_SIZE, CONTEXT_SAVE, CONTEXT_START);
    CONTEXT_SAVE = 0;
    END;

! ***** Note: The primary header of the current file is not necessarily
! resident at this point.
!
! If the index file or storage map is write accessed, flush the buffer pool
! of any of their blocks.
!

IF .CURRENT_VCB[VCB$V_WRITE_IF]
THEN FLUSH_FID (UPLIT WORD (1, 1, 0));
IF .CURRENT_VCB[VCB$V_WRITE_SM]
THEN FLUSH_FID (UPLIT WORD (2, 2, 0));

! If the volume is mounted /NOCACHE, flush all buffers from the buffer
! pool.
!

IF .CURRENT_VCB[VCB$V_NOCACHE]
THEN FLUSH_FID (0);

! Flush all dirty buffers.
!

FLUSH_BUFFERS ();

! If a directory FCB and window are left about with no use, dispose of them.
! If the directory file is write accessed, flush the buffer pool of any
! blocks that might be resident.
!

IF .DIR_FCB NEQ 0
THEN
    BEGIN
    IF .DIR_FCB[FCB$W_ACNT] EQL 0
    THEN
	BEGIN
	IF NOT .DIR_FCB[FCB$V_DIR]
	THEN
	    BEGIN
	    KERNEL_CALL (DEALLOCATE, .DIR_FCB);
	    DIR_FCB = 0;
	    END;
	END
    ELSE
	BEGIN
	IF .DIR_FCB[FCB$W_WCNT] NEQ 0
	THEN FLUSH_FID (DIR_FCB[FCB$W_FID]);
	END;
    END;

IF .DIR_WINDOW NEQ 0
THEN
    BEGIN
    KERNEL_CALL (DEALLOCATE, .DIR_WINDOW);
    DIR_WINDOW = 0;
    END;

! If an FCB is left about with no use, dispose of it.
!

IF .PRIMARY_FCB NEQ 0
THEN
    BEGIN
    IF .PRIMARY_FCB[FCB$W_ACNT] EQL 0
    AND NOT .PRIMARY_FCB[FCB$V_DIR]
    THEN
	BEGIN
	KERNEL_CALL (DEALLOCATE, .PRIMARY_FCB);
	PRIMARY_FCB = 0;
	END;
    END;

! Invalidate any windows on the file, if requested.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_INVWINDOW])
AND .PRIMARY_FCB NEQ 0
THEN KERNEL_CALL (ZERO_WINDOWS, .PRIMARY_FCB);

RETURN 1;

END;					! end of routine CLEANUP

GLOBAL ROUTINE ZERO_WINDOWS (FCB) =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine invalidates all windows currently in use on the
!	indicated FCB. This routine must be executed in kernel mode.
!
! CALLING SEQUENCE:
!	ZERO_WINDOWS (ARG1)
!
! INPUT PARAMETERS:
!	ARG1: address of FCB
!
! IMPLICIT INPUTS:
!	CURRENT_WINDOW: address of caller's window, if any
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	all windows marked empty, caller's turned
!
!--

BEGIN

MAP
	FCB		: REF BBLOCK;

LOCAL
	P		: REF BBLOCK,	! window pointer
	DUMMY,				! dummy sturage for REMQUE return
	WINDOW_SEGMENT	: REF BBLOCK,	! pointer to window segment
	NEXT_SEGMENT	: REF BBLOCK;	! pointer to window after next one


EXTERNAL ROUTINE
	DEALLOCATE;			! deallocate dynamic memory

! Loop through the window list off the FCB, zeroing all the retrieval pointer
! counts. Then turn the user's window to VBN 1 if it exists.
!

P = .FCB[FCB$L_WLFL];

UNTIL .P EQL FCB[FCB$L_WLFL] DO
    BEGIN
    P[WCB$W_NMAP] = 0;
    WINDOW_SEGMENT = .P[WCB$L_LINK];
    UNTIL .WINDOW_SEGMENT EQL 0
    DO
	BEGIN
	NEXT_SEGMENT = .WINDOW_SEGMENT[WCB$L_LINK];
	REMQUE (.WINDOW_SEGMENT, DUMMY);
	DEALLOCATE (.WINDOW_SEGMENT);
	WINDOW_SEGMENT = .NEXT_SEGMENT;
	END;
    P[WCB$L_LINK] = 0;
    P[WCB$V_COMPLETE] = 0;
    P = .P[WCB$L_WLFL];
    END;

! ***** Note: When handling of window misses goes into its final form,
! this routine must also scan the I/O queue on the UCB and look for I/O
! into the blocks just deallocated. All such requests must be yanked out
! of the queue and routed to the ACP for error processing.

RETURN 1;

END;					! end of routine ZERO_WINDOWS

GLOBAL ROUTINE ERR_CLEANUP =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine performs the cleanup needed after a file
!	operation that has terminated in an error.
!
! CALLING SEQUENCE:
!	ERR_CLEANUP ()
!
! INPUT PARAMETERS:
!	NONE
!
! IMPLICIT INPUTS:
!	CLEANUP_FLAGS: indicate specific actions to do
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	file deaccessed if necessary
!	channel window pointer cleared
!
!--

BEGIN

EXTERNAL
	PMS_SUB_NEST,			! depth count on subfunction metering
	CONTEXT_START,			! start of active context area
	CONTEXT_SAVE,			! start of context save area
	CLEANUP_FLAGS	: BITVECTOR,	! cleanup action flags
	UNREC_COUNT,			! count of unrecorded but allocated blocks
	UNREC_LBN,			! LBN of above
	NEW_FID,			! file number of unrecorded file ID
	USER_STATUS	: VECTOR,	! user I/O status block
	SUPER_FID	: BBLOCK,	! file ID of superseded file
	SECOND_FIB	: BBLOCK,	! FIB for secondary file operation
	CURRENT_FIB	: REF BBLOCK,	! pointer to FIB currently in use
	FILE_HEADER	: REF BBLOCK,	! current file header
	PRIMARY_FCB	: REF BBLOCK,	! FCB of this file
	CURRENT_WINDOW	: REF BBLOCK,	! window for this file
	IO_PACKET	: REF BBLOCK,	! I/O packet for this operation
	DIR_RECORD;			! record number of directory entry

EXTERNAL LITERAL
	CONTEXT_SIZE;			! length of context area

EXTERNAL ROUTINE
	PMS_END_SUB,			! end metering of current subfunction
	DEALLOCATE,			! deallocate dynamic memory
	SEND_SYMBIONT,			! send file to job controller
	DIRGET,				! read directory record
	DIRPUT,				! write directory record
	DELETE_FILE,			! delete a file
	DELETE_FID,			! delete a file number
	RETURN_BLOCKS,			! return blocks to storage map
	TRUNCATE,			! file truncate routine
	INVALIDATE,			! invalidate a buffer
	READ_HEADER,			! read file header
	INIT_FCB,			! initialize FCB
	UPDATE_FCB,			! update FCB contents
	NEXT_HEADER,			! read next extension file header
	MAKE_EXTFCB,			! create extension FCB
	CHECKSUM,			! checksum file header
	REMAP_FILE;			! rebuild the windows for a file


! If a subfunction was being executed, turn off metering now.
!

IF .PMS_SUB_NEST NEQ 0
THEN
    BEGIN
    PMS_SUB_NEST = 1;
    PMS_END_SUB ();
    END;

! We repeat the entire procedure twice if a secondary file operation was
! in progress (indicated by non-zero saved context).
!

WHILE 1 DO
BEGIN

! Locals are declared here to prevent their scope from extending around the
! entire main loop and raising havoc with register assignment.
!

LOCAL
	HEADER		: REF BBLOCK,	! address of file header
	FCB		: REF BBLOCK,	! FCB pointer
	WINDOW_SEGMENT	: REF BBLOCK,	! address of the next window segment
	NEXT_SEGMENT	: REF BBLOCK,	! address of one beyond the next window
	RECADDR		: REF BBLOCK,	! address of directory record
	T1,				! random temps
	T2,
	T3;

! Deaccess the file if requested.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_DEACCESS])
THEN KERNEL_CALL (MAKE_DEACCESS);

! Deallocate the window block if called for.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_DELWINDOW])
THEN
    IF .CURRENT_WINDOW NEQ 0
    THEN
	BEGIN
	WINDOW_SEGMENT = .CURRENT_WINDOW;
	DO
	    BEGIN
	    NEXT_SEGMENT = .WINDOW_SEGMENT[WCB$L_LINK];
	    KERNEL_CALL (DEALLOCATE, .WINDOW_SEGMENT);
	    WINDOW_SEGMENT = .NEXT_SEGMENT;
	    END
	UNTIL .WINDOW_SEGMENT EQL 0;
	CURRENT_WINDOW = 0;
	END;

! Clean out the window pointer in the user's channel if necessary.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_ZCHANNEL])
THEN KERNEL_CALL (ZERO_CHANNEL);

! If there is a file header resident, it probably needs to be checksummed,
! except in the case of a failed truncate, where we discard the header.
! Then read back the primary file header.
!

HEADER = .FILE_HEADER;
IF .HEADER NEQ 0
THEN
    BEGIN
    IF .CLEANUP_FLAGS[CLF_CLEANTRUNC]
    THEN INVALIDATE (.FILE_HEADER)
    ELSE CHECKSUM (.FILE_HEADER);
    HEADER = READ_HEADER ((IF .CURRENT_FIB NEQ 0
			   THEN CURRENT_FIB[FIB$W_FID]
			   ELSE 0),
			  .PRIMARY_FCB);
    END;

! Send the file to the job controller if it is to be spooled.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_DOSPOOL])
THEN SEND_SYMBIONT (.BBLOCK [.IO_PACKET[IRP$L_SVAPTE], AIB$L_DESCRIPT],
		    .HEADER, .PRIMARY_FCB);

! If a directory entry needs to be re-entered, do so.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_REENTER])
THEN
    BEGIN
    RECADDR = DIRGET (.DIR_RECORD, 0);
    CH$MOVE (FIB$S_FID, SUPER_FID, RECADDR[NMB$W_FID]);
    DIRPUT (.RECADDR);
    CLEANUP_FLAGS[CLF_REMOVE] = 0;
    END;

! If a directory entry needs to be removed, do so.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_REMOVE])
THEN
    BEGIN
    RECADDR = DIRGET (.DIR_RECORD, 0);
    RECADDR[NMB$W_FID_NUM] = 0;
    DIRPUT (.RECADDR);
    END;

! If there are unrecorded blocks allocated from the storage map, return them.
!

IF .UNREC_COUNT NEQ 0
THEN
    BEGIN
    RETURN_BLOCKS (.UNREC_LBN, .UNREC_COUNT);
    UNREC_COUNT = 0;
    END;

! If a file deletion is called for, do it. This is either a create that
! failed later on, or a real delete.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_DELFILE])
THEN
    BEGIN
    CLEANUP_FLAGS[CLF_TRUNCATE] = 0;	! no truncate necessary after a delete
    CLEANUP_FLAGS[CLF_DELFID] = 0;	! leave header behind if failure
    DELETE_FILE (.CURRENT_FIB, .HEADER);
    END;

! If an extend operation failed, truncate the file.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_TRUNCATE])
THEN
    BEGIN
    T1 = .CURRENT_FIB[FIB$L_EXSZ];	! save the data returned by EXTEND
    T2 = .CURRENT_FIB[FIB$L_EXVBN];	! so it won't be smashed by TRUNCATE
    T3 = .USER_STATUS[1];
    CURRENT_FIB[FIB$L_EXSZ] = 0;
    TRUNCATE (.CURRENT_FIB, .HEADER, DEALLOC_BLOCKS);
    HEADER = .FILE_HEADER;			! follow buffer shuffling
    CURRENT_FIB[FIB$L_EXSZ] = .T1;
    CURRENT_FIB[FIB$L_EXVBN] = .T2;
    USER_STATUS[1] = .T3;
    CLEANUP_FLAGS[CLF_INVWINDOW] = 0;	! windows were never extended, so no need
    CLEANUP_FLAGS[CLF_CLEANTRUNC] = 0;
    CHECKSUM (.HEADER);
    END;

! If a truncate has failed, redo the operation to produce a correct file
! header, but don't return blocks to the storage map. We assume the header
! was nfg and contained bogus retrieval pointers.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_CLEANTRUNC])
THEN
    BEGIN
    CURRENT_FIB[FIB$L_EXSZ] = 0;
    TRUNCATE (.CURRENT_FIB, .HEADER, 0);
    HEADER = .FILE_HEADER;			! follow buffer shuffling
    END;

! Various errors leave the file control block screwed up. If needed,
! rebuild it and its extensions from scratch.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_FIXFCB])
AND .PRIMARY_FCB NEQ 0
AND .HEADER NEQ 0
THEN
    IF .PRIMARY_FCB[FCB$V_DIR]
    OR .PRIMARY_FCB[FCB$W_ACNT] NEQ 0
    THEN
	BEGIN
	FCB = .PRIMARY_FCB;
	KERNEL_CALL (DEL_EXTFCB, .FCB);
	KERNEL_CALL (INIT_FCB, .FCB, .HEADER);
	WHILE 1 DO
	    BEGIN
	    HEADER = NEXT_HEADER (.HEADER, .FCB);
	    IF .HEADER EQL 0 THEN EXITLOOP;
	    FCB = KERNEL_CALL (MAKE_EXTFCB, .HEADER, .FCB, 1);
	    END;
	IF .FCB NEQ .PRIMARY_FCB
	THEN
	    BEGIN
	    HEADER = READ_HEADER (0, .PRIMARY_FCB);
	    KERNEL_CALL (UPDATE_FCB, .HEADER);
	    END;
	END;

! Clean up any Cathedral windows which have broken.
!

IF TESTBITSC (CLEANUP_FLAGS[CLF_REMAP]) THEN REMAP_FILE ();

! If there is a dangling file ID (from a partial create or header extension),
! dispose of it.
!

IF .NEW_FID NEQ 0
THEN DELETE_FID (.NEW_FID);

! Copy the saved context, if any back into the primary context and repeat
! the cleanup.
!

IF .CONTEXT_SAVE EQL 0 THEN EXITLOOP;
CH$MOVE (CONTEXT_SIZE, CONTEXT_SAVE, CONTEXT_START);
CONTEXT_SAVE = 0;

END;					! end of major loop

RETURN 1;

END;					! end of routine ERR_CLEANUP

ROUTINE MAKE_DEACCESS =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine performs the machinery for deaccessing a file.
!
! CALLING SEQUENCE:
!	MAKE_DEACCESS ()
!
! INPUT PARAMETERS:
!	NONE
!
! IMPLICIT INPUTS:
!	PRIMARY_FCB: FCB of file
!	CURRENT_WINDOW: window of file
!	CURRENT_VCB: VCB of volume in process
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	file deaccessed
!
!--

BEGIN

LOCAL
	WINDOW_SEGMENT	: REF BBLOCK,	! address of the next window segment
	DUMMY;				! dummy local to receive REMQUE

EXTERNAL
	PRIMARY_FCB	: REF BBLOCK,	! FCB of file
	CURRENT_WINDOW	: REF BBLOCK,	! window of file
	CURRENT_VCB	: REF BBLOCK,	! VCB of volume
	PMS$GL_OPEN	: ADDRESSING_MODE (ABSOLUTE);
					! system count of currently open files


! Unlink the window from the FCB. Clear the applicable access conditions
! in the FCB.
!

WINDOW_SEGMENT = .CURRENT_WINDOW;
DO
    BEGIN
    IF .WINDOW_SEGMENT[WCB$L_WLFL] NEQ 0 THEN REMQUE (.WINDOW_SEGMENT, DUMMY);
    WINDOW_SEGMENT = .WINDOW_SEGMENT[WCB$L_LINK];
    END
UNTIL .WINDOW_SEGMENT EQL 0;

IF .CURRENT_WINDOW[WCB$V_NOREAD]
THEN PRIMARY_FCB[FCB$V_EXCL] = 0;

IF .CURRENT_WINDOW[WCB$V_NOWRITE]
THEN PRIMARY_FCB[FCB$W_LCNT] = .PRIMARY_FCB[FCB$W_LCNT] - 1;

IF .CURRENT_WINDOW[WCB$V_NOTRUNC]
THEN PRIMARY_FCB[FCB$W_TCNT] = .PRIMARY_FCB[FCB$W_TCNT] - 1;

! For a write access, bump down the writer count. If this is the
! last write, and the file is the index file or the storage map, clear
! the appropriate flag in the VCB.
!

IF .CURRENT_WINDOW[WCB$V_WRITE]
THEN
    BEGIN
    PRIMARY_FCB[FCB$W_WCNT] = .PRIMARY_FCB[FCB$W_WCNT] - 1;
    IF .PRIMARY_FCB[FCB$W_WCNT] EQL 0
    THEN
	BEGIN
	IF .PRIMARY_FCB[FCB$W_FID_NUM] EQL 1
	    THEN CURRENT_VCB[VCB$V_WRITE_IF] = 0;
	IF .PRIMARY_FCB[FCB$W_FID_NUM] EQL 2
	    THEN CURRENT_VCB[VCB$V_WRITE_SM] = 0;
	END;
    END;

PRIMARY_FCB[FCB$W_ACNT] = .PRIMARY_FCB[FCB$W_ACNT] - 1;

! If this was the last access, yank the FCB out of the FCB list and dump its
! extensions, if any.
!

IF .PRIMARY_FCB[FCB$W_ACNT] EQL 0
THEN
    BEGIN
    REMQUE (.PRIMARY_FCB, DUMMY);
    DEL_EXTFCB (.PRIMARY_FCB);
    END;

PMS$GL_OPEN = .PMS$GL_OPEN - 1;		! bump down count of open files
CURRENT_VCB[VCB$W_TRANS] = .CURRENT_VCB[VCB$W_TRANS] - 1;

RETURN 1;

END;					! end of routine MAKE_DEACCESS

GLOBAL ROUTINE DEL_EXTFCB (START_FCB) =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine removes and deallocates all extension FCB's, if any,
!	linked to the indicated FCB.
!
! CALLING SEQUENCE:
!	DEL_EXTFCB (ARG1)
!
! INPUT PARAMETERS:
!	ARG1: address of primary FCB or 0
!
! IMPLICIT INPUTS:
!	NONE
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	FCB's deallocated
!
!--

BEGIN

MAP
	START_FCB	: REF BBLOCK;	! FCB argument

LOCAL
	FCB		: REF BBLOCK,	! running FCB pointer
	NEXT_FCB	: REF BBLOCK,	! next extension FCB
	DUMMY;				! dummy local to receive REMQUE

EXTERNAL ROUTINE
	DEALLOCATE;			! deallocate dynamic memory

! Checking for null pointers, find the first extension FCB. Follow the extension
! list and remove and deallocate the extension FCB's, cleaning out the pointers
! on the way.
!

IF .START_FCB EQL 0 THEN RETURN 1;
FCB = .START_FCB[FCB$L_EXFCB];
START_FCB[FCB$L_EXFCB] = 0;
UNTIL .FCB EQL 0 DO
    BEGIN
    NEXT_FCB = .FCB[FCB$L_EXFCB];
    FCB[FCB$L_EXFCB] = 0;
    REMQUE (.FCB, DUMMY);
    DEALLOCATE (.FCB);
    FCB = .NEXT_FCB;
    END;

RETURN 1;

END;					! end of routine DEL_EXTFCB

ROUTINE ZERO_CHANNEL =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine zeroes out the window pointer being returned to
!	the user for his channel control block. It also credits one to the
!	user's open file quota.
!	This routine must be executed in kernel mode.
!
! CALLING SEQUENCE:
!	ZERO_CHANNEL ()
!
! INPUT PARAMETERS:
!	NONE
!
! IMPLICIT INPUTS:
!	IO_PACKET: I/O packet of request
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	channel window pointer cleared, file quota bumped
!
!--

BEGIN

LOCAL
	ABD		: REF BBLOCKVECTOR [,ABD$C_LENGTH],
					! buffer descriptors
	JIB		: REF BBLOCK,	! Job information block
	PCB		: REF BBLOCK;	! address of user process control block

EXTERNAL
	IO_PACKET	: REF BBLOCK,	! I/O packet in process
	SCH$GL_PCBVEC	: REF VECTOR ADDRESSING_MODE (ABSOLUTE);
					! system PCB vector


					! pointer to buffer descriptors
ABD = .BBLOCK [.IO_PACKET[IRP$L_SVAPTE], AIB$L_DESCRIPT];
ABD[ABD$C_WINDOW, ABD$W_COUNT] = 4;
.ABD[ABD$C_WINDOW, ABD$W_TEXT] + ABD[ABD$C_WINDOW, ABD$W_TEXT] + 1 = 0;

PCB = .SCH$GL_PCBVEC[.(IO_PACKET[IRP$L_PID])<0,16>];
JIB = .PCB[PCB$L_JIB];
JIB[JIB$W_FILCNT] = .JIB[JIB$W_FILCNT] + 1;

RETURN 1;

END;					! end of routine ZERO_CHANNEL

END
ELUDOM
