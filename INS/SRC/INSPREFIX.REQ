!
!	Require file for INSTALL
!	Version 'V03-000'
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!+++
!
!   FACILITY: Install
!
!   ABSTRACT: The INSTALL utility makes image files known to the VMS
!	      operating system
!
!   ENVIRONMENT: VAX native, user and kernel mode
!
!   AUTHOR: Bob Grosso
!
!   CREATION DATE: June 1981
!
!   MODIFIED BY:
!
!	V02-001	RPG0001		Bob Grosso		28-Dec-1981
!		Use shared message openin.
!
!---

!
!   Define frequently used macros
!
!
! Define VMS block structures
!
STRUCTURE
	BBLOCK [O, P, S, E; N] =
		[N]
		(BBLOCK + O) <P, S, E>;
!
! Useful macros
!
MACRO
!
! Macro to generate a pointer to a counted string
!
    CSTRING (STRING) = UPLIT BYTE (%CHARCOUNT (STRING), STRING)%,
!
! Macro to describe a string
!
    STRINGDESC (STRING) = %CHARCOUNT (STRING), UPLIT (%ASCII STRING)%,
!
! Macro to generate a quadword string descriptor
!
    DESCRIPTOR (STRING) = BBLOCK [DSC$C_S_BLN] INITIAL (STRINGDESC (STRING))%,
!
! Macro to generate a counted string
!
    COUNTEDSTRING(STRING) = VECTOR [%CHARCOUNT (STRING)+1, BYTE] INITIAL (BYTE(%CHARCOUNT(STRING),%ASCII STRING ))%,
!
! Macro to execute a given sequence of commands and return if any error
!
    execute (command, errorcode) =
	BEGIN
	LOCAL
	    status;
	status = command;
	IF NOT .status			! if an error is detected
	THEN
	    %IF %LENGTH GTR 1		! If errorcode argument is present
	    %THEN SIGNAL (errorcode, %REMAINING, .status);
	    %ELSE RETURN .status;	! Return with error status
	    %FI
	END%,
!
! Macro to execute a given sequence of commands and signal if any error
!
    report (command) =
	BEGIN
	LOCAL
	    status;
	status = command;
	IF NOT .status			! if an error is detected
	THEN
	    SIGNAL (.status);
	END%;

LITERAL
    TRUE	=	1,		! Boolean TRUE
    FALSE	=	0;		! Boolean FALSE

!
! $SHR_MESSAGES - a macro which defines facility-specific message codes
!	which are based on the system-wide shared message codes.
!
!	$SHR_MESSAGES ( name, code, (msg,severity), ... )
!
!	where:
!	    "name" is the name of the facility (e.g., LIBRAR)
!	    "code" is the corresponding facility code (e.g., 134)
!	    "msg" is the name of the shared message (e.g., BEGIN)
!	    "severity" is the desired message severity (either STS$K_severity
!					or severity)
!

MACRO
    $SHR_MESSAGES ( FACILITY_NAME, FACILITY_CODE ) =
	LITERAL
	SHR$MSG_IDS ( FACILITY_NAME, FACILITY_CODE, %REMAINING ); %,

    SHR$MSG_IDS ( FACILITY_NAME, FACILITY_CODE) [ VALUE ] =
	SHR$MSG_CALC ( FACILITY_NAME, FACILITY_CODE, %REMOVE (VALUE) ) %,

    SHR$MSG_CALC ( FACILITY_NAME, FACILITY_CODE, MSG_ID, SEVERITY ) =
	%NAME (FACILITY_NAME,'$_',MSG_ID) = %NAME ('SHR$_',MSG_ID) + 
			FACILITY_CODE*65536 +
	    		%IF %DECLARED (%NAME ('STS$K_',SEVERITY))
				%THEN %NAME ('STS$K_',SEVERITY)
				%ELSE SEVERITY %FI %;

!
! INSTALL message codes from shared message definitions
!

$SHR_MESSAGES (INS,123,
		(openin, error),	! error opening "x" as input
		);
