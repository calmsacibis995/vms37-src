	.TITLE	YCDRIVER - Port Driver for DMF  Async
	.IDENT	'V03-001'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;++
; FACILITY:
;
;	VAX/VMS TERMINAL DRIVER
;
; ABSTRACT:
;
;	DMF  ASYNC PORT DRIVER
;
; AUTHOR:
;
;	RICK SPITZ
;
; Revision history:
;
;	V03-008	RKS008		RICK SPITZ		23-MAR-1982
;		USE SYSGEN VALUE FOR COMBO INPUT SILO TIMER.
;		CHANGE OUTPUT DELAY.
;
;	V03-007	RKS007		RICK SPITZ		13-FEB-1982
;		ADD DELAY LOGIC WHEN OUTPUTING TO TRANSMIT SILO
;
;	V03-006	RKS0006		RICK SPITZ		8-FEB-1982
;		DO NOT ALLOW UNLOAD
;		FIX CHARACTER TIMER
;		DO UNSIGNED COMPARISON IN SILO OUTPUT.
;
;	V03-005	RKS0005		RICK SPITZ		11-JAN-1982
;		REPAIR MODEM INTERRUPT LOGIC
;		ADD INPUT SILO TIMER.
;
;	V03-004	RKS0004		RICK SPITZ		15-DEC-1981
;		FIX MAINTENANCE BIT OFFSETS, ADD EXTERNAL LOOP SUPPORT.
;		ENHANCE RESUME LOGIC IN DMA MODE TO AVOID WRITING 0 BYTE
;		COUNT. 
;		USE SYSPARAM VALUES FOR CLASS DRIVER LOCATION,PARITY,RSPEED
;		CHANGE TYPE CODE TO DMF-32.
;
;	V03-003	JLV0104		Jake VanNoy		27-Oct-1981
;		Changed TTYDEFS to $TTYDEFS.
;
;	V03-002	RKS002		RICK SPITZ			8-OCT-1981
;		USE SUBTRACT TO COMPUTE VECTOR ADDRESS TO BE LOADED INTO
;		DMF-32 VECTOR REGISTER.
;
;	V03-001	RKS001		RICK SPITZ			28-SEPT-1981
;		THIS MODULE IS NEW STARTING WITH VMS V3. IT ACTS AS THE
;		PORT DRIVER FOR THE DMF ASYNC LINES.
;
;
;--
	.PAGE
	.SBTTL	DECLARATIONS

;
; EXTERNAL DEFINITIONS:
;
	$ACBDEF				; DEFINE ACB
	$CRBDEF				; DEFINE CRB
	$DCDEF				; DEVICE DEFINITIONS
	$DDBDEF				; DEFINE DDB
	$DYNDEF				; Dynamic structure definitions
	$IDBDEF				; DEFINE IDB OFFSETS
	$IODEF				; DEFINE I/O FUNCTION CODES
	$IRPDEF				; IRP definitions
	$TTYDEF				; DEFINE TERMINAL DRIVER SYMBOLS
	$TTDEF				; DEFINE TERMINAL TYPES
	$TT2DEF				; DEFINE EXTENDED CHARACTERISTICS
	$TQEDEF				; DEFINE TIMER QUEUE OFFSETS
	$UCBDEF				; DEFINE UCB
	$UBADEF				; DEFINE UBA
	$VECDEF				; DEFINE VECTOR FOR CRB
	$TTYMACS			; DEFINE TERMINAL DRIVER MACROS
	$TTYDEFS			; DEFINE TERMINAL DRIVER SYMBOLS
	$TTYMODEM			; DEFINE MODEM DEFINITIONS


;
; LOCAL STORAGE
;
	.PSECT	$$$105_PROLOGUE

;
; Driver prologue table:
;

YC$DPT::				; DRIVER START
	DPTAB	-			; DRIVER PROLOGUE TABLE
		END=YC$END,-		; End and offset to INIT's vectors
		UCBSIZE=UCB$C_TT_LENGTH,- ; SIZE OF UCB
		FLAGS=DPT$M_NOUNLOAD,-	; NO UNLOAD ALLOWED
		ADAPTER=UBA,-		; ADAPTER TYPE
		DEFUNITS=8,-		; Number of units to create
		NAME=YCDRIVER,-		; NAME OF DRIVER
		VECTOR=PORT_VECTOR	; PORT DRIVER VECTOR TABLE
	DPT_STORE INIT
	DPT_STORE UCB,UCB$B_FIPL,B,8	; FORK IPL
	DPT_STORE UCB,UCB$L_DEVCHAR,L,<-; CHARACTERISTICS
			DEV$M_REC!-	;
			DEV$M_AVL!-	;
			DEV$M_IDV!-	;
			DEV$M_ODV!-	;
			DEV$M_TRM!-	;
			DEV$M_CCL>
	DPT_STORE UCB,UCB$B_DEVCLASS,B,DC$_TERM;
	DPT_STORE UCB,UCB$B_TT_DETYPE,B,TT$_UNKNOWN	; TYPE
	DPT_STORE UCB,UCB$W_TT_DESIZE,@W,TTY$GW_DEFBUF	; BUFFER SIZE
	DPT_STORE UCB,UCB$L_TT_DECHAR,@L,TTY$GL_DEFCHAR	; DEFAULT CHARACTERS
	DPT_STORE UCB,UCB$L_TT_DECHA1,@L,TTY$GL_DEFCHAR2; DEFAULT CHARACTERS
	DPT_STORE UCB,UCB$W_TT_DESPEE,@B,TTY$GB_DEFSPEED; DEFAULT SPEED
	DPT_STORE UCB,UCB$W_TT_DESPEE+1,@B,TTY$GB_RSPEED; DEFAULT RSPEED
	DPT_STORE UCB,UCB$B_TT_DEPARI,@B,TTY$GB_PARITY	; DEFAULT PARITY
	DPT_STORE UCB,UCB$B_TT_PARITY,@B,TTY$GB_PARITY	; DEFAULT PARITY
	DPT_STORE UCB,UCB$B_DEVTYPE,B,TT$_UNKNOWN	; TYPE
	DPT_STORE UCB,UCB$W_DEVBUFSIZ,@W,TTY$GW_DEFBUF	; BUFFER SIZE
	DPT_STORE UCB,UCB$L_DEVDEPEND,@L,TTY$GL_DEFCHAR	; DEFAULT CHARACTERS
	DPT_STORE UCB,UCB$L_TT_DEVDP1,@L,TTY$GL_DEFCHAR2; DEFAULT CHARACTERS
	DPT_STORE UCB,UCB$W_TT_SPEED,@B,TTY$GB_DEFSPEED	; DEFAULT SPEED
	DPT_STORE UCB,UCB$W_TT_SPEED+1,@B,TTY$GB_RSPEED	; DEFAULT RSPEED
	DPT_STORE UCB,UCB$B_DIPL,B,21			; DEVICE IPL
	DPT_STORE UCB,UCB$L_TT_WFLINK,L,0		; Zero write queue.
	DPT_STORE UCB,UCB$L_TT_WBLINK,L,0		; Zero write queue.
	DPT_STORE UCB,UCB$L_TT_RTIMOU,L,0		; Zero read timed out disp.
	DPT_STORE UCB,UCB$W_VPROT,@W,TTY$GW_PROT    	; Default protection
	DPT_STORE UCB,UCB$L_OWNUIC,@L,TTY$GL_OWNUIC 	; Default owner UIC
	DPT_STORE DDB,DDB$L_DDT,D,YC$DDT

	DPT_STORE REINIT
	DPT_STORE CRB,CRB$L_INTD+4,D,YC$INTINP	; RECEIVER INTERRUPT
	DPT_STORE CRB,CRB$L_INTD2+4,D,YC$INTOUT	; TRANSMITTER INTERRUPT
	DPT_STORE CRB,CRB$L_INTD+VEC$L_INITIAL,D,YC$INITIAL  ; CONTROLLER INIT
	DPT_STORE CRB,CRB$L_INTD+VEC$L_UNITINIT,D,YC$INITLINE; UNIT INIT
	DPT_STORE END

	DDTAB	DEVNAM = YC,-	; DUMMY DMF PORT DRIVER DISPATCH TABLE
		START  = 0,-
		FUNCTB = 0

	.PSECT	$$$115_DRIVER,LONG

;
; THE ASSOCIATED CLASS DRIVER USES THIS TABLE TO COMMAND THE PORT DRIVER.
; THE ADDRESS OF THIS TABLE IS CONTAINED IN THE TERMINAL UCB EXTENSION AREA.
; THE OFFSET DEFINITONS ARE DEFINED BY TTYDEFS.
;

PORT_VECTOR:			

;
; DMF SPECIFIC DISPATCH TABLE
;

YC_VECTOR:
	.LONG	YC$STARTIO -	YC$DPT		; START NEW OUTPUT
	.LONG	YC$DISCONNECT -YC$DPT		; HANGUP PORT
	.LONG	YC$SET_LINE -	YC$DPT		; SET NEW SPEED/PARITY
	.LONG	YC$DS_SET -	YC$DPT		; SET OUTPUT MODEM SIGNALS
	.LONG	YC$XON -	YC$DPT		; SEND XON SEQUENCE
	.LONG	YC$XOFF -	YC$DPT		; SEND XOFF SEQUENCE
	.LONG	YC$STOP -	YC$DPT		; STOP OUTPUT
	.LONG	YC$STOP2 -	YC$DPT		; STOP OUTPUT UP TO 2 SECONDS
	.LONG	YC$ABORT -	YC$DPT		; ABORT OUTPUT IN PROGRESS
	.LONG	YC$RESUME -	YC$DPT		; RESUME STOPPED OUTPUT
	.LONG	YC$NULL-	YC$DPT		; PORT SPECIFIC MODEM INITIALIZATION
	.LONG	YC$DMA -	YC$DPT		; 
	.LONG	YC$MAINT -	YC$DPT		; INVOKE MAINTENANCE FUNCTION

	.LONG	0

;
YC$NULL:					; NULL PORT ROUTINE
	RSB

	.PAGE
	.SBTTL	REGISTER DEFINITIONS
 
;
; CSR BIT DEFINITIONS ( CSR ) ( READ/WRITE )
; (NOTE: THIS REGISTER MUST ONLY BE READ IN RESPONSE TO A TRANSMIT INTERRUPT.
;	TO LOAD AN INDIRECT REGISTER, ONLY WRITE OPERATIONS MAY BE USED)
       
	$VIELD	DMFCSR,0,<-
		<IADDR,5,M>,-	; INDERECT REGISTER ADDRESS
		<CLEAR,1,M>,-	; MASTER RESET
		<RCVINT,1,M>,-	; RECEIVER INTERRUPT ENABLE
		<,1,>,-		; DON'T USE THIS BIT
		<LINE,3,M>,-	; LINE NUMBAE (0 - 7)
		<,3,>,-
		<SNDINT,1,M>,-	; TRANSMIT INTERRUPT ENABLE
		<SNDRDY,1,M>-	; TRANSMITTER READY
		> 
;
; RECEIVER BUFFER ( CSR+4 ) ( READ ONLY )
;
	$VIELD	DMFRCV,0,<-
		<BUF,8,M>,-	; RECEIVER DATA
		<LINE,3,M>,-	; LINE NUMBER (0 - 7)
		<DS_CHG,1,M>,-	; DATA SET CHANGE INTERRUPT
		<PARERR,1,M>,-	; PARITY ERROR
		<FRAMER,1,M>,-	; FRAME ERROR
		<OVERRUN,1,M>,-	; OVERRUN ERROR
		<VALID,1,M>-	; DATA VALID
		> 
;
; LINE PARAMETER REGISTER ( CSR+2 ) ( WRITE ONLY )
;
 
	$VIELD	DMFLPR,0,<-
		<LINE,3,M>,-	; LINE NUMBER (0-7)
		<SIZE,2,M>,-	; CHARACTER SIZE
		<PARITY,1,M>,-	; PARITY ENABLE
		<ODD,1,M>,-	; ODD PARITY
		<STOP,1,M>,-	; NUMBER STOP BITS
		<RSPEED,4,M>,-	; RECEIVER LINE SPEED
		<TSPEED,4,M>,-	; TRANSMITTER LINE SPEED (BOTH RVC/TX FOR LINES 2-7)
		>
;
; LINE CONTROL INDIRECT REGISTER (INDIRECT REGISTER #2)
;

	$VIELD	DMFLCT,0,<-
		<XMIT,1,M>,-	; TRANSMIT ENABLE
		<AUTOX,1,M>,-	; AUTO XON/OFF 
		<RCV,1,M>,-	; RECEIVER ENABLE
		<BREAK,1,M>,-	; SEND BREAK
		<FLUSH,1,M>,-	; FLUSH OUTPUT SILO/ABORT TRANSMIT
		<DS_ENABLE,1,M>,- ; ENABLE MODEM TRANSITION INTERRUPTS
		<MAINT,2,M>,-	;
		<,7,>,-
		<PREMPT,1,M>,-	; PREMPT CURRENT OUTPUT
		>

;
; MACRO USED TO ACCESS INDIRECT REGISTERS
;

	.MACRO	GETIND	OFFSET,RX
	MOVL	UCB$L_CRB(R5),R0			; GET CRB ADDRESS
	MOVL	@CRB$L_INTD+VEC$L_IDB(R0),R0		; GET CSR ADDRESS
	BISW3	#<<^X4040>+<OFFSET*8>>,UCB$W_UNIT(R5),(R0); SELECT INDIRECT FIELD
	.IF	NB	RX
	MOVW	6(R0),RX				; GET CONTENTS OF INDIRECT FIELD
	.ENDC
	.ENDM	GETIND

;	OUTPUT DELAY MACRO
;	TO RELEASE THE UNIBUS

	.MACRO	DELAY
	.REPEAT	3
	NOP
	.ENDR
	.ENDM

	.PAGE
	.SBTTL	CONTROLLER INITIALIZATION 



;++
; YC$INITIAL - INITIALIZE INTERFACE
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE IS ENTERED AT SYSTEM STARTUP AND POWER RECOVERY.
;
; INPUTS:
;
;	R4 = ADDRESS OF THE UNIT CSR
;	R5 = IDB OF UNIT
;	R8 = ADDRESS OF THE UNIT CRB
;
; OUTPUTS:
;
;	R2 is destroyed.
;
; IMPLICIT INPUTS:
;
;	IPL = IPL$_POWER
;
;--
YC$INITIAL::				; INITIALIZE DMF UNIT
;
; SET UP CONTROLLER
;
	MOVL	G^TTY$GL_DPT,R1		; LOCATE CLASS DRIVER TO BIND TO
	MOVZWL	DPT$W_VECTOR(R1),R0	; GET ADDRESS OF CLASS VECTOR
	ADDL3	R0,R1,R0		; CALCULATE VIRTUAL ADDRESS
	
; RELOCATE CLASS VECTOR TABLE

5$:
	TSTL	(R0)			; ALREADY RELOCATED OR DONE?
	BLEQ	10$			; YES
	ADDL	R1,(R0)+		; ADD BIAS
	BRB	5$			; LOOP TILL DONE

; RELOCATE PORT VECTOR TABLE

10$:
	MOVAL	YC$DPT,R1
	MOVAL	PORT_VECTOR,R0
20$:
	TSTL	(R0)			; ALREADY RELOCATED OR DONE?
	BLEQ	25$			; YES
	ADDL	R1,(R0)+
	BRB	20$



25$:
	SUBB3	#4,IDB$B_VECTOR(R5),-12(R4)	; LOAD VECTOR ADDRESS
	MOVW	#DMFCSR$M_CLEAR,(R4)		; CONTROLLER RESET

;
;	WAIT TILL CONTROLLER INITIALIZATION IS COMPLETE
;
	TIMEWAIT	#100,#DMFCSR$M_CLEAR,(R4),W,.FALSE.

	MOVW	#<<DMFCSR$M_RCVINT>!-	; ENABLE RECEIVER INTERRUPTS
		<DMFCSR$M_SNDINT>>,(R4)	; ENABLE TRANSMITTER INTERRUPTS
	BLBC	R0,YC$CTRL_ERROR


100$:
	MOVB	G^TTY$GB_SILOTIME,4(R4)	; INIT INPUT SILO TIMEOUT VALUE

	MOVB	#DT$_DMF32,CRB$B_TT_TYPE(R8); CONTROLLER IS DMF

	RSB

YC$CTRL_ERROR:
	RSB
	
	.PAGE
	.SBTTL	UNIT INITIALIZATION
;++
; YC$INITLINE - UNIT INITIALIZATION
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE PERFORMS A SIMPLE UNIT INITIALIZATION.
;
; INPUTS:
;
;	R5 = UCB ADDRESS
;
; OUTPUTS:
;
;	R4,R5 ARE PRESERVED.
;--

YC$INITLINE::				
	MOVL	G^TTY$GL_DPT,R1		; ADDRESS OF CLASS DPT
	MOVZWL	DPT$W_VECTOR(R1),R0	; LOCATE CLASS DRIVER VECTOR TABLE
	ADDL	R0,R1			; RELOCATE BASE ADDRESS
	MOVL	R1,UCB$L_TT_CLASS(R5)	; SET TERMINAL CLASS DRIVER VECTOR

	MOVAL	YC_VECTOR,UCB$L_TT_PORT(R5)	; SET DMF PORT VECTOR TABLE
	MOVL	CLASS_GETNXT(R1),UCB$L_TT_GETNXT(R5)
	MOVL	CLASS_PUTNXT(R1),UCB$L_TT_PUTNXT(R5)
	MOVL	UCB$L_DDB(R5),R0		; GET DDB ADDRESS
	MOVL	CLASS_DDT(R1),DDB$L_DDT(R0)
	MOVL	CLASS_DDT(R1),UCB$L_DDT(R5)	; SET DDT ADDRESS IN UCB
;

	BISW	#UCB$M_ONLINE,UCB$W_STS(R5); SET ONLINE
10$:	ASHL	UCB$W_UNIT(R5),#1,R3	; BUILD UNIT'S BIT MASK
	MOVW	R3,UCB$B_ERTCNT(R5)	; SAVE IT

	BBS	#UCB$V_POWER,UCB$W_STS(R5),-
		20$			; SKIP IF POWER FAIL RECOVERY
	TSTW	UCB$W_REFC(R5)		; ANY CHANNELS ATTACHED?
	BNEQ	20$			; YES, DON'T REINIT UCB
	MOVL	UCB$L_TT_CLASS(R5),R1	; ADDRESS CLASS VECTOR TABLE
	JSB	@CLASS_SETUP_UCB(R1)	; INIT UCB FIELDS
	BISL	#TT2$M_DMA,UCB$L_TT_DEVDP1(R5)		; SET DMA INTERFACE
	BISL	#TT2$M_DMA,UCB$L_TT_DECHA1(R5)		; SET DMA INTERFACE
20$:
	JSB	YC$SET_LINE		; INIT SPEED/PARITY
	

;
; ENABLE LINE RECEIVER , TRANSMITTER AND MODEM INTERRUPTS
;

	BISW3	#^X4048,UCB$W_UNIT(R5),(R4)	; SELECT LCT
	MOVW	#<<DMFLCT$M_XMIT>!-		; ENABLE TRANSMIT
		<DMFLCT$M_RCV>!-		; ENABLE RECEIVE
		<DMFLCT$M_DS_ENABLE>>,6(R4)	; AND MODEM INTERRUPTS
;
; INIT RECEIVER MODEM STATUS FOR DMF
;
	
	BISW3	#^X4040,UCB$W_UNIT(R5),(R4)	; SELECT RECV MODEM INDIRECT REG
	MOVB	7(R4),UCB$B_TT_DS_RCV(R5)	; UPDATE RECEIVER MODEM STATUS

25$:
	BBC	#TT$V_MODEM,UCB$L_DEVDEPEND(R5),-
		30$			; SKIP IF NOT MODEM LINE
	MOVZBL	#MODEM$C_INIT,R1	; INIT MODEM PROTOCOL
	MOVL	UCB$L_TT_CLASS(R5),R0	; ADDRESS CLASS VECTOR TABLE
	JSB	@CLASS_DS_TRAN(R0)	; INVOKE TO INIT MODEM PROTOCOL
30$:
	RSB
;
; ERROR DETECTED DURING INITIALIZATION
;

YC$UNIT_ERROR:
	BICW	#UCB$M_ONLINE,UCB$W_STS(R5)	; UNIT NOT ON LINE
	RSB

	.PAGE
	.SBTTL	DISCONNECT LINE
;++
; YC$DISCONNECT - SHUT OFF UNIT
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE IS USED WHEN FOR SOME REASON THE UNIT MUST BE DISCONNECTED.
;
;
; INPUTS:
;
;	R5 = UBC ADDRESS
;
; OUTPUTS:
;
;	R3,R4 ARE USED.
;--

YC$DISCONNECT::				; DISCONNECT UNIT
	PUSHR	#^M<R0,R1,R2>
	MOVL	#MODEM$C_SHUTDWN,R1	; SIGNAL RESET
	MOVL	UCB$L_TT_CLASS(R5),R0	; ACCESS CLASS VECTOR TABLE
	JSB	@CLASS_DS_TRAN(R0)	; INVOKE MODEM TRANSITION ROUTINE
	POPR	#^M<R0,R1,R2>
	RSB

	.PAGE
	.SBTTL	MAINTENANCE ROUTINES
;++
; YC$MAINT - MAINTENANCE FUNCTIONS
;
; FUNCTIONAL DESCRIPTION:
; THIS ROUTINE PERFORMS MAINTENANCE FUNCTIONS FOR THE DMF
; (LOOPBACK IS ONLY ALLOWED ON LINES 0 AND 1)
;
; INPUTS:
;
;	R5 = UBC ADDRESS
;	UCB$B_TT_MAINT = FUNCTION TO BE PERFORMED
;
; OUTPUTS:
;	R0-R4 SCRATCH
;--

YC$MAINT:
	BITB	#IO$M_LOOP@-7,-			; LOOPBACK FUNCTION
		UCB$B_TT_MAINT(R5)	
	BEQL	5$				; NO
	MOVW	#TT$M_DS_DTR!TT$M_DS_RTS,-	; SET REQUIRED MODEM LINES
		R2
	JSB	YC$DS_SET			; SET THEM TO ALLOW LOOPBACK
	MOVZWL	#^X02,R2			; SPECIFY LOOPBACK CODE
	BRB	10$
5$:
	BITB	#IO$M_UNLOOP@-7,-		; RESET LOOPBACK FUNCTION
		UCB$B_TT_MAINT(R5)	
	BEQL	15$				; NO
	MOVW	#256*<TT$M_DS_DTR!TT$M_DS_RTS!^X01>,-; RESET REQUIRED MODEM LINES
		R2
	JSB	YC$DS_SET			; SET THEM TO ALLOW LOOPBACK
	MOVZWL	#^X00,R2			; SPECIFY UNLOOP CODE
10$:
	CMPW	#2,UCB$W_UNIT(R5)		; ALLOWED ON THIS UNIT?
	BLEQ	50$				; NO
	GETIND	1,R1				; GET INDIRECT FIELD
	INSV	R2,#DMFLCT$V_MAINT,#2,R1	; SET MAINT FIELD
	MOVW	R1,6(R0)			; UPDATE
	MOVZBL	#1,R0				; INDICATE SUCCESS
	RSB
15$:
	BITB	#IO$M_LOOP_EXT@-7,-		; LOOPBACK FUNCTION
		UCB$B_TT_MAINT(R5)	
	BEQL	20$				; NO
	MOVW	#<TT$M_DS_DTR!TT$M_DS_RTS!^X01>,-; SET REQUIRED MODEM LINES
		R2
	JSB	YC$DS_SET			; SET THEM TO ALLOW LOOPBACK
	MOVZWL	#^X00,R2			; SPECIFY  NO LOOPBACK CODE
	BRB	10$
20$:						; CHECK OTHER FUNCTIONS
	BITB	#IO$M_LINE_OFF@-7,-		; LINE OFF
		UCB$B_TT_MAINT(R5)
	BEQL	30$				; NO
	GETIND	1,R1				; GET FIELD
	BICW	#<<DMFLCT$M_XMIT>!-		; DISABLE TRANSMIT AND RECEIVE
		<DMFLCT$M_RCV>>,R1
	MOVW	R1,6(R0)			; UPDATE
	BRB	40$
30$:
	BITB	#IO$M_LINE_ON@-7,-		; LINE ON
		UCB$B_TT_MAINT(R5)
	BEQL	50$				; NO
	GETIND	1,R1				; GET FIELD
	BISW	#<<DMFLCT$M_XMIT>!-		; ENABLE TRANSMIT AND RECEIVE
		<DMFLCT$M_RCV>>,R1
	MOVW	R1,6(R0)			; UPDATE

40$:	MOVZBL	#1,R0
	RSB
50$:
	CLRL	R0
	RSB


	.PAGE
	.SBTTL	OUTPUT MODEM CONTROL
;++
; YC$DS_SET - SET OUTPUT MODEM SIGNALS
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE OUTPUTS THE OUTPUT MODEM SIGNALS FOR THE SPECIFIED UNIT
;
; INPUTS:
;
;	R2 = LOW BYTE - SIGNALS TO ACTIVATE
;	     HIGH BYTE- SIGNALS TO DEACTIVATE
;	
;	R5 = UCB ADDRESS
;	
; OUTPUTS:
;
;	R0-R3 ARE USED.
;--

YC$DS_SET:
	BISB	R2,UCB$B_TT_DS_TX(R5)	; SET NEW OUTPUT SIGNALS
	ASHL	#-8,R2,R2		; ACCESS SIGNALS TO RESET
	BICB	R2,UCB$B_TT_DS_TX(R5)	; RESET THEM
	GETIND	1,-(SP)
	BICB3	#^X0E0,UCB$B_TT_DS_TX(R5),- ; OR IN OUTPUT MODEM SIGNALS
		1(SP)
	MOVW	(SP)+,6(R0)		; OUTPUT UPDATED VALUE
	RSB


	.PAGE
	.SBTTL	RECEIVER INTERRUPT SERIVCE
;++
; YC$INTINP - DMF RECEIVER READY INTERRUPTS
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE IS ENTERED WHEN A CHARACTER IS AVAILABLE IN THE UNIT'S
; SILO. THE CHARACTER IS EXTRACTED AND IS PASSED TO THE ASSOCIATED
; CLASS DRIVER. IF THE CLASS DRIVER RETURNS CHARACTERS(S) THEN NEW
; OUTPUT IT INITIATED (NORMALLY ECHO).
;
; INPUTS:
;
;	00(SP) = ADDRESS OF IDB
;
; IMPLICIT INPUTS:
;
;	R0,R1,R2,R3,R4,R5 ARE SAVED ON STACK.
;
; OUTPUTS:
;
;	THE INTERRUPT IS DISMISSED WHEN THE SILO IS EMPTY.
;
;--
YC$INTINP::				; DMF INPUT INTERRUPTS
;
; GET THE CSR ADDRESS
;
	MOVL	@(SP)+,R4		; GET THE IDB ADDRESS
	PUSHL	R4			; SAVE IDB ADDRESS
	MOVL	(R4),R0			; GET THE CSR ADDRESS
;
; GET THE CHARACTER FROM THE INTERFACE
;
25$:	MOVW	4(R0),R3		; GET THE CHARACTER,ERRORS AND LINE NUMBER
	BGEQ	100$			;SILO EMPTY
	BITW	#<DMFRCV$M_PARERR>!-
		<DMFRCV$M_OVERRUN>!-
		<DMFRCV$M_FRAMER>!-
		<DMFRCV$M_DS_CHG>,R3	;ERRORS OR MODEM TRANSITION ?
		
	BNEQ	50$			;YES,PROCESS THEM
27$:	ASHL	#-8,R3,R2		; GET THE LINE NUMBER
	BICL	#^C<7>,R2		; 
	MOVZBL	R3,R3			; CLEAR THE HIGH BYTES OF CHARACTER
	MOVL	IDB$L_UCBLST(R4)[R2],R5	; GET THE UCB FOR THAT LINE
	BEQL	25$			; IF EQL THEN NOT THERE
	JSB	@UCB$L_TT_PUTNXT(R5)	; BUFFER THE CHARACTER
	BLEQ	40$			; NONE OR STRING OUTPUT
	BISW3	#^X4040,UCB$W_UNIT(R5),(R0)	; SELECT OUTPUT SILO INDIRECT REGISTER
	MOVB	R3,6(R0)		; OUTPUT CHARACTER
30$:	MOVL	(SP),R4			; GET IDB ADDRESS
	BRB	25$			; CONTINUE
40$:
	BEQL	30$			; NO CHARACTER
	BISW3	#^X4040,UCB$W_UNIT(R5),(R0)	; SELECT OUTPUT SILO INDIRECT REGISTER
	JSB	BURST_OUTPUT		; START BURST
	BRB	30$
;
50$:
;
; PROCESS PARITY, FRAME OVERRUN ERROR OR MODEM TRANSITION
;
	ASHL	#-8,R3,R2		; GET LINE NUMBER
	BICL	#^C<7>,R2		;
	MOVL	IDB$L_UCBLST(R4)[R2],R5	; GET UCB ADDRESS
	BEQL	70$			; IF EQL THEN NO UCB
	BBS	#DMFRCV$V_DS_CHG,R3,200$; MODEM TRANSITION
	MOVL	UCB$L_TT_CLASS(R5),R2	; GET CLASS DISPATCH
	JSB	@CLASS_READERROR(R2)	; SIGNAL ERROR
	BNEQ	27$			; CHRRACTER TO ECHO
70$:
	BRB	30$

100$:	ADDL	#4,SP			; REMOVE IDB ADDRESS
	MOVQ	(SP)+,R0		; RESTORE REGISTERS
	MOVQ	(SP)+,R2		;
	MOVQ	(SP)+,R4		;
	REI
200$:
	BISW3	#^X4040,R2,(R0)		; SELECT MODEM INDIRECT REGISTER
	MOVZBL	7(R0),R2		; GET CURRENT RECEIVE MODEM SIGNALS
	MOVB	R2,UCB$B_TT_DS_RCV(R5)	; UPDATE CURRENT INPUT MODEM SIGNALS
	BBC	#TT$V_MODEM,UCB$L_DEVDEPEND(R5),-
		30$			; SKIP TRANSITION IF NOT MODEM LINE
	MOVZBL	#MODEM$C_DATASET,R1	; TRANSITION TYPE IS DATASET 
	PUSHL	R0			; SAVE CSR ADDRESS
	MOVL	UCB$L_TT_CLASS(R5),R4	; GET CLASS DISPATCH
	JSB	@CLASS_DS_TRAN(R4)	; INVOKE TRANSITION ROUTINE
	POPL	R0			; RESTORE CSR ADDRESS
	BRW	30$
	

	.PAGE
	.SBTTL	 START I/O ROUTINE
;++
; YC$STARTIO - START I/O OERATION ON DMF
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE IS ENTERED FROM THE DEVICE INDEPENDENT TERMINAL STARTIO
; ROUTINE TO ENABLE OUTPUT INTERRUPTS ON AN IDLE DMF UNIT.
;
; INPUTS:
;
;	R3 =	CHARACTER	AND	CC = PLUS
;		ADDRESS		AND	CC = NEGATIVE
;
;	R5 = UCB ADDRESS
;
; OUTPUTS:
;
;	R5 = UCB ADDRESS
;--
	.ENABLE	LSB
YC$STARTIO::				; START I/O ON UNIT
	BGEQ	90$			; SINGLE CHARACTER
	MOVL	UCB$L_CRB(R5),R0	; GET CRB OF UNIT
	MOVL	@CRB$L_INTD+VEC$L_IDB(R0),R0; GET CSR

BURST_OUTPUT:
	BISW3	#^X4040,UCB$W_UNIT(R5),(R0); SELECT TRANSMIT SILO
	MOVZBL	6(R0),R1		; GET SILO DEPTH
	SUBL3	R1,#32,R1			; CONVERT TO NUMBER SLOTS AVAILABLE
	MOVZWL	UCB$W_TT_OUTLEN(R5),R2	; GET LENGTH
	CMPW	R2,R1			; BURST LARGER THAN SILO?
	BLEQU	50$			; NO
	MOVZBL	R1,R2			; SLOTS AVAILABLE IS MAXIMUM
50$:
	MOVL	UCB$L_TT_OUTADR(R5),R3	; GET ADDRESS
	ADDL	R2,UCB$L_TT_OUTADR(R5)	; UPDATE POINTER
	SUBW	R2,UCB$W_TT_OUTLEN(R5)	; AND COUNT
	BEQL	60$			; ALL DONE, NO NEED FOR BURST
	BISW	#TTY$M_TANK_BURST,-	; SIGNAL BURST ACTIVE
		UCB$W_TT_HOLD(R5)
60$:
	BLBC	R2,70$			; EVEN TRANSFER
	MOVB	(R3)+,6(R0)		; OUTPUT ODD BYTE
	DECL	R2			; UPDATE COUNT
	BEQL	80$			; DONE
70$:
	ASHL	#-1,R2,R2		; CONVERT TO WORD COUNT
75$:
	MOVW	(R3)+,6(R0)
	DELAY				; TO RELEASE THE UNIBUS
	SOBGTR	R2,75$			; LOOP TILL DONE

80$:	RSB				; RETURN TO CALLER

90$:
	GETIND	0			; SELECT OUTPUT SILO
	MOVB	R3,6(R0)		; OUTPUT CHARACTER
	RSB
	.DISABLE	LSB

	.PAGE
	.SBTTL	 START DMA OUTPUT
;++
; YC$DMA - START DMA OUTPUT ON DMF
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE IS INVOKED BY THE TERMINAL CLASS DRIVER TO INITIATE
; A DMA OUTPUT TRANSFER.
;
; INPUTS:
;
;	R2 = UNIBUS HIGH ADDRESS BITS /BYTE COUNT
;	R3 = UNIBUS LOAW ADDRESS BITS
;
;	R5 = UCB ADDRESS
;
; OUTPUTS:
;
;	R0-R1 SCRATCH
;	R5 = UCB ADDRESS
;--
YC$DMA::
	GETIND	2				; SELECT DMA INDIRECT REG
	MOVW	R3,6(R0)			; LOAD ADDRESS (INDIRECT REGISTER
						; AUTO INCREMENTS TO COUNT)
	MOVW	R2,6(R0)			; LOAD COUNT /INITIATE TRANSFER
	RSB

	.PAGE
	.SBTTL	PORT ROUTINES  STOP,RESUME,XON,XOFF
;++
; YC$XOFF -	SEND XOFF
; YC$XON -	SEND XON
; YC$STOP -	STOP OUTPUT
; YC$STOP2 -	ALTERNATE STOP
; YC$ABORT -	ABORT CURRENT OUTPUT
; YC$RESUME -	RESUME STOPPED OUTPUT
;
; FUNCTIONAL DESCRIPTION:
;
; THESE ROUTINES ARE USED BY THE THE TERMINAL CLASS DRIVER TO
; CONTROL OUTPUT ON THE PORT
;
; INPUTS:
;
;	R5 = UCB ADDRESS
;
; OUTPUTS:
;
;	R5 = UCB ADDRESS
;--
;
; SCHEDULE XOFF OR XON TO BE SEND
;
YC$XOFF:
	.ENABLE	LSB
	PUSHR	#^M<R0,R1,R3>
	BBC	#TTY$V_SX_TYPFUL,-
		UCB$Q_TT_STATE(R5),10$	; SKIP IF NOT TYPEAHEAD FULL
	BBS	#TT$V_HOSTSYNC,UCB$L_DEVDEPEND(R5),10$ ; XOFF CORRECT
	MOVZBL	#TTY$C_BELL,R3		; USE BELL CHARACTER
	BRB	25$
10$:
	MOVZBL	#TTY$C_XOFF,R3		; USE XOFF
	BRB	20$
YC$XON:
	PUSHR	#^M<R0,R1,R3>
	MOVZBL	#TTY$C_XON,R3		; SEND XON
20$:
	BITL	#TT$M_HOSTSYNC!TT$M_READSYNC,-
		UCB$L_DEVDEPEND(R5)	; HOST OR READ SYNC TERMINAL?
	BEQL	30$			; NO, SKIP OUTPUT
25$:	
	GETIND	1,R1
	BISW3	#DMFLCT$M_PREMPT,R1,6(R0)	; PREMPT ANY CURRENT OUTPUT
	BISW3	#^X4040,UCB$W_UNIT(R5),(R0)	; SELECT TRANSMIT SILO
	MOVB	R3,6(R0)			; OUTPUT CHARACTER
30$:
	POPR	#^M<R0,R1,R3>
	RSB
	.DISABLE	LSB


;
; STOP PORT OUTPUT
;
YC$STOP:
	PUSHR	#^M<R0,R1>
	BICB	#UCB$M_INT!UCB$M_TIM,-		; RESET TIMER AND OUTPUT ACTIVE
		UCB$W_STS(R5)
	BBSS	#TTY$V_ST_CTRLS,-
		UCB$Q_TT_STATE(R5),5$		; BLOCK NEW OUTPUT
5$:
	GETIND	1				; 
	BICW2	#DMFLCT$M_XMIT,6(R0)		; RESET TRANSMIT ENABLE	
	POPR	#^M<R0,R1>
	RSB
;
; STOP PORT OUTPUT FOR UP TO 2 SECONDS
;
YC$STOP2:						; USED FOR BROADCAST DELAY
	PUSHR	#^M<R0,R1>
	BICB	#UCB$M_INT,UCB$W_STS(R5)		; RESET OUTPUT ACTIVE
	BBSS	#TTY$V_ST_CTRLS,-
		UCB$Q_TT_STATE(R5),5$			; BLOCK NEW OUTPUT
5$:
	BISB	#UCB$M_TIM,UCB$W_STS(R5)		; SHOW TIMER ACTIVE
	ADDL3	#2,G^EXE$GL_ABSTIM,UCB$L_DUETIM(R5)	; SET TIME OUT
	GETIND	1
	BICW2	#DMFLCT$M_XMIT,6(R0)		; RESET TRANSMIT ENABLE	
	POPR	#^M<R0,R1>
	RSB
;
; ABORT ANY CURRENT PORT OUTPUT ACTIVITY
;
YC$ABORT:
	PUSHR	#^M<R0,R1>
	BBCC	#TTY$V_ST_CTRLS,UCB$Q_TT_STATE(R5),-	; ALLOW CLASS OUTPUT
		5$
5$:
	BBCC	#TTY$V_TANK_BURST,UCB$W_TT_HOLD(R5),-	; RESET BURST ACTIVE
		10$					; 
10$:
	BICB	#UCB$M_TIM!UCB$M_INT,UCB$W_STS(R5)	; SIGNAL TIMER ACTIVE

	GETIND	1
	BISW2	#<DMFLCT$M_XMIT!DMFLCT$M_FLUSH>,6(R0)	; FLUSH OUTPUT AND SET XMIT

	BBSS	#TTY$V_SX_DMAABO,UCB$Q_TT_STATE(R5),15$	; ABORT ACTIVE DMA
15$:		
	POPR	#^M<R0,R1>
	RSB

;
; RESUME PREVIOUSLY STOPPED PORT OUTPUT
;
YC$RESUME:
	PUSHR	#^M<R0,R1,R2,R3>
	BBCC	#TTY$V_ST_CTRLS,UCB$Q_TT_STATE(R5),-	; ALLOW CLASS OUTPUT
		5$
5$:
	GETIND	1
	BISW2	#DMFLCT$M_XMIT,6(R0)			; ENABLE TRANSMIT
	BBS	#UCB$V_INT,UCB$W_STS(R5),40$		; SKIP IF OUTPUT ON
	BBS	#TTY$V_SX_DMA,UCB$Q_TT_STATE(R5),-	; DMA IN PROGRESS
		50$
	BBCC	#TTY$V_TANK_BURST,UCB$W_TT_HOLD(R5),40$	; NO BURST IN PROGRESS
							; (RESET ANYWAY. WILL BE
							; SET IF NEEDED BY BURST_OUTPUT)
	MOVZWL	UCB$W_TT_OUTLEN(R5),R1			; GET NUMBER CHARACTERS
	DIVL	#4,R1					; COMPUTE CHAR TIMEOUT
	ADDL	#2,R1					; ADD MINIMUM BIAS
	ADDL3	R1,G^EXE$GL_ABSTIM,-			; RESTORE TIMER FOR BURST
		UCB$L_DUETIM(R5)
	BISB	#UCB$M_TIM!UCB$M_INT,UCB$W_STS(R5)	; SIGNAL TIMER ACTIVE
	JSB	BURST_OUTPUT				; RESTART OUTPUT
40$:
	POPR	#^M<R0,R1,R2,R3>
	RSB
50$:
	BISW3	#^X4058,UCB$W_UNIT(R5),(R0)		; GET CURRENT BYTE COUNT
	MOVZWL	6(R0),R1
	BICW	#^X0C000,R1				; MASK OFF ADDRESS BITS
	BEQL	40$					; DMA BYTE COUNT DONE.
	DIVL	#4,R1					; COMPUTE CHAR TIMEOUT
	ADDL	#2,R1					; ADD MINIMUM BIAS
	ADDL3	R1,G^EXE$GL_ABSTIM,-			; RESTORE TIMER FOR BURST
		UCB$L_DUETIM(R5)
	BISB	#UCB$M_TIM!UCB$M_INT,UCB$W_STS(R5)	; SIGNAL TIMER ACTIVE
	BRB	40$

	.PAGE
	.SBTTL	OUTPUT INTERRUPT SERVICE
;++
; YC$INTOUT - DMF OUTPUT INTERRUPT SERVICE
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE IS ENTERED WHEN THE DMF FINDS A LINE ENABLED
; AND AN EMPTY UART. THE CORRESPONDING UCB IS FOUND AND 
; ANY OUTSTANDING PORT OUTPUT IS DONE. WHEN ALL OUTSTANDING PORT
; OUTPUT IS COMPLETED, THE CLASS DRIVER IS CALLED TO RETURN THE NEXT
; CHARACTER OR STRING TO BE OUTPUT. IF NO MORE OUTPUT IS FOUND, THEN
; THE LINE IS DISBALED.
;
; INPUTS:
;
;	SP(00) = ADDRESS OF THE IDB
;
; IMPLICIT INPUTS:
;
;	R0,R1,R2,R3,R4,R5 SAVED ON THE STACK.
;
; OUTPUTS:
;
;	THE INTERRUPT IS DISMISSED.
;
;--
YC_OUT_EXIT:				; EXIT OUTPUT INTERRUPT
	ADDL	#4,SP			; REMOVE IDB ADDRESS
	MOVQ	(SP)+,R0		; RESTORE REGISTERS
	MOVQ	(SP)+,R2		;
	MOVQ	(SP)+,R4		;
	REI				; DISMISS INTERRUPT

YC$INTOUT::				; DMF OUTPUT INTERRUPT SERVICE

YC_OUT_LOOP:
	MOVL	@(SP),R4		; GET THE IDB ADDRESS
	MOVL	(R4),R0			; GET THE CSR ADDRESS
;
; GET THE LINE INFO FROM THE CSR
;

	MOVW	(R0),R2			; GET THE CSR VALUE
	BGEQ	YC_OUT_EXIT		; NO MORE LINES
	ASHL	#-8,R2,R2		; GET THE LINE NUMBER
	BICL	#^C<7>,R2		;
	MOVL	IDB$L_UCBLST(R4)[R2],R5; GET THE UCB ADDRESS
	BEQL	YC_OUT_LOOP		; IF EQL THEN DISMISS 
;
;	CHECK FOR BURST ACTIVE ON LINE
;
	CMPB	#TTY$M_TANK_BURST@-8,-	; ONLY BURST ACTIVE?
		UCB$W_TT_HOLD+1(R5)
	BEQL	YC_BURST		; YES, CONTINUE BURST
;
; NO PENDING DATA - LOOK FOR NEXT CHARACTER
;
	BICB	#UCB$M_TIM!UCB$M_INT,UCB$W_STS(R5); CLEAR TIMEOUT AND EXPECTED
;
; CALL CLASS DRIVER FOR MORE OUTPUT
;
	JSB	@UCB$L_TT_GETNXT(R5)	; GET THE NEXT CHARACTER
	BLSS	YC_START_BURST		; BURST SPECIFIED
	BEQL	YC_OUT_LOOP		; NONE
;
; OUTPUT A CHARACTER TO THE DMF
;
20$:
	BISW3	#^X4040,UCB$W_UNIT(R5),(R0)	; SELECT OUTPUT SILO INDIRECT REGISTER
	MOVB	R3,6(R0)		; OUTPUT CHARACTER
	BRB	YC_OUT_LOOP


	.PAGE
	
YC_START_BURST:
	BISW	#TTY$M_TANK_BURST,-	; SIGNAL BURST ACTIVE
		UCB$W_TT_HOLD(R5)
;
;  CONTINUE BURST OUTPUT
;
YC_BURST:
	BISW3	#^X4040,UCB$W_UNIT(R5),(R0)	; SELECT OUTPUT SILO INDIRECT REGISTER
	MOVZBL	6(R0),R1		; GET SILO DEPTH
	SUBL3	R1,#32,R1			; CONVERT TO NUMBER SLOTS AVAILABLE

	MOVZWL	UCB$W_TT_OUTLEN(R5),R2	; GET CURRENT LENGTH
	MOVL	UCB$L_TT_OUTADR(R5),R3	; GET CURRENT ADDRESS
	CMPW	R2,R1			; BURST LARGER THAN SILO?
	BLEQU	50$			; NO
	MOVZBL	R1,R2			; MAXIMUM
50$:
	ADDL	R2,UCB$L_TT_OUTADR(R5)	; UPDATE POINTER
	SUBW	R2,UCB$W_TT_OUTLEN(R5)	; AND COUNT
	BNEQ	60$			; NOT DONE 
	BICW	#TTY$M_TANK_BURST,-	; RESET BURST ACTIVE
		UCB$W_TT_HOLD(R5)
60$:
	BLBC	R2,70$			; EVEN TRANSFER
	MOVB	(R3)+,6(R0)		; OUTPUT ODD BYTE
	DECL	R2			; UPDATE COUNT
	BEQL	80$			; DONE
70$:
	ASHL	#-1,R2,R2		; CONVERT TO WORD COUNT
75$:
	MOVW	(R3)+,6(R0)
	DELAY				; TO RELEASE THE UNIBUS
	SOBGTR	R2,75$			; LOOP TILL DONE

80$:	BRW	YC_OUT_LOOP


	.PAGE
	.SBTTL	SET SPEED, PARITY PARAMETERS

;++
; YC$SET_LINE - RESET SPEED, PARITY
;
; FUNCTIONAL DESCRIPTION:
;
; INPUTS:
;
;	R5 - UCB ADDRESS
;
; OUTPUTS:
;
;	R4 USED
;--

YC$SET_LINE:
	PUSHL	R3
	MOVL	UCB$L_CRB(R5),R4	; ADDRESS CRB
	MOVL	@CRB$L_INTD+VEC$L_IDB(R4),R4	; GET THE CSR ADDRESS VIA CRB
	CLRL	-(SP)				; RESET A TEMPORARY LOCATION
;
;	SET UP LINE SPEED AND PARITY
;

	TSTB	UCB$W_TT_SPEED+1(R5)	; RECEIVE SPEED SPECIFIED?
	BNEQ	5$			; YES
	MOVB	UCB$W_TT_SPEED(R5),-	; NO, SO USE TRANSMITTER SPEED
		UCB$W_TT_SPEED+1(R5)
5$:	SUBB3	#1,UCB$W_TT_SPEED(R5),R3	; ADJUST TRANSMIT SPEED
	INSV	R3,#DMFLPR$V_TSPEED,#4,(SP)	; SET TRANSMIT SPEED
	SUBB3	#1,UCB$W_TT_SPEED+1(R5),R3	; ADJUST RECEIVER SPEED
	INSV	R3,#DMFLPR$V_RSPEED,#4,(SP)	; SET RECEIVE SPEED

;	NONSTANDARD PARITY/STOP LAYOUT

	EXTV	#UCB$V_TT_LEN,#2,UCB$B_TT_PARITY(R5),R3		; GET CHAR SIZE
	INSV	R3,#DMFLPR$V_SIZE,#2,(SP)			; SET IT
	EXTV	#UCB$V_TT_PARTY,#2,UCB$B_TT_PARITY(R5),R3	; GET PARITY/ODD
	INSV	R3,#DMFLPR$V_PARITY,#2,(SP)
	EXTV	#UCB$V_TT_STOP,#1,UCB$B_TT_PARITY(R5),R3	; GET STOP
	INSV	R3,#DMFLPR$V_STOP,#1,(SP)

	BICW	#^X0007,(SP)		; CLEAR SPECIAL FIELDS
	BISW	UCB$W_UNIT(R5),(SP)	; SET LINE NUMBER
	CVTLW	(SP)+,2(R4)		; INSERT AS LINE PARAMETER
	POPL	R3
	RSB



YC$END:					; End of driver

	.END
