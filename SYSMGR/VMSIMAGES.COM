$ ! See documentation at end
$ SET NOON
$ REMOVE = P1 .EQS. "REMOVE"
$ FAC = "STARTUP"
$ IF REMOVE THEN FAC = "SHUTDOWN"
$ DATAFILE = F$PARSE(P2,"SYS$MANAGER:.DAT")
$ MINSYS = P3 .EQS. "MINIMUM"
$ IMMED = 0
$ INSTALL = "$INSTALL"
$RESTART:
$ DEFINE /USER SYS$OUTPUT NL:
$ DEFINE /USER SYS$ERROR NL:
$ DELETE SYS$MANAGER:VMSIMAGES.TMP;*
$RESTART_10:
$ DELFILE = ""
$ OPEN /READ /ERROR=NO_FILE F1 'DATAFILE'
$ IF IMMED THEN GOTO NEXT_LINE
$ OPEN /WRITE /ERROR=NO_ROOM F2 SYS$MANAGER:VMSIMAGES.TMP
$ IF REMOVE THEN WRITE F2 /ERROR=WRITEERR -
    "$ SET MESSAGE /NOFACILITY /NOSEVERITY /NOIDENT /NOTEXT"
$ WRITE F2 /ERROR=WRITEERR "$ RUN SYS$SYSTEM:INSTALL"
$NEXT_LINE:
$ READ /END=END_OF_FILE F1 LINE
$ FIRSTCHAR = F$EXTRACT(0,1,LINE)
$ IF FIRSTCHAR .EQS. "!" THEN GOTO END_OF_FILE
$ IF .NOT. MINSYS THEN GOTO CHECK_FILE
$ IF F$LOCATE("/NOMINI",LINE) .NE. F$LENGTH(LINE) THEN GOTO NEXT_LINE
$CHECK_FILE:
$ IF F$LOCATE("/DEL",LINE) .EQ. F$LENGTH(LINE) THEN GOTO CHECK_10
$ DELFILE = F$EXTRACT(0,F$LOCATE(" ",LINE),LINE)
$ IF REMOVE THEN GOTO NEXT_LINE
$ GOTO WRITE_FILE
$CHECK_10:
$ IF F$LOCATE("/NOREM",LINE) .EQ. F$LENGTH(LINE) THEN GOTO CHECK_20
$ IF REMOVE THEN GOTO NEXT_LINE
$CHECK_20:
$ LINE := 'LINE
$ FILE = F$EXTRACT(0,F$LOCATE(" ",LINE),LINE)
$ IF REMOVE THEN GOTO WRITE_REMOVE
$ IF F$SEARCH(F$PARSE(FILE,".EXE")) .EQS. "" THEN GOTO NEXT_LINE
$WRITE_FILE:
$ WRITE F2 /ERROR=WRITEERR LINE
$ GOTO NEXT_LINE
$WRITE_REMOVE:
$ IF FILE .EQS. DELFILE THEN GOTO NEXT_LINE
$ IF IMMED THEN GOTO IMMED_REMOVE
$ WRITE /ERROR=REMWRITERR F2 FILE,"	/DELETE"
$ GOTO NEXT_LINE
$IMMED_REMOVE:
$ SET MESSAGE /NOFACILITY /NOSEVERITY /NOIDENT /NOTEXT
$ INSTALL 'FILE' /DELETE
$ SET MESSAGE /FACILITY /SEVERITY /IDENT /TEXT
$ GOTO NEXT_LINE
$END_OF_FILE:
$ CLOSE F1
$END_10:
$ WRITE /ERROR=REMWRITERR F2 "$ SET MESSAGE /FACILITY /SEVERITY /IDENT /TEXT"
$ CLOSE F2
$ @SYS$MANAGER:VMSIMAGES.TMP
$CLEANUP:
$ DEFINE /USER SYS$OUTPUT NL:
$ DEFINE /USER SYS$ERROR NL:
$ DELETE SYS$MANAGER:VMSIMAGES.TMP;*
$ EXIT
$NO_FILE:
$ OPER = "installed"
$ EMSG = "IMGNOTINS"
$ IF REMOVE THEN OPER = "removed"
$ IF REMOVE THEN OPER = "IMGNOTREM"
$ WRITE SYS$OUTPUT -
 "%''FAC'-E-OPENIN, error opening input file ''DATAFILE'"
$ WRITE SYS$OUTPUT -
 "-''FAC'-E-''EMSG', known images will not be ''OPER'"
$ GOTO CLEANUP
$NO_ROOM:
$ IF REMOVE THEN GOTO SET_IMMED
$ WRITE SYS$OUTPUT -
"%STARTUP-W-OPENOUT, error opening output file SYS$MANAGER:VMSIMAGES.TMP"
$ CLOSE F1
$USE_DAT:
$ WRITE SYS$OUTPUT -
"-''FAC'-I-INSMSG, messages from INSTALL utility may follow"
$ DEFINE /USER SYS$INPUT 'DATAFILE'
$ RUN SYS$SYSTEM:INSTALL
$ GOTO CLEANUP
$SET_IMMED:
$ IMMED = 1
$ GOTO NEXT_LINE
$WRITEERR:
$ IF REMOVE THEN GOTO REMWRITERR
$ WRITE SYS$OUTPUT -
"%''FAC'-E-WRITEERR, error writing file SYS$MANAGER:VMSIMAGES.TMP"
$ CLOSE F1
$ CLOSE F2
$ GOTO USE_DAT
$REMWRITERR:
$ CLOSE F1
$ CLOSE F2
$ IMMED = 1
$ GOTO RESTART			! Restart and do it all in immediate mode
$ !
$ ! P1 - "INSTALL" or "REMOVE"
$ ! P2 - File specification of data file
$ ! P3 - "MINIMUM" if minimum system
