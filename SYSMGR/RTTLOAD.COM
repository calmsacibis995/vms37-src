$ !
$ ! Command procedure to load remote terminal driver and create remote
$ ! device ancillary control process (ACP).
$ !
$ REQUIRED_PRIVILEGES = "ACNT,DETACH,CMKRNL,LOG_IO,NETMBX,TMPMBX,SYSNAM"
$ PREV_PRIVS = F$SETPRV(REQUIRED_PRIVILEGES)
$ IF .NOT. F$PRIVILEGE(REQUIRED_PRIVILEGES) THEN GOTO NO_PRIVILEGES
$ ON ERROR THEN GOTO EXIT
$ ON CONTROL_Y THEN GOTO EXIT
$
$ RUN SYS$SYSTEM:SYSGEN
CONNECT RTA0/NOADAPTER/DRIVER=RTTDRIVER
$ RUN SYS$SYSTEM:REMACP -
	/NOACCOUNTING-
	/AST_LIMIT=100-
	/NOAUTHORIZE-
	/BUFFER_LIMIT=65535-
	/FILE_LIMIT=70-
	/IO_BUFFERED=65535-
	/PRIORITY=8-
	/PRIVILEGES=(CMKRNL,LOG_IO,NETMBX,TMPMBX,SYSNAM)-
	/PROCESS_NAME=REMACP-
	/UIC=[1,3]
$ IF "''F$LOG("SYS$LOGIN")'" .NES. "" THEN PURGE SYS$LOGIN:RTTLOAD.LOG/KEEP:5
$EXIT:
$ PREV_PRIVS = F$SETPRV(PREV_PRIVS)
$ EXIT
$
$NO_PRIVILEGES:
$ WRITE SYS$OUTPUT "Insufficient privileges to start the remote terminal ACP"
$ WRITE SYS$OUTPUT "Requires ",REQUIRED_PRIVILEGES," privileges."
$ GOTO EXIT
