define type privileges
	keyword acnt
	keyword allspool
	keyword altpri
	keyword bugchk
	keyword bypass
	keyword cmexec
	keyword cmkrnl
	keyword detach
	keyword diagnose
	keyword exquota
	keyword group
	keyword grpnam
	keyword log_io
	keyword mount
	keyword netmbx
	keyword oper
	keyword pfnmap
	keyword phy_io
	keyword prmceb
	keyword prmgbl
	keyword prmmbx
	keyword pswapm
	keyword same
	keyword setprv
	keyword shmem
	keyword syslck
	keyword sysgbl
	keyword sysnam
	keyword sysprv
	keyword tmpmbx
	keyword volpro
	keyword world

define verb allocate
	routine allocate
	parameter p1,prompt="Device",value(required,list,type=$device)
	parameter p2,prompt="Log name",value(type=$outlog)

define syntax merge_queue
	image queman

define verb asn
	routine assign
	prefix cli$k_assi_
	parameter p1,prompt="Device",value(type=$infile)
	parameter p2,prompt="Log name",value(type=$outlog)
	qualifier global
	qualifier group
	qualifier term
	qualifier queue,	syntax=merge_queue
	qualifier merge,	syntax=merge_queue

	outputs mcr(p1)


define verb (continue,c)
	routine continue

define verb bye
	routine logout
	prefix cli$k_logo_

define syntax deallocate_all_devices
	noparameters

define verb deallocate
	routine deallocat
	prefix cli$k_deal_
	parameter p1,prompt="Device",value(required,type=$device)
	qualifier all,syntax=deallocate_all_devices

define verb debug
	routine debug

define verb logout
	routine logout
	prefix cli$k_logo_
	qualifier brief
	qualifier full

define syntax run_process
	image rundet

define verb (run,r,ru)
	routine run
	prefix cli$k_run_
	parameter p1,prompt="File",value(required,type=$infile)
	qualifier delay,	value(required,type=$datetime),
				syntax=run_process
	qualifier error,	value(required,type=$outfile),
				syntax=run_process
	qualifier input,	value(required,type=$outfile),
				syntax=run_process
	qualifier interval,	value(required,type=$datetime),
				syntax=run_process
	qualifier output,	value(required,type=$outfile),
				syntax=run_process
	qualifier priority,	value(required),
				syntax=run_process
	qualifier privileges,	value(required,list,type=privileges),
				syntax=run_process
	qualifier process,	value(required),
				syntax=run_process
	qualifier resource_wait,default,syntax=run_process
	qualifier schedule,	value(required,type=$datetime),
				syntax=run_process
	qualifier service_failure,syntax=run_process
	qualifier swapping,	default,syntax=run_process
	qualifier uic,		value(required,type=$uic),
				syntax=run_process
	qualifier accounting,	default,syntax=run_process
	qualifier buffer_limit,	value(required),syntax=run_process
	qualifier time_limit,	value(required),syntax=run_process
	qualifier file_limit,	value(required),syntax=run_process
	qualifier page_file,	value(required),syntax=run_process
	qualifier queue_limit,	value(required),syntax=run_process
	qualifier subprocess_limit,value(required),syntax=run_process
	qualifier maximum_working_set,value(required),syntax=run_process
	qualifier working_set,	value(required),syntax=run_process
	qualifier debug
	qualifier authorize,	default,syntax=run_process
	qualifier mailbox,	value(required),syntax=run_process
	qualifier ast_limit,	value(required),syntax=run_process
	qualifier io_buffered,	value(required),syntax=run_process
	qualifier io_direct,	value(required),syntax=run_process
	qualifier enqueue_limit,value(required),syntax=run_process
	qualifier extent,	value(required),syntax=run_process

define syntax stop_queue
	image queman
	parameter p1,prompt="Queue",value(required)

define verb stop
	routine stop
	prefix cli$k_stop_
	parameter p1,prompt="Process",value(type=$process)
	qualifier queue,	syntax=stop_queue
	qualifier abort,	syntax=stop_queue
	qualifier next,		syntax=stop_queue
	qualifier identification,value(required)
	qualifier requeue,	value,syntax=stop_queue
	qualifier entry,	value(required),syntax=stop_queue
	qualifier priority,	value(required),syntax=stop_queue

