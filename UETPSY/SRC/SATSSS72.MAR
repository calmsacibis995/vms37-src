	.TITLE	SATSSS72 SATS SYSTEM SERVICE TESTS  $CRETVA (SUCC S.C.)
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SYSTST (SATS SYSTEM SERVICE TESTS)
;
; ABSTRACT:
;
;		THIS MODULE CONTAINS SUBROUTINES WHICH, WHEN LINKED
; WITH SUCCOMMON.OBJ, FORM TEST MODULE SATSSS72 TO TEST SUCCESSFUL
; OPERATION OF THE $CRETVA SYSTEM SERVICE. THE SERVICE IS INVOKED
; UNDER VARIOUS INPUT CONDITIONS WITH VARYING INPUT PARAMETERS. ONLY
; SUCCESSFUL STATUS CODES ARE EXPECTED IN THIS TEST MODULE.  CORRECT 
; OPERATION OF THE SERVICE FOR EACH OF ITS ISSUANCES IS VERIFIED BY
; CHECKING FOR AN SS$_NORMAL STATUS CODE, EXPECTED RETURN ARGUMENTS
; AND EXPECTED FUNCTIONALITY PERFORMED.
;
; ENVIRONMENT:	USER MODE IMAGE; NEEDS CMKRNL PRIVILEGE,
;		DYNAMICALLY ACQUIRES OTHER PRIVILEGES, AS NEEDED.
;
; AUTHOR: THOMAS L. CAFARELLA,		CREATION DATE: JUL, 1977
;
; MODIFIED BY:
;
;	, : VERSION
; 01	- 
;--
	.PAGE
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$PRVDEF				; PRIVILEGE BIT DEFINITIONS
	$PHDDEF				; PROCESS HEADER OFFSETS
	$PSLDEF				; PROCESSOR STATUS LONGWORD DEFINITIONS
;
; MACROS:
;
;
; EQUATED SYMBOLS:
;
;
; OWN STORAGE:
;
	.PAGE
	.PSECT	RODATA,RD,NOWRT,NOEXE,LONG
TEST_MOD_NAME::	STRING	C,<SATSSS72>	; TEST MODULE NAME
TEST_MOD_NAME_D: STRING	I,<SATSSS72>	; TEST MODULE NAME DESCRIPTOR
MSG1_INP_CTL:	STRING	I,<  SSCVA!4ZW: CONDITIONS:>
					; FAO CTL STRING FOR MSG1 IN SUCCOMMON.MAR
MSG3_ERR_CTL::	STRING	I,< *SSCVA!4ZW: !AS> 
					; FAO CTL STRING FOR MSG3 IN SUCCOMMON.MAR
PAGESIZE:	.LONG	512		; PAGE SIZE IN BYTES
	.PAGE
	.PSECT	RWDATA,RD,WRT,NOEXE,LONG
PRIVMASK:	.BLKQ	1		; ADDR OF PRIVILEGE MASK (IN PHD)
PAGCNT_ERG:	.BLKL	1		; PAGCNT ARGUMENT FOR EXPREG
RETADR_ERG:	.BLKQ	1		; RETADR ARGUMENT FOR EXPREG
INADR_CVA:	.BLKQ	1		; INADR ARGUMENT FOR PRELIM CRETVA
INADR:		.BLKQ	1		; INADR ARGUMENT FOR SUBJECT CRETVA
RETADR:		.BLKQ	1		; RETADR ARGUMENT FOR SUBJECT CRETVA
STARTADDR:	.BLKL	1		; STARTING ADDR OF CREATED AREA
ENDADDR:	.BLKL	1		; ENDING ADDR OF CREATED AREA
DISPL:		.BLKL	1		; NO. OF PAGES (DISPLACEMENT) TO CREATE
NZERR:		.BLKB	1		; NON-ZERO ERROR FLAG: O MEANS NO N-Z ERROR
	.PAGE
	.SBTTL	CONDITION TABLES
;
;	***** CONDITION TABLES FOR CRETVA SYSTEM SERVICE *****
;
	COND	1,NOTARG,<PREVIOUS CONDITION OF CREATED PAGES>,-
		  <PREVIOUSLY CREATED BY CRETVA>,-
		  <PREVIOUSLY CREATED BY EXPREG>,-
		  <PREVIOUSLY NON-EXISTENT>,-
 
	COND	2,NOTARG,<ORDERING OF INADR PAIR>,-
		  <1ST ADDR LESS THAN OR EQUAL TO 2ND ADDR>,-
		  <1ST ADDR GREATER THAN OR EQUAL TO 2ND ADDR>,-
 
	COND	3,LONG,<ACMODE>,-
		  <KERNEL>,-
		  <EXEC>,-
		  <SUPER>,-
		  <USER>,-
 
		    .LONG	PSL$C_KERNEL
		    .LONG	PSL$C_EXEC
		    .LONG	PSL$C_SUPER
		    .LONG	PSL$C_USER
;
	COND	4,NOTARG,<LOCATION OF CREATED PAGES>,-
		  <END OF PROGRAM REGION>,-
		  <END OF CONTROL REGION>,-
		  <MIDDLE OF PROGRAM REGION>,-
		  <MIDDLE OF CONTROL REGION>,-
 
		    .LONG	0,1,2,3
;
	COND	5,NOTARG,<PAGE COUNT>,-
		  <ONE PAGE>,-
		  <SMALL COUNT>,-
		  <LARGE COUNT>,-
 
		    .LONG	1
		    .LONG	5
		    .LONG	25
;
	.PSECT	SATSSS72,RD,WRT,EXE
	.PAGE
	.SBTTL	TM_SETUP, TM_CLEANUP
;++
; FUNCTIONAL DESCRIPTION:
;
;		TM_SETUP AND TM_CLEANUP ARE CALLED TO PERFORM
; REQUIRED HOUSEKEEPING AT THE BEGINNING AND END, RESPECTIVELY, OF
; TEST MODULE EXECUTION.
;
; CALLING SEQUENCE:
;
;	BSBW TM_SETUP   BSBW TM_CLEANUP
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	TM_SETUP:  COND TABLE INDEX REGISTERS (R2,3,4,5,6) CLEARED;
;		   ALL PRIVILEGES ACQUIRED.
;
; COMPLETION CODES:
;
;	EFLAG SET TO NON-ZERO IF ERROR ENCOUNTERED.
;
; SIDE EFFECTS:
;
;	SS_CHECK AND ERR_EXIT MACROS CAUSE PREMATURE EXIT 
;	(VIA RSB) IF ERROR ENCOUNTERED.
;
;--



TM_SETUP::
	CLRL	R2			; INITIALIZE
	CLRL	R3			; .. CONDITION
	CLRL	R4			; .... TABLE
	CLRL	R5			; ...... INDEX
	CLRL	R6			; ........ REGISTERS
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE BEGIN MSG
	MOVAL	TEST_MOD_SUCC,TMD_ADDR	; ASSUME END MSG WILL SHOW SUCCESS
	INSV	#SUCCESS,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR SUCCESS
	MODE	TO,5$,KRNL		; KERNEL MODE TO ACCESS PHD
	MOVL	@#CTL$GL_PHD,R9		; GET PROCESS HEADER ADDRESS
	MOVAL	PHD$Q_PRIVMSK(R9),PRIVMASK ; GET PRIV MASK ADDRESS
	MODE	FROM,5$	; BACK TO USER MODE
	PRIV	ADD,ALL			; GET ALL PRIVILEGES
	$SETPRN_S TEST_MOD_NAME_D 	; SET PROCESS NAME
	SS_CHECK  NORMAL		; CHECK STATUS CODE RETURNED FROM SETPRN
	RSB				; RETURN TO MAIN ROUTINE
TM_CLEANUP::
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE END MSG
	RSB				; RETURN TO MAIN ROUTINE
	.PAGE
	.SBTTL	CONDITION SUBROUTINES - SETUP AND CLEANUP 
;++
; FUNCTIONAL DESCRIPTION:
;
;		CONDX AND CONDX_CLEANUP ARE SUBROUTINES WHICH ARE EXECUTED
; BEFORE AND AFTER THE VERIFY SUBROUTINE, RESPECTIVELY, WHENEVER A NEW
; CONDITION X VALUE IS SELECTED (SEE FUNCTIONAL DESCRIPTION OF SUCCOMMON
; ROUTINE IN SUCCOMMON.MAR). ANY SETUP FUNCTION PARTICULAR TO THE 
; CONDITION X TABLE IS INCLUDED IN THE CONDX SUBROUTINE AND CLEANED
; UP, IF NECESSARY, IN THE CONDX_CLEANUP SUBROUTINE. THIS INCLUDES,
; ESPECIALLY, CODE TO DETECT CONFLICTS AMONG CURRENT ENTRIES IN TWO
; OR MORE CONDITION TABLES. IF A CONFLICT IS DETECTED, A NON-ZERO
; VALUE IS STORED INTO CONFLICT, WHICH CAUSES THE CALLING ROUTINE
; (SUCCOMMON) TO SKIP THE CURRENT ENTRY IN THE CONDITION X TABLE.
;
; CALLING SEQUENCE:
;
;	BSBW CONDX   BSBW CONDX_CLEANUP
;	  WHERE X = 1,2,3,4,5
;
; INPUT PARAMETERS:
;
;	CONFLICT = 0
;
; IMPLICIT INPUTS:
;
;	R2,3,4,5,6 CONTAIN CURRENT CONDITION TABLE INDEX VALUES
;	  FOR COND TABLES 1,2,3,4,5, RESPECTIVELY.
;
; OUTPUT PARAMETERS:
;
;	CONFLICT SET TO NON-ZERO IF COND TABLE CONFLICT DETECTED.
;
; IMPLICIT OUTPUTS:
;
;	R2,3,4,5,6 PRESERVED
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--



COND1::
	RSB				; RETURN TO MAIN ROUTINE
COND1_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
COND2::
	RSB				; RETURN TO MAIN ROUTINE
COND2_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
COND3::
	RSB				; RETURN TO MAIN ROUTINE
COND3_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
COND4::
	RSB				; RETURN TO MAIN ROUTINE
COND4_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
COND5::
	RSB				; RETURN TO MAIN ROUTINE
COND5_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
	.PAGE
	.SBTTL	FORM_CONDS
;++
; FUNCTIONAL DESCRIPTION:
;
; 		FORM_CONDS FORMATS AND PRINTS INFORMATION ABOUT
;  THE CURRENT ELEMENT IN EACH OF THE CONDITION TABLES.
;
; CALLING SEQUENCE:
;
;	BSBW FORM_CONDS
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	R2,3,4,5,6 CONTAIN CURRENT CONDITION TABLE INDEX VALUES
;	  FOR COND TABLES 1,2,3,4,5, RESPECTIVELY.
;	FOR X = 1,2,3,4,5 :
;		CONDX_T - TITLE TEXT FOR CONDX TABLE
;		CONDX_TAB - ELEMENT TEXT FOR CONDX TABLE
;		CONDX_C - CONTEXT OF THE CONDX TABLE
;		CONDX_E - DATA ELEMENTS OF THE CONDX TABLE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--



FORM_CONDS::
	$FAO_S	MSG1_INP_CTL,FAO_LEN,FAO_DESC,TESTNUM
					; FORMAT CONDITIONS HEADER MSG
	BSBW	OUTPUT_MSG		; ... AND PRINT IT
	CMPB	#COND1_C,#NULL		; IS CONDITION 1 NULL ?
	BNEQU	10$			; NO -- CONTINUE
	BRW	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
10$:
	MOVAL	COND1_T,MSG_A		; SAVE ADDRESS OF CONDITION 1 TITLE FOR FAO
	MOVL	COND1_TAB[R2],MSG_B 	; SAVE ADDR OF COND 1 CURR TEXT ELT FOR FAO
	MOVB	#COND1_C,MSG_CTXT 	; SAVE CONDITION 1 CONTEXT FOR FAO
	MOV_VAL	COND1_C,COND1_E[R2],MSG_DATA1 ; GIVE COND 1 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 1 MSG
	CMPB	#COND2_C,#NULL		; IS CONDITION 2 NULL ?
	BNEQU	20$			; NO -- CONTINUE
	BRW	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
20$:
	MOVAL	COND2_T,MSG_A		; SAVE ADDRESS OF CONDITION 2 TITLE FOR FAO
	MOVL	COND2_TAB[R3],MSG_B 	; SAVE ADDR OF COND 2 CURR TEXT ELT FOR FAO
	MOVB	#COND2_C,MSG_CTXT 	; SAVE CONDITION 2 CONTEXT FOR FAO
	MOV_VAL	COND2_C,COND2_E[R3],MSG_DATA1 ; GIVE COND 2 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 2 MSG
	CMPB	#COND3_C,#NULL		; IS CONDITION 3 NULL ?
	BNEQU	30$			; NO -- CONTINUE
	BRW	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
30$:
	MOVAL	COND3_T,MSG_A		; SAVE ADDRESS OF CONDITION 3 TITLE FOR FAO
	MOVL	COND3_TAB[R4],MSG_B 	; SAVE ADDR OF COND 3 CURR TEXT ELT FOR FAO
	MOVB	#COND3_C,MSG_CTXT 	; SAVE CONDITION 3 CONTEXT FOR FAO
	MOV_VAL	COND3_C,COND3_E[R4],MSG_DATA1 ; GIVE COND 3 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 3 MSG
	CMPB	#COND4_C,#NULL		; IS CONDITION 4 NULL ?
	BEQLU	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
	MOVAL	COND4_T,MSG_A		; SAVE ADDRESS OF CONDITION 4 TITLE FOR FAO
	MOVL	COND4_TAB[R5],MSG_B 	; SAVE ADDR OF COND 4 CURR TEXT ELT FOR FAO
	MOVB	#COND4_C,MSG_CTXT 	; SAVE CONDITION 4 CONTEXT FOR FAO
	MOV_VAL	COND4_C,COND4_E[R5],MSG_DATA1 ; GIVE COND 4 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 4 MSG
	CMPB	#COND5_C,#NULL		; IS CONDITION 5 NULL ?
	BEQLU	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
	MOVAL	COND5_T,MSG_A		; SAVE ADDRESS OF CONDITION 5 TITLE FOR FAO
	MOVL	COND5_TAB[R6],MSG_B 	; SAVE ADDR OF COND 5 CURR TEXT ELT FOR FAO
	MOVB	#COND5_C,MSG_CTXT 	; SAVE CONDITION 5 CONTEXT FOR FAO
	MOV_VAL	COND5_C,COND5_E[R6],MSG_DATA1 ; GIVE COND 5 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 5 MSG
FORM_CONDSX:
	RSB				; RETURN TO CALLER
	.PAGE
	.SBTTL	VERIFY
;++
; FUNCTIONAL DESCRIPTION:
;
;		VERIFY IS CALLED ONCE FOR EACH COMBINATION OF CONDITION
; TABLE VALUES (AS DETERMINED BY THE INDEX REGISTERS R2,3,4,5,6 FOR
; COND TABLES 1,2,3,4,5, RESPECTIVELY). VERIFY ESTABLISHES THE CONDITIONS
; SPECIFIED BY THE COND TABLES AND ISSUES THE SUBJECT SYSTEM SERVICE
; ($CRETVA). THEN, THE SUCCESSFUL OPERATION OF THE SERVICE IS VERIFIED
; BY EXAMINING THE STATUS CODE RETURNED, THE VALUES FOR RETURN ARGUMENTS
; AND THE FUNCTIONALITY PERFORMED. THE EXAMINATIONS TAKE THE FORM OF 
; COMPARISONS AGAINST EXPECTED VALUES. ANY FAILING COMPARISON CAUSES AN
; ERR_EXIT MACRO TO BE EXECUTED (EITHER DIRECTLY, OR INDIRECTLY,
; THROUGH THE SS_CHECK MACRO); ERR_EXIT SETS EFLAG TO NON-ZERO,
; PRINTS ERROR MESSAGES AND CAUSES AN IMMEDIATE RSB TO CALLER.
; WHEN ERR_EXIT IS EXECUTED, FURTHER CALLS TO VERIFY ARE SUPPRESSED,
; AND, AFTER EXECUTING CLEANUP SUBROUTINES, THE IMAGE EXITS.
;
; CALLING SEQUENCE:
;
;	BSBW VERIFY
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	R2,3,4,5,6 CONTAIN CURRENT CONDITION TABLE INDEX VALUES
;	  FOR COND TABLES 1,2,3,4,5, RESPECTIVELY.
;	FOR X = 1,2,3,4,5 :
;		CONDX_E - ADDRESS OF TABLE OF DATA VALUES FOR CONDX
;		  TABLE. IF THE CONTEXT OF TABLE X IS A SYSTEM SERVICE
;		  ARGUMENT, THE ARGUMENT NAME MAY BE USED AS A SYNONYM
;		  FOR CONDX_E.
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	VERIFY HAS NO OUTPUT. SINCE ITS PURPOSE IS TO TEST FOR ERRORS,
;	IT MERELY RETURNS TO CALLER NORMALLY AFTER THE TESTS, PROVIDING
;	ALL WERE SUCCESSFUL; IF AN ERROR IS DISCOVERED, RETURN IS VIA
;	AN ERR_EXIT OR SS_CHECK MACRO, BOTH OF WHICH DOCUMENT DETECTED
;	ERRORS.
;
; COMPLETION CODES:
;
;	EFLAG SET TO NON-ZERO IF ERROR ENCOUNTERED.
;
; SIDE EFFECTS:
;
;	SS_CHECK AND ERR_EXIT MACROS CAUSE PREMATURE EXIT 
;	(VIA RSB) IF ERROR ENCOUNTERED.
;
;--



VERIFY::
	TSTB	CFLAG			; SHOULD CONDITIONS BE PRINTED ?
	BEQL	5$			; NO -- CONTINUE
	BSBW	FORM_CONDS		; YES -- FMT & PRINT ALL CONDS FOR THIS T.C.
5$:
	CLRL	STARTADDR		; CLEAR STARTING ADDR OF AREA TO BE CREATED
	MOVL	COND5_E[R6],PAGCNT_ERG 	; GET PAGE COUNT
	MOVL	PAGCNT_ERG,DISPL 	; ... AND REMEMBER IT FOR LATER
	CMPL	COND4_E[R5],#1		; ARE PAGES IN MIDDLE OF A REGION ?
	BLEQ	10$			; NO -- PAGCNT_ERG IS OK
	ADDL2	#5,PAGCNT_ERG		; YES -- ADD SOME PAGES
10$:
	MODE	TO,20$,KRNL		; GO TO KERNEL MODE TO ISSUE SERVICE
	$EXPREG_S PAGCNT_ERG,RETADR_ERG, -
		  ACMODE[R4],COND4_E[R5] ; ISSUE PRELIM S.S. TO GET SOME PAGES
	MODE	FROM,20$		; BACK TO USER MODE
	TSTL	RETADR_ERG		; DID EXPREG DO ANYTHING ?
	BNEQ	30$			; YES -- THAT'S FINE
	MOVL	#SS$_NORMAL,EXPV 	; NO -- LOAD EXPECTED AND
	MOVL	R0,RECV			; ... RECEIVED VALUES, THEN EXIT
	ERR_EXIT LONG,<UNEXPECTED RETADR PAIR RETURNED BY NON-SUBJECT>,-
		      < EXPREG>
30$:
	MOVL	RETADR_ERG+4,ENDADDR 	; GET ENDING ADDR OF AREA TO BE CREATED
	MOVL	RETADR_ERG,STARTADDR 	; GET TENTATIVE START ADDR OF AREA
	CMPL	PAGCNT_ERG,DISPL 	; WERE 5 PAGES ADDED TO DISPLACEMENT ?
	BEQL	45$			; NO -- LEAVE STARTADDR ALONE
	MOVL	PAGESIZE,R9		; YES -- STARTADDR TO BE MODIFIED
	BLBC	COND4_E[R5],40$		; IF PROGRAM REGION, LEAVE R9 ALONE
	MNEGL	R9,R9			; PROGRAM REGION -- NEGATE R9
40$:
	MULL2	#5,R9			; GET LENGTH OF 5 PAGES
	ADDL2	R9,STARTADDR		; COMPUTE START ADDR OF AREA TO BE CREATED
45$:
	MODE	TO,50$,KRNL		; INTO KERNEL TO DO A STORE
	MOVB	ONES,@ENDADDR		; INDICATE WE WERE HERE
	MODE	FROM,50$		; ... AND GET BACK TO USER
	CMPB	R2,#1			; 2ND CONDITION 1 ELEMENT ?
	BNEQU	73$			; NO -- CONTINUE
	BRW	71$			; YES -- LEAVE EXPANDED REGION AS IS
73$:
	CMPB	R2,#2			; 3RD CONDITION 1 ELEMENT ?
	BNEQU	74$			; NO -- CONTINUE
	BRW	60$			; YES -- GO CONTRACT REGION
74$:
	MOVL	STARTADDR,INADR_CVA 	; MUST BE 1ST COND 1 ELEMENT
	MOVL	ENDADDR,INADR_CVA+4 	; ... GET READY FOR DELTVA
	MODE	TO,69$,KRNL		; NEED KERNEL MODE
	$DELTVA_S INADR=INADR_CVA, -
		  ACMODE=ACMODE[R4]	; DELETE EXPANDED AREA
	MODE	FROM,69$		; BACK TO USER MODE
	SS_CHECK NORMAL			; DID IT GO OK ?
	MODE	TO,70$,KRNL		; INTO KERNEL FOR A PRELIM CRETVA
	$CRETVA_S INADR=INADR_CVA, ACMODE=ACMODE[R4]
	MODE	FROM,70$		; USER MODE
	SS_CHECK NORMAL			; CHECK FOR NORMAL RETURN
	BRB	71$			; ... AND GO DETERMINE ORDER OF  INADR PAIR
60$:
	MODE	TO,68$,KRNL		; KERNEL MODE FOR CNTREG
	$CNTREG_S DISPL,,ACMODE[R4],COND4_E[R5]
					; CONTRACT REQUESTED SIZE (NOT THE EXTRA 5)
	MODE	FROM,68$		; ... AND GET BACK TO USER MODE
	SS_CHECK NORMAL			; CHECK FOR NORMAL RETURN
71$:
	CMPL	STARTADDR,ENDADDR 	; FIND LARGER ADDRESS
	BGTRU	72$			; STARTADDR LARGER
	MOVL	STARTADDR,R10		; SMALLER VALUE IN R10,
	MOVL	ENDADDR,R11		; ... LARGER IN R11
	BRB	75$			; CONTINUE
72$:
	MOVL	ENDADDR,R10		; SMALLER VALUE IN R10,
	MOVL	STARTADDR,R11		; ... LARGER IN R11
75$:
	TSTB	R3			; 1ST CONDITION 2 ELEMENT ?
	BEQL	77$			; YES -- LOWER ADDRESS GOES FIRST
	MOVL	R11,INADR		; NO -- HIGHER ADDRESS GOES FIRST
	MOVL	R10,INADR+4		; .......
	BRB	79$			; CONTINUE
77$:
	MOVL	R10,INADR		; LOWER ADDRESS GOES FIRST
	MOVL	R11,INADR+4		; ........
79$:
	MODE	TO,80$,KRNL		; INTO KERNEL MODE
;
; ****** SYSTEM SERVICE CALL WHICH IS THE SUBJECT OF THIS TEST CASE ******
;
	$CRETVA_S INADR,RETADR,ACMODE[R4]
	MODE	FROM,80$		; BACK TO USER
	CMPL	R0,#SS$_NORMAL		; CODE RECEIVED = CODE EXPECTED ?
	BEQLU	81$			; YES -- MORE VERIFYING
	MOVL	#SS$_NORMAL,EXPV 	; NO -- LOAD UP EXPECTED AND
	MOVL	R0,RECV			; ... RECEIVED VALUES, THEN EXIT
	ERR_EXIT LONG,<INCORRECT STATUS CODE RETURNED FROM CRETVA>
81$:
	CMPL	INADR,RETADR		; DID CRETVA GIVE US THE 1ST PAGE ?
	BNEQU	82$			; NO -- ERROR
	CMPL	INADR+4,RETADR+4 	; HOW ABOUT THE WHOLE RANGE ?
	BNEQU	82$			; NO -- ERROR
	BRB	83$			; CONTINUE
82$:
	MOVQ	INADR,EXPV		; LOAD UP EXPECTED AND
	MOVQ	RETADR,RECV		; ... RECEIVED VALUES, THEN EXIT
	ERR_EXIT QUAD,<UNEXPECTED VALUE FROM CRETVA FOR>,-
		      < RETADR PAIR>
83$:
	CMPL	ACMODE[R4],#PSL$C_USER 	; USER MODE PAGES REQUESTED ?
	BEQLU	90$			; YES -- GO ON
	CMPL	ACMODE[R4],#PSL$C_KERNEL ; KERNEL MODE PAGES REQUESTED ?
	BEQLU	84$			; YES -- GO CHG TO KERNEL MODE
	MODE	TO,85$,EXEC		; GET INTO EXEC MODE TO ACCESS NEW PAGES
	BRB	90$			; ... AND CONTINUE
84$:
	MODE	TO,85$,KRNL		; GET INTO KERNEL MODE TO ACCESS NEW PAGES
90$:
	CLRB	NZERR			; INDICATE NO NON-ZERO ERROR
91$:
	TSTB	(R10)			; DOES THIS PAGE HAVE A ZERO BYTE ?
	BNEQ	92$			; NO -- GO INDICATE ERROR
	MOVB	ONES,(R10)		; STORE INTO PAGE -- NO ACCVIO EXPECTED
	BRB	93$			; GO LOOP TO NEXT PAGE
92$:
	MOVB	ONES,NZERR		; INDICATE A NON-ZERO ERROR WAS FOUND
	CLRB	EXPV			; LOAD UP EXPECTED AND
	MOVB	(R10),RECV		; ... RECEIVED VALUES
	BRB	100$			; GO GET USER MODE BACK
93$:
	ACBL	R11,PAGESIZE,R10,91$ 	; ADVANCE TO NEXT PAGE
100$:
	CMPL	ACMODE[R4],#PSL$C_USER 	; USER MODE PAGES ?
	BEQLU	110$			; YES -- NO NEED TO CHG MODE
	MODE	FROM,85$		; GET BACK TO USER MODE
110$:
	TSTB	NZERR			; NON-ZERO ERROR FOUND ?
	BEQL	VERIFYX			; NO -- EVERYTHING VERIFIES
	ERR_EXIT BYTE,<A PAGE IN THE CREATED AREA IS NON-ZERO>
VERIFYX:
	RSB				; RETURN TO CALLER
	.PAGE
	.SBTTL	VFY_CLEANUP
;++
; FUNCTIONAL DESCRIPTION:
;
;		VFY_CLEANUP EXECUTES SYSTEM SERVICES TO UNDO THE
; EFFECT OF THOSE ISSUED IN THE VERIFY SUBROUTINE. VFY_CLEANUP MUST
; ASSUME THAT VERIFY MAY NOT HAVE EXECUTED IN ITS ENTIRETY (IF AN
; ERROR IS FOUND). ALSO, VFY_CLEANUP MAY ISSUE SS_CHECK OR ERR_EXIT
; ONLY AFTER PERFORMING ALL OF ITS CLEANUP OPERATIONS; THIS IS REQUIRED
; IN THE EVENT THAT VFY_CLEANUP IS CALLED DURING ERROR PROCESSING,
; WHEN PERFORMING THE REQUIRED CLEANUP IS MORE IMPORTANT THAN
; POSSIBLY DISCOVERING A SECOND ERROR.
;
; CALLING SEQUENCE:
;
;	BSBW VFY_CLEANUP
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	R2,3,4,5,6 CONTAIN CURRENT CONDITION TABLE INDEX VALUES
;	  FOR COND TABLES 1,2,3,4,5, RESPECTIVELY.
;	FOR X = 1,2,3,4,5 :
;		CONDX_E - ADDRESS OF TABLE OF DATA VALUES FOR CONDX
;		  TABLE. IF THE CONTEXT OF TABLE X IS A SYSTEM SERVICE
;		  ARGUMENT, THE ARGUMENT NAME MAY BE USED AS A SYNONYM
;		  FOR CONDX_E.
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	EFLAG SET TO NON-ZERO IF ERROR ENCOUNTERED.
;
; SIDE EFFECTS:
;
;	SS_CHECK AND ERR_EXIT MACROS CAUSE PREMATURE EXIT 
;	(VIA RSB) IF ERROR ENCOUNTERED.
;
;--



VFY_CLEANUP::
	TSTL	STARTADDR		; DID REGION EVER GET ACQUIRED ?
	BNEQ	5$			; YES -- CONTINUE
	BRW	VFY_CLEANUPX		; NO -- JUST GET OUT NOW
5$:
	MOVL	STARTADDR,INADR		; GET READY FOR DELTVA
	MOVL	ENDADDR,INADR+4		; ........
	MODE	TO,10$,KRNL		; KERNEL MODE FOR DELETE
	$DELTVA_S INADR=INADR, ACMODE=ACMODE[R4]
					; DELETE CREATED AREA
	MODE	FROM,10$		; BACK TO USER MODE
	SS_CHECK NORMAL			; CHECK FOR NORMAL RETURN
	CMPL	COND4_E[R5],#1		; WAS AREA IN "MIDDLE" OF A REGION ?
	BLEQU	VFY_CLEANUPX		; NO -- TIME TO EXIT
	MODE	TO,20$,KRNL		; YES -- KERNEL MODE TO ISSUE CNTREG
	$CNTREG_S #5,,ACMODE[R4],COND4_E[R5] ; GET RID OF THE EXTRA 5 PAGES
	MODE	FROM,20$		; BACK TO USER MODE
	SS_CHECK NORMAL			; CHECK FOR NORMAL RETURN
VFY_CLEANUPX:
	RSB				; RETURN TO CALLER
	.END
