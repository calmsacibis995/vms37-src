	.TITLE	SATSSF11 - SATS SYSTEM SERVICE TESTS  (FAILING S.C.)
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SATS SYSTEM SERVICE TESTS
;
; ABSTRACT:	THE SATSSF11 MODULE TESTS THE EXECUTION OF CERTAIN
; VMS SYSTEM SERVICES, INVOKED IN SUCH A WAY AS TO EXPECT FAILING
; STATUS CODES. THE SYSTEM SERVICES TESTED AND THE STATUS CODES
; EXPECTED ARE SUMMARIZED AS ARGUMENTS TO THE TESTSERV MACROS
; WHICH APPEAR NEAR THE END OF THIS LISTING. SUCCESSFUL STATUS
; CODES ARE TESTED IN OTHER MODULES.
;
;
; ENVIRONMENT:	USER MODE IMAGE; NEEDS CMKRNL PRIVILEGE,
;		DYNAMICALLY ACQUIRES OTHER PRIVILEGES, AS NEEDED.
;
; AUTHOR: THOMAS L. CAFARELLA,		CREATION DATE: MMM, 1978
;	  PAUL D. FAY (DISPSERV & TESTSERV MACROS)
;
; MODIFIED BY:
;
;	V03-002	LDJ0002		Larry D. Jones,		09-Feb-1982
;		Modified test to conform to a PROBE change in the length
;		checking fail path of $BRDCST.
;
;	V03-001	LDJ0001		Larry D. Jones,		05-Mar-1981
;		Modified the test to support 64kb length $BRDCST message
;		lengths and fixed the non-detached running on OPA0 bug.
;
;**
;--
	.PAGE
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$JPIDEF				; GETJPI definitions
	$PHDDEF				; PROCESS HEADER OFFSET SYMBOLS
	$PCBDEF				; PROCESS CONTROL BLOCK OFFSET SYMBS
	$STSDEF				; STATUS MESSAGE SYMBOLS
	$PRVDEF				; SYMBOL DEFS FOR PRIVILEGES
	$UETPDEF			; UETP MSG CODE DEFINITIONS
	$SHR_MESSAGES UETP,116,<<TEXT,INFO>> 
					; DEFINE UETP$_TEXT
					; GET RID OF MACRO DEFINITIONS
	$PSLDEF				; ACCESS MODE SYMBOLS
;
; MACROS:
;
;
; EQUATED SYMBOLS:
;
WARNING		= 0			; WARNING SEVERITY VALUE FOR MSGS
SUCCESS		= 1			; SUCCESS SEVERITY VALUE FOR MSGS
ERROR		= 2			; ERROR SEVERITY VALUE FOR MSGS
INFO		= 3			; INFORMATIONAL SEV VALUE FOR MSGS
SEVERE		= 4			; SEVERE (FATAL) SEV VALUE FOR MSGS
TCG_NO		=  0			; INITIALIZE TEST CASE GROUP NUMBER
GRP_TOTAL	=  0			; INITIALIZE TEST CASE GROUP TOTAL
DETACHV		=  0			; Detached flag
DETACHM		= 1@DETACHV
R0_THRU_SP	= ^M<R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,AP,FP,SP>
;
; OWN STORAGE:
;
	.PAGE
	.PSECT	RODATA,RD,NOWRT,NOEXE,LONG
REG_COMP_MASK:	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,AP,FP> ! ^X8000 -
					; REG COMPARE MASK (HIGH-ORDER ... 
					; ... BIT MUST BE ON
ERR_MSG_FAOCTL:	STRING	I,<!/!AC!1ZB!1ZB: REGISTER !2UW CONTENTS ALTERED>, -
		<;  BEFORE SERVICE CALL: !8XL   AFTER SERVICE CALL: !8XL>
TEST_MOD_NAME:	STRING	C,<SATSSF11>	; TEST MODULE NAME
TEST_MOD_BEG:	STRING	C,<begun>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_SUCC:	STRING	C,<successful>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_FAIL:	STRING	C,<failed>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_NAME_D: STRING	I,<SATSSF11>	; TEST MODULE NAME DESCRIPTOR
TTNAME:     	STRING  I,<TT>		; TERMINAL LOGICAL NAME
INADR:		.LONG	NOACCESS,NOACCESS ; PAGE ADDRESS OF NOACCESS PSECT
PROT:		.LONG	PRT$C_NA	; PROTECTION CODE FOR NOACCESS PSECT
ONES:		.LONG	-1,-1		; A QUADWORD OF 1-BITS
DEVNAM_ALC:				; DEVNAM ARGUMENT FOR ALLOC
DEVNAM_DAL:				; DEVNAM ARGUMENT FOR DALLOC
DEVNAM_ALC10:				; DEVNAM ARGUMENT FOR ALLOC
		STRING	I,<SYSTST$MBOX>
DEVNAM_ALC11:				; DEVNAM ARGUMENT FOR ALLOC
DEVNAM_DAL11:				; DEVNAM ARGUMENT FOR DALLOC
		STRING	I,<_##>
DEVNAM_ALC12:				; DEVNAM ARGUMENT FOR ALLOC
DEVNAM_DAL14:				; DEVNAM ARGUMENT FOR DALLOC
		.LONG	0
		.ADDRESS .		; ...........
DEVNAM_ALC13:	STRING	I,   -		; DEVNAM ARGUMENT FOR ALLOC
		  <SIXTY-FOUR CHARACTERS>,  -
		  <$$$$$$$$$$$$$$$$$$$$$$>, -
		  <$$$$$$$$$$$$$$$$$$$$$>
DEVNAM_ALC14:				; DEVNAM ARGUMENT FOR ALLOC
DEVNAM_DAL12:				; DEVNAM ARGUMENT FOR DALLOC
		STRING	I,<_ZZA0:>
SYSTEM_DISK:				; LOGICAL NAME FOR SYSTEM DISK
MY_DISK:	STRING	I,<SYS$DISK>	; LOGICAL NAME FOR USER DISK
PHYLEN_ALC20:	.BLKW	1		; PHYLEN ARGUMENT FOR ALLOC
PHYBUF_ALC30:	STRING	O,1		; PHYBUF ARGUMENT FOR ALLOC
ACMODE_ALC:				; ACMODE ARGUMENT FOR ALLOC
ACMODE_DAL:				; ACMODE ARGUMENT FOR DALLOC
		.LONG	PSL$C_USER
MSGBUF_BRD:	STRING	I,< >		; MSGBUF ARGUMENT FOR BRDCST
MSGBUF_BRD11:	.LONG	-1		; MSGBUF ARGUMENT FOR BRDCST
		.ADDRESS .		; .....
MSGBUF_BRD13	= MSGBUF_BRD		; MSGBUF ARGUMENT FOR BRDCST
DEVNAM_BRD:	STRING	I,<_OPA0:>	; DEVNAM ARGUMENT FOR BRDCST
DEVNAM_BRD21:	STRING	I,<_ZZA0:>	; DEVNAM ARGUMENT FOR BRDCST
DEVNAM_BRD22:	STRING	I,<_!!>		; DEVNAM ARGUMENT FOR BRDCST
CHAN_CAN:				; CHAN ARGUMENT FOR CANCEL
CHAN_CAN11:				; CHAN ARGUMENT FOR CANCEL
		.LONG	0
CHAN_CAN12:	.LONG	1000000000	; CHAN ARGUMENT FOR CANCEL
ILIST:					; $GETJPI item list
	.WORD	8,JPI$_TERMINAL
	.ADDRESS NAME
	.LONG	0
	.LONG	0
	.PAGE
	.PSECT	RWDATA,RD,WRT,NOEXE
TPID:		.BLKL	1		; PROCESS ID FOR THIS PROCESS
CURRENT_TC:	.BLKL	1 		; PTR TO CURRENT TEST CASE
REG_SAVE_AREA:	.BLKL	15		; SAVE AREA FOR ALL REGS (SANS PC)
MOD_MSG_CODE:	.LONG	UETP$_SATSMS	; TEST MODULE MSG CODE FOR PUTMSG
CLOB_REG_NO:	.BLKL	1		; CLOBBERED REG NO (FOR FAO ERR MSG)
REG_BEFORE_SS:	.BLKL	1		; REG CONTENTS BEFORE S.S. 
					; ... (FOR FAO ERROR MSG)
REG_AFTER_SS:	.BLKL	1		; REG CONTENTS AFTER S.S. 
					; ... (FOR FAO ERROR MSG)
$$TSTN$$:	STRING	C,<  SF   >	; ASCII PORTION OF TEST CASE NAME
TMN_ADDR:	.ADDRESS TEST_MOD_NAME	; ADDR OF TEST MOD NAME FOR FAO
TMD_ADDR:	.ADDRESS TEST_MOD_BEG	; ADDR OF T.M. DISP FIELD FOR FAO
TS_EP:		.BLKL	1		; ENTRY PNT FOR CURR TESTSERV MACRO
RETADR:		.BLKL	2		; RETURN LONGWORDS FOR SETPRT
PRVPRT:		.BLKB	1		; PROT RETURN BYTE FOR SETPRT
PRIVMASK:	.BLKQ	1		; ADDR OF PRIVILEGE MASK (IN PHD)
CHM_CONT:	.BLKL	1		; CHANGE MODE CONTINUE ADDRESS
REGS:		.BLKL	5		; AREA FOR COND INDEX REGS (R2-R6)
CHAN_CAN10:	.BLKL	1		; CHAN ARGUMENT FOR CANCEL
PHYLEN_ALC:	.BLKW	1		; PHYLEN ARGUMENT FOR ALLOC
PHYBUF_ALC:	STRING	O,25		; PHYBUF ARGUMENT FOR ALLOC
MBOXCHAN:	.BLKL	1		; CHANNEL NUMBER FOR SFALC10 $CREMBX
LENGTH_DAL13:	.BLKW	1		; LENGTH OF SYSTEM DISK TRANSLATION
DEVNAM_DAL13:	STRING	O,20		; DEVNAM ARGUMENT FOR DALLOC
NAME:					; Place to store the terminal name
	.BLKB	8
FLAG:					; Status flags
	.BYTE	0
	.PAGE
	.PSECT	SATS_ACCVIO_1,RD,WRT,NOEXE,PAGE
EMPTY:		.BLKB	512		; RESERVE A PAGE OF SPACE
;
; + 
; ******************************************************************
; *                                                                *
; * 	THE ORDER OF STATEMENTS IN THIS PSECT IS CRITICAL.         *
; *	DO NOT RE-ARRANGE THE VARIABLES. CONSULT SATS              *
; *	FUNCTIONAL SPECIFICATION FOR A DESCRIPTION OF THE USE      *
; *	OF THE EMPTY PSECT (AND ITS COMPANION PSECT, NOACCESS).    *
; *                                                                *
; ******************************************************************
; -
;
; TYPE AAAAA_SSSX1 (TYPE AAAAA_SSSX2 IF NOT DESC) GO HERE:
		. = . - 13		; ALLOW ROOM FOR STRING DESCRIPTOR
; TYPE AAAAA_SSSX5 GO HERE:
		.LONG	6		; STRING LENGTH (WILL CROSS PSECT BOUNDARY)
		.ADDRESS .+4		; STRING ADDRESS
; TYPE AAAAA_SSSX3 GO HERE:
		.BLKB	1		; LOW-ORDER BYTE OF STRING LENGTH
; TYPE AAAAA_SSSX2 GO HERE:
		.BLKL	1		; STRING LENGTH
;
;
;
;
	.PSECT	SATS_ACCVIO_2,RD,WRT,NOEXE,PAGE
NOACCESS:	.BLKB	512		; RESERVE A PAGE OF SPACE
		. = . - 512		; RETURN LOC CTR TO BEGINNING OF PSECT
		.ADDRESS EMPTY		; ADDRESS OF ACCESSIBLE STRING
		.ADDRESS EMPTY/^X100 	; ADDRESS OF ACCESSIBLE STRING
;+
; *** NOTE -- DO NOT CHANGE LOCATION OR SEQUENCE OF ABOVE STATEMENTS!
; ***         THIS PSECT (NOACCESS) MUST APPEAR IN MEMORY IMMEDIATELY
; ***         FOLLOWING THE EMPTY PSECT. PSECT NAMES AND OPTIONS WILL BE 
; ***         CHOSEN TO FORCE THE DESIRED PSECT ORDERING.
;-
;
DEVNAM_DAL10:	STRING	I,<SFDAL10>	; DEVNAM ARGUMENT FOR DALLOC
MSGBUF_BRD10:				; MSGBUF ARGUMENT FOR BRDCST
DEVNAM_BRD20:				; DEVNAM ARGUMENT FOR BRDCST
		STRING	I,<SFBRD>
;
;
;
	.PSECT	SATSSF11,RD,WRT,EXE,LONG
	.PAGE
	.SBTTL	SATSSF11
;++
; FUNCTIONAL DESCRIPTION:
;
;		AFTER PERFORMING SOME INITIAL HOUSEKEEPING, SUCH AS
; PRINTING THE MODULE BEGIN MESSAGE AND ACQUIRING ALL PRIVILEGES,
; THE SATSSF11 ROUTINE EXECUTES THE TEST_SERV_EXEC MACRO TO RUN
; ALL TEST CASES. WHEN THE MACRO COMPLETES ITS EXECUTION, SATSSF11
; PRINTS A TEST MODULE SUCCESS OR FAIL MESSAGE AND EXITS TO THE
; OPERATING SYSTEM. TEST_SERV_EXEC CALLS THE TC_CONTROL/TESTSERV
; CO-ROUTINE PAIR ONCE PER TEST CASE GROUP TO EXECUTE ALL TEST
; CASES IN THAT GROUP. EACH TEST CASE GROUP IS DEFINED BY BOUNDING
; ITS TEST CASES WITH A TC_GROUP MACRO BEFORE THE FIRST TEST CASE
; AND A TCEND MACRO AFTER THE LAST ONE. THE TEST CASES THEMSELVES
; ARE DEFINED WITHIN THESE BOUNDS BY PRECEDING EACH WITH A
; NEXT_TEST_CASE MACRO. TC_CONTROL/TESTSERV EXECUTES THE CODE 
; FOLLOWING EACH NEXT_TEST_CASE MACRO IMMEDIATELY BEFORE ISSUING
; THE SYSTEM SERVICE AS REQUESTED IN THE TESTSERV MACRO. TC_CONTROL/
; TESTSERV ALSO CHECKS THE RESULTS OF THE SERVICE WITH RESPECT
; TO ITS EXPECTED STATUS CODE AND PRINTS ANY REQUIRED FAILURE
; MESSAGES FOR THE TEST CASE. THE CODE APPEARING AFTER EACH
; NEXT_TEST_CASE MACRO IS MERELY TO SET UP CONDITIONS REQUIRED
; FOR THE SYSTEM SERVICE AND TO CLEAN UP ANY RESOURCES ACQUIRED
; BY THE PREVIOUS TEST CASE.
;
; CALLING SEQUENCE:
;
;	$ RUN SATSSF11  ...  (DCL COMMAND)
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	MESSAGES TO SYS$OUTPUT ARE THE ONLY OUTPUT FROM SATSSF11.
;	THEY ARE OF THE FORM:
;
;		%UETP-S-SATSMS, TEST MODULE SATSSF11 BEGUN ... (BEGIN MSG)
;		%UETP-S-SATSMS, TEST MODULE SATSSF11 SUCCESSFUL ... (END MSG)
;		%UETP-E-SATSMS, TEST MODULE SATSSF11 FAILED ... (END MSG)
;		%UETP-I-TEXT, ... (VARIABLE INFORMATION ABOUT A TEST MODULE FAILURE)
;
; COMPLETION CODES:
;
;	THE SATSSF11 ROUTINE TERMINATES WITH A $EXIT TO THE
;	OPERATING SYSTEM WITH A STATUS CODE DEFINED BY UETP$_SATSMS.
;
; SIDE EFFECTS:
;
;	NONE
;
;--



SATSSF11:
	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
					; ENTRY MASK
	$WAKE_S  TPID			; GET PID OF THIS PROCESS
	$HIBER_S			; UNDO WAKE
	$SETPRN_S TEST_MOD_NAME_D 	; SET PROCESS NAME
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE BEGIN MSG
	MOVAL	TEST_MOD_SUCC,TMD_ADDR	; ASSUME END MSG WILL SHOW SUCCESS
	INSV	#SUCCESS,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR SUCCESS
	MODE	TO,10$,KRNL,NOREGS 	; KERNEL MODE TO ACCESS PHD
	MOVL	@#CTL$GL_PHD,R9		; GET PROCESS HEADER ADDRESS
	MOVAL	PHD$Q_PRIVMSK(R9),PRIVMASK ; GET PRIV MASK ADDRESS
	MODE	FROM,10$		; GET BACK TO USER MODE
	PRIV	ADD,ALL			; GET ALL PRIVILEGES
	DISPSERV			; SET UP DISPLAY INFO FOR TESTSERV
	$SETPRT_S INADR=INADR, RETADR=RETADR, -
		  PROT=PROT, PRVPRT=PRVPRT
					; SET NOACCESS PSECT ...
					; ... FOR NO USER ACCESS
	BRW	EXECUTE			; GO EXECUTE ALL TEST CASES
;
	TC_GROUP	ALC,1,TS1
;
	NEXT_TEST_CASE	SFALC10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFALC10      
; *
; *  SYSTEM SERVICE:		ALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_ALC10         
; *
; *  INPUT CONDITIONS:
; *	ALLOCATE A MAILBOX
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	DEVALLOC
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$CREMBX_S PRMFLG=#1, -		; CREATE A MAILBOX FOR ALLOCATION
		  CHAN=MBOXCHAN, -
		  LOGNAM=DEVNAM_ALC10
;
	NEXT_TEST_CASE	SFALC11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFALC11      
; *
; *  SYSTEM SERVICE:		ALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_ALC11         
; *
; *  INPUT CONDITIONS:
; *	INVALID DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVDEVNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$DELMBX_S CHAN=MBOXCHAN		; DELETE MAILBOX ACQUIRED BY SFALC10
;
	NEXT_TEST_CASE	SFALC12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFALC12      
; *
; *  SYSTEM SERVICE:		ALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_ALC12         
; *
; *  INPUT CONDITIONS:
; *	ZERO LENGTH DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVLOGNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFALC13
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFALC13      
; *
; *  SYSTEM SERVICE:		ALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_ALC13         
; *
; *  INPUT CONDITIONS:
; *	DEVICE NAME IS LENGTH 64.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVLOGNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFALC14
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFALC14      
; *
; *  SYSTEM SERVICE:		ALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_ALC14         
; *
; *  INPUT CONDITIONS:
; *	VALID, NON-EXISTENT DEVICE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOSUCHDEV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFALC20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFALC20      
; *
; *  SYSTEM SERVICE:		ALLOC               
; *
; *  ARGUMENT UNDER TEST: 	PHYLEN_ALC20         
; *
; *  INPUT CONDITIONS:
; *	PHYSICAL DEVICE NAME LENGTH FIELD IN READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFALC30
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFALC30      
; *
; *  SYSTEM SERVICE:		ALLOC               
; *
; *  ARGUMENT UNDER TEST: 	PHYBUF_ALC30         
; *
; *  INPUT CONDITIONS:
; *	PHYSICAL DEVICE BUFFER DESCRIPTOR IN READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	DAL,1,TS2
;
	NEXT_TEST_CASE	SFDAL10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDAL10      
; *
; *  SYSTEM SERVICE:		DALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_DAL10         
; *
; *  INPUT CONDITIONS:
; *	DEVICE NAME DESCRIPTOR IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFDAL11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDAL11      
; *
; *  SYSTEM SERVICE:		DALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_DAL11         
; *
; *  INPUT CONDITIONS:
; *	INVALID DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVDEVNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFDAL12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDAL12      
; *
; *  SYSTEM SERVICE:		DALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_DAL12         
; *
; *  INPUT CONDITIONS:
; *	VALID, NON-EXISTENT DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOSUCHDEV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFDAL13
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDAL13      
; *
; *  SYSTEM SERVICE:		DALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_DAL13         
; *
; *  INPUT CONDITIONS:
; *	DE-ALLOCATE A DEVICE NEVER ALLOCATED (SYSTEM DISK).
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	DEVNOTALLOC
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$TRNLOG_S LOGNAM=SYSTEM_DISK, -	; GET SYSTEM DISK DEVICE NAME
		  RSLLEN=LENGTH_DAL13, -
		  RSLBUF=DEVNAM_DAL13, -
		  DSBMSK=#^B0110
	MOVZWL	LENGTH_DAL13,DEVNAM_DAL13 ; GET LENGTH INTO DESCRIPTOR
;
	NEXT_TEST_CASE	SFDAL14
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDAL14      
; *
; *  SYSTEM SERVICE:		DALLOC               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_DAL14         
; *
; *  INPUT CONDITIONS:
; *	ZERO LENGTH DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVLOGNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	CAN,1,TS3
;
	NEXT_TEST_CASE	SFCAN10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCAN10      
; *
; *  SYSTEM SERVICE:		CANCEL               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_CAN10         
; *
; *  INPUT CONDITIONS:
; *	CANCEL I/O ON A CHANNEL ASSIGNED IN EXEC MODE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,10$,EXEC,NOREGS	; EXEC MODE TO ISSUE $ASSIGN
	$ASSIGN_S DEVNAM=MY_DISK, -	; ASSIGN CHANNEL IN EXEC MODE
		CHAN=CHAN_CAN10
	MODE	FROM,10$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFCAN11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCAN11      
; *
; *  SYSTEM SERVICE:		CANCEL               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_CAN11         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (ZERO)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,20$,EXEC,NOREGS	; EXEC MODE FOR $DASSGN
	$DASSGN_S CHAN=CHAN_CAN10	; DE-ASSIGN CHANNEL ASSIGNED IN SFCAN10
	MODE	FROM,20$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFCAN12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCAN12      
; *
; *  SYSTEM SERVICE:		CANCEL               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_CAN12         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (1 BILLION)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	BRD,1,TS4
;
	NEXT_TEST_CASE	SFBRD10
;
;++

; ******************************************************
; *
; *  TEST CASE NAME:		SFBRD10      
; *
; *  SYSTEM SERVICE:		BRDCST               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_BRD10         
; *
; *  INPUT CONDITIONS:
; *	MESSAGE BUFFER DESCRIPTOR IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
;	NEXT_TEST_CASE	SFBRD11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFBRD11      
; *
; *  SYSTEM SERVICE:		BRDCST               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_BRD11         
; *
; *  INPUT CONDITIONS:
; *	NEGATIVE MESSAGE LENGTH (-1)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BADPARAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFBRD13
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFBRD13      
; *
; *  SYSTEM SERVICE:		BRDCST               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_BRD13         
; *
; *  INPUT CONDITIONS:
; *	ISSUE $BRDCST WITHOUT THE REQUIRED PRIVILEGE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	PRIV	REM,OPER		; REMOVE PRIVILEGE REQUIRED FOR $BRDCST
	$GETJPI_S ITMLST = ILIST	; Get the terminal name
	CMPL	#^A/OPA0/,NAME		; Are we running on OPA0?
	BNEQ	10$			; Br if not
	BISB2	#DETACHM,FLAG		; Set the disable flag for this step
10$:
;
	NEXT_TEST_CASE	SFBRD20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFBRD20      
; *
; *  SYSTEM SERVICE:		BRDCST               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_BRD20         
; *
; *  INPUT CONDITIONS:
; *	DEVICE NAME DESCRIPTOR IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	PRIV	ADD,OPER		; GET BACK PRIVILEGE REMOVED BY SFBRD13
;
	NEXT_TEST_CASE	SFBRD21
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFBRD21      
; *
; *  SYSTEM SERVICE:		BRDCST               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_BRD21         
; *
; *  INPUT CONDITIONS:
; *	VALID, NON-EXISTENT DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOSUCHDEV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFBRD22
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFBRD22      
; *
; *  SYSTEM SERVICE:		BRDCST               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_BRD22         
; *
; *  INPUT CONDITIONS:
; *	INVALID DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVDEVNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
TS1:
	TESTSERV	ALLOC,ERR,SATS,				-
                                          				-
	   <1,DEVNAM_ALC,						-
	        	DEVNAM_ALC10,DEVALLOC,	-  ; SFALC10
	        	DEVNAM_ALC11,IVDEVNAM,	-  ; SFALC11
	        	DEVNAM_ALC12,IVLOGNAM,	-  ; SFALC12
	        	DEVNAM_ALC13,IVLOGNAM,	-  ; SFALC13
	        	DEVNAM_ALC14,NOSUCHDEV,	-  ; SFALC14
                                                  >,			-
                                          				-
	   <1,PHYLEN_ALC,						-
	        	PHYLEN_ALC20,ACCVIO,	-  ; SFALC20
                                                  >,			-
                                          				-
	   <1,PHYBUF_ALC,						-
	        	PHYBUF_ALC30,ACCVIO,	-  ; SFALC30
                                                  >,			-
                                          				-
	   <1,ACMODE_ALC,						-
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS2:
	TESTSERV	DALLOC,ERR,SATS,				-
                                          				-
	   <1,DEVNAM_DAL,						-
	        	DEVNAM_DAL10,ACCVIO,	-  ; SFDAL10
	        	DEVNAM_DAL11,IVDEVNAM,	-  ; SFDAL11
	        	DEVNAM_DAL12,NOSUCHDEV,	-  ; SFDAL12
	        	DEVNAM_DAL13,DEVNOTALLOC, - ; SFDAL13
	        	DEVNAM_DAL14,IVLOGNAM,	-  ; SFDAL14
                                                  >,			-
                                          				-
	   <1,ACMODE_DAL,						-
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS3:
	TESTSERV	CANCEL,ERR,SATS,				-
                                          				-
	   <1,CHAN_CAN,						-
	        	CHAN_CAN10,NOPRIV,	-  ; SFCAN10
	        	CHAN_CAN11,IVCHAN,	-  ; SFCAN11
	        	CHAN_CAN12,IVCHAN,	-  ; SFCAN12
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS4:
	TESTSERV	BRDCST,ERR,SATS,				-
                                          				-
	   <1,MSGBUF_BRD,						-
	        	MSGBUF_BRD10,ACCVIO,	-  ; SFBRD10
	        	MSGBUF_BRD13,NOPRIV,	-  ; SFBRD13
                                                  >,			-
                                          				-
	   <1,DEVNAM_BRD,						-
	        	DEVNAM_BRD20,ACCVIO,	-  ; SFBRD20
	        	DEVNAM_BRD21,NOSUCHDEV,	-  ; SFBRD21
	        	DEVNAM_BRD22,IVDEVNAM,	-  ; SFBRD22
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
	.SBTTL	EXECUTE & CLEANUP
EXECUTE:
	TEST_SERV_EXEC			; EXECUTE ALL T. CASES IN ALL GROUPS
CLEANUP:
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE END MSG
	INSV	#1,#STS$V_INHIB_MSG,#1,MOD_MSG_CODE 
					; INHIBIT PRINTING
	$EXIT_S	MOD_MSG_CODE		; EXIT TO OP SYS WITH MSG CODE
	.PAGE
	.SBTTL	TC_CONTROL
;++
; FUNCTIONAL DESCRIPTION:
;
;		THE TC_CONTROL SUBROUTINE IS CALLED BY THE TEST_SERV_EXEC
; MACRO TO EXECUTE A GROUP OF TEST CASES. A GROUP IS DEFINED BY A TC_GROUP
; MACRO. FOR EACH TC_GROUP MACRO, THERE IS A CORRESPONDING TESTSERV MACRO.
; TESTSERV CONTAINS CODE TO EXECUTE SYSTEM SERVICES AND CHECK THE RETURNED
; STATUS CODE VALUES. TESTSERV ARGUMENTS ARE CODED TO SPECIFY ALL THE SYSTEM
; SERVICE ARGUMENT VALUES AND THE EXPECTED STATUS CODE FOR EACH TEST CASE
; DEFINED BY A NEXT_TEST_CASE MACRO WITHIN THE GROUP. TC_CONTROL USES A
; CO-ROUTINE INTERFACE TO ENTER THE CODE OF THE APPROPRIATE TESTSERV MACRO
; IN VARIOUS PLACES. THE FIRST ENTRY OCCURS ONCE PER GROUP TO ALLOW TESTSERV
; TO DO SOME INITIALIZATION. THEN TWO ENTRIES ARE MADE FOR EACH TEST CASE IN
; THE GROUP. THE FIRST ALLOWS TESTSERV TO ISSUE THE SUBJECT SYSTEM SERVICE.
; THE SECOND ENTRY FOR THE TEST CASE CAUSES TESTSERV TO CHECK THE RETURNED
; STATUS CODE, PRINTING A FAILURE MESSAGE IF IT IS NOT THE EXPECTED CODE.
; IF THERE ARE NO MORE TEST CASES IN THE CURRENT GROUP, TESTSERV (NOT TC_CONTROL)
; RETURNS DIRECTLY TO TEST_SERV_EXEC (RSB ACTUALLY ISSUED IN TS_CLEANUP MACRO)
; FROM THIS SECOND ENTRY; OTHERWISE, CONTROL RETURNS TO TC_CONTROL WHICH
; IN TURN ENTERS TESTSERV AGAIN FOR THE NEXT TEST CASE. THE FAILURE OF A
; TEST CASE DOES NOT CAUSE TERMINATION OF THE TEST MODULE.
;
; CALLING SEQUENCE:
;
;	BSBW TC_CONTROL (ISSUED WITHIN THE TEST_SERV_EXEC MACRO)
;			(RSB IS ISSUED WITHIN THE TS_CLEANUP MACRO)
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	ARGUMENTS SPECIFIED ON EACH TESTSERV MACRO MAY BE VIEWED AS
;	INPUTS, SINCE TC_CONTROL AND TESTSERV ACT AS CO-ROUTINES.
;
; OUTPUT PARAMETERS:
;
;	SEVERITY CODE FIELD OF MOD_MSG_CODE (BITS 0,1,2) IS SET TO ERROR
;	IF ANY TEST CASE IN THE CURRENT GROUP FAILS; OTHERWISE IT REMAINS
;	SET TO SUCCESSFUL.
;
; IMPLICIT OUTPUTS:
;
;	%UETP-I-TEXT,   ERROR MESSAGES ARE WRITTEN TO SYS$OUTPUT BY
;			THE TESTSERV MACRO (CO-ROUTINE WITH TC_CONTROL)
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--



TC_CONTROL:
	PUSHL	TS_EP			; PUSH TESTSERV ENTRY POINT
	JSB	@(SP)+			; ENTER TESTSERV INITIALIZATION
10$:					; PROCESS NEXT TEST CASE
	MOVB	#^A/ /,$$TSTN$$+2 	; MAKE SURE T.C. NAME HAS A BLANK
	BSBW	REG_SAVE		; SAVE REGISTERS
	JSB	@CURRENT_TC		; JUMP TO CURRENT TEST CASE
	BSBW	REG_REST		; RESTORE REGS FOR TESTSERV
	JSB	@(SP)+			; LET TESTSERV ISSUE SYSTEM SERVICE
	BSBW	REG_COMP		; COMPARE REGS TO SEE IF ...
					; ... SYSTEM SERVICE CHANGED ANY
	BBCC	#DETACHV,FLAG,20$	; Br if not running on OPA0
	MOVL	#SS$_NOPRIV,R0		; Otherwise fake the return status
20$:
	JSB	@(SP)+			; LET TESTSERV CHEK S.S. STATUS CODE
	CMPB	#^A/*/,$$TSTN$$+2	; HAS TESTSERV INDICATED FAILURE ?
	BNEQU	10$			; NO -- PROCESS NEXT TEST CASE
	MOVAL	TEST_MOD_FAIL,TMD_ADDR	; YES -- INDICATE FAILED IN END MSG
	INSV	#ERROR,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR ERROR
	BRB	10$			; LOOP BAK TO PROCESS NEXT TEST CASE
;
;   TC_CONTROL RETURNS TO TEST_SERV_EXEC VIA TESTSERV (IN TS_CLEANUP MACRO)
;
	.PAGE
	.SBTTL	SUBROUTINES
REG_SAVE:
;
;  *****************************************************************
;  *                                                               *
;  *  SAVES R0 THRU SP IN REG_SAVE_AREA                            *
;  *                                                               *
;  *****************************************************************
;
	PUSHR	#R0_THRU_SP		; SAVE ALL REGS ON STACK
	MOVC3	#60,(SP),REG_SAVE_AREA	; SAVE REGS (BEFORE S.S.)
	POPR	#R0_THRU_SP		; CLEAN UP STACK
	RSB				; .... AND RETURN
;
;
;
;
REG_REST:
;
;
;  *****************************************************************
;  *                                                               *
;  *  RESTORES R0 THRU SP FROM REG_SAVE_AREA                       *
;  *                                                               *
;  *****************************************************************
;
	SUBL2	#60,SP			; MOVE SP TO MAKE ROOM FOR REGS
	MOVC3	#60,REG_SAVE_AREA,(SP)	; MOVE REGS ONTO STACK FOR POP
	POPR	#R0_THRU_SP		; RESTORE ALL REGS FOR TESTSERV
	RSB				; ... AND RETURN
	.PAGE
REG_COMP:
;
;  *****************************************************************
;  *                                                               *
;  *  1) PUSHES ALL REGS ONTO STACK                                *
;  *  2) COMPARES REGISTER IMAGES FROM STACK WITH CORRESPONDING    *
;  *       IMAGES FROM REG_SAVE_AREA FOR ALL REGISTERS SPECIFIED   *
;  *       IN REG_COMP_MASK.                                       *
;  *  3) FOR EACH UNEQUAL COMPARE, AN ERROR MESSAGE IS PRINTED     *
;  *       (USING $FAO AND $OUTPUT SYSTEM SERVICES).               *
;  *  4) POPS ALL REGS OFF OF STACK                                *
;  *                                                               *
;  *****************************************************************
;
	PUSHR	#R0_THRU_SP		; SAVE ALL REGISTERS ON STACK
	MOVAL	REG_SAVE_AREA,R6 	; POINT R6 TO BEG OF ...
					; ... REGS (BEFORE S.S.)
	MOVL	SP,R4			; POINT R4 TO BEG OF
					; ... REGS (AFTER S.S.)
	CVTBL	#-1,R3			; INITIALIZE REG_COMP_MASK INDEX
REG_COMP_NEXT:
	INCL	R3			; POINT TO NEXT BIT IN MASK
	CMPB	#15,R3			; END OF THE MASK ?
	BGTRU	REG_COMP_CONT		; NO -- CONTINUE
	BRW	REG_COMP_RSB		; YES -- GO TO COMMON RETURN
REG_COMP_CONT:
	CMPL	(R6)+,(R4)+		; REG BEFORE = REG AFTER ?
	BEQLU	REG_COMP_NEXT		; YES -- LOOK FOR NEXT REG
	BBC	R3,REG_COMP_MASK,REG_COMP_NEXT
					; NO -- GET NEXT IF BIT NOT SET
	MOVL	R3,CLOB_REG_NO		; NO -- GIVE REG NUMBER TO FAO
	MOVL	-4(R6),REG_BEFORE_SS 	; GIVE "BEFORE" CONTENTS TO FAO
	MOVL	-4(R4),REG_AFTER_SS  	; GIVE "AFTER" CONTENTS TO FAO
	MOVB	#^A/*/,$$TSTN$$+2 	; GIVE FAILURE INDIC'N IN ERROR MSG
;
	$FAO_S	ERR_MSG_FAOCTL,OUTL,OUTD,$$SNAD$$,  -
		$$ASEQ$$,$$PSEQ$$,CLOB_REG_NO,REG_BEFORE_SS,REG_AFTER_SS
;
	MOVW	OUTL,OUTD		; ACTUAL OUTPUT LEN IN STRING DESC'R
	PUTMSG	<#UETP$_TEXT,#1,#OUTD>	; PRINT THE MSG
	MOVW	#OUTE-OUTB,OUTD		; GET MAX LEN BACK INTO DESCRIPTOR
	MOVB	#^A/ /,$$TSTN$$+2 	; REMOVE FAIL INDIC'N FOR NEXT MSG
	MOVAL	TEST_MOD_FAIL,TMD_ADDR	; INDICATE FAILED IN END MSG
	INSV	#ERROR,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR ERROR
	BRW	REG_COMP_NEXT		; GO LOOK FOR NEXT REG TO COMPARE
REG_COMP_RSB:
	POPR	#R0_THRU_SP		; CLEAN UP STACK
	RSB				; RETURN TO CALLER
	.PAGE
MOD_MSG_PRINT:
;
;  *****************************************************************
;  *                                                               *
;  *  PRINTS THE TEST MODULE BEGUN/SUCCESSFUL/FAILED MESSAGES      *
;  *	   (USING THE PUTMSG MACRO).                               *
;  *                                                               *
;  *****************************************************************
;
	PUTMSG	<MOD_MSG_CODE,#2,TMN_ADDR,TMD_ADDR> ; PRINT MSG
	RSB				; ... AND RETURN TO CALLER
;
CHMRTN:
; **********************************************************
; *
; *	CHANGE MODE ROUTINE. THIS ROUTINE GETS CONTROL WHENEVER
; *	A CMKRNL, CMEXEC, OR CMSUP SYSTEM SERVICE IS ISSUED
; *	BY THE MODE MACRO ('TO' OPTION).  IT MERELY DOES 
; *	A JUMP INDIRECT ON A FIELD SET UP BY MODE. IT HAS 
; *	THE EFFECT OF RETURNING TO THE END OF THE MODE
; *	MACRO EXPANSION.
; *
; *************************************************************
;
	.WORD	0			; ENTRY MASK
	JMP	@CHM_CONT		; RETURN TO MODE MACRO IN NEW MODE
;
; *	RET INSTR WILL BE ISSUED IN EXPANSION OF 'MODE FROM, ....' MACRO
;
	.END	SATSSF11
