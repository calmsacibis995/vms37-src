	.TITLE	SATSSS78 SATS SYSTEM SERV TESTS  $LK/ULWSET (SUCC S.C.)
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SYSTST (SATS SYSTEM SERVICE TESTS)
;
; ABSTRACT:
;
;		THIS MODULE CONTAINS SUBROUTINES WHICH, WHEN LINKED
; WITH SUCCOMMON.OBJ, FORM TEST MODULE SATSSS78 TO TEST SUCCESSFUL
; OPERATION OF THE $LK/ULWSET SYSTEM SERVICES. EACH SERVICE IS INVOKED
; UNDER VARIOUS INPUT CONDITIONS WITH VARYING INPUT PARAMETERS. ONLY
; SUCCESSFUL STATUS CODES ARE EXPECTED IN THIS TEST MODULE.  CORRECT 
; OPERATION OF EACH SERVICE FOR EACH OF ITS ISSUANCES IS VERIFIED BY
; CHECKING FOR AN SS$_NORMAL STATUS CODE, EXPECTED RETURN ARGUMENTS
; AND EXPECTED FUNCTIONALITY PERFORMED.
;
; ENVIRONMENT:	USER MODE IMAGE; NEEDS CMKRNL PRIVILEGE,
;		DYNAMICALLY ACQUIRES OTHER PRIVILEGES, AS NEEDED.
;
; AUTHOR: THOMAS L. CAFARELLA,		CREATION DATE: APR, 1978
;
; MODIFIED BY:
;
;	, : VERSION
; 01	- 
;--
	.PAGE
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$PRVDEF				; PRIVILEGE BIT DEFINITIONS
	$PHDDEF				; PROCESS HEADER OFFSETS
	$PSLDEF				; PROCESSOR STATUS LONGWORD DEFINITIONS
;
; MACROS:
;
;
; EQUATED SYMBOLS:
;
;
; OWN STORAGE:
;
	.PAGE
	.PSECT	RODATA,RD,NOWRT,NOEXE,LONG
TEST_MOD_NAME::	STRING	C,<SATSSS78>	; TEST MODULE NAME
TEST_MOD_NAME_D: STRING	I,<SATSSS78>	; TEST MODULE NAME DESCRIPTOR
MSG1_INP_CTL:	STRING	I,<  SSLUW!4ZW: CONDITIONS:>
					; FAO CTL STRING FOR MSG1 IN SUCCOMMON.MAR
MSG3_ERR_CTL::	STRING	I,< *SSLUW!4ZW: !AS> 
					; FAO CTL STRING FOR MSG3 IN SUCCOMMON.MAR
	.PAGE
	.PSECT	RWDATA,RD,WRT,NOEXE,LONG
PRIVMASK:	.BLKQ	1		; ADDR OF PRIVILEGE MASK (IN PHD)
PAGCNT_ERG:	.BLKL	1		; PAGCNT ARGUMENT FOR EXPREG
REGION_ERG:	.BLKL	1		; REGION ARGUMENT FOR EXPREG
RETADR:		.BLKQ	1		; RETADR ARG FOR SUBJ $LKWSET & $ULWSET
	.PAGE
	.SBTTL	CONDITION TABLES
;
;	***** CONDITION TABLES FOR LKWSET/ULWSET SYSTEM SERVICES *****
;
	COND	1,QUAD,<INADR>,-
		  <CURRENT IMAGE PAGE>,-
		  <PROGRAM REGION -- SINGLE PAGE>,-
		  <PROGRAM REGION -- MULTIPLE PAGES>,-
		  <CONTROL REGION -- SINGLE PAGE>,-
		  <CONTROL REGION -- MULTIPLE PAGES>,-
 
		    .LONG	TM_SETUP,TM_SETUP
		    .LONG	0[8]
;
	COND	2,NOTARG,<PAGE OWNER ACCESS MODE>,-
		  <KERNEL>,-
		  <EXECUTIVE>,-
		  <SUPERVISOR>,-
		  <USER>,-
 
		    .LONG	PSL$C_KERNEL
		    .LONG	PSL$C_EXEC
		    .LONG	PSL$C_SUPER
		    .LONG	PSL$C_USER
;
	COND	3,LONG,<ACMODE>,-
		  <KERNEL>,-
		  <EXECUTIVE>,-
		  <SUPERVISOR>,-
		  <USER>,-
 
		    .LONG	PSL$C_KERNEL
		    .LONG	PSL$C_EXEC
		    .LONG	PSL$C_SUPER
		    .LONG	PSL$C_USER
;
	COND	4,NULL
 
	COND	5,NULL
 
	.PSECT	SATSSS78,RD,WRT,EXE
	.PAGE
	.SBTTL	TM_SETUP, TM_CLEANUP
;++
; FUNCTIONAL DESCRIPTION:
;
;		TM_SETUP AND TM_CLEANUP ARE CALLED TO PERFORM
; REQUIRED HOUSEKEEPING AT THE BEGINNING AND END, RESPECTIVELY, OF
; TEST MODULE EXECUTION.
;
; CALLING SEQUENCE:
;
;	BSBW TM_SETUP   BSBW TM_CLEANUP
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	TM_SETUP:  COND TABLE INDEX REGISTERS (R2,3,4,5,6) CLEARED;
;		   ALL PRIVILEGES ACQUIRED.
;
; COMPLETION CODES:
;
;	EFLAG SET TO NON-ZERO IF ERROR ENCOUNTERED.
;
; SIDE EFFECTS:
;
;	SS_CHECK AND ERR_EXIT MACROS CAUSE PREMATURE EXIT 
;	(VIA RSB) IF ERROR ENCOUNTERED.
;
;--



TM_SETUP::
	CLRL	R2			; INITIALIZE
	CLRL	R3			; .. CONDITION
	CLRL	R4			; .... TABLE
	CLRL	R5			; ...... INDEX
	CLRL	R6			; ........ REGISTERS
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE BEGIN MSG
	MOVAL	TEST_MOD_SUCC,TMD_ADDR	; ASSUME END MSG WILL SHOW SUCCESS
	INSV	#SUCCESS,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR SUCCESS
	MODE	TO,5$,KRNL		; KERNEL MODE TO ACCESS PHD
	MOVL	@#CTL$GL_PHD,R9		; GET PROCESS HEADER ADDRESS
	MOVAL	PHD$Q_PRIVMSK(R9),PRIVMASK ; GET PRIV MASK ADDRESS
	MODE	FROM,5$	; BACK TO USER MODE
	PRIV	ADD,ALL			; GET ALL PRIVILEGES
	$SETPRN_S TEST_MOD_NAME_D 	; SET PROCESS NAME
	SS_CHECK  NORMAL		; CHECK STATUS CODE RETURNED FROM SETPRN
	RSB				; RETURN TO MAIN ROUTINE
TM_CLEANUP::
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE END MSG
	RSB				; RETURN TO MAIN ROUTINE
	.PAGE
	.SBTTL	CONDITION SUBROUTINES - SETUP AND CLEANUP 
;++
; FUNCTIONAL DESCRIPTION:
;
;		CONDX AND CONDX_CLEANUP ARE SUBROUTINES WHICH ARE EXECUTED
; BEFORE AND AFTER THE VERIFY SUBROUTINE, RESPECTIVELY, WHENEVER A NEW
; CONDITION X VALUE IS SELECTED (SEE FUNCTIONAL DESCRIPTION OF SUCCOMMON
; ROUTINE IN SUCCOMMON.MAR). ANY SETUP FUNCTION PARTICULAR TO THE 
; CONDITION X TABLE IS INCLUDED IN THE CONDX SUBROUTINE AND CLEANED
; UP, IF NECESSARY, IN THE CONDX_CLEANUP SUBROUTINE. THIS INCLUDES,
; ESPECIALLY, CODE TO DETECT CONFLICTS AMONG CURRENT ENTRIES IN TWO
; OR MORE CONDITION TABLES. IF A CONFLICT IS DETECTED, A NON-ZERO
; VALUE IS STORED INTO CONFLICT, WHICH CAUSES THE CALLING ROUTINE
; (SUCCOMMON) TO SKIP THE CURRENT ENTRY IN THE CONDITION X TABLE.
;
; CALLING SEQUENCE:
;
;	BSBW CONDX   BSBW CONDX_CLEANUP
;	  WHERE X = 1,2,3,4,5
;
; INPUT PARAMETERS:
;
;	CONFLICT = 0
;
; IMPLICIT INPUTS:
;
;	R2,3,4,5,6 CONTAIN CURRENT CONDITION TABLE INDEX VALUES
;	  FOR COND TABLES 1,2,3,4,5, RESPECTIVELY.
;
; OUTPUT PARAMETERS:
;
;	CONFLICT SET TO NON-ZERO IF COND TABLE CONFLICT DETECTED.
;
; IMPLICIT OUTPUTS:
;
;	R2,3,4,5,6 PRESERVED
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--



COND1::
	RSB				; RETURN TO MAIN ROUTINE
COND1_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
COND2::
	TSTL	R2			; FIRST COND 1 ELEMENT ??
	BNEQ	COND2X			; NO -- NO CONFLICT
	CMPL	COND2_E[R3],#PSL$C_USER ; IS PAGE-OWNER USER MODE ??
	BEQL	COND2X			; YES -- NO CONFLICT
	MOVB	ONES,CONFLICT		; NO -- INDICATE CONFLICT BECAUSE ...
					; ... CURRENT IMAGE PAGE MUST BE USER
COND2X:
	RSB				; RETURN TO MAIN ROUTINE
COND2_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
COND3::
	CMPL	ACMODE[R4],COND2_E[R3] 	; IS LOCKER AT LEAST AS ...
				       	; ... PRIVILEGED AS OWNER ??
	BLEQ	COND3X			; YES -- THAT'S OK
	MOVB	ONES,CONFLICT		; NO -- INDICATE CONFLICT
COND3X:
	RSB				; RETURN TO MAIN ROUTINE
COND3_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
COND4::
	RSB				; RETURN TO MAIN ROUTINE
COND4_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
COND5::
	RSB				; RETURN TO MAIN ROUTINE
COND5_CLEANUP::
	RSB				; RETURN TO MAIN ROUTINE
	.PAGE
	.SBTTL	FORM_CONDS
;++
; FUNCTIONAL DESCRIPTION:
;
; 		FORM_CONDS FORMATS AND PRINTS INFORMATION ABOUT
;  THE CURRENT ELEMENT IN EACH OF THE CONDITION TABLES.
;
; CALLING SEQUENCE:
;
;	BSBW FORM_CONDS
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	R2,3,4,5,6 CONTAIN CURRENT CONDITION TABLE INDEX VALUES
;	  FOR COND TABLES 1,2,3,4,5, RESPECTIVELY.
;	FOR X = 1,2,3,4,5 :
;		CONDX_T - TITLE TEXT FOR CONDX TABLE
;		CONDX_TAB - ELEMENT TEXT FOR CONDX TABLE
;		CONDX_C - CONTEXT OF THE CONDX TABLE
;		CONDX_E - DATA ELEMENTS OF THE CONDX TABLE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--



FORM_CONDS::
	$FAO_S	MSG1_INP_CTL,FAO_LEN,FAO_DESC,TESTNUM
					; FORMAT CONDITIONS HEADER MSG
	BSBW	OUTPUT_MSG		; ... AND PRINT IT
	CMPB	#COND1_C,#NULL		; IS CONDITION 1 NULL ?
	BNEQU	10$			; NO -- CONTINUE
	BRW	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
10$:
	MOVAL	COND1_T,MSG_A		; SAVE ADDRESS OF CONDITION 1 TITLE FOR FAO
	MOVL	COND1_TAB[R2],MSG_B 	; SAVE ADDR OF COND 1 CURR TEXT ELT FOR FAO
	MOVB	#COND1_C,MSG_CTXT 	; SAVE CONDITION 1 CONTEXT FOR FAO
	MOV_VAL	COND1_C,COND1_E[R2],MSG_DATA1 ; GIVE COND 1 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 1 MSG
	CMPB	#COND2_C,#NULL		; IS CONDITION 2 NULL ?
	BNEQU	20$			; NO -- CONTINUE
	BRW	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
20$:
	MOVAL	COND2_T,MSG_A		; SAVE ADDRESS OF CONDITION 2 TITLE FOR FAO
	MOVL	COND2_TAB[R3],MSG_B 	; SAVE ADDR OF COND 2 CURR TEXT ELT FOR FAO
	MOVB	#COND2_C,MSG_CTXT 	; SAVE CONDITION 2 CONTEXT FOR FAO
	MOV_VAL	COND2_C,COND2_E[R3],MSG_DATA1 ; GIVE COND 2 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 2 MSG
	CMPB	#COND3_C,#NULL		; IS CONDITION 3 NULL ?
	BNEQU	30$			; NO -- CONTINUE
	BRW	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
30$:
	MOVAL	COND3_T,MSG_A		; SAVE ADDRESS OF CONDITION 3 TITLE FOR FAO
	MOVL	COND3_TAB[R4],MSG_B 	; SAVE ADDR OF COND 3 CURR TEXT ELT FOR FAO
	MOVB	#COND3_C,MSG_CTXT 	; SAVE CONDITION 3 CONTEXT FOR FAO
	MOV_VAL	COND3_C,COND3_E[R4],MSG_DATA1 ; GIVE COND 3 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 3 MSG
	CMPB	#COND4_C,#NULL		; IS CONDITION 4 NULL ?
	BEQLU	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
	MOVAL	COND4_T,MSG_A		; SAVE ADDRESS OF CONDITION 4 TITLE FOR FAO
	MOVL	COND4_TAB[R5],MSG_B 	; SAVE ADDR OF COND 4 CURR TEXT ELT FOR FAO
	MOVB	#COND4_C,MSG_CTXT 	; SAVE CONDITION 4 CONTEXT FOR FAO
	MOV_VAL	COND4_C,COND4_E[R5],MSG_DATA1 ; GIVE COND 4 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 4 MSG
	CMPB	#COND5_C,#NULL		; IS CONDITION 5 NULL ?
	BEQLU	FORM_CONDSX		; YES -- SUBROUTINE IS FINISHED
	MOVAL	COND5_T,MSG_A		; SAVE ADDRESS OF CONDITION 5 TITLE FOR FAO
	MOVL	COND5_TAB[R6],MSG_B 	; SAVE ADDR OF COND 5 CURR TEXT ELT FOR FAO
	MOVB	#COND5_C,MSG_CTXT 	; SAVE CONDITION 5 CONTEXT FOR FAO
	MOV_VAL	COND5_C,COND5_E[R6],MSG_DATA1 ; GIVE COND 5 DATA VALUE TO FAO
	BSBW	WRITE_MSG2		; FORMAT AND WRITE CONDITION 5 MSG
FORM_CONDSX:
	RSB				; RETURN TO CALLER
	.PAGE
	.SBTTL	VERIFY
;++
; FUNCTIONAL DESCRIPTION:
;
;		VERIFY IS CALLED ONCE FOR EACH COMBINATION OF CONDITION
; TABLE VALUES (AS DETERMINED BY THE INDEX REGISTERS R2,3,4,5,6 FOR
; COND TABLES 1,2,3,4,5, RESPECTIVELY). VERIFY ESTABLISHES THE CONDITIONS
; SPECIFIED BY THE COND TABLES AND ISSUES THE SUBJECT SYSTEM SERVICES
; ($LKWSET & $ULWSET). THEN, THE SUCCESSFUL OPERATION OF THE SERVICES IS
; VERIFIED BY EXAMINING THE STATUS CODES RETURNED, THE VALUES FOR RETURN ARGS
; AND THE FUNCTIONALITY PERFORMED. THE EXAMINATIONS TAKE THE FORM OF 
; COMPARISONS AGAINST EXPECTED VALUES. ANY FAILING COMPARISON CAUSES AN
; ERR_EXIT MACRO TO BE EXECUTED (EITHER DIRECTLY, OR INDIRECTLY,
; THROUGH THE SS_CHECK MACRO); ERR_EXIT SETS EFLAG TO NON-ZERO,
; PRINTS ERROR MESSAGES AND CAUSES AN IMMEDIATE RSB TO CALLER.
; WHEN ERR_EXIT IS EXECUTED, FURTHER CALLS TO VERIFY ARE SUPPRESSED,
; AND, AFTER EXECUTING CLEANUP SUBROUTINES, THE IMAGE EXITS.
;
; CALLING SEQUENCE:
;
;	BSBW VERIFY
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	R2,3,4,5,6 CONTAIN CURRENT CONDITION TABLE INDEX VALUES
;	  FOR COND TABLES 1,2,3,4,5, RESPECTIVELY.
;	FOR X = 1,2,3,4,5 :
;		CONDX_E - ADDRESS OF TABLE OF DATA VALUES FOR CONDX
;		  TABLE. IF THE CONTEXT OF TABLE X IS A SYSTEM SERVICE
;		  ARGUMENT, THE ARGUMENT NAME MAY BE USED AS A SYNONYM
;		  FOR CONDX_E.
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	VERIFY HAS NO OUTPUT. SINCE ITS PURPOSE IS TO TEST FOR ERRORS,
;	IT MERELY RETURNS TO CALLER NORMALLY AFTER THE TESTS, PROVIDING
;	ALL WERE SUCCESSFUL; IF AN ERROR IS DISCOVERED, RETURN IS VIA
;	AN ERR_EXIT OR SS_CHECK MACRO, BOTH OF WHICH DOCUMENT DETECTED
;	ERRORS.
;
; COMPLETION CODES:
;
;	EFLAG SET TO NON-ZERO IF ERROR ENCOUNTERED.
;
; SIDE EFFECTS:
;
;	SS_CHECK AND ERR_EXIT MACROS CAUSE PREMATURE EXIT 
;	(VIA RSB) IF ERROR ENCOUNTERED.
;
;--



VERIFY::
	TSTB	CFLAG			; SHOULD CONDITIONS BE PRINTED ?
	BEQL	5$			; NO -- CONTINUE
	BSBW	FORM_CONDS		; YES -- FMT & PRINT ALL CONDS FOR THIS T.C.
5$:
;
; SET UP INADR ARGUMENT IN COND 1 TABLE BY ISSUING $EXPREG
;
	TSTL	R2			; FIRST COND 1 ELEMENT ??
	BNEQ	10$			; NO -- CONTINUE
	BRW	40$			; YES -- INADR ALREADY SET UP
10$:
	CLRQ	INADR[R2]		; CLEAR $EXPREG RETURN AREA
	CLRL	REGION_ERG		; ASSUME PROGRAM REGION
	MOVZBL	#1,PAGCNT_ERG		; ... AND SINGLE PAGE COUNT
	CMPL	R2,#2			; IS IT PROGRAM REGION ??
	BLEQ	15$			; YES -- GO CHECK PAGE COUNT
	MOVZBL	#1,REGION_ERG		; NO -- MUST BE CONTROL REGION
15$:
	CMPL	R2,#1			; IS IT SINGLE PAGE ??
	BEQL	20$			; YES -- GO ESTABLISH MODE
	CMPL	R2,#3			; COULD STILL BE SINGLE PAGE
	BEQL	20$			; IT IS -- GO ESTABLISH MODE
	MOVZBL	#10,PAGCNT_ERG		; MULTIPLE PAGES
20$:
	MODE	TO,30$,KRNL		; GET KERNEL MODE, SO ACMODE ARG WORKS
	$EXPREG_S REGION=REGION_ERG, PAGCNT=PAGCNT_ERG, -
		  ACMODE=COND2_E[R3], RETADR=INADR[R2]
	MODE	FROM,30$		; BACK TO USER MODE
	SS_CHECK NORMAL			; CHECK FOR NORMAL STATUS RETURN
40$:
;
; ****** SYSTEM SERVICE CALLS WHICH ARE THE SUBJECT OF THIS TEST CASE ******
;
	MODE	TO,50$,KRNL		; GET KERNEL MODE, SO ACMODE ARG WORKS
	$LKWSET_S INADR=INADR[R2], RETADR=RETADR,-
		  ACMODE=ACMODE[R4]	; ISSUE SUBJECT SERVICE
	MODE	FROM,50$		; BACK TO USER MODE
	CMPL	R0,#SS$_WASCLR		; CODE RECEIVED = CODE EXPECTED ?
	BEQLU	60$			; YES -- CONTINUE
	MOVL	#SS$_WASCLR,EXPV 	; NO -- LOAD UP EXPECTED AND ...
	MOVL	R0,RECV			; ... RECEIVED VALUES, THEN EXIT
	ERR_EXIT LONG,<INCORRECT STATUS CODE RETURNED FROM LKWSET>
60$:
;
; COMPARE INADR AGAINST RETADR (EXCEPT LOW-ORDER 9 BITS) AND,
; IF OK, GO ISSUE $ULWSET.
;
	MOVQ	INADR[R2],R7		; GET INADR PAIR INTO REGS
	EXTZV	#9,#23,R7,R7		; SHIFT RIGHT 9 BITS
	CMPZV	#9,#23,RETADR,R7 	; INADR = RETADR (FIRST OF PAIR) ??
	BNEQU	70$			; NO -- ERROR
	EXTZV	#9,#23,R8,R8		; SHIFT RIGHT 9 BITS
	CMPZV	#9,#23,RETADR+4,R8 	; INADR = RETADR (2ND OF PAIR) ??
	BNEQU	70$			; NO -- ERROR
	BRW	80$			; YES -- SKIP ERROR PROCESSING
70$:
	MOVQ	INADR[R2],EXPV		; LOAD UP EXPECTED AND ...
	MOVQ	RETADR,RECV		; ... RECEIVED VALUES, THEN EXIT
	ERR_EXIT QUAD,<UNEXPECTED VALUE FROM LKWSET FOR>,-
		      < RETADR PAIR>
80$:
;
; SET UP TO ISSUE $ULWSET
;
	MODE	TO,90$,KRNL		; GET KERNEL MODE, SO ACMODE ARG WORKS
	$ULWSET_S INADR=INADR[R2], RETADR=RETADR,-
		  ACMODE=ACMODE[R4]	; ISSUE SUBJECT SERVICE
	MODE	FROM,90$		; BACK TO USER MODE
	CMPL	R0,#SS$_WASSET		; CODE RECEIVED = CODE EXPECTED ?
	BEQLU	100$			; YES -- CONTINUE
	MOVL	#SS$_WASSET,EXPV 	; NO -- LOAD UP EXPECTED AND ...
	MOVL	R0,RECV			; ... RECEIVED VALUES, THEN EXIT
	ERR_EXIT LONG,<INCORRECT STATUS CODE RETURNED FROM ULWSET>
100$:
;
; COMPARE INADR AGAINST RETADR (EXCEPT LOW-ORDER 9 BITS) AND,
; IF OK, VERIFY ROUTINE IS COMPLETE, GO DO ANOTHER TEST CASE.
;
	MOVQ	INADR[R2],R7		; GET INADR PAIR INTO REGS
	EXTZV	#9,#23,R7,R7		; SHIFT RIGHT 9 BITS
	CMPZV	#9,#23,RETADR,R7 	; INADR = RETADR (FIRST OF PAIR) ??
	BNEQU	110$			; NO -- ERROR
	EXTZV	#9,#23,R8,R8		; SHIFT RIGHT 9 BITS
	CMPZV	#9,#23,RETADR+4,R8 	; INADR = RETADR (2ND OF PAIR) ??
	BNEQU	110$			; NO -- ERROR
	BRW	VERIFYX			; YES -- SKIP ERROR PROCESSING
110$:
	MOVQ	INADR[R2],EXPV		; LOAD UP EXPECTED AND ...
	MOVQ	RETADR,RECV		; ... RECEIVED VALUES, THEN EXIT
	ERR_EXIT QUAD,<UNEXPECTED VALUE FROM ULWSET FOR>,-
		      < RETADR PAIR>
VERIFYX:
	RSB				; RETURN TO CALLER
	.PAGE
	.SBTTL	VFY_CLEANUP
;++
; FUNCTIONAL DESCRIPTION:
;
;		VFY_CLEANUP EXECUTES SYSTEM SERVICES TO UNDO THE
; EFFECT OF THOSE ISSUED IN THE VERIFY SUBROUTINE. VFY_CLEANUP MUST
; ASSUME THAT VERIFY MAY NOT HAVE EXECUTED IN ITS ENTIRETY (IF AN
; ERROR IS FOUND). ALSO, VFY_CLEANUP MAY ISSUE SS_CHECK OR ERR_EXIT
; ONLY AFTER PERFORMING ALL OF ITS CLEANUP OPERATIONS; THIS IS REQUIRED
; IN THE EVENT THAT VFY_CLEANUP IS CALLED DURING ERROR PROCESSING,
; WHEN PERFORMING THE REQUIRED CLEANUP IS MORE IMPORTANT THAN
; POSSIBLY DISCOVERING A SECOND ERROR.
;
; CALLING SEQUENCE:
;
;	BSBW VFY_CLEANUP
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	R2,3,4,5,6 CONTAIN CURRENT CONDITION TABLE INDEX VALUES
;	  FOR COND TABLES 1,2,3,4,5, RESPECTIVELY.
;	FOR X = 1,2,3,4,5 :
;		CONDX_E - ADDRESS OF TABLE OF DATA VALUES FOR CONDX
;		  TABLE. IF THE CONTEXT OF TABLE X IS A SYSTEM SERVICE
;		  ARGUMENT, THE ARGUMENT NAME MAY BE USED AS A SYNONYM
;		  FOR CONDX_E.
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	EFLAG SET TO NON-ZERO IF ERROR ENCOUNTERED.
;
; SIDE EFFECTS:
;
;	SS_CHECK AND ERR_EXIT MACROS CAUSE PREMATURE EXIT 
;	(VIA RSB) IF ERROR ENCOUNTERED.
;
;--



VFY_CLEANUP::
	TSTL	R2			; FIRST COND 1 ELEMENT ??
	BNEQ	10$			; NO -- CONTINUE
	BRW	VFY_CLEANUPX		; YES -- NO PAGES TO DELETE
10$:
	MOVQ	INADR[R2],R7		; AVOID INDEX MODE, USE REGS
	TSTL	R7			; DID $EXPREG GET SOME SPACE ABOVE ??
	BNEQU	20$			; YES -- CONTINUE
	BRW	VFY_CLEANUPX		; NO -- NO PAGES TO DELETE
20$:
	MODE TO,30$,KRNL		; KRNL MODE TO DELETE ACQUIRED PAGES
	$DELTVA_S INADR=INADR[R2], -
		  ACMODE=COND2_E[R3]	; ....
	MODE FROM,30$			; BACK TO USER MODE
	SS_CHECK NORMAL			; CHECK RETURN STATUS VALUE
VFY_CLEANUPX:
	RSB				; RETURN TO CALLER
	.END
