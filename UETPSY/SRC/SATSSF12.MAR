	.TITLE	SATSSF12 - SATS SYSTEM SERVICE TESTS  (FAILING S.C.)
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SATS SYSTEM SERVICE TESTS
;
; ABSTRACT:	THE SATSSF12 MODULE TESTS THE EXECUTION OF CERTAIN
; VMS SYSTEM SERVICES, INVOKED IN SUCH A WAY AS TO EXPECT FAILING
; STATUS CODES. THE SYSTEM SERVICES TESTED AND THE STATUS CODES
; EXPECTED ARE SUMMARIZED AS ARGUMENTS TO THE TESTSERV MACROS
; WHICH APPEAR NEAR THE END OF THIS LISTING. SUCCESSFUL STATUS
; CODES ARE TESTED IN OTHER MODULES.
;
;
; ENVIRONMENT:	USER MODE IMAGE; NEEDS CMKRNL PRIVILEGE,
;		DYNAMICALLY ACQUIRES OTHER PRIVILEGES, AS NEEDED.
;
; AUTHOR: THOMAS L. CAFARELLA,		CREATION DATE: MMM, 1978
;	  PAUL D. FAY (DISPSERV & TESTSERV MACROS)
;
; MODIFIED BY:
;
;	, : VERSION
; 01	- 
;--
	.PAGE
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$PHDDEF				; PROCESS HEADER OFFSET SYMBOLS
	$PCBDEF				; PROCESS CONTROL BLOCK OFFSET SYMBS
	$STSDEF				; STATUS MESSAGE SYMBOLS
	$PRVDEF				; SYMBOL DEFS FOR PRIVILEGES
	$UETPDEF			; UETP MSG CODE DEFINITIONS
	$SHR_MESSAGES UETP,116,<<TEXT,INFO>> 
					; DEFINE UETP$_TEXT
					; GET RID OF MACRO DEFINITIONS
	$PSLDEF				; ACCESS MODE SYMBOLS
;
; MACROS:
;
;
; EQUATED SYMBOLS:
;
WARNING		= 0			; WARNING SEVERITY VALUE FOR MSGS
SUCCESS		= 1			; SUCCESS SEVERITY VALUE FOR MSGS
ERROR		= 2			; ERROR SEVERITY VALUE FOR MSGS
INFO		= 3			; INFORMATIONAL SEV VALUE FOR MSGS
SEVERE		= 4			; SEVERE (FATAL) SEV VALUE FOR MSGS
TCG_NO		=  0			; INITIALIZE TEST CASE GROUP NUMBER
GRP_TOTAL	=  0			; INITIALIZE TEST CASE GROUP TOTAL
R0_THRU_SP	= ^M<R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,AP,FP,SP>
RETADR_ERG20	= 1			; RETADR ARG FOR EXPREG (LOCATION 1)
RETADR_CRG20	= 1			; RETADR ARG FOR CNTREG (LOCATION 1)
INADR_SPT13	= 0			; INADR ARG FOR SETPRT (LOCATION 0)
RETADR_SPT20	= 1			; RETADR ARG FOR SETPRT (LOCATION 1)
PRVPRT_SPT50	= 1			; PRVPRT ARG FOR SETPRT (LOCATION 1)
;
; OWN STORAGE:
;
	.PAGE
	.PSECT	RODATA,RD,NOWRT,NOEXE,LONG
REG_COMP_MASK:	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,AP,FP> ! ^X8000 -
					; REG COMPARE MASK (HIGH-ORDER ... 
					; ... BIT MUST BE ON
ERR_MSG_FAOCTL:	STRING	I,<!/!AC!1ZB!1ZB: REGISTER !2UW CONTENTS ALTERED>, -
		<;  BEFORE SERVICE CALL: !8XL   AFTER SERVICE CALL: !8XL>
TEST_MOD_NAME:	STRING	C,<SATSSF12>	; TEST MODULE NAME
TEST_MOD_BEG:	STRING	C,<begun>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_SUCC:	STRING	C,<successful>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_FAIL:	STRING	C,<failed>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_NAME_D: STRING	I,<SATSSF12>	; TEST MODULE NAME DESCRIPTOR
TTNAME:     	STRING  I,<TT>		; TERMINAL LOGICAL NAME
INADR:		.LONG	NOACCESS,NOACCESS ; PAGE ADDRESS OF NOACCESS PSECT
PROT:		.LONG	PRT$C_NA	; PROTECTION CODE FOR NOACCESS PSECT
ONES:		.LONG	-1,-1		; A QUADWORD OF 1-BITS
PAGCNT_ERG:	.LONG	2		; PAGCNT ARGUMENT FOR EXPREG
RETADR_ERG21:	.BLKL	2		; RETADR ARGUMENT FOR EXPREG
ACMODE_ERG:				; ACMODE ARGUMENT FOR EXPREG
ACMODE_CRG:				; ACMODE ARGUMENT FOR CNTREG
ACMODE_SPT:				; ACMODE ARGUMENT FOR SETPRT
		.LONG	PSL$C_USER
REGION_ERG:				; REGION ARG FOR EXPREG (PGM REGION)
REGION_CRG:				; REGION ARG FOR CNTREG (PGM REGION)
		.LONG	0
PAGCNT_CRG:	.LONG	1		; PAGCNT ARGUMENT FOR CNTREG
PAGCNT_CRG13:	.LONG	1		; PAGCNT ARGUMENT FOR CNTREG
RETADR_CRG21:	.BLKL	2		; RETADR ARGUMENT FOR CNTREG
INADR_SPT11:	.ADDRESS ^X80000000	; INADR ARGUMENT FOR SETPRT
		.ADDRESS ^X8F000000	; .......
RETADR_SPT21:	.BLKL	2		; RETADR ARGUMENT FOR SETPRT
PROT_SPT:	.LONG	PRT$C_UW	; PROT ARGUMENT FOR SETPRT
PROT_SPT40:	.LONG	PRT$C_RESERVED	; PROT ARG (CODE 1 -- NO ACCESS) FOR SETPRT
PROT_SPT41:	.LONG	16		; PROT ARGUMENT FOR SETPRT
PRVPRT_SPT51:	.BLKB	1		; PRVPRT ARGUMENT FOR SETPRT
SWPFLG_SSM:	.LONG	0		; SWPFLG ARGUMENT FOR SETSWM
SWPFLG_SSM10:	.LONG	1		; SWPFLG ARGUMENT FOR SETSWM
	.PAGE
	.PSECT	RWDATA,RD,WRT,NOEXE
TPID:		.BLKL	1		; PROCESS ID FOR THIS PROCESS
CURRENT_TC:	.BLKL	1 		; PTR TO CURRENT TEST CASE
REG_SAVE_AREA:	.BLKL	15		; SAVE AREA FOR ALL REGS (SANS PC)
MOD_MSG_CODE:	.LONG	UETP$_SATSMS	; TEST MODULE MSG CODE FOR PUTMSG
CLOB_REG_NO:	.BLKL	1		; CLOBBERED REG NO (FOR FAO ERR MSG)
REG_BEFORE_SS:	.BLKL	1		; REG CONTENTS BEFORE S.S. 
					; ... (FOR FAO ERROR MSG)
REG_AFTER_SS:	.BLKL	1		; REG CONTENTS AFTER S.S. 
					; ... (FOR FAO ERROR MSG)
$$TSTN$$:	STRING	C,<  SF   >	; ASCII PORTION OF TEST CASE NAME
TMN_ADDR:	.ADDRESS TEST_MOD_NAME	; ADDR OF TEST MOD NAME FOR FAO
TMD_ADDR:	.ADDRESS TEST_MOD_BEG	; ADDR OF T.M. DISP FIELD FOR FAO
TS_EP:		.BLKL	1		; ENTRY PNT FOR CURR TESTSERV MACRO
RETADR:		.BLKL	2		; RETURN LONGWORDS FOR SETPRT
PRVPRT:		.BLKB	1		; PROT RETURN BYTE FOR SETPRT
PRIVMASK:	.BLKQ	1		; ADDR OF PRIVILEGE MASK (IN PHD)
CHM_CONT:	.BLKL	1		; CHANGE MODE CONTINUE ADDRESS
REGS:		.BLKL	5		; AREA FOR COND INDEX REGS (R2-R6)
PAGCNT_ERG10:				; PAGCNT ARGUMENT FOR EXPREG
PAGCNT_ERG11:				; PAGCNT ARGUMENT FOR EXPREG
PAGCNT_ERG12:				; PAGCNT ARGUMENT FOR EXPREG
PAGCNT_CRG10:				; PAGCNT ARGUMENT FOR CNTREG
PAGCNT_CRG11:				; PAGCNT ARGUMENT FOR CNTREG
PAGCNT_CRG12:				; PAGCNT ARGUMENT FOR CNTREG
		.BLKL	1
RETADR_ERG:	.LONG	0,0		; RETADR ARGUMENT FOR EXPREG
RETADR_CRG:	.LONG	0,0		; RETADR ARGUMENT FOR CNTREG
RETADR_CRG13:	.BLKL	2		; RETADR ARG FOR NON-SUBJECT EXPREG
INADR_SPT:	.BLKL	2		; INADR ARGUMENT FOR SETPRT SERVICE
INADR_SPT10:	.BLKL	2		; INADR ARGUMENT FOR SETPRT SERVICE
EXP_RANGE:	.BLKQ	1		; EXPREG RANGE FOR SFPRT10
INADR_SPT12:	.BLKL	2		; INADR ARGUMENT FOR SETPRT SERVICE
RETADR_SPT:	.BLKL	2		; RETADR ARGUMENT FOR SETPRT SERVICE
PRVPRT_SPT:	.BLKB	1		; PRVPRT ARGUMENT FOR SETPRT
	.PAGE
	.PSECT	SATS_ACCVIO_1,RD,WRT,NOEXE,PAGE
EMPTY:		.BLKB	512	; RESERVE A PAGE OF SPACE
;
; + 
; ******************************************************************
; *                                                                *
; * 	THE ORDER OF STATEMENTS IN THIS PSECT IS CRITICAL.         *
; *	DO NOT RE-ARRANGE THE VARIABLES. CONSULT SATS              *
; *	FUNCTIONAL SPECIFICATION FOR A DESCRIPTION OF THE USE      *
; *	OF THE EMPTY PSECT (AND ITS COMPANION PSECT, NOACCESS).    *
; *                                                                *
; ******************************************************************
; -
;
; TYPE AAAAA_SSSX1 (TYPE AAAAA_SSSX2 IF NOT DESC) GO HERE:
RETADR_ERG22	= . - 4		; RETADR ARGUMENT FOR EXPREG
RETADR_ERG23	= . - 1		; RETADR ARG FOR EXPREG (LAST BYTE IN PAGE)
RETADR_ERG24	= . - 7		; RETADR ARGUMENT FOR EXPREG
RETADR_CRG22	= . - 4		; RETADR ARGUMENT FOR CNTREG
RETADR_CRG23	= . - 1		; RETADR ARG FOR CNTREG (LAST BYTE IN PAGE)
RETADR_CRG24	= . - 7		; RETADR ARGUMENT FOR CNTREG
RETADR_SPT22	= . - 7			; RETADR ARGUMENT FOR SETPRT
		. = . - 13	; ALLOW ROOM FOR STRING DESCRIPTOR
; TYPE AAAAA_SSSX5 GO HERE:
		.LONG	6	; STRING LENGTH (WILL CROSS PSECT BOUNDARY)
		.ADDRESS .+4	; STRING ADDRESS
; TYPE AAAAA_SSSX3 GO HERE:
		.BLKB	1	; LOW-ORDER BYTE OF STRING LENGTH
; TYPE AAAAA_SSSX2 GO HERE:
		.BLKL	1	; STRING LENGTH
;
;
;
;
	.PSECT	SATS_ACCVIO_2,RD,WRT,NOEXE,PAGE
NOACCESS:	.BLKB	512	; RESERVE A PAGE OF SPACE
		. = . - 512	; RETURN LOC CTR TO BEGINNING OF PSECT
		.ADDRESS EMPTY	; ADDRESS OF ACCESSIBLE STRING
		.ADDRESS EMPTY/^X100 ; ADDRESS OF ACCESSIBLE STRING
;+
; *** NOTE -- DO NOT CHANGE LOCATION OR SEQUENCE OF ABOVE STATEMENTS!
; ***         THIS PSECT (NOACCESS) MUST APPEAR IN MEMORY IMMEDIATELY
; ***         FOLLOWING THE EMPTY PSECT. PSECT NAMES AND OPTIONS WILL BE 
; ***         CHOSEN TO FORCE THE DESIRED PSECT ORDERING.
;-
;
INADR_SPT14:	.BLKL	2		; INADR ARGUMENT FOR SETPRT
;
;
;
	.PSECT	SATSSF12,RD,WRT,EXE,LONG
	.PAGE
	.SBTTL	SATSSF12
;++
; FUNCTIONAL DESCRIPTION:
;
;		AFTER PERFORMING SOME INITIAL HOUSEKEEPING, SUCH AS
; PRINTING THE MODULE BEGIN MESSAGE AND ACQUIRING ALL PRIVILEGES,
; THE SATSSF12 ROUTINE EXECUTES THE TEST_SERV_EXEC MACRO TO RUN
; ALL TEST CASES. WHEN THE MACRO COMPLETES ITS EXECUTION, SATSSF12
; PRINTS A TEST MODULE SUCCESS OR FAIL MESSAGE AND EXITS TO THE
; OPERATING SYSTEM. TEST_SERV_EXEC CALLS THE TC_CONTROL/TESTSERV
; CO-ROUTINE PAIR ONCE PER TEST CASE GROUP TO EXECUTE ALL TEST
; CASES IN THAT GROUP. EACH TEST CASE GROUP IS DEFINED BY BOUNDING
; ITS TEST CASES WITH A TC_GROUP MACRO BEFORE THE FIRST TEST CASE
; AND A TCEND MACRO AFTER THE LAST ONE. THE TEST CASES THEMSELVES
; ARE DEFINED WITHIN THESE BOUNDS BY PRECEDING EACH WITH A
; NEXT_TEST_CASE MACRO. TC_CONTROL/TESTSERV EXECUTES THE CODE 
; FOLLOWING EACH NEXT_TEST_CASE MACRO IMMEDIATELY BEFORE ISSUING
; THE SYSTEM SERVICE AS REQUESTED IN THE TESTSERV MACRO. TC_CONTROL/
; TESTSERV ALSO CHECKS THE RESULTS OF THE SERVICE WITH RESPECT
; TO ITS EXPECTED STATUS CODE AND PRINTS ANY REQUIRED FAILURE
; MESSAGES FOR THE TEST CASE. THE CODE APPEARING AFTER EACH
; NEXT_TEST_CASE MACRO IS MERELY TO SET UP CONDITIONS REQUIRED
; FOR THE SYSTEM SERVICE AND TO CLEAN UP ANY RESOURCES ACQUIRED
; BY THE PREVIOUS TEST CASE.
;
; CALLING SEQUENCE:
;
;	$ RUN SATSSF12  ...  (DCL COMMAND)
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	MESSAGES TO SYS$OUTPUT ARE THE ONLY OUTPUT FROM SATSSF12.
;	THEY ARE OF THE FORM:
;
;		%UETP-S-SATSMS, TEST MODULE SATSSF12 BEGUN ... (BEGIN MSG)
;		%UETP-S-SATSMS, TEST MODULE SATSSF12 SUCCESSFUL ... (END MSG)
;		%UETP-E-SATSMS, TEST MODULE SATSSF12 FAILED ... (END MSG)
;		%UETP-I-TEXT, ... (VARIABLE INFORMATION ABOUT A TEST MODULE FAILURE)
;
; COMPLETION CODES:
;
;	THE SATSSF12 ROUTINE TERMINATES WITH A $EXIT TO THE
;	OPERATING SYSTEM WITH A STATUS CODE DEFINED BY UETP$_SATSMS.
;
; SIDE EFFECTS:
;
;	NONE
;
;--



SATSSF12:
	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
					; ENTRY MASK
	$WAKE_S  TPID			; GET PID OF THIS PROCESS
	$HIBER_S			; UNDO WAKE
	$SETPRN_S TEST_MOD_NAME_D 	; SET PROCESS NAME
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE BEGIN MSG
	MOVAL	TEST_MOD_SUCC,TMD_ADDR	; ASSUME END MSG WILL SHOW SUCCESS
	INSV	#SUCCESS,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR SUCCESS
	MODE	TO,10$,KRNL,NOREGS 	; KERNEL MODE TO ACCESS PHD
	MOVL	@#CTL$GL_PHD,R9		; GET PROCESS HEADER ADDRESS
	MOVAL	PHD$Q_PRIVMSK(R9),PRIVMASK ; GET PRIV MASK ADDRESS
	MODE	FROM,10$		; GET BACK TO USER MODE
	PRIV	ADD,ALL			; GET ALL PRIVILEGES
	DISPSERV			; SET UP DISPLAY INFO FOR TESTSERV
	$SETPRT_S INADR=INADR, RETADR=RETADR, -
		  PROT=PROT, PRVPRT=PRVPRT
					; SET NOACCESS PSECT ...
					; ... FOR NO USER ACCESS
	BRW	EXECUTE			; GO EXECUTE ALL TEST CASES
;
	TC_GROUP	ERG,1,TS1
;
	NEXT_TEST_CASE	SFERG10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFERG10      
; *
; *  SYSTEM SERVICE:		EXPREG               
; *
; *  ARGUMENT UNDER TEST: 	PAGCNT_ERG10         
; *
; *  INPUT CONDITIONS:
; *	ILLEGAL PAGE COUNT (ZERO)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ILLPAGCNT
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	CLRL	PAGCNT_ERG10		; CLEAR PAGE COUNT
;
	NEXT_TEST_CASE	SFERG11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFERG11      
; *
; *  SYSTEM SERVICE:		EXPREG               
; *
; *  ARGUMENT UNDER TEST: 	PAGCNT_ERG11         
; *
; *  INPUT CONDITIONS:
; *	ILLEGAL PAGE COUNT (NEGATIVE)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ILLPAGCNT
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	CVTBL	#-1,PAGCNT_ERG11	; ASK FOR -1 PAGES
;
	NEXT_TEST_CASE	SFERG12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFERG12      
; *
; *  SYSTEM SERVICE:		EXPREG               
; *
; *  ARGUMENT UNDER TEST: 	PAGCNT_ERG12         
; *
; *  INPUT CONDITIONS:
; *	ILLEGAL PAGE COUNT (NEGATIVE)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ILLPAGCNT
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MOVL	#-100000,PAGCNT_ERG12	; ASK FOR LARGE NEG NUMBER OF PAGES
;
	NEXT_TEST_CASE	SFERG20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFERG20      
; *
; *  SYSTEM SERVICE:		EXPREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_ERG20         
; *
; *  INPUT CONDITIONS:
; *	RETURN ADDRESS FIELD AT LOCATION 1.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFERG21
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFERG21      
; *
; *  SYSTEM SERVICE:		EXPREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_ERG21         
; *
; *  INPUT CONDITIONS:
; *	FIRST LONGWORD OF RETURN ADDRESS FIELD IN
; *	  NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFERG22
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFERG22      
; *
; *  SYSTEM SERVICE:		EXPREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_ERG22         
; *
; *  INPUT CONDITIONS:
; *	FIRST LONGWORD OF RETURN ADDRESS FIELD IN
; *	  ACCESSIBLE PSECT, SECOND LONGWORD IN
; *	  NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFERG23
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFERG23      
; *
; *  SYSTEM SERVICE:		EXPREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_ERG23         
; *
; *  INPUT CONDITIONS:
; *	FIRST LONGWORD OF RETURN ADDRESS FIELD BEGINS
; *	  IN ACCESSIBLE PSECT, ENDS IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFERG24
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFERG24      
; *
; *  SYSTEM SERVICE:		EXPREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_ERG24         
; *
; *  INPUT CONDITIONS:
; *	SECOND LONGWORD OF RETURN ADDRESS FIELD BEGINS
; *	IN ACCESSIBLE PSECT, ENDS IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	CRG,1,TS2
;
	NEXT_TEST_CASE	SFCRG10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCRG10      
; *
; *  SYSTEM SERVICE:		CNTREG               
; *
; *  ARGUMENT UNDER TEST: 	PAGCNT_CRG10         
; *
; *  INPUT CONDITIONS:
; *	ILLEGAL PAGE COUNT (ZERO)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ILLPAGCNT
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	CLRL	PAGCNT_CRG10		; ZERO OUT REQUESTED PAGE COUNT
;
	NEXT_TEST_CASE	SFCRG11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCRG11      
; *
; *  SYSTEM SERVICE:		CNTREG               
; *
; *  ARGUMENT UNDER TEST: 	PAGCNT_CRG11         
; *
; *  INPUT CONDITIONS:
; *	ILLEGAL PAGE COUNT (NEGATIVE)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ILLPAGCNT
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	CVTBL	#-1,PAGCNT_CRG11	; REQUEST -1 PAGES
;
	NEXT_TEST_CASE	SFCRG12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCRG12      
; *
; *  SYSTEM SERVICE:		CNTREG               
; *
; *  ARGUMENT UNDER TEST: 	PAGCNT_CRG12         
; *
; *  INPUT CONDITIONS:
; *	ILLEGAL PAGE COUNT (NEGATIVE)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ILLPAGCNT
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MOVL	#-100000,PAGCNT_CRG12	; REQUEST LARGE NEG. PAGE COUNT
;
	NEXT_TEST_CASE	SFCRG13
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCRG13      
; *
; *  SYSTEM SERVICE:		CNTREG               
; *
; *  ARGUMENT UNDER TEST: 	PAGCNT_CRG13         
; *
; *  INPUT CONDITIONS:
; *	TRY TO CONTRACT EXECUTIVE REGION
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	PAGOWNVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,10$,EXEC,NOREGS	; GET TO EXEC MODE FOR $EXPREG
	$EXPREG_S PAGCNT=PAGCNT_CRG13, - ; EXPAND BY ONE PAGE
		  RETADR=RETADR_CRG13
	MODE	FROM,10$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFCRG20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCRG20      
; *
; *  SYSTEM SERVICE:		CNTREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_CRG20         
; *
; *  INPUT CONDITIONS:
; *	RETURN ADDRESS FIELD AT LOCATION 1.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,20$,EXEC,NOREGS	; EXEC MODE TO ISSUE $DELTVA
	$DELTVA_S INADR=RETADR_CRG13	; DELETE PAGE ACQUIRED BY SFCRG13
	MODE	FROM,20$		; ... BACK TO USER MODE
;
	NEXT_TEST_CASE	SFCRG21
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCRG21      
; *
; *  SYSTEM SERVICE:		CNTREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_CRG21         
; *
; *  INPUT CONDITIONS:
; *	FIRST LONGWORD OF RETURN ADDRESS FIELD IN
; *	  NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFCRG22
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCRG22      
; *
; *  SYSTEM SERVICE:		CNTREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_CRG22         
; *
; *  INPUT CONDITIONS:
; *	FIRST LONGWORD OF RETURN ADDRESS FIELD IN ACCESSIBLE
; *	  PSECT, SECOND LONGWORD IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFCRG23
;
;++
; ******************************************************
; *

; *  TEST CASE NAME:		SFCRG23      
; *
; *  SYSTEM SERVICE:		CNTREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_CRG23         
; *
; *  INPUT CONDITIONS:
; *	FIRST LONGWORD OF RETURN ADDRESS FIELD BEGINS IN ACCESSIBLE
; *	  PSECT, ENDS IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFCRG24
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFCRG24      
; *
; *  SYSTEM SERVICE:		CNTREG               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_CRG24         
; *
; *  INPUT CONDITIONS:
; *	SECOND LONGWORD OF RETURN ADDRESS FIELD BEGINS IN ACCESSIBLE
; *	  PSECT, ENDS IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	SPT,1,TS3
;
	NEXT_TEST_CASE	SFSPT10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT10      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	INADR_SPT10         
; *
; *  INPUT CONDITIONS:
; *	SET PROTECTION FOR A PAGE OWNED BY EXEC MODE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	PAGOWNVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,10$,EXEC,NOREGS	; GET INTO EXEC MODE FOR EXPREG
	$EXPREG_S PAGCNT=#1, RETADR=EXP_RANGE
					; GET A 1-PAGE REGION OWNED BY EXEC MODE
	MODE	FROM,10$		; BACK TO USER MODE
	MOVQ	EXP_RANGE,INADR_SPT10	; POINT TO EXEC-OWNED RANGE
;
	NEXT_TEST_CASE	SFSPT11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT11      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	INADR_SPT11         
; *
; *  INPUT CONDITIONS:
; *	SET PROTECTION ON A RANGE OF PAGES IN SYSTEM SPACE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,20$,EXEC,NOREGS	; INTO EXEC MODE FOR CNTREG
	$CNTREG_S PAGCNT=#1		; GIVE BACK PAGE ACQUIRED BY SFSPT10
	MODE	FROM,20$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFSPT12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT12      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	INADR_SPT12         
; *
; *  INPUT CONDITIONS:
; *	SET PROTECTION ON A NON-EXISTENT PAGE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	LENVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MOVQ	EXP_RANGE,INADR_SPT12	; POINT TO NOW UNAVAILABLE RANGE
;
	NEXT_TEST_CASE	SFSPT13
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT13      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	INADR_SPT13         
; *
; *  INPUT CONDITIONS:
; *	INPUT ADDRESS FIELD AT LOCATION 0.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSPT14
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT14      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	INADR_SPT14         
; *
; *  INPUT CONDITIONS:
; *	INPUT ADDRESS FIELD IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSPT20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT20      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_SPT20         
; *
; *  INPUT CONDITIONS:
; *	RETURN ADDRESS FIELD AT LOCATION 1.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSPT21
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT21      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_SPT21         
; *
; *  INPUT CONDITIONS:
; *	RETURN ADDRESS FIELD IN READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSPT22
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT22      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	RETADR_SPT22         
; *
; *  INPUT CONDITIONS:
; *	SECOND LONGWORD OF RETURN ADDRESS FIELD BEGINS IN
; *	  ACCESSIBLE PSECT, ENDS IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSPT40
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT40      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	PROT_SPT40         
; *
; *  INPUT CONDITIONS:
; *	PROTECTION CODE 1 SPECIFIED.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVPROTECT
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSPT41
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT41      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	PROT_SPT41         
; *
; *  INPUT CONDITIONS:
; *	PROTECTION CODE 16 SPECIFIED.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVPROTECT
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSPT50
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT50      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	PRVPRT_SPT50         
; *
; *  INPUT CONDITIONS:
; *	PREVIOUS PROTECTION FIELD AT LOCATION 1.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSPT51
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSPT51      
; *
; *  SYSTEM SERVICE:		SETPRT               
; *
; *  ARGUMENT UNDER TEST: 	PRVPRT_SPT51         
; *
; *  INPUT CONDITIONS:
; *	PREVIOUS PROTECTION FIELD IN READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	SSM,1,TS4
;
	NEXT_TEST_CASE	SFSSM10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSSM10      
; *
; *  SYSTEM SERVICE:		SETSWM               
; *
; *  ARGUMENT UNDER TEST: 	SWPFLG_SSM10         
; *
; *  INPUT CONDITIONS:
; *	USER DOES NOT HAVE THE PRIVILEGE TO ALTER
; *	  PROCESS SWAP MODE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	PRIV	REM,PSWAPM		; REMOVE SWAP MODE PRIVILEGE
;
	TCEND
TS1:
	TESTSERV	EXPREG,ERR,SATS,				-
                                          				-
	   <1,PAGCNT_ERG,						-
	        	PAGCNT_ERG10,ILLPAGCNT,	-  ; SFERG10
	        	PAGCNT_ERG11,ILLPAGCNT,	-  ; SFERG11
	        	PAGCNT_ERG12,ILLPAGCNT,	-  ; SFERG12
                                                  >,			-
                                          				-
	   <1,RETADR_ERG,						-
	        	RETADR_ERG20,ACCVIO,	-  ; SFERG20
	        	RETADR_ERG21,ACCVIO,	-  ; SFERG21
	        	RETADR_ERG22,ACCVIO,	-  ; SFERG22
	        	RETADR_ERG23,ACCVIO,	-  ; SFERG23
	        	RETADR_ERG24,ACCVIO,	-  ; SFERG24
                                                  >,			-
                                          				-
	   <1,ACMODE_ERG,						-
                                                  >,			-
                                          				-
	   <1,REGION_ERG,						-
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS2:
	TESTSERV	CNTREG,ERR,SATS,				-
                                          				-
	   <1,PAGCNT_CRG,						-
	        	PAGCNT_CRG10,ILLPAGCNT,	-  ; SFCRG10
	        	PAGCNT_CRG11,ILLPAGCNT,	-  ; SFCRG11
	        	PAGCNT_CRG12,ILLPAGCNT,	-  ; SFCRG12
	        	PAGCNT_CRG13,PAGOWNVIO,	-  ; SFCRG13
                                                  >,			-
                                          				-
	   <1,RETADR_CRG,						-
	        	RETADR_CRG20,ACCVIO,	-  ; SFCRG20
	        	RETADR_CRG21,ACCVIO,	-  ; SFCRG21
	        	RETADR_CRG22,ACCVIO,	-  ; SFCRG22
	        	RETADR_CRG23,ACCVIO,	-  ; SFCRG23
	        	RETADR_CRG24,ACCVIO,	-  ; SFCRG24
                                                  >,			-
                                          				-
	   <1,ACMODE_CRG,						-
                                                  >,			-
                                          				-
	   <1,REGION_CRG,						-
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS3:
	TESTSERV	SETPRT,ERR,SATS,				-
                                          				-
	   <1,INADR_SPT,						-
	        	INADR_SPT10,PAGOWNVIO,	-  ; SFSPT10
	        	INADR_SPT11,NOPRIV,	-  ; SFSPT11
	        	INADR_SPT12,LENVIO,	-  ; SFSPT12
	        	INADR_SPT13,ACCVIO,	-  ; SFSPT13
	        	INADR_SPT14,ACCVIO,	-  ; SFSPT14
                                                  >,			-
                                          				-
	   <1,RETADR_SPT,						-
	        	RETADR_SPT20,ACCVIO,	-  ; SFSPT20
	        	RETADR_SPT21,ACCVIO,	-  ; SFSPT21
	        	RETADR_SPT22,ACCVIO,	-  ; SFSPT22
                                                  >,			-
                                          				-
	   <1,ACMODE_SPT,						-
                                                  >,			-
                                          				-
	   <1,PROT_SPT,						-
			PROT_SPT40,IVPROTECT,	-  ; SFSPT40
			PROT_SPT41,IVPROTECT,	-  ; SFSPT41
                                                  >,			-
                                          				-
	   <1,PRVPRT_SPT,						-
	        	PRVPRT_SPT50,ACCVIO,	-  ; SFSPT50
	        	PRVPRT_SPT51,ACCVIO,	-  ; SFSPT51
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS4:
	TESTSERV	SETSWM,ERR,SATS,				-
                                          				-
	   <1,SWPFLG_SSM,						-
	        	SWPFLG_SSM10,NOPRIV,	-  ; SFSSM10
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
	.SBTTL	EXECUTE & CLEANUP
EXECUTE:
	TEST_SERV_EXEC			; EXECUTE ALL T. CASES IN ALL GROUPS
CLEANUP:
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE END MSG
	INSV	#1,#STS$V_INHIB_MSG,#1,MOD_MSG_CODE 
					; INHIBIT PRINTING
	$EXIT_S	MOD_MSG_CODE		; EXIT TO OP SYS WITH MSG CODE
	.PAGE
	.SBTTL	TC_CONTROL
;++
; FUNCTIONAL DESCRIPTION:
;
;		THE TC_CONTROL SUBROUTINE IS CALLED BY THE TEST_SERV_EXEC
; MACRO TO EXECUTE A GROUP OF TEST CASES. A GROUP IS DEFINED BY A TC_GROUP
; MACRO. FOR EACH TC_GROUP MACRO, THERE IS A CORRESPONDING TESTSERV MACRO.
; TESTSERV CONTAINS CODE TO EXECUTE SYSTEM SERVICES AND CHECK THE RETURNED
; STATUS CODE VALUES. TESTSERV ARGUMENTS ARE CODED TO SPECIFY ALL THE SYSTEM
; SERVICE ARGUMENT VALUES AND THE EXPECTED STATUS CODE FOR EACH TEST CASE
; DEFINED BY A NEXT_TEST_CASE MACRO WITHIN THE GROUP. TC_CONTROL USES A
; CO-ROUTINE INTERFACE TO ENTER THE CODE OF THE APPROPRIATE TESTSERV MACRO
; IN VARIOUS PLACES. THE FIRST ENTRY OCCURS ONCE PER GROUP TO ALLOW TESTSERV
; TO DO SOME INITIALIZATION. THEN TWO ENTRIES ARE MADE FOR EACH TEST CASE IN
; THE GROUP. THE FIRST ALLOWS TESTSERV TO ISSUE THE SUBJECT SYSTEM SERVICE.
; THE SECOND ENTRY FOR THE TEST CASE CAUSES TESTSERV TO CHECK THE RETURNED
; STATUS CODE, PRINTING A FAILURE MESSAGE IF IT IS NOT THE EXPECTED CODE.
; IF THERE ARE NO MORE TEST CASES IN THE CURRENT GROUP, TESTSERV (NOT TC_CONTROL)
; RETURNS DIRECTLY TO TEST_SERV_EXEC (RSB ACTUALLY ISSUED IN TS_CLEANUP MACRO)
; FROM THIS SECOND ENTRY; OTHERWISE, CONTROL RETURNS TO TC_CONTROL WHICH
; IN TURN ENTERS TESTSERV AGAIN FOR THE NEXT TEST CASE. THE FAILURE OF A
; TEST CASE DOES NOT CAUSE TERMINATION OF THE TEST MODULE.
;
; CALLING SEQUENCE:
;
;	BSBW TC_CONTROL (ISSUED WITHIN THE TEST_SERV_EXEC MACRO)
;			(RSB IS ISSUED WITHIN THE TS_CLEANUP MACRO)
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	ARGUMENTS SPECIFIED ON EACH TESTSERV MACRO MAY BE VIEWED AS
;	INPUTS, SINCE TC_CONTROL AND TESTSERV ACT AS CO-ROUTINES.
;
; OUTPUT PARAMETERS:
;
;	SEVERITY CODE FIELD OF MOD_MSG_CODE (BITS 0,1,2) IS SET TO ERROR
;	IF ANY TEST CASE IN THE CURRENT GROUP FAILS; OTHERWISE IT REMAINS
;	SET TO SUCCESSFUL.
;
; IMPLICIT OUTPUTS:
;
;	%UETP-I-TEXT,   ERROR MESSAGES ARE WRITTEN TO SYS$OUTPUT BY
;			THE TESTSERV MACRO (CO-ROUTINE WITH TC_CONTROL)
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--



TC_CONTROL:
	PUSHL	TS_EP			; PUSH TESTSERV ENTRY POINT
	JSB	@(SP)+			; ENTER TESTSERV INITIALIZATION
10$:					; PROCESS NEXT TEST CASE
	MOVB	#^A/ /,$$TSTN$$+2 	; MAKE SURE T.C. NAME HAS A BLANK
	BSBW	REG_SAVE		; SAVE REGISTERS
	JSB	@CURRENT_TC		; JUMP TO CURRENT TEST CASE
	BSBW	REG_REST		; RESTORE REGS FOR TESTSERV
	JSB	@(SP)+			; LET TESTSERV ISSUE SYSTEM SERVICE
	BSBW	REG_COMP		; COMPARE REGS TO SEE IF ...
					; ... SYSTEM SERVICE CHANGED ANY
	JSB	@(SP)+			; LET TESTSERV CHEK S.S. STATUS CODE
	CMPB	#^A/*/,$$TSTN$$+2	; HAS TESTSERV INDICATED FAILURE ?
	BNEQU	10$			; NO -- PROCESS NEXT TEST CASE
	MOVAL	TEST_MOD_FAIL,TMD_ADDR	; YES -- INDICATE FAILED IN END MSG
	INSV	#ERROR,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR ERROR
	BRB	10$			; LOOP BAK TO PROCESS NEXT TEST CASE
;
;   TC_CONTROL RETURNS TO TEST_SERV_EXEC VIA TESTSERV (IN TS_CLEANUP MACRO)
;
	.PAGE
	.SBTTL	SUBROUTINES
REG_SAVE:
;
;  *****************************************************************
;  *                                                               *
;  *  SAVES R0 THRU SP IN REG_SAVE_AREA                            *
;  *                                                               *
;  *****************************************************************
;
	PUSHR	#R0_THRU_SP		; SAVE ALL REGS ON STACK
	MOVC3	#60,(SP),REG_SAVE_AREA	; SAVE REGS (BEFORE S.S.)
	POPR	#R0_THRU_SP		; CLEAN UP STACK
	RSB				; .... AND RETURN
;
;
;
;
REG_REST:
;
;
;  *****************************************************************
;  *                                                               *
;  *  RESTORES R0 THRU SP FROM REG_SAVE_AREA                       *
;  *                                                               *
;  *****************************************************************
;
	SUBL2	#60,SP			; MOVE SP TO MAKE ROOM FOR REGS
	MOVC3	#60,REG_SAVE_AREA,(SP)	; MOVE REGS ONTO STACK FOR POP
	POPR	#R0_THRU_SP		; RESTORE ALL REGS FOR TESTSERV
	RSB				; ... AND RETURN
	.PAGE
REG_COMP:
;
;  *****************************************************************
;  *                                                               *
;  *  1) PUSHES ALL REGS ONTO STACK                                *
;  *  2) COMPARES REGISTER IMAGES FROM STACK WITH CORRESPONDING    *
;  *       IMAGES FROM REG_SAVE_AREA FOR ALL REGISTERS SPECIFIED   *
;  *       IN REG_COMP_MASK.                                       *
;  *  3) FOR EACH UNEQUAL COMPARE, AN ERROR MESSAGE IS PRINTED     *
;  *       (USING $FAO AND $OUTPUT SYSTEM SERVICES).               *
;  *  4) POPS ALL REGS OFF OF STACK                                *
;  *                                                               *
;  *****************************************************************
;
	PUSHR	#R0_THRU_SP		; SAVE ALL REGISTERS ON STACK
	MOVAL	REG_SAVE_AREA,R6 	; POINT R6 TO BEG OF ...
					; ... REGS (BEFORE S.S.)
	MOVL	SP,R4			; POINT R4 TO BEG OF
					; ... REGS (AFTER S.S.)
	CVTBL	#-1,R3			; INITIALIZE REG_COMP_MASK INDEX
REG_COMP_NEXT:
	INCL	R3			; POINT TO NEXT BIT IN MASK
	CMPB	#15,R3			; END OF THE MASK ?
	BGTRU	REG_COMP_CONT		; NO -- CONTINUE
	BRW	REG_COMP_RSB		; YES -- GO TO COMMON RETURN
REG_COMP_CONT:
	CMPL	(R6)+,(R4)+		; REG BEFORE = REG AFTER ?
	BEQLU	REG_COMP_NEXT		; YES -- LOOK FOR NEXT REG
	BBC	R3,REG_COMP_MASK,REG_COMP_NEXT
					; NO -- GET NEXT IF BIT NOT SET
	MOVL	R3,CLOB_REG_NO		; NO -- GIVE REG NUMBER TO FAO
	MOVL	-4(R6),REG_BEFORE_SS 	; GIVE "BEFORE" CONTENTS TO FAO
	MOVL	-4(R4),REG_AFTER_SS  	; GIVE "AFTER" CONTENTS TO FAO
	MOVB	#^A/*/,$$TSTN$$+2 	; GIVE FAILURE INDIC'N IN ERROR MSG
;
	$FAO_S	ERR_MSG_FAOCTL,OUTL,OUTD,$$SNAD$$,  -
		$$ASEQ$$,$$PSEQ$$,CLOB_REG_NO,REG_BEFORE_SS,REG_AFTER_SS
;
	MOVW	OUTL,OUTD		; ACTUAL OUTPUT LEN IN STRING DESC'R
	PUTMSG	<#UETP$_TEXT,#1,#OUTD>	; PRINT THE MSG
	MOVW	#OUTE-OUTB,OUTD		; GET MAX LEN BACK INTO DESCRIPTOR
	MOVB	#^A/ /,$$TSTN$$+2 	; REMOVE FAIL INDIC'N FOR NEXT MSG
	MOVAL	TEST_MOD_FAIL,TMD_ADDR	; INDICATE FAILED IN END MSG
	INSV	#ERROR,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR ERROR
	BRW	REG_COMP_NEXT		; GO LOOK FOR NEXT REG TO COMPARE
REG_COMP_RSB:
	POPR	#R0_THRU_SP		; CLEAN UP STACK
	RSB				; RETURN TO CALLER
	.PAGE
MOD_MSG_PRINT:
;
;  *****************************************************************
;  *                                                               *
;  *  PRINTS THE TEST MODULE BEGUN/SUCCESSFUL/FAILED MESSAGES      *
;  *	   (USING THE PUTMSG MACRO).                               *
;  *                                                               *
;  *****************************************************************
;
	PUTMSG	<MOD_MSG_CODE,#2,TMN_ADDR,TMD_ADDR> ; PRINT MSG
	RSB				; ... AND RETURN TO CALLER
;
CHMRTN:
; **********************************************************
; *
; *	CHANGE MODE ROUTINE. THIS ROUTINE GETS CONTROL WHENEVER
; *	A CMKRNL, CMEXEC, OR CMSUP SYSTEM SERVICE IS ISSUED
; *	BY THE MODE MACRO ('TO' OPTION).  IT MERELY DOES 
; *	A JUMP INDIRECT ON A FIELD SET UP BY MODE. IT HAS 
; *	THE EFFECT OF RETURNING TO THE END OF THE MODE
; *	MACRO EXPANSION.
; *
; *************************************************************
;
	.WORD	0			; ENTRY MASK
	JMP	@CHM_CONT		; RETURN TO MODE MACRO IN NEW MODE
;
; *	RET INSTR WILL BE ISSUED IN EXPANSION OF 'MODE FROM, ....' MACRO
;
	.END	SATSSF12
