	.TITLE	SATSSF09 - SATS SYSTEM SERVICE TESTS  (FAILING S.C.)
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SATS SYSTEM SERVICE TESTS
;
; ABSTRACT:	THE SATSSF09 MODULE TESTS THE EXECUTION OF CERTAIN
; VMS SYSTEM SERVICES, INVOKED IN SUCH A WAY AS TO EXPECT FAILING
; STATUS CODES. THE SYSTEM SERVICES TESTED AND THE STATUS CODES
; EXPECTED ARE SUMMARIZED AS ARGUMENTS TO THE TESTSERV MACROS
; WHICH APPEAR NEAR THE END OF THIS LISTING. SUCCESSFUL STATUS
; CODES ARE TESTED IN OTHER MODULES.
;
;
; ENVIRONMENT:	USER MODE IMAGE; NEEDS CMKRNL PRIVILEGE,
;		DYNAMICALLY ACQUIRES OTHER PRIVILEGES, AS NEEDED.
;
; AUTHOR: THOMAS L. CAFARELLA,		CREATION DATE: MMM, 1978
;	  PAUL D. FAY (DISPSERV & TESTSERV MACROS)
;
; MODIFIED BY:
;
;	, : VERSION
; 01	- 
;--
	.PAGE
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$PHDDEF				; PROCESS HEADER OFFSET SYMBOLS
	$PCBDEF				; PROCESS CONTROL BLOCK OFFSET SYMBS
	$STSDEF				; STATUS MESSAGE SYMBOLS
	$PRVDEF				; SYMBOL DEFS FOR PRIVILEGES
	$UETPDEF			; UETP MSG CODE DEFINITIONS
	$SHR_MESSAGES UETP,116,<<TEXT,INFO>> 
					; DEFINE UETP$_TEXT
	$PSLDEF				; ACCESS MODE SYMBOLS
;
; MACROS:
;
;
; EQUATED SYMBOLS:
;
WARNING		= 0			; WARNING SEVERITY VALUE FOR MSGS
SUCCESS		= 1			; SUCCESS SEVERITY VALUE FOR MSGS
ERROR		= 2			; ERROR SEVERITY VALUE FOR MSGS
INFO		= 3			; INFORMATIONAL SEV VALUE FOR MSGS
SEVERE		= 4			; SEVERE (FATAL) SEV VALUE FOR MSGS
TCG_NO		=  0			; INITIALIZE TEST CASE GROUP NUMBER
GRP_TOTAL	=  0			; INITIALIZE TEST CASE GROUP TOTAL
R0_THRU_SP	= ^M<R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,AP,FP,SP>
MBXNAM_ASN	= 0		; MBXNAM ARG FOR $ASSIGN (MISSING ARG)
;
; OWN STORAGE:
;
	.PAGE
	.PSECT	RODATA,RD,NOWRT,NOEXE,LONG
REG_COMP_MASK:	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,AP,FP> ! ^X8000 -
					; REG COMPARE MASK (HIGH-ORDER ... 
					; ... BIT MUST BE ON
ERR_MSG_FAOCTL:	STRING	I,<!/!AC!1ZB!1ZB: REGISTER !2UW CONTENTS ALTERED>, -
		<;  BEFORE SERVICE CALL: !8XL   AFTER SERVICE CALL: !8XL>
TEST_MOD_NAME:	STRING	C,<SATSSF09>	; TEST MODULE NAME
TEST_MOD_BEG:	STRING	C,<begun>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_SUCC:	STRING	C,<successful>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_FAIL:	STRING	C,<failed>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_NAME_D: STRING	I,<SATSSF09>	; TEST MODULE NAME DESCRIPTOR
TTNAME:     	STRING  I,<TT>		; TERMINAL LOGICAL NAME
INADR:		.LONG	NOACCESS,NOACCESS ; PAGE ADDRESS OF NOACCESS PSECT
PROT:		.LONG	PRT$C_NA	; PROTECTION CODE FOR NOACCESS PSECT
ONES:		.LONG	-1,-1		; A QUADWORD OF 1-BITS
DEVNAM_GDV:				; DEVNAM ARGUMENT FOR GETDEV
DEVNAM_ASN:				; DEVNAM ARGUMENT FOR ASSIGN
MBXNAM_ASN42:				; MBXNAM ARGUMENT FOR ASSIGN
		STRING	I,<SYS$DISK>
DEVNAM_GDV10:	STRING	I,<_$$>		; DEVNAM ARGUMENT FOR GETDEV
DEVNAM_GDV11:				; DEVNAM ARGUMENT FOR GETDEV
DEVNAM_ASN10:				; DEVNAM ARGUMENT FOR ASSIGN
		.LONG	0
		.ADDRESS .		; ...........
DEVNAM_GDV12:				; DEVNAM ARGUMENT FOR GETDEV
MBXNAM_ASN40:				; MBXNAM ARGUMENT FOR ASSIGN
		STRING	I,   -
		  <SIXTY-FOUR CHARACTERS>,  -
		  <$$$$$$$$$$$$$$$$$$$$$$>, -
		  <$$$$$$$$$$$$$$$$$$$$$>
DEVNAM_GDV13:				; DEVNAM ARGUMENT FOR GETDEV
DEVNAM_ASN11:				; DEVNAM ARGUMENT FOR ASSIGN
		STRING	I,<_ZZA0:>
PRILEN_GDV20:				; PRILEN ARGUMENT FOR GETDEV
SECLEN_GDV40:				; SECLEN ARGUMENT FOR GETDEV
PRILEN_GCH20:				; PRILEN ARGUMENT FOR GETCHN
SECLEN_GCH40:				; SECLEN ARGUMENT FOR GETCHN
		.BLKW	1
PRIBUF_GDV31:				; PRIBUF ARGUMENT FOR GETDEV
SECBUF_GDV51:				; SECBUF ARGUMENT FOR GETDEV
PRIBUF_GCH31:				; PRIBUF ARGUMENT FOR GETCHN
SECBUF_GCH51:				; SECBUF ARGUMENT FOR GETCHN
		STRING	O,100
MY_DISK:	STRING	I,<SYS$DISK>	; LOGICAL NAME FOR USER DISK
CHAN_GCH10:				; CHAN ARGUMENT FOR GETCHN
CHAN_DAS11:				; CHAN ARGUMENT FOR DASSGN
CHAN_DMX12:				; CHAN ARGUMENT FOR DELMBX
		.LONG	0
CHAN_GCH11:				; CHAN ARGUMENT FOR GETCHN
CHAN_DAS12:				; CHAN ARGUMENT FOR DASSGN
CHAN_DMX13:				; CHAN ARGUMENT FOR DELMBX
		.LONG	1000000000
CHAN_ASN20:	.BLKL	1		; CHAN ARGUMENT FOR ASSIGN
ACMODE_ASN:	.LONG	PSL$C_USER	; ACMODE ARGUMENT FOR ASSIGN
MBXNAM_ASN41:	STRING	I,<_??>		; MBXNAM ARGUMENT FOR ASSIGN
	.PAGE
	.PSECT	RWDATA,RD,WRT,NOEXE
TPID:		.BLKL	1		; PROCESS ID FOR THIS PROCESS
CURRENT_TC:	.BLKL	1 		; PTR TO CURRENT TEST CASE
REG_SAVE_AREA:	.BLKL	15		; SAVE AREA FOR ALL REGS (SANS PC)
MOD_MSG_CODE:	.LONG	UETP$_SATSMS	; TEST MODULE MSG CODE FOR PUTMSG
CLOB_REG_NO:	.BLKL	1		; CLOBBERED REG NO (FOR FAO ERR MSG)
REG_BEFORE_SS:	.BLKL	1		; REG CONTENTS BEFORE S.S. 
					; ... (FOR FAO ERROR MSG)
REG_AFTER_SS:	.BLKL	1		; REG CONTENTS AFTER S.S. 
					; ... (FOR FAO ERROR MSG)
$$TSTN$$:	STRING	C,<  SF   >	; ASCII PORTION OF TEST CASE NAME
TMN_ADDR:	.ADDRESS TEST_MOD_NAME	; ADDR OF TEST MOD NAME FOR FAO
TMD_ADDR:	.ADDRESS TEST_MOD_BEG	; ADDR OF T.M. DISP FIELD FOR FAO
TS_EP:		.BLKL	1		; ENTRY PNT FOR CURR TESTSERV MACRO
RETADR:		.BLKL	2		; RETURN LONGWORDS FOR SETPRT
PRVPRT:		.BLKB	1		; PROT RETURN BYTE FOR SETPRT
PRIVMASK:	.BLKQ	1		; ADDR OF PRIVILEGE MASK (IN PHD)
CHM_CONT:	.BLKL	1		; CHANGE MODE CONTINUE ADDRESS
REGS:		.BLKL	5		; AREA FOR COND INDEX REGS (R2-R6)
PRILEN_GDV:				; PRILEN ARGUMENT FOR GETDEV
SECLEN_GDV:				; SECLEN ARGUMENT FOR GETDEV
PRILEN_GCH:				; PRILEN ARGUMENT FOR GETCHN
SECLEN_GCH:				; SECLEN ARGUMENT FOR GETCHN
		.BLKW	1
PRIBUF_GDV:				; PRIBUF ARGUMENT FOR GETDEV
SECBUF_GDV:				; SECBUF ARGUMENT FOR GETDEV
PRIBUF_GCH:				; PRIBUF ARGUMENT FOR GETCHN
SECBUF_GCH:				; SECBUF ARGUMENT FOR GETCHN
		STRING	O,100
PRIBUF_GDV30:				; PRIBUF ARGUMENT FOR GETDEV
PRIBUF_GCH30:				; PRIBUF ARGUMENT FOR GETCHN
		STRING	O,1
SECBUF_GDV50:				; SECBUF ARGUMENT FOR GETDEV
SECBUF_GCH50:				; SECBUF ARGUMENT FOR GETCHN
		.LONG	0
		.ADDRESS .		; .............
CHAN_GCH:	.LONG	0		; CHAN ARGUMENT FOR GETCHN
CHAN_GCH12:	.LONG	0		; CHAN ARGUMENT FOR GETCHN
CHAN_ASN:	.BLKW	1		; CHAN ARGUMENT FOR ASSIGN
CHAN_DAS:	.LONG	0		; CHAN ARGUMENT FOR DASSGN
CHAN_DAS10:	.LONG	0		; CHAN ARGUMENT FOR DASSGN
CHAN_DMX:	.LONG	0		; CHAN ARGUMENT FOR DELMBX
CHAN_DMX10:	.LONG	0		; CHAN ARGUMENT FOR DELMBX
CHAN_DMX11:	.LONG	0		; CHAN ARGUMENT FOR DELMBX
CHAN_DMX14:	.LONG	0		; CHAN ARGUMENT FOR DELMBX
	.PAGE
	.PSECT	SATS_ACCVIO_1,RD,WRT,NOEXE,PAGE
EMPTY:		.BLKB	512		; RESERVE A PAGE OF SPACE
;
; + 
; ******************************************************************
; *                                                                *
; * 	THE ORDER OF STATEMENTS IN THIS PSECT IS CRITICAL.         *
; *	DO NOT RE-ARRANGE THE VARIABLES. CONSULT SATS              *
; *	FUNCTIONAL SPECIFICATION FOR A DESCRIPTION OF THE USE      *
; *	OF THE EMPTY PSECT (AND ITS COMPANION PSECT, NOACCESS).    *
; *                                                                *
; ******************************************************************
; -
;
; TYPE AAAAA_SSSX1 (TYPE AAAAA_SSSX2 IF NOT DESC) GO HERE:
		. = . - 13		; ALLOW ROOM FOR STRING DESCRIPTOR
; TYPE AAAAA_SSSX5 GO HERE:
		.LONG	6		; STRING LENGTH (WILL CROSS PSECT BOUNDARY)
		.ADDRESS .+4		; STRING ADDRESS
; TYPE AAAAA_SSSX3 GO HERE:
		.BLKB	1		; LOW-ORDER BYTE OF STRING LENGTH
; TYPE AAAAA_SSSX2 GO HERE:
		.BLKL	1		; STRING LENGTH
;
;
;
;
	.PSECT	SATS_ACCVIO_2,RD,WRT,NOEXE,PAGE
NOACCESS:	.BLKB	512		; RESERVE A PAGE OF SPACE
		. = . - 512		; RETURN LOC CTR TO BEGINNING OF PSECT
		.ADDRESS EMPTY		; ADDRESS OF ACCESSIBLE STRING
		.ADDRESS EMPTY/^X100 	; ADDRESS OF ACCESSIBLE STRING
;+
; *** NOTE -- DO NOT CHANGE LOCATION OR SEQUENCE OF ABOVE STATEMENTS!
; ***         THIS PSECT (NOACCESS) MUST APPEAR IN MEMORY IMMEDIATELY
; ***         FOLLOWING THE EMPTY PSECT. PSECT NAMES AND OPTIONS WILL BE 
; ***         CHOSEN TO FORCE THE DESIRED PSECT ORDERING.
;-
;
DEVNAM_GDV14:	STRING	I,<SYS$DISK>	; DEVNAM ARGUMENT FOR GETDEV
DEVNAM_ASN12:	STRING	I,<SYS$DISK>	; DEVNAM ARGUMENT FOR ASSIGN
MBXNAM_ASN43:	STRING	I,<_MB0:>	; MBXNAM ARGUMENT FOR ASSIGN
;
;
;
	.PSECT	SATSSF09,RD,WRT,EXE,LONG
	.PAGE
	.SBTTL	SATSSF09
;++
; FUNCTIONAL DESCRIPTION:
;
;		AFTER PERFORMING SOME INITIAL HOUSEKEEPING, SUCH AS
; PRINTING THE MODULE BEGIN MESSAGE AND ACQUIRING ALL PRIVILEGES,
; THE SATSSF09 ROUTINE EXECUTES THE TEST_SERV_EXEC MACRO TO RUN
; ALL TEST CASES. WHEN THE MACRO COMPLETES ITS EXECUTION, SATSSF09
; PRINTS A TEST MODULE SUCCESS OR FAIL MESSAGE AND EXITS TO THE
; OPERATING SYSTEM. TEST_SERV_EXEC CALLS THE TC_CONTROL/TESTSERV
; CO-ROUTINE PAIR ONCE PER TEST CASE GROUP TO EXECUTE ALL TEST
; CASES IN THAT GROUP. EACH TEST CASE GROUP IS DEFINED BY BOUNDING
; ITS TEST CASES WITH A TC_GROUP MACRO BEFORE THE FIRST TEST CASE
; AND A TCEND MACRO AFTER THE LAST ONE. THE TEST CASES THEMSELVES
; ARE DEFINED WITHIN THESE BOUNDS BY PRECEDING EACH WITH A
; NEXT_TEST_CASE MACRO. TC_CONTROL/TESTSERV EXECUTES THE CODE 
; FOLLOWING EACH NEXT_TEST_CASE MACRO IMMEDIATELY BEFORE ISSUING
; THE SYSTEM SERVICE AS REQUESTED IN THE TESTSERV MACRO. TC_CONTROL/
; TESTSERV ALSO CHECKS THE RESULTS OF THE SERVICE WITH RESPECT
; TO ITS EXPECTED STATUS CODE AND PRINTS ANY REQUIRED FAILURE
; MESSAGES FOR THE TEST CASE. THE CODE APPEARING AFTER EACH
; NEXT_TEST_CASE MACRO IS MERELY TO SET UP CONDITIONS REQUIRED
; FOR THE SYSTEM SERVICE AND TO CLEAN UP ANY RESOURCES ACQUIRED
; BY THE PREVIOUS TEST CASE.
;
; CALLING SEQUENCE:
;
;	$ RUN SATSSF09  ...  (DCL COMMAND)
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	MESSAGES TO SYS$OUTPUT ARE THE ONLY OUTPUT FROM SATSSF09.
;	THEY ARE OF THE FORM:
;
;		%UETP-S-SATSMS, TEST MODULE SATSSF09 BEGUN ... (BEGIN MSG)
;		%UETP-S-SATSMS, TEST MODULE SATSSF09 SUCCESSFUL ... (END MSG)
;		%UETP-E-SATSMS, TEST MODULE SATSSF09 FAILED ... (END MSG)
;		%UETP-I-TEXT, ... (VARIABLE INFORMATION ABOUT A TEST MODULE FAILURE)
;
; COMPLETION CODES:
;
;	THE SATSSF09 ROUTINE TERMINATES WITH A $EXIT TO THE
;	OPERATING SYSTEM WITH A STATUS CODE DEFINED BY UETP$_SATSMS.
;
; SIDE EFFECTS:
;
;	NONE
;
;--



SATSSF09:
	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
					; ENTRY MASK
	$WAKE_S  TPID			; GET PID OF THIS PROCESS
	$HIBER_S			; UNDO WAKE
	$SETPRN_S TEST_MOD_NAME_D 	; SET PROCESS NAME
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE BEGIN MSG
	MOVAL	TEST_MOD_SUCC,TMD_ADDR	; ASSUME END MSG WILL SHOW SUCCESS
	INSV	#SUCCESS,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR SUCCESS
	MODE	TO,10$,KRNL,NOREGS 	; KERNEL MODE TO ACCESS PHD
	MOVL	@#CTL$GL_PHD,R9		; GET PROCESS HEADER ADDRESS
	MOVAL	PHD$Q_PRIVMSK(R9),PRIVMASK ; GET PRIV MASK ADDRESS
	MODE	FROM,10$		; GET BACK TO USER MODE
	PRIV	ADD,ALL			; GET ALL PRIVILEGES
	DISPSERV			; SET UP DISPLAY INFO FOR TESTSERV
	$SETPRT_S INADR=INADR, RETADR=RETADR, -
		  PROT=PROT, PRVPRT=PRVPRT
					; SET NOACCESS PSECT ...
					; ... FOR NO USER ACCESS
	BRW	EXECUTE			; GO EXECUTE ALL TEST CASES
;
	TC_GROUP	GDV,1,TS1
;
	NEXT_TEST_CASE	SFGDV10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV10      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_GDV10         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHARACTERS IN DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVDEVNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV11      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_GDV11         
; *
; *  INPUT CONDITIONS:
; *	ZERO LENGTH DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVLOGNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV12      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_GDV12         
; *
; *  INPUT CONDITIONS:
; *	DEVICE NAME IS 64 CHARACTERS LONG.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVLOGNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV13
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV13      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_GDV13         
; *
; *  INPUT CONDITIONS:
; *	VALID, NON-EXISTENT DEVICE
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOSUCHDEV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV14
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV14      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_GDV14         
; *
; *  INPUT CONDITIONS:
; *	DEVICE NAME IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV20      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	PRILEN_GDV20         
; *
; *  INPUT CONDITIONS:
; *	PRIMARY CHARACTERISTICS LENGTH FIELD IN 
; *	  READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV30
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV30      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	PRIBUF_GDV30         
; *
; *  INPUT CONDITIONS:
; *	PRIMARY CHARACTERISTICS BUFFER (LENGTH 1) IS
; *	  TOO SHORT FOR INCOMING DATA.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BUFFEROVF
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV31
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV31      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	PRIBUF_GDV31         
; *
; *  INPUT CONDITIONS:
; *	PRIMARY CHARACTERISTICS BUFFER IN READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV40
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV40      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	SECLEN_GDV40         
; *
; *  INPUT CONDITIONS:
; *	SECONDARY CHARACTERISTICS LENGTH FIELD IN
; *	  READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV50
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV50      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	SECBUF_GDV50         
; *
; *  INPUT CONDITIONS:
; *	SECONDARY CHARACTERISTICS BUFFER (LENGTH 0) IS
; *	  TOO SHORT FOR INCOMING DATA.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BUFFEROVF
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGDV51
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGDV51      
; *
; *  SYSTEM SERVICE:		GETDEV               
; *
; *  ARGUMENT UNDER TEST: 	SECBUF_GDV51         
; *
; *  INPUT CONDITIONS:
; *	SECONDARY CHARACTERISTICS BUFFER IN READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	GCH,1,TS2
;
	NEXT_TEST_CASE	SFGCH10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGCH10      
; *
; *  SYSTEM SERVICE:		GETCHN               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_GCH10         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (ZERO)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$ASSIGN_S DEVNAM=MY_DISK, -	; GET A VALID CHANNEL NUMBER ...
		  CHAN=CHAN_GCH		; ... FOR ALL TEST CASES
;
	NEXT_TEST_CASE	SFGCH11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGCH11      
; *
; *  SYSTEM SERVICE:		GETCHN               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_GCH11         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (1 BILLION)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGCH12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGCH12      
; *
; *  SYSTEM SERVICE:		GETCHN               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_GCH12         
; *
; *  INPUT CONDITIONS:
; *	GET CHARACTERISTICS FOR CHANNEL ASSIGNED IN EXEC MODE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,10$,EXEC,NOREGS	; TO EXEC MODE FOR $ASSIGN
	$ASSIGN_S DEVNAM=MY_DISK, -	; ASSIGN A CHANNEL IN EXEC MODE
		  CHAN=CHAN_GCH12	;
	MODE	FROM,10$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFGCH20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGCH20      
; *
; *  SYSTEM SERVICE:		GETCHN               
; *
; *  ARGUMENT UNDER TEST: 	PRILEN_GCH20         
; *
; *  INPUT CONDITIONS:
; *	PRIMARY CHARACTERISTICS LENGTH FIELD IN 
; *	  READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO

; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,20$,EXEC,NOREGS	; TO EXEC MODE FOR $DASSGN
	$DASSGN_S CHAN=CHAN_GCH12	; DE-ASSIGN CHANNEL ASSIGNED IN SFGCH12
	MODE	FROM,20$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFGCH30
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGCH30      
; *
; *  SYSTEM SERVICE:		GETCHN               
; *
; *  ARGUMENT UNDER TEST: 	PRIBUF_GCH30         
; *
; *  INPUT CONDITIONS:
; *	PRIMARY CHARACTERISTICS BUFFER (LENGTH 1) IS
; *	  TOO SHORT FOR INCOMING DATA.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BUFFEROVF
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGCH31
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGCH31      
; *
; *  SYSTEM SERVICE:		GETCHN               
; *
; *  ARGUMENT UNDER TEST: 	PRIBUF_GCH31         
; *
; *  INPUT CONDITIONS:
; *	PRIMARY CHARACTERISTICS BUFFER IN READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGCH40
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGCH40      
; *
; *  SYSTEM SERVICE:		GETCHN               
; *
; *  ARGUMENT UNDER TEST: 	SECLEN_GCH40         
; *
; *  INPUT CONDITIONS:
; *	SECONDARY CHARACTERISTICS LENGTH FIELD IN
; *	  READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGCH50
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGCH50      
; *
; *  SYSTEM SERVICE:		GETCHN               
; *
; *  ARGUMENT UNDER TEST: 	SECBUF_GCH50         
; *
; *  INPUT CONDITIONS:
; *	SECONDARY CHARACTERISTICS BUFFER (LENGTH 0) IS
; *	  TOO SHORT FOR INCOMING DATA.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BUFFEROVF
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFGCH51
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFGCH51      
; *
; *  SYSTEM SERVICE:		GETCHN               
; *
; *  ARGUMENT UNDER TEST: 	SECBUF_GCH51         
; *
; *  INPUT CONDITIONS:
; *	SECONDARY CHARACTERISTICS BUFFER IN READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	ASN,1,TS3
;
	NEXT_TEST_CASE	SFASN10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFASN10      
; *
; *  SYSTEM SERVICE:		ASSIGN               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_ASN10         
; *
; *  INPUT CONDITIONS:
; *	ZERO LENGTH DEVICE NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVLOGNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFASN11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFASN11      
; *
; *  SYSTEM SERVICE:		ASSIGN               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_ASN11         
; *
; *  INPUT CONDITIONS:
; *	VALID, NON-EXISTENT DEVICE
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOSUCHDEV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFASN12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFASN12      
; *
; *  SYSTEM SERVICE:		ASSIGN               
; *
; *  ARGUMENT UNDER TEST: 	DEVNAM_ASN12         
; *
; *  INPUT CONDITIONS:
; *	DEVICE NAME IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFASN20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFASN20      
; *
; *  SYSTEM SERVICE:		ASSIGN               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_ASN20         
; *
; *  INPUT CONDITIONS:
; *	CHANNEL BUFFER IN READ-ONLY PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFASN40
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFASN40      
; *
; *  SYSTEM SERVICE:		ASSIGN               
; *
; *  ARGUMENT UNDER TEST: 	MBXNAM_ASN40         
; *
; *  INPUT CONDITIONS:
; *	MAILBOX NAME IS LENGTH 64
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVLOGNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFASN41
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFASN41      
; *
; *  SYSTEM SERVICE:		ASSIGN               
; *
; *  ARGUMENT UNDER TEST: 	MBXNAM_ASN41         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHARACTERS IN MAILBOX NAME
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVDEVNAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFASN42
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFASN42      
; *
; *  SYSTEM SERVICE:		ASSIGN               
; *
; *  ARGUMENT UNDER TEST: 	MBXNAM_ASN42         
; *
; *  INPUT CONDITIONS:
; *	MAILBOX SPECIFIED IS ACTUALLY A DISK.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	DEVNOTMBX
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFASN43
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFASN43      
; *
; *  SYSTEM SERVICE:		ASSIGN               
; *
; *  ARGUMENT UNDER TEST: 	MBXNAM_ASN43         
; *
; *  INPUT CONDITIONS:
; *	MAILBOX NAME IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	DAS,1,TS4
;
	NEXT_TEST_CASE	SFDAS10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDAS10      
; *
; *  SYSTEM SERVICE:		DASSGN               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_DAS10         
; *
; *  INPUT CONDITIONS:
; *	DE-ASSIGN CHANNEL ASSIGNED BY EXEC MODE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,10$,EXEC,NOREGS	; GET EXEC MODE FOR $ASSIGN
	$ASSIGN_S DEVNAM=MY_DISK, -	; ASSIGN A CHANNEL IN EXEC MODE
		  CHAN=CHAN_DAS10
	MODE	FROM,10$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFDAS11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDAS11      
; *
; *  SYSTEM SERVICE:		DASSGN               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_DAS11         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (ZERO)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,20$,EXEC,NOREGS	; EXEC MODE FOR $DASSGN
	$DASSGN_S CHAN=CHAN_DAS10	; DE-ASSIGN CHANNEL ASSIGNED IN SFDAS10
	MODE	FROM,20$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFDAS12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDAS12      
; *
; *  SYSTEM SERVICE:		DASSGN               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_DAS12         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (1 BILLION)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	DMX,1,TS5
;
	NEXT_TEST_CASE	SFDMX10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDMX10      
; *
; *  SYSTEM SERVICE:		DELMBX               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_DMX10         
; *
; *  INPUT CONDITIONS:
; *	DELETE A MAILBOX CREATED BY EXEC MODE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,10$,EXEC,NOREGS	; GET EXEC MODE FOR $CREMBX
	$CREMBX_S PRMFLG=#1, -		; CREATE A PERM MBOX AT EXEC MODE
		  CHAN=CHAN_DMX10
	MODE	FROM,10$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFDMX11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDMX11      
; *
; *  SYSTEM SERVICE:		DELMBX               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_DMX11         
; *
; *  INPUT CONDITIONS:
; *	DELETE A PERMANENT MAILBOX WITHOUT THE PRMMBX PRIVILEGE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,20$,EXEC,NOREGS	; EXEC MODE FOR $DELMBX
	$DELMBX_S CHAN=CHAN_DMX10	; MARK FOR DELETION MBOX ACQUIRED IN SFDMX10
	$DASSGN_S CHAN=CHAN_DMX10	; ... AND GET IT DELETED
	MODE	FROM,20$		; BACK TO USER MODE
	$CREMBX_S PRMFLG=#1, -		; CREATE A USER MODE MAILBOX
		  CHAN=CHAN_DMX11
	PRIV	REM,PRMMBX		; REMOVE PRMMBX PRIVILEGE
;
	NEXT_TEST_CASE	SFDMX12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDMX12      
; *
; *  SYSTEM SERVICE:		DELMBX               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_DMX12         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (ZERO).
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	PRIV	ADD,PRMMBX		; GET BACK PRIVILEGE REMOVED BY SFDMX11
	$DELMBX_S CHAN=CHAN_DMX11	; MARK FOR DELETION MBOX ACQUIRED IN SFDMX11
;
	NEXT_TEST_CASE	SFDMX13
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDMX13      
; *
; *  SYSTEM SERVICE:		DELMBX               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_DMX13         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (1 BILLION).
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFDMX14
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFDMX14      
; *
; *  SYSTEM SERVICE:		DELMBX               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_DMX14         
; *
; *  INPUT CONDITIONS:
; *	ISSUE $DELMBX FOR A DEVICE WHICH IS NOT A MAILBOX.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	DEVNOTMBX
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$ASSIGN_S DEVNAM=MY_DISK, CHAN=CHAN_DMX14
					; ASSIGN A DEVICE WHICH IS NOT A MAILBOX
;
	TCEND
TS1:
	TESTSERV	GETDEV,ERR,SATS,				-
                                          				-
	   <1,DEVNAM_GDV,						-
	        	DEVNAM_GDV10,IVDEVNAM,	-  ; SFGDV10
	        	DEVNAM_GDV11,IVLOGNAM,	-  ; SFGDV11
	        	DEVNAM_GDV12,IVLOGNAM,	-  ; SFGDV12
	        	DEVNAM_GDV13,NOSUCHDEV,	-  ; SFGDV13
	        	DEVNAM_GDV14,ACCVIO,	-  ; SFGDV14
                                                  >,			-
                                          				-
	   <1,PRILEN_GDV,						-
	        	PRILEN_GDV20,ACCVIO,	-  ; SFGDV20
                                                  >,			-
                                          				-
	   <1,PRIBUF_GDV,						-
	        	PRIBUF_GDV30,BUFFEROVF,	-  ; SFGDV30
	        	PRIBUF_GDV31,ACCVIO,	-  ; SFGDV31
                                                  >,			-
                                          				-
	   <1,SECLEN_GDV,						-
	        	SECLEN_GDV40,ACCVIO,	-  ; SFGDV40
                                                  >,			-
                                          				-
	   <1,SECBUF_GDV,						-
	        	SECBUF_GDV50,BUFFEROVF,	-  ; SFGDV50
	        	SECBUF_GDV51,ACCVIO,	-  ; SFGDV51
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS2:
	TESTSERV	GETCHN,ERR,SATS,				-
                                          				-
	   <1,CHAN_GCH,						-
	        	CHAN_GCH10,IVCHAN,	-  ; SFGCH10
	        	CHAN_GCH11,IVCHAN,	-  ; SFGCH11
	        	CHAN_GCH12,NOPRIV,	-  ; SFGCH12
                                                  >,			-
                                          				-
	   <1,PRILEN_GCH,						-
	        	PRILEN_GCH20,ACCVIO,	-  ; SFGCH20
                                                  >,			-
                                          				-
	   <1,PRIBUF_GCH,						-
	        	PRIBUF_GCH30,BUFFEROVF,	-  ; SFGCH30
	        	PRIBUF_GCH31,ACCVIO,	-  ; SFGCH31
                                                  >,			-
                                          				-
	   <1,SECLEN_GCH,						-
	        	SECLEN_GCH40,ACCVIO,	-  ; SFGCH40
                                                  >,			-
                                          				-
	   <1,SECBUF_GCH,						-
	        	SECBUF_GCH50,BUFFEROVF,	-  ; SFGCH50
	        	SECBUF_GCH51,ACCVIO,	-  ; SFGCH51
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS3:
	TESTSERV	ASSIGN,ERR,SATS,				-
                                          				-
	   <1,DEVNAM_ASN,						-
	        	DEVNAM_ASN10,IVLOGNAM,	-  ; SFASN10
	        	DEVNAM_ASN11,NOSUCHDEV,	-  ; SFASN11
	        	DEVNAM_ASN12,ACCVIO,	-  ; SFASN12
                                                  >,			-
                                          				-
	   <1,CHAN_ASN,						-
	        	CHAN_ASN20,ACCVIO,	-  ; SFASN20
                                                  >,			-
                                          				-
	   <1,ACMODE_ASN,						-
                                                  >,			-
                                          				-
	   <1,MBXNAM_ASN,						-
	        	MBXNAM_ASN40,IVLOGNAM,	-  ; SFASN40
	        	MBXNAM_ASN41,IVDEVNAM,	-  ; SFASN41
	        	MBXNAM_ASN42,DEVNOTMBX,	-  ; SFASN42
	        	MBXNAM_ASN43,ACCVIO,	-  ; SFASN43
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS4:
	TESTSERV	DASSGN,ERR,SATS,				-
                                          				-
	   <1,CHAN_DAS,						-
	        	CHAN_DAS10,NOPRIV,	-  ; SFDAS10
	        	CHAN_DAS11,IVCHAN,	-  ; SFDAS11
	        	CHAN_DAS12,IVCHAN,	-  ; SFDAS12
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS5:
	TESTSERV	DELMBX,ERR,SATS,				-
                                          				-
	   <1,CHAN_DMX,						-
	        	CHAN_DMX10,NOPRIV,	-  ; SFDMX10
	        	CHAN_DMX11,NOPRIV,	-  ; SFDMX11
	        	CHAN_DMX12,IVCHAN,	-  ; SFDMX12
	        	CHAN_DMX13,IVCHAN,	-  ; SFDMX13
	        	CHAN_DMX14,DEVNOTMBX,	-  ; SFDMX14
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
	.SBTTL	EXECUTE & CLEANUP
EXECUTE:
	TEST_SERV_EXEC			; EXECUTE ALL T. CASES IN ALL GROUPS
CLEANUP:
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE END MSG
	INSV	#1,#STS$V_INHIB_MSG,#1,MOD_MSG_CODE 
					; INHIBIT PRINTING
	$EXIT_S	MOD_MSG_CODE		; EXIT TO OP SYS WITH MSG CODE
	.PAGE
	.SBTTL	TC_CONTROL
;++
; FUNCTIONAL DESCRIPTION:
;
;		THE TC_CONTROL SUBROUTINE IS CALLED BY THE TEST_SERV_EXEC
; MACRO TO EXECUTE A GROUP OF TEST CASES. A GROUP IS DEFINED BY A TC_GROUP
; MACRO. FOR EACH TC_GROUP MACRO, THERE IS A CORRESPONDING TESTSERV MACRO.
; TESTSERV CONTAINS CODE TO EXECUTE SYSTEM SERVICES AND CHECK THE RETURNED
; STATUS CODE VALUES. TESTSERV ARGUMENTS ARE CODED TO SPECIFY ALL THE SYSTEM
; SERVICE ARGUMENT VALUES AND THE EXPECTED STATUS CODE FOR EACH TEST CASE
; DEFINED BY A NEXT_TEST_CASE MACRO WITHIN THE GROUP. TC_CONTROL USES A
; CO-ROUTINE INTERFACE TO ENTER THE CODE OF THE APPROPRIATE TESTSERV MACRO
; IN VARIOUS PLACES. THE FIRST ENTRY OCCURS ONCE PER GROUP TO ALLOW TESTSERV
; TO DO SOME INITIALIZATION. THEN TWO ENTRIES ARE MADE FOR EACH TEST CASE IN
; THE GROUP. THE FIRST ALLOWS TESTSERV TO ISSUE THE SUBJECT SYSTEM SERVICE.
; THE SECOND ENTRY FOR THE TEST CASE CAUSES TESTSERV TO CHECK THE RETURNED
; STATUS CODE, PRINTING A FAILURE MESSAGE IF IT IS NOT THE EXPECTED CODE.
; IF THERE ARE NO MORE TEST CASES IN THE CURRENT GROUP, TESTSERV (NOT TC_CONTROL)
; RETURNS DIRECTLY TO TEST_SERV_EXEC (RSB ACTUALLY ISSUED IN TS_CLEANUP MACRO)
; FROM THIS SECOND ENTRY; OTHERWISE, CONTROL RETURNS TO TC_CONTROL WHICH
; IN TURN ENTERS TESTSERV AGAIN FOR THE NEXT TEST CASE. THE FAILURE OF A
; TEST CASE DOES NOT CAUSE TERMINATION OF THE TEST MODULE.
;
; CALLING SEQUENCE:
;
;	BSBW TC_CONTROL (ISSUED WITHIN THE TEST_SERV_EXEC MACRO)
;			(RSB IS ISSUED WITHIN THE TS_CLEANUP MACRO)
;
; INPUT PARAMETERS:
;

;	NONE
;
; IMPLICIT INPUTS:
;
;	ARGUMENTS SPECIFIED ON EACH TESTSERV MACRO MAY BE VIEWED AS
;	INPUTS, SINCE TC_CONTROL AND TESTSERV ACT AS CO-ROUTINES.
;
; OUTPUT PARAMETERS:
;
;	SEVERITY CODE FIELD OF MOD_MSG_CODE (BITS 0,1,2) IS SET TO ERROR
;	IF ANY TEST CASE IN THE CURRENT GROUP FAILS; OTHERWISE IT REMAINS
;	SET TO SUCCESSFUL.
;
; IMPLICIT OUTPUTS:
;
;	%UETP-I-TEXT,   ERROR MESSAGES ARE WRITTEN TO SYS$OUTPUT BY
;			THE TESTSERV MACRO (CO-ROUTINE WITH TC_CONTROL)
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--



TC_CONTROL:
	PUSHL	TS_EP			; PUSH TESTSERV ENTRY POINT
	JSB	@(SP)+			; ENTER TESTSERV INITIALIZATION
10$:					; PROCESS NEXT TEST CASE
	MOVB	#^A/ /,$$TSTN$$+2 	; MAKE SURE T.C. NAME HAS A BLANK
	BSBW	REG_SAVE		; SAVE REGISTERS
	JSB	@CURRENT_TC		; JUMP TO CURRENT TEST CASE
	BSBW	REG_REST		; RESTORE REGS FOR TESTSERV
	JSB	@(SP)+			; LET TESTSERV ISSUE SYSTEM SERVICE
	BSBW	REG_COMP		; COMPARE REGS TO SEE IF ...
					; ... SYSTEM SERVICE CHANGED ANY
	JSB	@(SP)+			; LET TESTSERV CHEK S.S. STATUS CODE
	CMPB	#^A/*/,$$TSTN$$+2	; HAS TESTSERV INDICATED FAILURE ?
	BNEQU	10$			; NO -- PROCESS NEXT TEST CASE
	MOVAL	TEST_MOD_FAIL,TMD_ADDR	; YES -- INDICATE FAILED IN END MSG
	INSV	#ERROR,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR ERROR
	BRB	10$			; LOOP BAK TO PROCESS NEXT TEST CASE
;
;   TC_CONTROL RETURNS TO TEST_SERV_EXEC VIA TESTSERV (IN TS_CLEANUP MACRO)
;
	.PAGE
	.SBTTL	SUBROUTINES
REG_SAVE:
;
;  *****************************************************************
;  *                                                               *
;  *  SAVES R0 THRU SP IN REG_SAVE_AREA                            *
;  *                                                               *
;  *****************************************************************
;
	PUSHR	#R0_THRU_SP		; SAVE ALL REGS ON STACK
	MOVC3	#60,(SP),REG_SAVE_AREA	; SAVE REGS (BEFORE S.S.)
	POPR	#R0_THRU_SP		; CLEAN UP STACK
	RSB				; .... AND RETURN
;
;
;
;
REG_REST:
;
;
;  *****************************************************************
;  *                                                               *
;  *  RESTORES R0 THRU SP FROM REG_SAVE_AREA                       *
;  *                                                               *
;  *****************************************************************
;
	SUBL2	#60,SP			; MOVE SP TO MAKE ROOM FOR REGS
	MOVC3	#60,REG_SAVE_AREA,(SP)	; MOVE REGS ONTO STACK FOR POP
	POPR	#R0_THRU_SP		; RESTORE ALL REGS FOR TESTSERV
	RSB				; ... AND RETURN
	.PAGE
REG_COMP:
;
;  *****************************************************************
;  *                                                               *
;  *  1) PUSHES ALL REGS ONTO STACK                                *
;  *  2) COMPARES REGISTER IMAGES FROM STACK WITH CORRESPONDING    *
;  *       IMAGES FROM REG_SAVE_AREA FOR ALL REGISTERS SPECIFIED   *
;  *       IN REG_COMP_MASK.                                       *
;  *  3) FOR EACH UNEQUAL COMPARE, AN ERROR MESSAGE IS PRINTED     *
;  *       (USING $FAO AND $OUTPUT SYSTEM SERVICES).               *
;  *  4) POPS ALL REGS OFF OF STACK                                *
;  *                                                               *
;  *****************************************************************
;
	PUSHR	#R0_THRU_SP		; SAVE ALL REGISTERS ON STACK
	MOVAL	REG_SAVE_AREA,R6 	; POINT R6 TO BEG OF ...
					; ... REGS (BEFORE S.S.)
	MOVL	SP,R4			; POINT R4 TO BEG OF
					; ... REGS (AFTER S.S.)
	CVTBL	#-1,R3			; INITIALIZE REG_COMP_MASK INDEX
REG_COMP_NEXT:
	INCL	R3			; POINT TO NEXT BIT IN MASK
	CMPB	#15,R3			; END OF THE MASK ?
	BGTRU	REG_COMP_CONT		; NO -- CONTINUE
	BRW	REG_COMP_RSB		; YES -- GO TO COMMON RETURN
REG_COMP_CONT:
	CMPL	(R6)+,(R4)+		; REG BEFORE = REG AFTER ?
	BEQLU	REG_COMP_NEXT		; YES -- LOOK FOR NEXT REG
	BBC	R3,REG_COMP_MASK,REG_COMP_NEXT
					; NO -- GET NEXT IF BIT NOT SET
	MOVL	R3,CLOB_REG_NO		; NO -- GIVE REG NUMBER TO FAO
	MOVL	-4(R6),REG_BEFORE_SS 	; GIVE "BEFORE" CONTENTS TO FAO
	MOVL	-4(R4),REG_AFTER_SS  	; GIVE "AFTER" CONTENTS TO FAO
	MOVB	#^A/*/,$$TSTN$$+2 	; GIVE FAILURE INDIC'N IN ERROR MSG
;
	$FAO_S	ERR_MSG_FAOCTL,OUTL,OUTD,$$SNAD$$,  -
		$$ASEQ$$,$$PSEQ$$,CLOB_REG_NO,REG_BEFORE_SS,REG_AFTER_SS
;
	MOVW	OUTL,OUTD		; ACTUAL OUTPUT LEN IN STRING DESC'R
	PUTMSG	<#UETP$_TEXT,#1,#OUTD>	; PRINT THE MSG
	MOVW	#OUTE-OUTB,OUTD		; GET MAX LEN BACK INTO DESCRIPTOR
	MOVB	#^A/ /,$$TSTN$$+2 	; REMOVE FAIL INDIC'N FOR NEXT MSG
	MOVAL	TEST_MOD_FAIL,TMD_ADDR	; INDICATE FAILED IN END MSG
	INSV	#ERROR,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR ERROR
	BRW	REG_COMP_NEXT		; GO LOOK FOR NEXT REG TO COMPARE
REG_COMP_RSB:
	POPR	#R0_THRU_SP		; CLEAN UP STACK
	RSB				; RETURN TO CALLER
	.PAGE
MOD_MSG_PRINT:
;
;  *****************************************************************
;  *                                                               *
;  *  PRINTS THE TEST MODULE BEGUN/SUCCESSFUL/FAILED MESSAGES      *
;  *	   (USING THE PUTMSG MACRO).                               *
;  *                                                               *
;  *****************************************************************
;
	PUTMSG	<MOD_MSG_CODE,#2,TMN_ADDR,TMD_ADDR> ; PRINT MSG
	RSB				; ... AND RETURN TO CALLER
;
CHMRTN:
; **********************************************************
; *
; *	CHANGE MODE ROUTINE. THIS ROUTINE GETS CONTROL WHENEVER
; *	A CMKRNL, CMEXEC, OR CMSUP SYSTEM SERVICE IS ISSUED
; *	BY THE MODE MACRO ('TO' OPTION).  IT MERELY DOES 
; *	A JUMP INDIRECT ON A FIELD SET UP BY MODE. IT HAS 
; *	THE EFFECT OF RETURNING TO THE END OF THE MODE
; *	MACRO EXPANSION.
; *
; *************************************************************
;
	.WORD	0			; ENTRY MASK
	JMP	@CHM_CONT		; RETURN TO MODE MACRO IN NEW MODE
;
; *	RET INSTR WILL BE ISSUED IN EXPANSION OF 'MODE FROM, ....' MACRO
;
	.END	SATSSF09
