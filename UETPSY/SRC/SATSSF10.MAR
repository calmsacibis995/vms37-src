	.TITLE	SATSSF10 - SATS SYSTEM SERVICE TESTS  (FAILING S.C.)
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SATS SYSTEM SERVICE TESTS
;
; ABSTRACT:	THE SATSSF10 MODULE TESTS THE EXECUTION OF CERTAIN
; VMS SYSTEM SERVICES, INVOKED IN SUCH A WAY AS TO EXPECT FAILING
; STATUS CODES. THE SYSTEM SERVICES TESTED AND THE STATUS CODES
; EXPECTED ARE SUMMARIZED AS ARGUMENTS TO THE TESTSERV MACROS
; WHICH APPEAR NEAR THE END OF THIS LISTING. SUCCESSFUL STATUS
; CODES ARE TESTED IN OTHER MODULES.
;
;
; ENVIRONMENT:	USER MODE IMAGE; NEEDS CMKRNL PRIVILEGE,
;		DYNAMICALLY ACQUIRES OTHER PRIVILEGES, AS NEEDED.
;
; AUTHOR: THOMAS L. CAFARELLA,		CREATION DATE: MMM, 1978
;	  PAUL D. FAY (DISPSERV & TESTSERV MACROS)
;
; MODIFIED BY:
;
;	V03-002	LDJ0002		Larry D. Jones,		12-Dec-1981
;		Fixed message buffer length test to conform to new PROBE in
;		system service.
;
;	V03-001	LDJ0001		Larry D. Jones,		07-Aug-1981
;		Added support for larger $SNDSMB buffer size support.
;**
;--
	.PAGE
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$PHDDEF				; PROCESS HEADER OFFSET SYMBOLS
	$PCBDEF				; PROCESS CONTROL BLOCK OFFSET SYMBS
	$STSDEF				; STATUS MESSAGE SYMBOLS
	$PRVDEF				; SYMBOL DEFS FOR PRIVILEGES
	$UETPDEF			; UETP MSG CODE DEFINITIONS
	$SHR_MESSAGES UETP,116,<<TEXT,INFO>> 
					; DEFINE UETP$_TEXT
	$OPCDEF				; OPERATOR MSG TYPES ($SNDOPR)
	$ACCDEF				; ACCOUNTING MSG TYPES ($SNDACC)
;
; MACROS:
;
;
; EQUATED SYMBOLS:
;
WARNING		= 0			; WARNING SEVERITY VALUE FOR MSGS
SUCCESS		= 1			; SUCCESS SEVERITY VALUE FOR MSGS
ERROR		= 2			; ERROR SEVERITY VALUE FOR MSGS
INFO		= 3			; INFORMATIONAL SEV VALUE FOR MSGS
SEVERE		= 4			; SEVERE (FATAL) SEV VALUE FOR MSGS
TCG_NO		=  0			; INITIALIZE TEST CASE GROUP NUMBER
GRP_TOTAL	=  0			; INITIALIZE TEST CASE GROUP TOTAL
R0_THRU_SP	= ^M<R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,AP,FP,SP>
;
; OWN STORAGE:
;
	.PAGE
	.PSECT	RODATA,RD,NOWRT,NOEXE,LONG
REG_COMP_MASK:	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,AP,FP> ! ^X8000 -
					; REG COMPARE MASK (HIGH-ORDER ... 
					; ... BIT MUST BE ON
ERR_MSG_FAOCTL:	STRING	I,<!/!AC!1ZB!1ZB: REGISTER !2UW CONTENTS ALTERED>, -
		<;  BEFORE SERVICE CALL: !8XL   AFTER SERVICE CALL: !8XL>
TEST_MOD_NAME:	STRING	C,<SATSSF10>	; TEST MODULE NAME
TEST_MOD_BEG:	STRING	C,<begun>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_SUCC:	STRING	C,<successful>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_FAIL:	STRING	C,<failed>	; DISPOSITION FIELD OF TEST MOD MSG
TEST_MOD_NAME_D: STRING	I,<SATSSF10>	; TEST MODULE NAME DESCRIPTOR
TTNAME:     	STRING  I,<TT>		; TERMINAL LOGICAL NAME
INADR:		.LONG	NOACCESS,NOACCESS ; PAGE ADDRESS OF NOACCESS PSECT
PROT:		.LONG	PRT$C_NA	; PROTECTION CODE FOR NOACCESS PSECT
ONES:		.LONG	-1,-1		; A QUADWORD OF 1-BITS
MSGBUF_SNA:	.LONG	2		; MSGBUF ARGUMENT FOR SNDACC
		.ADDRESS .+4
		.WORD	ACC$K_ENABACC
MSGBUF_SNO:	.LONG	1		; MSGBUF ARGUMENT FOR SNDOPR
		.ADDRESS .+4
		.BYTE	OPC$_RQ_CANCEL
MSGBUF_SNS:	.LONG	4		; MSGBUF ARGUMENT FOR SNDSMB
		.ADDRESS .+4
		.LONG	0
MSGBUF_SNA11:				; MSGBUF ARGUMENT FOR SNDACC
MSGBUF_SNO11:				; MSGBUF ARGUMENT FOR SNDOPR
MSGBUF_SNS11:				; MSGBUF ARGUMENT FOR SNDSMB
		.LONG	0
		.ADDRESS .
MSGBUF_SNA12:	.LONG	255		; MSGBUF ARGUMENT FOR SNDACC
		.ADDRESS .
MSGBUF_SNO12:	.LONG	2001		; MSGBUF ARGUMENT FOR SNDOPR
		.ADDRESS SATSSF10
MSGBUF_SNS12:	.LONG	2001		; MSGBUF ARGUMENT FOR SNDSMB
		.ADDRESS SATSSF10
CHAN_SNA:	.LONG	0		; CHAN ARG FOR SNDACC (MISSING ARG)
CHAN_SNO:	.LONG	0		; CHAN ARG FOR SNDOPR (MISSING ARG)
CHAN_SNS:	.LONG	0		; CHAN ARG FOR SNDSMB (MISSING ARG)
CHAN_SNA21:				; CHAN ARGUMENT FOR SNDACC
CHAN_SNO21:				; CHAN ARGUMENT FOR SNDOPR
CHAN_SNS21:				; CHAN ARGUMENT FOR SNDSMB
		.LONG	-1000000000
CHAN_SNA23:				; CHAN ARGUMENT FOR SNDACC
CHAN_SNO23:				; CHAN ARGUMENT FOR SNDOPR
CHAN_SNS23:				; CHAN ARGUMENT FOR SNDSMB
		.LONG	-1
CHAN_SNA24:				; CHAN ARGUMENT FOR SNDACC
CHAN_SNO24:				; CHAN ARGUMENT FOR SNDOPR
CHAN_SNS24:				; CHAN ARGUMENT FOR SNDSMB
		.LONG	1000000000
MY_DISK:	STRING	I,<SYS$DISK>	; LOGICAL NAME FOR USER DISK
	.PAGE
	.PSECT	RWDATA,RD,WRT,NOEXE
TPID:		.BLKL	1		; PROCESS ID FOR THIS PROCESS
CURRENT_TC:	.BLKL	1 		; PTR TO CURRENT TEST CASE
REG_SAVE_AREA:	.BLKL	15		; SAVE AREA FOR ALL REGS (SANS PC)
MOD_MSG_CODE:	.LONG	UETP$_SATSMS	; TEST MODULE MSG CODE FOR PUTMSG
CLOB_REG_NO:	.BLKL	1		; CLOBBERED REG NO (FOR FAO ERR MSG)
REG_BEFORE_SS:	.BLKL	1		; REG CONTENTS BEFORE S.S. 
					; ... (FOR FAO ERROR MSG)
REG_AFTER_SS:	.BLKL	1		; REG CONTENTS AFTER S.S. 
					; ... (FOR FAO ERROR MSG)
$$TSTN$$:	STRING	C,<  SF   >	; ASCII PORTION OF TEST CASE NAME
TMN_ADDR:	.ADDRESS TEST_MOD_NAME	; ADDR OF TEST MOD NAME FOR FAO
TMD_ADDR:	.ADDRESS TEST_MOD_BEG	; ADDR OF T.M. DISP FIELD FOR FAO
TS_EP:		.BLKL	1		; ENTRY PNT FOR CURR TESTSERV MACRO
RETADR:		.BLKL	2		; RETURN LONGWORDS FOR SETPRT
PRVPRT:		.BLKB	1		; PROT RETURN BYTE FOR SETPRT
PRIVMASK:	.BLKQ	1		; ADDR OF PRIVILEGE MASK (IN PHD)
CHM_CONT:	.BLKL	1		; CHANGE MODE CONTINUE ADDRESS
REGS:		.BLKL	5		; AREA FOR COND INDEX REGS (R2-R6)
CHAN_SNA20:				; CHAN ARGUMENT FOR SNDACC
CHAN_SNO20:				; CHAN ARGUMENT FOR SNDOPR
CHAN_SNS20:				; CHAN ARGUMENT FOR SNDSMB
		.BLKL	1
CHAN_SNA22:				; CHAN ARGUMENT FOR SNDACC
CHAN_SNO22:				; CHAN ARGUMENT FOR SNDOPR
CHAN_SNS22:				; CHAN ARGUMENT FOR SNDSMB
		.BLKL	1
	.PAGE
	.PSECT	SATS_ACCVIO_1,RD,WRT,NOEXE,PAGE
EMPTY:		.BLKB	512		; RESERVE A PAGE OF SPACE
;
; + 
; ******************************************************************
; *                                                                *
; * 	THE ORDER OF STATEMENTS IN THIS PSECT IS CRITICAL.         *
; *	DO NOT RE-ARRANGE THE VARIABLES. CONSULT SATS              *
; *	FUNCTIONAL SPECIFICATION FOR A DESCRIPTION OF THE USE      *
; *	OF THE EMPTY PSECT (AND ITS COMPANION PSECT, NOACCESS).    *
; *                                                                *
; ******************************************************************
; -
;
; TYPE AAAAA_SSSX1 (TYPE AAAAA_SSSX2 IF NOT DESC) GO HERE:
		. = . - 13		; ALLOW ROOM FOR STRING DESCRIPTOR
; TYPE AAAAA_SSSX5 GO HERE:
		.LONG	6		; STRING LENGTH (WILL CROSS PSECT BOUNDARY)
		.ADDRESS .+4		; STRING ADDRESS
; TYPE AAAAA_SSSX3 GO HERE:
		.BLKB	1		; LOW-ORDER BYTE OF STRING LENGTH
; TYPE AAAAA_SSSX2 GO HERE:
		.BLKL	1		; STRING LENGTH
;
;
;
;
	.PSECT	SATS_ACCVIO_2,RD,WRT,NOEXE,PAGE
NOACCESS:	.BLKB	512		; RESERVE A PAGE OF SPACE
		. = . - 512		; RETURN LOC CTR TO BEGINNING OF PSECT
		.ADDRESS EMPTY		; ADDRESS OF ACCESSIBLE STRING
		.ADDRESS EMPTY/^X100 	; ADDRESS OF ACCESSIBLE STRING
;+
; *** NOTE -- DO NOT CHANGE LOCATION OR SEQUENCE OF ABOVE STATEMENTS!
; ***         THIS PSECT (NOACCESS) MUST APPEAR IN MEMORY IMMEDIATELY
; ***         FOLLOWING THE EMPTY PSECT. PSECT NAMES AND OPTIONS WILL BE 
; ***         CHOSEN TO FORCE THE DESIRED PSECT ORDERING.
;-
;
MSGBUF_SNA10:	STRING	I,<SFSNA10>	; MSGBUF ARGUMENT FOR SNDACC
MSGBUF_SNO10:	STRING	I,<SFSNO10>	; MSGBUF ARGUMENT FOR SNDOPR
MSGBUF_SNS10:	STRING	I,<SFSNS10>	; MSGBUF ARGUMENT FOR SNDSMB
;
;
;
	.PSECT	SATSSF10,RD,WRT,EXE,LONG
	.PAGE
	.SBTTL	SATSSF10
;++
; FUNCTIONAL DESCRIPTION:
;
;		AFTER PERFORMING SOME INITIAL HOUSEKEEPING, SUCH AS
; PRINTING THE MODULE BEGIN MESSAGE AND ACQUIRING ALL PRIVILEGES,
; THE SATSSF10 ROUTINE EXECUTES THE TEST_SERV_EXEC MACRO TO RUN
; ALL TEST CASES. WHEN THE MACRO COMPLETES ITS EXECUTION, SATSSF10
; PRINTS A TEST MODULE SUCCESS OR FAIL MESSAGE AND EXITS TO THE
; OPERATING SYSTEM. TEST_SERV_EXEC CALLS THE TC_CONTROL/TESTSERV
; CO-ROUTINE PAIR ONCE PER TEST CASE GROUP TO EXECUTE ALL TEST
; CASES IN THAT GROUP. EACH TEST CASE GROUP IS DEFINED BY BOUNDING
; ITS TEST CASES WITH A TC_GROUP MACRO BEFORE THE FIRST TEST CASE
; AND A TCEND MACRO AFTER THE LAST ONE. THE TEST CASES THEMSELVES
; ARE DEFINED WITHIN THESE BOUNDS BY PRECEDING EACH WITH A
; NEXT_TEST_CASE MACRO. TC_CONTROL/TESTSERV EXECUTES THE CODE 
; FOLLOWING EACH NEXT_TEST_CASE MACRO IMMEDIATELY BEFORE ISSUING
; THE SYSTEM SERVICE AS REQUESTED IN THE TESTSERV MACRO. TC_CONTROL/
; TESTSERV ALSO CHECKS THE RESULTS OF THE SERVICE WITH RESPECT
; TO ITS EXPECTED STATUS CODE AND PRINTS ANY REQUIRED FAILURE
; MESSAGES FOR THE TEST CASE. THE CODE APPEARING AFTER EACH
; NEXT_TEST_CASE MACRO IS MERELY TO SET UP CONDITIONS REQUIRED
; FOR THE SYSTEM SERVICE AND TO CLEAN UP ANY RESOURCES ACQUIRED
; BY THE PREVIOUS TEST CASE.
;
; CALLING SEQUENCE:
;
;	$ RUN SATSSF10  ...  (DCL COMMAND)
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	MESSAGES TO SYS$OUTPUT ARE THE ONLY OUTPUT FROM SATSSF10.
;	THEY ARE OF THE FORM:
;
;		%UETP-S-SATSMS, TEST MODULE SATSSF10 BEGUN ... (BEGIN MSG)
;		%UETP-S-SATSMS, TEST MODULE SATSSF10 SUCCESSFUL ... (END MSG)
;		%UETP-E-SATSMS, TEST MODULE SATSSF10 FAILED ... (END MSG)
;		%UETP-I-TEXT, ... (VARIABLE INFORMATION ABOUT A TEST MODULE FAILURE)
;
; COMPLETION CODES:
;
;	THE SATSSF10 ROUTINE TERMINATES WITH A $EXIT TO THE
;	OPERATING SYSTEM WITH A STATUS CODE DEFINED BY UETP$_SATSMS.
;
; SIDE EFFECTS:
;
;	NONE
;
;--



SATSSF10:
	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
					; ENTRY MASK
	$WAKE_S  TPID			; GET PID OF THIS PROCESS
	$HIBER_S			; UNDO WAKE
	$SETPRN_S TEST_MOD_NAME_D 	; SET PROCESS NAME
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE BEGIN MSG
	MOVAL	TEST_MOD_SUCC,TMD_ADDR	; ASSUME END MSG WILL SHOW SUCCESS
	INSV	#SUCCESS,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR SUCCESS
	MODE	TO,10$,KRNL,NOREGS 	; KERNEL MODE TO ACCESS PHD
	MOVL	@#CTL$GL_PHD,R9		; GET PROCESS HEADER ADDRESS
	MOVAL	PHD$Q_PRIVMSK(R9),PRIVMASK ; GET PRIV MASK ADDRESS
	MODE	FROM,10$		; GET BACK TO USER MODE
	PRIV	ADD,ALL			; GET ALL PRIVILEGES
	DISPSERV			; SET UP DISPLAY INFO FOR TESTSERV
	$SETPRT_S INADR=INADR, RETADR=RETADR, -
		  PROT=PROT, PRVPRT=PRVPRT
					; SET NOACCESS PSECT ...
					; ... FOR NO USER ACCESS
	BRW	EXECUTE			; GO EXECUTE ALL TEST CASES
;
	TC_GROUP	SNA,1,TS1
;
	NEXT_TEST_CASE	SFSNA10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNA10      
; *
; *  SYSTEM SERVICE:		SNDACC               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_SNA10         
; *
; *  INPUT CONDITIONS:
; *	MESSAGE BUFFER DESCRIPTOR IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSNA11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNA11      
; *
; *  SYSTEM SERVICE:		SNDACC               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_SNA11         
; *
; *  INPUT CONDITIONS:
; *	INVALID MESSAGE LENGTH (ZERO)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BADPARAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSNA12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNA12      
; *
; *  SYSTEM SERVICE:		SNDACC               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_SNA12         
; *
; *  INPUT CONDITIONS:
; *	INVALID MESSAGE LENGTH (255)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BADPARAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSNA20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNA20      
; *
; *  SYSTEM SERVICE:		SNDACC               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNA20         
; *
; *  INPUT CONDITIONS:
; *	DEVICE IDENTIFIED BY CHAN ARGUMENT IS NOT A MAILBOX.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	DEVNOTMBX
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$ASSIGN_S DEVNAM=MY_DISK, -	; ASSIGN A CHANNEL TO A REAL DEVICE
		  CHAN=CHAN_SNA20
;
	NEXT_TEST_CASE	SFSNA21
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNA21      
; *
; *  SYSTEM SERVICE:		SNDACC               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNA21         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (LARGE NEGATIVE)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$DASSGN_S CHAN=CHAN_SNA20	; DE-ASSIGN CHANNEL ASSIGNED IN SFSNA20
;
	NEXT_TEST_CASE	SFSNA22
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNA22      
; *
; *  SYSTEM SERVICE:		SNDACC               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNA22         
; *
; *  INPUT CONDITIONS:
; *	ISSUE $SNDACC WITH MAILBOX ASSIGNED FROM KERNEL MODE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,10$,KRNL,NOREGS	; KERNEL MODE FOR $CREMBX
	$CREMBX_S PRMFLG=#1, -		; ASSIGN MAILBOX FROM KERNEL MODE
		  CHAN=CHAN_SNA22
	MODE	FROM,10$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFSNA23
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNA23      
; *
; *  SYSTEM SERVICE:		SNDACC               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNA23         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (-1)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,20$,KRNL,NOREGS	; KERNEL MODE FOR $DELMBX
	$DELMBX_S CHAN=CHAN_SNA22	; DELETE MAILBOX CREATED IN SFSNA22
	$DASSGN_S CHAN=CHAN_SNA22	; ........
	MODE	FROM,20$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFSNA24
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNA24      
; *
; *  SYSTEM SERVICE:		SNDACC               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNA24         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (1 BILLION)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	SNO,1,TS2
;
	NEXT_TEST_CASE	SFSNO10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNO10      
; *
; *  SYSTEM SERVICE:		SNDOPR               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_SNO10         
; *
; *  INPUT CONDITIONS:
; *	MESSAGE BUFFER DESCRIPTOR IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSNO11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNO11      
; *
; *  SYSTEM SERVICE:		SNDOPR               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_SNO11         
; *
; *  INPUT CONDITIONS:
; *	INVALID MESSAGE LENGTH (ZERO)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BADPARAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSNO12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNO12      
; *
; *  SYSTEM SERVICE:		SNDOPR               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_SNO12         
; *
; *  INPUT CONDITIONS:
; *	INVALID MESSAGE LENGTH (2001)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BADPARAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSNO20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNO20      
; *
; *  SYSTEM SERVICE:		SNDOPR               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNO20         
; *
; *  INPUT CONDITIONS:
; *	DEVICE IDENTIFIED BY CHAN ARGUMENT IS NOT A MAILBOX.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	DEVNOTMBX
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$ASSIGN_S DEVNAM=MY_DISK, -	; ASSIGN A CHANNEL TO A REAL DEVICE
		  CHAN=CHAN_SNO20
;
	NEXT_TEST_CASE	SFSNO21
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNO21      
; *
; *  SYSTEM SERVICE:		SNDOPR               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNO21         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (LARGE NEGATIVE)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$DASSGN_S CHAN=CHAN_SNO20	; DE-ASSIGN CHANNEL ASSIGNED IN SFSNO20
;
	NEXT_TEST_CASE	SFSNO22
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNO22      
; *
; *  SYSTEM SERVICE:		SNDOPR               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNO22         
; *
; *  INPUT CONDITIONS:
; *	ISSUE $SNDOPR WITH MAILBOX ASSIGNED FROM KERNEL MODE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,10$,KRNL,NOREGS	; KERNEL MODE FOR $CREMBX
	$CREMBX_S PRMFLG=#1, -		; ASSIGN MAILBOX FROM KERNEL MODE
		  CHAN=CHAN_SNO22
	MODE	FROM,10$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFSNO23
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNO23      
; *
; *  SYSTEM SERVICE:		SNDOPR               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNO23         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (-1)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,20$,KRNL,NOREGS	; KERNEL MODE FOR $DELMBX
	$DELMBX_S CHAN=CHAN_SNO22	; DELETE MAILBOX CREATED IN SFSNO22
	$DASSGN_S CHAN=CHAN_SNO22	; ........
	MODE	FROM,20$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFSNO24
;
;++
; ******************************************************
; *

; *  TEST CASE NAME:		SFSNO24      
; *
; *  SYSTEM SERVICE:		SNDOPR               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNO24         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (1 BILLION)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
;
	TC_GROUP	SNS,1,TS3
;
	NEXT_TEST_CASE	SFSNS10
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNS10      
; *
; *  SYSTEM SERVICE:		SNDSMB               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_SNS10         
; *
; *  INPUT CONDITIONS:
; *	MESSAGE BUFFER DESCRIPTOR IN NON-ACCESSIBLE PSECT.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	ACCVIO
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSNS11
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNS11      
; *
; *  SYSTEM SERVICE:		SNDSMB               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_SNS11         
; *
; *  INPUT CONDITIONS:
; *	INVALID MESSAGE LENGTH (ZERO)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BADPARAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSNS12
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNS12      
; *
; *  SYSTEM SERVICE:		SNDSMB               
; *
; *  ARGUMENT UNDER TEST: 	MSGBUF_SNS12         
; *
; *  INPUT CONDITIONS:
; *	INVALID MESSAGE LENGTH (2001)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	BADPARAM
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	NEXT_TEST_CASE	SFSNS20
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNS20      
; *
; *  SYSTEM SERVICE:		SNDSMB               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNS20         
; *
; *  INPUT CONDITIONS:
; *	DEVICE IDENTIFIED BY CHAN ARGUMENT IS NOT A MAILBOX.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	DEVNOTMBX
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$ASSIGN_S DEVNAM=MY_DISK, -	; ASSIGN A CHANNEL TO A REAL DEVICE
		  CHAN=CHAN_SNS20
;
	NEXT_TEST_CASE	SFSNS21
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNS21      
; *
; *  SYSTEM SERVICE:		SNDSMB               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNS21         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (LARGE NEGATIVE)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	$DASSGN_S CHAN=CHAN_SNS20	; DE-ASSIGN CHANNEL ASSIGNED IN SFSNS20
;
	NEXT_TEST_CASE	SFSNS22
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNS22      
; *
; *  SYSTEM SERVICE:		SNDSMB               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNS22         
; *
; *  INPUT CONDITIONS:
; *	ISSUE $SNDSMB WITH MAILBOX ASSIGNED FROM KERNEL MODE.
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	NOPRIV
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,10$,KRNL,NOREGS	; KERNEL MODE FOR $CREMBX
	$CREMBX_S PRMFLG=#1, -		; ASSIGN MAILBOX FROM KERNEL MODE
		  CHAN=CHAN_SNS22
	MODE	FROM,10$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFSNS23
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNS23      
; *
; *  SYSTEM SERVICE:		SNDSMB               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNS23         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (-1)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
	MODE	TO,20$,KRNL,NOREGS	; KERNEL MODE FOR $DELMBX
	$DELMBX_S CHAN=CHAN_SNS22	; DELETE MAILBOX CREATED IN SFSNS22
	$DASSGN_S CHAN=CHAN_SNS22	; ........
	MODE	FROM,20$		; BACK TO USER MODE
;
	NEXT_TEST_CASE	SFSNS24
;
;++
; ******************************************************
; *
; *  TEST CASE NAME:		SFSNS24      
; *
; *  SYSTEM SERVICE:		SNDSMB               
; *
; *  ARGUMENT UNDER TEST: 	CHAN_SNS24         
; *
; *  INPUT CONDITIONS:
; *	INVALID CHANNEL NUMBER (1 BILLION)
; *
; *  EXPECTED RESULTS:
; *	1) SYSTEM STATUS CODE:	IVCHAN
; *	2) REGISTERS R2 THROUGH FP UNCHANGED
; *
; ******************************************************
;--
;
;
	TCEND
TS1:
	TESTSERV	SNDACC,ERR,SATS,				-
                                          				-
	   <1,MSGBUF_SNA,						-
	        	MSGBUF_SNA10,ACCVIO,	-  ; SFSNA10
	        	MSGBUF_SNA11,BADPARAM,	-  ; SFSNA11
	        	MSGBUF_SNA12,BADPARAM,	-  ; SFSNA12
                                                  >,			-
                                          				-
	   <1,CHAN_SNA,						-
	        	CHAN_SNA20,DEVNOTMBX,	-  ; SFSNA20
	        	CHAN_SNA21,IVCHAN,	-  ; SFSNA21
	        	CHAN_SNA22,NOPRIV,	-  ; SFSNA22
	        	CHAN_SNA23,IVCHAN,	-  ; SFSNA23
	        	CHAN_SNA24,IVCHAN,	-  ; SFSNA24
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS2:
	TESTSERV	SNDOPR,ERR,SATS,				-
                                          				-
	   <1,MSGBUF_SNO,						-
	        	MSGBUF_SNO10,ACCVIO,	-  ; SFSNO10
	        	MSGBUF_SNO11,BADPARAM,	-  ; SFSNO11
	        	MSGBUF_SNO12,BADPARAM,	-  ; SFSNO12
                                                  >,			-
                                          				-
	   <1,CHAN_SNO,						-
	        	CHAN_SNO20,DEVNOTMBX,	-  ; SFSNO20
	        	CHAN_SNO21,IVCHAN,	-  ; SFSNO21
	        	CHAN_SNO22,NOPRIV,	-  ; SFSNO22
	        	CHAN_SNO23,IVCHAN,	-  ; SFSNO23
	        	CHAN_SNO24,IVCHAN,	-  ; SFSNO24
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
TS3:
	TESTSERV	SNDSMB,ERR,SATS,				-
                                          				-
	   <1,MSGBUF_SNS,						-
	        	MSGBUF_SNS10,ACCVIO,	-  ; SFSNS10
	        	MSGBUF_SNS11,BADPARAM,	-  ; SFSNS11
	        	MSGBUF_SNS12,BADPARAM,	-  ; SFSNS12
                                                  >,			-
                                          				-
	   <1,CHAN_SNS,						-
	        	CHAN_SNS20,DEVNOTMBX,	-  ; SFSNS20
	        	CHAN_SNS21,IVCHAN,	-  ; SFSNS21
	        	CHAN_SNS22,NOPRIV,	-  ; SFSNS22
	        	CHAN_SNS23,IVCHAN,	-  ; SFSNS23
	        	CHAN_SNS24,IVCHAN,	-  ; SFSNS24
                                                  >,			-
 
	TS_CLEANUP		; CLEAN UP & RETURN TO TEST_SERV_EXEC
	.SBTTL	EXECUTE & CLEANUP
EXECUTE:
	TEST_SERV_EXEC			; EXECUTE ALL T. CASES IN ALL GROUPS
CLEANUP:
	BSBW	MOD_MSG_PRINT		; PRINT TEST MODULE END MSG
	INSV	#1,#STS$V_INHIB_MSG,#1,MOD_MSG_CODE 
					; INHIBIT PRINTING
	$EXIT_S	MOD_MSG_CODE		; EXIT TO OP SYS WITH MSG CODE
	.PAGE
	.SBTTL	TC_CONTROL
;++
; FUNCTIONAL DESCRIPTION:
;
;		THE TC_CONTROL SUBROUTINE IS CALLED BY THE TEST_SERV_EXEC
; MACRO TO EXECUTE A GROUP OF TEST CASES. A GROUP IS DEFINED BY A TC_GROUP
; MACRO. FOR EACH TC_GROUP MACRO, THERE IS A CORRESPONDING TESTSERV MACRO.
; TESTSERV CONTAINS CODE TO EXECUTE SYSTEM SERVICES AND CHECK THE RETURNED
; STATUS CODE VALUES. TESTSERV ARGUMENTS ARE CODED TO SPECIFY ALL THE SYSTEM
; SERVICE ARGUMENT VALUES AND THE EXPECTED STATUS CODE FOR EACH TEST CASE
; DEFINED BY A NEXT_TEST_CASE MACRO WITHIN THE GROUP. TC_CONTROL USES A
; CO-ROUTINE INTERFACE TO ENTER THE CODE OF THE APPROPRIATE TESTSERV MACRO
; IN VARIOUS PLACES. THE FIRST ENTRY OCCURS ONCE PER GROUP TO ALLOW TESTSERV
; TO DO SOME INITIALIZATION. THEN TWO ENTRIES ARE MADE FOR EACH TEST CASE IN
; THE GROUP. THE FIRST ALLOWS TESTSERV TO ISSUE THE SUBJECT SYSTEM SERVICE.
; THE SECOND ENTRY FOR THE TEST CASE CAUSES TESTSERV TO CHECK THE RETURNED
; STATUS CODE, PRINTING A FAILURE MESSAGE IF IT IS NOT THE EXPECTED CODE.
; IF THERE ARE NO MORE TEST CASES IN THE CURRENT GROUP, TESTSERV (NOT TC_CONTROL)
; RETURNS DIRECTLY TO TEST_SERV_EXEC (RSB ACTUALLY ISSUED IN TS_CLEANUP MACRO)
; FROM THIS SECOND ENTRY; OTHERWISE, CONTROL RETURNS TO TC_CONTROL WHICH
; IN TURN ENTERS TESTSERV AGAIN FOR THE NEXT TEST CASE. THE FAILURE OF A
; TEST CASE DOES NOT CAUSE TERMINATION OF THE TEST MODULE.
;
; CALLING SEQUENCE:
;
;	BSBW TC_CONTROL (ISSUED WITHIN THE TEST_SERV_EXEC MACRO)
;			(RSB IS ISSUED WITHIN THE TS_CLEANUP MACRO)
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	ARGUMENTS SPECIFIED ON EACH TESTSERV MACRO MAY BE VIEWED AS
;	INPUTS, SINCE TC_CONTROL AND TESTSERV ACT AS CO-ROUTINES.
;
; OUTPUT PARAMETERS:
;
;	SEVERITY CODE FIELD OF MOD_MSG_CODE (BITS 0,1,2) IS SET TO ERROR
;	IF ANY TEST CASE IN THE CURRENT GROUP FAILS; OTHERWISE IT REMAINS
;	SET TO SUCCESSFUL.
;
; IMPLICIT OUTPUTS:
;
;	%UETP-I-TEXT,   ERROR MESSAGES ARE WRITTEN TO SYS$OUTPUT BY
;			THE TESTSERV MACRO (CO-ROUTINE WITH TC_CONTROL)
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--



TC_CONTROL:
	PUSHL	TS_EP			; PUSH TESTSERV ENTRY POINT
	JSB	@(SP)+			; ENTER TESTSERV INITIALIZATION
10$:					; PROCESS NEXT TEST CASE
	MOVB	#^A/ /,$$TSTN$$+2 	; MAKE SURE T.C. NAME HAS A BLANK
	BSBW	REG_SAVE		; SAVE REGISTERS
	JSB	@CURRENT_TC		; JUMP TO CURRENT TEST CASE
	BSBW	REG_REST		; RESTORE REGS FOR TESTSERV
	JSB	@(SP)+			; LET TESTSERV ISSUE SYSTEM SERVICE
	BSBW	REG_COMP		; COMPARE REGS TO SEE IF ...
					; ... SYSTEM SERVICE CHANGED ANY
	JSB	@(SP)+			; LET TESTSERV CHEK S.S. STATUS CODE
	CMPB	#^A/*/,$$TSTN$$+2	; HAS TESTSERV INDICATED FAILURE ?
	BNEQU	10$			; NO -- PROCESS NEXT TEST CASE
	MOVAL	TEST_MOD_FAIL,TMD_ADDR	; YES -- INDICATE FAILED IN END MSG
	INSV	#ERROR,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR ERROR
	BRB	10$			; LOOP BAK TO PROCESS NEXT TEST CASE
;
;   TC_CONTROL RETURNS TO TEST_SERV_EXEC VIA TESTSERV (IN TS_CLEANUP MACRO)
;
	.PAGE
	.SBTTL	SUBROUTINES
REG_SAVE:
;
;  *****************************************************************
;  *                                                               *
;  *  SAVES R0 THRU SP IN REG_SAVE_AREA                            *
;  *                                                               *
;  *****************************************************************
;
	PUSHR	#R0_THRU_SP		; SAVE ALL REGS ON STACK
	MOVC3	#60,(SP),REG_SAVE_AREA	; SAVE REGS (BEFORE S.S.)
	POPR	#R0_THRU_SP		; CLEAN UP STACK
	RSB				; .... AND RETURN
;
;
;
;
REG_REST:
;
;
;  *****************************************************************
;  *                                                               *
;  *  RESTORES R0 THRU SP FROM REG_SAVE_AREA                       *
;  *                                                               *
;  *****************************************************************
;
	SUBL2	#60,SP			; MOVE SP TO MAKE ROOM FOR REGS
	MOVC3	#60,REG_SAVE_AREA,(SP)	; MOVE REGS ONTO STACK FOR POP
	POPR	#R0_THRU_SP		; RESTORE ALL REGS FOR TESTSERV
	RSB				; ... AND RETURN
	.PAGE
REG_COMP:
;
;  *****************************************************************
;  *                                                               *
;  *  1) PUSHES ALL REGS ONTO STACK                                *
;  *  2) COMPARES REGISTER IMAGES FROM STACK WITH CORRESPONDING    *
;  *       IMAGES FROM REG_SAVE_AREA FOR ALL REGISTERS SPECIFIED   *
;  *       IN REG_COMP_MASK.                                       *
;  *  3) FOR EACH UNEQUAL COMPARE, AN ERROR MESSAGE IS PRINTED     *
;  *       (USING $FAO AND $OUTPUT SYSTEM SERVICES).               *
;  *  4) POPS ALL REGS OFF OF STACK                                *
;  *                                                               *
;  *****************************************************************
;
	PUSHR	#R0_THRU_SP		; SAVE ALL REGISTERS ON STACK
	MOVAL	REG_SAVE_AREA,R6 	; POINT R6 TO BEG OF ...
					; ... REGS (BEFORE S.S.)
	MOVL	SP,R4			; POINT R4 TO BEG OF
					; ... REGS (AFTER S.S.)
	CVTBL	#-1,R3			; INITIALIZE REG_COMP_MASK INDEX
REG_COMP_NEXT:
	INCL	R3			; POINT TO NEXT BIT IN MASK
	CMPB	#15,R3			; END OF THE MASK ?
	BGTRU	REG_COMP_CONT		; NO -- CONTINUE
	BRW	REG_COMP_RSB		; YES -- GO TO COMMON RETURN
REG_COMP_CONT:
	CMPL	(R6)+,(R4)+		; REG BEFORE = REG AFTER ?
	BEQLU	REG_COMP_NEXT		; YES -- LOOK FOR NEXT REG
	BBC	R3,REG_COMP_MASK,REG_COMP_NEXT
					; NO -- GET NEXT IF BIT NOT SET
	MOVL	R3,CLOB_REG_NO		; NO -- GIVE REG NUMBER TO FAO
	MOVL	-4(R6),REG_BEFORE_SS 	; GIVE "BEFORE" CONTENTS TO FAO
	MOVL	-4(R4),REG_AFTER_SS  	; GIVE "AFTER" CONTENTS TO FAO
	MOVB	#^A/*/,$$TSTN$$+2 	; GIVE FAILURE INDIC'N IN ERROR MSG
;
	$FAO_S	ERR_MSG_FAOCTL,OUTL,OUTD,$$SNAD$$,  -
		$$ASEQ$$,$$PSEQ$$,CLOB_REG_NO,REG_BEFORE_SS,REG_AFTER_SS
;
	MOVW	OUTL,OUTD		; ACTUAL OUTPUT LEN IN STRING DESC'R
	PUTMSG	<#UETP$_TEXT,#1,#OUTD>	; PRINT THE MSG
	MOVW	#OUTE-OUTB,OUTD		; GET MAX LEN BACK INTO DESCRIPTOR
	MOVB	#^A/ /,$$TSTN$$+2 	; REMOVE FAIL INDIC'N FOR NEXT MSG
	MOVAL	TEST_MOD_FAIL,TMD_ADDR	; INDICATE FAILED IN END MSG
	INSV	#ERROR,#0,#3,MOD_MSG_CODE ; ADJUST STATUS CODE FOR ERROR
	BRW	REG_COMP_NEXT		; GO LOOK FOR NEXT REG TO COMPARE
REG_COMP_RSB:
	POPR	#R0_THRU_SP		; CLEAN UP STACK
	RSB				; RETURN TO CALLER
	.PAGE
MOD_MSG_PRINT:
;
;  *****************************************************************
;  *                                                               *
;  *  PRINTS THE TEST MODULE BEGUN/SUCCESSFUL/FAILED MESSAGES      *
;  *	   (USING THE PUTMSG MACRO).                               *
;  *                                                               *
;  *****************************************************************
;
	PUTMSG	<MOD_MSG_CODE,#2,TMN_ADDR,TMD_ADDR> ; PRINT MSG
	RSB				; ... AND RETURN TO CALLER
;
CHMRTN:
; **********************************************************
; *
; *	CHANGE MODE ROUTINE. THIS ROUTINE GETS CONTROL WHENEVER
; *	A CMKRNL, CMEXEC, OR CMSUP SYSTEM SERVICE IS ISSUED
; *	BY THE MODE MACRO ('TO' OPTION).  IT MERELY DOES 
; *	A JUMP INDIRECT ON A FIELD SET UP BY MODE. IT HAS 
; *	THE EFFECT OF RETURNING TO THE END OF THE MODE
; *	MACRO EXPANSION.
; *
; *************************************************************
;
	.WORD	0			; ENTRY MASK
	JMP	@CHM_CONT		; RETURN TO MODE MACRO IN NEW MODE
;
; *	RET INSTR WILL BE ISSUED IN EXPANSION OF 'MODE FROM, ....' MACRO
;
	.END	SATSSF10
