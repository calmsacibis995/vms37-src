$ ON CONTROL_Y THEN GOTO DONE
$ VMS$RL02_DETAILORED = 0
$ SET PROT=(S:RWED,G:RWED,O:RWED,W:RE)/DEFAULT
$ SYSDISK = F$LOGICAL("SYS$DISK")
$ DEFDIR:='F$DIR()
$ DEFUIC:='F$USER()
$ ASSIGN CSA1: DXA1
$ ASSIGN CSA1: DX1
$ ASSIGN CSA1: SYS$KIT
$ ASSIGN SYS$DISK:[SYSLIB] VMS$STARLET
$ ASSIGN SYS$DISK:[SYSHLP] VMS$HELPLIB
$ CPU_TYPE = F$GETSYI("CPU")
$ EXIT_MESSAGE = " Update sequence abnormally terminated."
$ SET NOON
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT "                  VMS Update Procedure"
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT " This command procedure performs VAX/VMS software updates and"
$ WRITE SYS$OUTPUT " optional  software  installations  for  VAX/VMS  Release  3."
$ IF CPU_TYPE .EQ. 3 THEN GOTO DETAILOR_RL02
$ WRITE SYS$OUTPUT " During this sequence, the standard  console  medium will not"
$ WRITE SYS$OUTPUT " be present in the console  drive. Therefore,  the system may"
$ WRITE SYS$OUTPUT " be vulnerable to a power  failure or other fatal crash. If a"
$ WRITE SYS$OUTPUT " system  crash  should  occur  during  this period the update"
$ WRITE SYS$OUTPUT " sequence  can  be  restarted  at  the beginning of the first"
$ WRITE SYS$OUTPUT " incomplete update."
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT " Dismount the current console medium."
$
$DETAILOR_RL02:
$ IF F$GETDVI(SYSDISK,"MAXBLOCK") .GE. 25000 THEN GOTO CONNECT_CONSOLE
$ IF F$GETDVI(SYSDISK,"DEVNAM") .NES. F$GETDVI("SYS$SYSDEVICE","DEVNAM") -
     THEN GOTO CONNECT_CONSOLE
$ VMS$RL02 = 1
$ DEASSIGN VMS$STARLET
$ DEASSIGN VMS$HELPLIB
$ @SYS$UPDATE:VMSINSTAL "" "" DETAILOR
$ IF .NOT. $STATUS THEN GOTO DONE
$ VMS$RL02_DETAILORED = 1
$ ASSIGN LIB$SYSROOT:[SYSLIB] VMS$STARLET
$ ASSIGN LIB$SYSROOT:[SYSHLP] VMS$HELPLIB
$
$CONNECT_CONSOLE:
$ RUN SYS$SYSTEM:SYSGEN
CONNECT CONSOLE
$ IF F$GETDVI("CSA1:","MNT") THEN DISMOUNT CSA1:
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT " Please place the first volume in the console drive"
$ WRITE SYS$OUTPUT " "
$ 20:
$ INQUIRE READY "Are you ready to continue?"
$ IF .NOT. READY THEN GOTO 20
$ GOTO 30
$
$ 25:
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT " Please place the next volume in the console drive"
$ WRITE SYS$OUTPUT " "
$ INQUIRE READY "Are you ready to continue?"
$ IF .NOT. READY THEN GOTO 25
$
$ 30:
$ SET UIC [001,004]
$ SET DEFAULT SYS$DISK:[SYSUPD]
$ MOUNT/NOWRITE/PROCESS=UNIQUE/OVER=ID CSA1: 
$ IF .NOT. $STATUS THEN GOTO DONE
$ IF F$GETDVI("CSA1","DEVNAM") .NES. "_CSA1:" THEN GOTO LBL_ERROR
$ IF .NOT. F$GETDVI("CSA1","MNT") THEN GOTO LBL_ERROR
$ FILE := 'F$GETDVI("CSA1","VOLNAM")'.COM
$ IF "''VMS$RL02'" .AND. FILE .EQS. "VAXPASC1.COM" THEN GOTO INCOMPAT_PASCAL
$ GOTO EXECUTE_PROCEDURE
$
$INCOMPAT_PASCAL:
$ WRITE SYS$OUTPUT "INCOMPATIBLE PASCAL KIT; CONTACT DIGITAL FOR CORRECT KIT"
$ GOTO DISMOUNT_KIT
$
$ LBL_ERROR:
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT " Unable to determine volume label"
$ GOTO DONE
$
$ EXECUTE_PROCEDURE:
$ COPY CSA1:'FILE SYS$DISK:
$ @'FILE
$ SET UIC 'DEFUIC'
$ SET DEFAULT 'SYSDISK'[SYSUPD]
$ DELETE 'FILE;*
$
$DISMOUNT_KIT:
$ DISMOUNT CSA1:
$ WRITE SYS$OUTPUT " "
$ INQUIRE READY "Are there more kits to process?"
$ IF READY THEN GOTO 25
$
$ IF CPU_TYPE .EQ. 3 THEN GOTO 730_CONSOLE
$ WRITE SYS$OUTPUT " "
$ WRITE SYS$OUTPUT " Please place the system console medium in the console drive."
$ WRITE SYS$OUTPUT " "
$ INQUIRE READY "Are you ready to continue?"
$ IF .NOT. READY THEN GOTO DONE
$ MOUNT/SYSTEM/FOR/NOWRITE CSA1: CONSOLE
$ GOTO SUCCESS
$
$730_CONSOLE:
$ IF .NOT. F$GETDVI("CSA2:","MNT") THEN MOUNT/FOR/SYSTEM/NOWRITE CSA2: CONSOLE
$
$SUCCESS:
$ EXIT_MESSAGE = " Requested update sequence is complete."
$
$DONE:
$ ON CONTROL_Y THEN GOTO DONE3
$ SET NOON
$ IF "''VMS$RL02_DETAILORED'" THEN @SYS$UPDATE:VMSINSTAL "" "" RETAILOR
$
$DONE2:
$ IF .NOT. F$GETDVI("CSA1:", "EXISTS") THEN GOTO DONE3
$ IF F$GETDVI("CSA1:", "MNT") THEN DISMOUNT CSA1:
$
$DONE3:
$ IF F$LOGICAL ("SYS$KIT") .NES. "" THEN DEASSIGN SYS$KIT
$ IF F$LOGICAL ("DX1") .NES. "" THEN DEASSIGN DX1
$ IF F$LOGICAL ("DXA1") .NES. "" THEN DEASSIGN DXA1
$ IF F$LOGICAL ("VMS$STARLET") .NES. "" THEN DEASSIGN VMS$STARLET
$ IF F$LOGICAL ("VMS$HELPLIB") .NES. "" THEN DEASSIGN VMS$HELPLIB
$ IF "''DEFUIC'" .NES. "" THEN SET UIC 'DEFUIC'
$ IF "''DEFDIR'" .NES. "" THEN SET DEFAULT 'SYSDISK''DEFDIR
$ WRITE SYS$OUTPUT EXIT_MESSAGE
$ SET ON
$ EXIT
