	.TITLE	IF - IF COMMAND EXECUTION
	.IDENT	'V03-003'
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; IF COMMAND EXECUTION
;
; D. N. CUTLER 9-MAY-77
;
; MODIFIED BY:
;
;	V003	PCG0001		Peter George	22-Feb-1982
;		Check that THEN clause does indeed follow expression.
;
;	V002	TMH0002		Tim Halvorsen	11-Feb-1981
;		Remove reference to global register R9.
;		Use R10 rather than FP as WRK address.
;		Set NOSTAT bit in IF routine.
;		Call binary expression parser rather than
;		general expression parser.
;
;	V001	TMH0001		Tim Halvorsen	09-Sep-1980
;		Convert to MDL structures and remove macro references.
;---

;
; MACRO LIBRARY CALLS
;
 
	PRCDEF				;DEFINE PROCESS WORK AREA
	WRKDEF				;DEFINE COMMAND WORK AREA

	$CLIMSGDEF			;DEFINE ERROR/STATUS CODES
	.PSECT	DCL$ZCODE,BYTE,RD,NOWRT

	.SBTTL	IF COMMAND 
;+
; DCL$IF - IF COMMAND
;
; THIS ROUTINE IS CALLED AS AN INTERNAL COMMAND TO EXECUTE THE IF DCLS COMMAND.
;
; INPUTS:
;
;	R10 = BASE ADDRESS OF COMMAND WORK AREA.
;	R11 = BASE ADDRESS OF PROCESS WORK AREA.
;
; OUTPUTS:
;
;	THE IF CLAUSE IS EVALUATED AND IF THE RESULTANT VALUE IS TRUE, THE
;	THEN CLAUSE IS EXECUTED BY RETURNING CONTROL TO THE COMMAND EXECUTION
;	ENTRY POINT. OTHERWISE CONTROL IS RETURNED VIA THE RETURN ADDRESS.
;
;	R0 LOW BIT CLEAR INDICATES EXPRESSION EVALUATION FAILURE.
;
;		R0 = DCL$_COMPLX - EXPRESSION TOO COMPLEX.
;		R0 = DCL$_EXPSYN - EXPRESSION SYNTAX ERROR.
;		R0 = DCL$_IVCHAR - INVALID CHARACTER IN NUMERIC STRING.
;		R0 = DCL$_IVOPER - INVALID EXPRESSION OPERATOR.
;		R0 = DCL$_UNDSYM - UNDEFINED SYMBOL.
;
;		R0 = DCL$_INVIFTHEN - THEN KEYWORD NOT IN EXPECTED LOCATION
;
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = DCL$_NORMAL - NORMAL COMPLETION.
;-
 
DCL$IF::					;IF COMMAND
	SETBIT	WRK_V_NOSTAT,WRK_W_FLAGS(R10)	;DO NOT SET STATUS ON SUCCESS
	BSBW	DCL$BINEXPR			;EVALUATE ARITHMETIC EXPRESSION
	BLBC	R0,20$				;IF LBC EXPRESSION EVALUATION ERROR
	SUBL3	#4,WRK_L_EXPANDPTR(R10),R2	;GET POINTER TO "THEN" STRING
	CMPL	#^A/THEN/,(R2)			;"THEN" KEYWORD?
	BNEQ	30$				;NO, THEN ERROR
	BLBC	R1,10$				;IF LBC FALSE RESULT
 
;
; EXECUTE THEN CLAUSE OF IF COMMAND
;
 
	BRW	DCL$CMDSTART
 
;
; BYPASS THEN CLAUSE OF IF COMMAND
;

10$:	STATUS	NORMAL 
20$:	RSB

;
; COMMAND SYNTAX ERROR
;

30$:	BSBW	DCL$MARK
	MOVL	#CLI$_INVIFTHEN,R0
	RSB
 
	.END
