	.TITLE	DCXSTART - DCL DEBUG VERSION START MODULE
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; COMMAND LANGUAGE INTERPRETER DEBUGGER INITIALIZATION
;
; TIM HALVORSEN, 24-JAN-1980
;
; MODIFIED BY:
;
;	V005	TMH0005		Tim Halvorsen	09-Aug-1981
;		Exit from process if any debugger initialization problems.
;
;	V004	TMH0004		Tim Halvorsen	04-Apr-1981
;		Use new LOGIN-CLI protocol.
;
;	V003	TMH0003		Tim Halvorsen	28-Mar-1981
;		Skip the debugger entry point if not interactive.
;
;	V002	TMH0002		Tim Halvorsen	23-Feb-1981
;		Remove STEP-OVER DELTA string
;
;	001	TMH0001		T. Halvorsen	09-Jul-1980
;		Call normal initialization AFTER it resets SP/FP.
;---

;
; MACRO LIBRARY CALLS
;
 
	$PPDDEF				;PROCESS PERMANENT DATA AREA
	$PSLDEF				;DEFINE PROCESSOR STATUS FIELDS

	.SBTTL	DEBUGGER START UP
;+
; START-UP WITH DEBUGGER
;
; THIS ENTRY POINT IS JUMPED TO AT THE CONCLUSION OF LOGGING A USER ONTO
; THE SYSTEM. ALL INPUT AND OUTPUT FILES ARE OPEN AND THE PROCESS PERMANENT
; DATA AREA (PPD) HAS BEEN INITIALIZED.
;-
 
	.PSECT	DCL$$BASE,BYTE,RD,NOWRT

BASE_OF_CLI:
	BBC	#PPD$V_MODE,G^CTL$AG_CLIDATA+PPD$W_FLAGS,10$ ;BR IF NOT BATCH
	BRW	DCL$STARTUP+2		; IF BATCH, SKIP DEBUGGER ENTRY POINT
10$:	MOVL	G^CTL$AL_STACK+8,SP	; RESET SUPERVISOR MODE STACK POINTER
	PUSHL	#0			; ALLOCATE SPACE FOR XFER VECTOR ADDR
	CALLS	#1,B^20$		; MAKE DUMMY CALL FRAME (HANDLER=0)
20$:	.WORD	0
	MOVAB	XFER_ARRAY,R0		; GET ADDRESS OF TRANSFER VECTOR
	MOVAB	W^DCL$STARTUP,4(R0)	; SET SECOND TRANFER ADDR TO HERE
	MOVL	R0,4(AP)		; SET TRANSFER ARRAY FOR DEBUGGER
	MOVAB	BASE_OF_CLI,R11		; R11 = BASE OF CLI FOR DEBUGGING
	CALLG	(AP),G^XDT$START	; CALL DEBUGGER INITIALIZATION
	$EXIT_S				; EXIT IF ANY DEBUG INIT PROBLEMS

	.PSECT	DCL$DEBUG,WRT
XFER_ARRAY:
	.LONG	0			; PRIMARY TRANSFER (NOT USED)
	.LONG	0			; SECONDARY = DCL INITIALIZATION

	.END
