	.TITLE	EXIT - EXIT DCLS COMMAND EXECUTION
	.IDENT	'V03-000'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; W. H. BROWN 25-JUN-1977
;
; EXIT COMMAND EXECUTION
;
; MODIFIED BY:
;
;	V006	PCG0002		Peter George 	09-Nov-1981
;		Avoid side effects of R8 getting clobbered 
;		during image rundown.
;
;	V005	PCG0001		Peter George	05-Oct-1981
;		Set $STATUS when a status value has been supplied.
;
;	V004	TMH0004		Tim Halvorsen	28-May-1981
;		Set COMMAND flag before calling exit handlers, so that
;		any errors detected later don't show an error text segment.
;
;	V003	TMH0003		Tim Halvorsen	27-Apr-1981
;		Make use of PRC_L_INDEPTH to determine if level 0 or not.
;
;	V002	TMH0002		Tim Halvorsen	11-Feb-1981
;		Use DCL$MARK rather than R6 as error display pointer.
;		Use WRK_L_EXPANDPTR rather than global register R9.
;		Use R10 rather than FP as WRK address.
;		Make use of DCL$MARKEDTOKEN routine.
;		Call binary expression parser rather than
;		general expression parser.
;		Always call rundown on EXIT regardless of YLEVEL flag.
;		Do not report status from DCL$UNSTACK as it signals
;		all of its errors before returning to caller.
;
;	V001	TMH0001		Tim Halvorsen	09-Sep-1980
;		Convert to MDL structures and remove macro references.
;---

;
; MACRO LIBRARY CALLS
;
 
	PRCDEF				; DEFINE PROCESS WORK AREA
	WRKDEF				; DEFINE COMMAND WORK AREA
	$CLIMSGDEF			; DEFINE CLI RELATED ERROR MESSAGES
	$STSDEF				; DEFINE STATUS CODE FORMAT

	.PSECT	DCL$ZCODE,BYTE,RD,NOWRT

	.SBTTL	EXIT COMMAND 
;+
; DCL$EXIT - EXIT COMMAND
;
; THIS ROUTINE IS CALLED AS AN INTERNAL COMMAND TO EXECUTE THE EXIT DCLS COMMAND.
;
; INPUTS:
;
;	R10 = BASE ADDRESS OF COMMAND WORK AREA.
;	R11 = BASE ADDRESS OF PROCESS WORK AREA.
;
; OUTPUTS:
;
;	THE EXIT EXPRESSION IS EVALUATED
;
;	R0 LOW BIT CLEAR INDICATES EXPRESSION EVALUATION FAILURE.
;
;		R0 = DCL$_COMPLX - EXPRESSION TOO COMPLEX.
;		R0 = DCL$_EXPSYN - EXPRESSION SYNTAX ERROR.
;		R0 = DCL$_IVCHAR - INVALID CHARACTER IN NUMERIC STRING.
;		R0 = DCL$_IVOPER - INVALID EXPRESSION OPERATOR.
;		R0 = DCL$_UNDSYM - UNDEFINED SYMBOL.
;
;	IF THE EXPRESSION EVALUTES OK, THE INDIRECT LEVEL IS UNSTACKED
;	ONE LEVEL AND THE STATUS IS SET TO THE VALUE OF THE EXPRESSION,
;	OR IS LEFT AS THE ORIGINAL VALUE IF THE EXPRESSION WAS NULL.
;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
;
;		R0 = DCL$_NORMAL - NORMAL COMPLETION.
;-
 
DCL$EXIT::				; EXIT COMMAND
	BISL3	#STS$M_INHIB_MSG,PRC_L_LSTSTATUS(R11),R8 ; GET LAST STATUS WITH INHIBIT MSG
	BSBW	DCL$SETNBLK		; FIND NON-BLANK CHARACTER
	BEQL	10$			; BR IF NONE-STATUS GETS SAVED
	BSBW	DCL$BINEXPR		; EVALUATE BINARY EXPRESSION
	BLBC	R0,40$			; BR IF ERROR EVALUATING EXPRESSION
	MOVL	R1,R8			; SET RESULT OF EXPRESSION EVALUATION
10$:	MOVL	R8,PRC_L_LSTSTATUS(R11)	; STORE STATUS
	TSTL	PRC_L_INDEPTH(R11)	; INDIRECT LEVEL 0?
	BEQL	50$			; BR IF NO THERE IS NOTHING TO UNSTACK
	BSBW	DCL$UNSTACK		; REMOVE ONE LEVEL OF INDIRECT
20$:	MOVL	R8,R0			; SET RESULTANT STATUS
	BISW	#WRK_M_COMMAND,WRK_W_FLAGS(R10) ; SET COMMAND IN EXECUTION
40$:	RSB
 
;
; AN EXIT COMMAND ISSUED FOR COMMAND LEVEL 0
; THIS IS A CALL EXIT HANDLER/FORCE EXIT FOR INTERACTIVE JOBS AND
; A LOGOUT FOR BATCH JOBS.
;
50$:	BBS	#PRC_V_MODE,PRC_W_FLAGS(R11),70$ ; BRANCH IF NOT INTERACTIVE
	BISW	#WRK_M_COMMAND,WRK_W_FLAGS(R10) ; SET COMMAND IN EXECUTION
	BSBW	DCL$RUNDOWN		; RUN THE IMAGE DOWN, CALL EXIT HANDLERS
	MOVL	PRC_L_LSTSTATUS(R11),R0	; SET STATUS
	RSB
70$:
	BRW	DCL$ABORT		; ELSE FORCE LOGOUT

	.SBTTL	WAIT COMMAND 
;+
; DCL$WAIT - WAIT COMMAND
;
; THIS ROUTINE IS CALLED AS AN INTERNAL COMMAND TO EXECUTE THE WAIT COMMAND.
;
; INPUTS:
;
;	R10 = BASE ADDRESS OF COMMAND WORK AREA.
;	R11 = BASE ADDRESS OF PROCESS WORK AREA.
;
;	THE NEXT TOKEN ON THE COMMAND LINE MUST BE A VALID
;	EXPRESSION FOR DELTA TIME MINUS THE NUMBER OF DAYS FIELD.
;
; OUTPUTS:
;
;	THE COMMAND FILE EXECUTION IS WAITED FOR THE SPECIFIED TIME.
;-
DCL$WAIT::				;
	BSBW	DCL$MARK		; MARK CURRENT POSITION IN BUFFER
	MOVL	WRK_L_EXPANDPTR(R10),R2	; COPY START OF TIME FIELD
	MOVW	#^A/0 /,(R2)		; APPEND ZERO DAY COUNT FOR WAIT
	ADDL	#2,WRK_L_EXPANDPTR(R10)	; UPDATE EXPANSION POINTER
10$:	BSBW	DCL$MOVCHAR		; MOVE A CHARACTER TO PARSE BUFFER
	BNEQ	10$			; UNTIL END OF LINE
	BSBW	DCL$MARKEDTOKEN		; GET DESCRIPTOR OF TIME STRING
	DECL	R1			; DISCOUNT EOL CHARACTER
	PUSHR	#^M<R1,R2>		; BUILD DESCRIPTOR FOR TIME STRING
	$CANTIM_S			; CANCEL ANY PREVIOUS TIMERS
	MOVL	SP,R3			; SAVE ADDRESS OF QUAD WORD DECRIPTOR
	$BINTIM_S  (R3),(R3)		; CONVERT TIME TO BINARY DELTA FORMAT
	BLBC	R0,30$			; BR IF ERROR IN TIME
	$SETIMR_S  #31,(R3)		; SET TIMER FOR SPECIFIED TIME
	BLBC	R0,30$			; BR IF ERROR SETTING TIMER
	$WAITFR_S  #31			; WAIT THE SPECIFIED TIME
30$:	CLRQ	(SP)+			; REMOVE SCRATCH QUAD WORD FROM STACK
	RSB
 
	.END
