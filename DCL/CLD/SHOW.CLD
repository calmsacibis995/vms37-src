define syntax show_cpu
	image mp
	noparameters
	noqualifiers

define syntax show_daytime
	routine showtime
	noparameters
	noqualifiers

define syntax show_default
	routine showdef
	noparameters
	noqualifiers

define syntax show_logical
	prefix cli$k_shlg_
	parameter p1,prompt="Log_name",value(type=$inlog)
	qualifier system
	qualifier group
	qualifier process
	qualifier all

define syntax show_protection
	routine showprot
	noparameters
	noqualifiers

define syntax show_generic_queues
	noparameters

define syntax show_queue
	prefix cli$k_shqu_
	parameter p1,prompt="Queue",value(required)
	qualifier all
	qualifier batch		syntax=show_generic_queues
	qualifier brief
	qualifier device	syntax=show_generic_queues
	qualifier entry,	value(required)
	qualifier full

define syntax show_all_symbols
	noparameters

define syntax show_symbol
	routine showsymbl
	prefix cli$k_shsy_
	parameter p1,prompt="Symbol",value(required,type=$insym)
	qualifier global,	placement=positional
	qualifier local,	placement=positional
	qualifier all,		placement=positional,syntax=show_all_symbols

define syntax show_translation
	routine showtran
	parameter p1,prompt="Log_name",value(required,type=$inlog)
	noqualifiers

define syntax show_device
	prefix cli$k_shdv_
	parameter p1,prompt="Device",value(type=$device)
	qualifier allocated,
	qualifier brief
	qualifier full
	qualifier mounted
    	qualifier files
    	qualifier system
	qualifier windows

define syntax show_status
	routine showstat
	noparameters
	noqualifiers

define syntax show_process
	prefix cli$k_shpr_
	parameter p1,prompt="Process",value(type=$process)
	qualifier quotas		nonnegatable
	qualifier accounting		nonnegatable
	qualifier privileges		nonnegatable
	qualifier memory		nonnegatable
	qualifier subprocesses		nonnegatable
	qualifier identification	value(required),nonnegatable
	qualifier continuous		nonnegatable
	qualifier all			nonnegatable

define syntax show_system
	prefix cli$k_shss_
	noparameters
	qualifier process,	default,nonnegatable
	qualifier batch		nonnegatable
	qualifier subprocess	nonnegatable
	qualifier network	nonnegatable

define syntax show_terminal
	prefix cli$k_shte_
	parameter p1,prompt="Device",value(type=$device)
	qualifier permanent	nonnegatable

define syntax show_printer
	parameter p1,prompt="Device",value(required,type=$device)
	noqualifiers

define syntax show_magtape
	parameter p1,prompt="Device",value(required,type=$device)
	noqualifiers

define syntax show_quota
	routine showquota
	prefix cli$k_shqo_
	noparameters
	qualifier disk,		default,value(type=$device)
	qualifier user,		value(required,type=$uic)

define syntax show_memory
	prefix cli$k_shme_
	noparameters
	qualifier physical_memory	nonnegatable
	qualifier slots			nonnegatable
	qualifier pool			nonnegatable
	qualifier files			nonnegatable
	qualifier full			nonnegatable
	qualifier all			nonnegatable

define syntax show_error
	prefix cli$k_sher_
	noparameters
	qualifier brief			nonnegatable
	qualifier full			nonnegatable

define syntax show_user
	prefix cli$k_shus_
	parameter p1,prompt="Username",value
	noqualifiers

define syntax no_parameter
	noparameters
	noqualifiers

define type show_options
	prefix cli$k_show_
	keyword cpu,		syntax=show_cpu
	keyword daytime,	syntax=show_daytime
	keyword time,		syntax=show_daytime
	keyword default,	syntax=show_default
	keyword logical,	syntax=show_logical
	keyword protection,	syntax=show_protection
	keyword queue,		syntax=show_queue
	keyword symbol,		syntax=show_symbol
	keyword translation,	syntax=show_translation
	keyword working_set,	syntax=no_parameter
	keyword device,		syntax=show_device
	keyword status,		syntax=show_status
	keyword process,	syntax=show_process
	keyword system,		syntax=show_system
	keyword terminal,	syntax=show_terminal
	keyword printer,	syntax=show_printer
	keyword network,	syntax=no_parameter
	keyword rms_default,	syntax=no_parameter
	keyword magtape,	syntax=show_magtape
	keyword quota,		syntax=show_quota
	keyword	memory,		syntax=show_memory
	keyword users,		syntax=show_user
	keyword	error,		syntax=show_error

define verb show
	parameter p1,prompt="What",value(required,type=show_options)
