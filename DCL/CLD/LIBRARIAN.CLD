define syntax library_only
	parameter p1,prompt="Library",value(required,type=$infile)

define syntax optional_files
	parameter p1,prompt="Library",value(required,type=$infile)
	parameter p2,prompt="File",value(list,impcat,type=$infile)

define syntax library_using_lbr
	image backtrans

define type create_options
	keyword blocks,		value(required)
	keyword globals,	value(required)
	keyword modules,	value(required)
	keyword keysize,	value(required)
	keyword history,	value(required)
	keyword version,	value(required)

define type compress_options
	keyword blocks,		value(required)
	keyword globals,	value(required)
	keyword modules,	value(required)
	keyword keysize,	value(required)
	keyword history,	value(required)
	keyword version,	value(required)
	keyword keep

define type cross_options
	keyword all
	keyword module
	keyword none
	keyword symbol
	keyword value

define verb library
	image librarian
	prefix cli$k_libr_
	parameter p1,prompt="Library",value(required,type=$infile)
	parameter p2,prompt="File",value(required,list,impcat,type=$infile)
	qualifier compress,	value(list,type=compress_options),
				syntax=library_only
	qualifier create,	value(list,type=create_options),
				syntax=optional_files
	qualifier delete,	value(required,list),
				syntax=optional_files
	qualifier extract,	value(list),
				syntax=library_only
	qualifier insert
	qualifier list,		value(type=$outfile),
				syntax=optional_files
	qualifier remove,	value(required,list),
				syntax=optional_files
	qualifier replace
	qualifier macro
	qualifier object
	qualifier output,	value(type=$outfile),default
	qualifier names
	qualifier full
	qualifier globals,	default
	qualifier rsx11,	syntax=library_using_lbr
	qualifier selective_search
	qualifier squeeze
	qualifier log
	qualifier width,	value(required)
	qualifier cross_reference,value(list,type=cross_options),
				syntax=library_only
	qualifier only,		value(required,list)
	qualifier help
	qualifier text
	qualifier modules,	value(required,list),placement=local
	qualifier share
	qualifier history
	qualifier before,	value(default=today, type=$datetime)
	qualifier since,	value(default=today, type=$datetime)
	outputs dcl(output) mcr(output)
