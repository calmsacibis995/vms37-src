$ !
$ !	COMMAND FILE TO BUILD THE VAX/VMS DCL COMMAND INTERPRETER
$ !
$ GOTO 'PHASE'
$
$INIT:
$ !
$ !	CREATE THE DIRECTORIES
$ !
$ @SYSBLDCOM$:CREATEDIR
$ CREATE/DIR SRCD$:[.CLD]
$ EXIT
$
$UPDATE:
$ !
$ !	Create updated sources.
$ !
$ SRCUPDATE CLIMSG.MSG			! Used to generate MLB file
$ PUTSYSMSG SRC$:CLIMSG.MSG		! Insert in SYSMSG.TLB
$ SRCUPDATE CLIDEF.MDL
$ SRCUPDATE DCLDEF.MDL
$ SRCUPDATE CLIMAC.MAR
$ SRCUPDATE INTIMAGES.MAR
$ SRCUPDATE QUAMAC.MAR
$ SRCUPDATE QUADEF.MAR
$ DELETE SRCD$:[.CLD]*.CLD;*
$ COPY MASD$:[.CLD]*.CLD SRCD$:[.CLD]
$ CPYSHRLIB SRCD$:[.CLD]BACKUP.CLD	! Share file with BACKUP
$ EXIT
$
$SRCLIB:
$ !
$ !	DELETE AND RE-CREATE MACRO LIBRARIES
$ !
$ DELSHRLIB LIB$:CLIDEF.B32
$ DELSHRLIB LIB$:CLIMSG.B32
$ DELSHRLIB LIB$:CLIMSG.MAR
$ DELSHRLIB LIB$:DCL.MLB
$ DELETE LIB$:CLIDEF.MAR;*, LIB$:CLIDEF.B32;*
$ MCR MDL LIB$:CLIDEF=SRC$:CLIDEF
$ CPYSHRLIB LIB$:CLIDEF.B32		! Share file with CLE
$ CPYSHRLIB LIB$:CLIDEF.MAR		! Share file with MCR
$ DELETE LIB$:CLIMSG.MAR;*, LIB$:CLIMSG.B32;*
$ MESSAGE/MDL=SRC$:CLIMSG/NOOBJ SRC$:CLIMSG
$ MCR MDL LIB$:CLIMSG=SRC$:CLIMSG
$ CPYSHRLIB LIB$:CLIMSG.B32		! Share file with DIR, CLIUTL, and VMSLIB
$ CPYSHRLIB LIB$:CLIMSG.MAR		! Share file with MCR, and VMSLIB
$ DELETE LIB$:DCLDEF.MAR;*, LIB$:DCLDEF.B32;*
$ MCR MDL LIB$:DCLDEF=SRC$:DCLDEF
$ CPYSHRLIB LIB$:DCLDEF.B32		! Share file with CLE for PRC_L_TAB_VEC
$ DELETE LIB$:DCL.MLB;*
$ LIB/CREATE/MACRO LIB$:DCL -
	SRC$:CLIMAC,QUAMAC,QUADEF,INTIMAGES,-
	LIB$:CLIMSG,CLIDEF,DCLDEF,-
	SHRLIB$:PPDDEF			! Get file from LOGIN
$ CPYSHRLIB LIB$:DCL.MLB		! Share file with CLE(INTIMAGES), LOGIN, MCR, and CLIUTL
$ EXIT
$
$ASSEM:
$ !
$ !	DELETE AND RE-CREATE THE DCL OBJECTS AND LISTINGS
$ !
$ DELETE OBJ$:*.OBJ;*,LIS$:*.LIS;*
$ @SYSBLDCOM$:FASM DCL "" N 'UPDATES'
$ RENAME OBJ$:CLIGBL.OBJ LIB$:CLIGBL.STB	! For VMSLIB
$ !
$ ! Generate the global symbols needed by all programs using the old interface
$ !
$ @COM$:DCLSYM				! Generates LIB$:CLISYM.STB for VMSLIB
$ EXIT
$
$OBJLIB:
$ !
$ !	Create object library from object modules.
$ !
$ @SYSBLDCOM$:OBJLIB DCL 200 512 40
$ EXIT
$
$LNKLIB:
$ !
$ ! Null LNKLIB phase
$ !
$ EXIT
$
$LINK:
$ !
$ !  RECREATE MAP AND IMAGE FILES, DCL AND DCLTABLES
$ !
$ @COM$:DCLTABLNK			! Tables w/only special commands
$ @COM$:DCLLNK
$ !
$ ! Add the entire DCL language to the sharable image
$ !
$ DELETE LIS$:DCLTABLES.LIS;*
$ SET COMMAND SRCD$:[.CLD]* -
	/TABLES=EXE$:DCLTABLES -
	/OUTPUT=EXE$:DCLTABLES -
	/LISTING=LIS$:DCLTABLES
$ EXIT
$
$RESULT:
$ !
$ !	COPY THE IMAGE AND MAP FILES TO SYSTEM AREAS
$ !
$ CPYRESEXE EXE$:DCL.EXE
$ CPYSYSLIB EXE$:DCLTABLES.EXE
$ CPYRESMAP MAP$:DCLTABLES.MAP,DCL
$ CPYRESLIS LIS$:*.LIS DCL.LSS
