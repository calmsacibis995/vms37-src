MODULE	DEVICE	(
			 LANGUAGE (BLISS32),
			 IDENT = 'V03-001'
			) =

!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
! FACILITY:
!
!	OPCOM
!
! ABSTRACT:
!
!	This module contains the specialized logic to service
!	a particular type of request sent by a user to OPCOM.
!
! Environment:
!
!	VAX/VMS operating system.
!
! Author:
!
!	Steven T. Jeffreys
!
! Creation date:
!
!	March 10, 1981
!
! Revision history:
!
!
!	V03-001	RLRV3A1		Robert L. Rappaport	5-Apr-1982
!		Added MSG$_UDA50MVER and MSG$_DUPUNITNO.
!
!	V02-003	STJ0155		Steven T. Jeffreys,	05-Feb-1982
!		More mount verification message support.
!
!	V02-002	STJ0065		Steven T. Jeffreys,	14-Jul-1981
!		Added support for mount verification messages.
!
!--

BEGIN						! Start of DEVICE

LIBRARY	'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:OPCOMDEF.REQ';

FORWARD ROUTINE
	DEVICE_HANDLER	: NOVALUE;

BUILTIN

	INSQUE,					! Insert entry onto a queue
	REMQUE;					! Remove entry from a queue

GLOBAL ROUTINE DEVICE_HANDLER (BUFFER_DESC) : NOVALUE =

!++
! Functional description:
!
!	This routine is the handler for all device ONLINE or OFFLINE messages.
!	Note that this message has a special format, different from all other
!	messages.  This is because the message is sent via  EXE$SNDEVMSG
!	instead of $SNDOPR.
!
!
!
! Input:
!
!	BUFFER_DESC : The address of a quadword buffer descriptor that
!		      describes the buffer containig the message.
!
! Implicit Input:
!
!	The format of the message is as follows:
!		WORD  <MSG$_DEVONLIN or MSG$_DEVOFFLIN>
!		WORD  <device unit number>
!		ASCIC <device name (DDC)>
!
! Output:
!
!	None.
!
! Implict output:
!
!	Some accounting data will be updated 
!	to reflect the receipt of the message.
!
! Side effects:
!
!	None.
!
! Routine value:
!
!	None.
!--

BEGIN						! Start of DEVICE_HANDLER

MAP

	BUFFER_DESC	: REF BBLOCK;

EXTERNAL ROUTINE
	ALLOCATE_DS,				! Allocate a data structure
	DEALLOCATE_RQCB	: NOVALUE,		! Dispose of an RQCB
	FORMAT_MESSAGE,				! Format a message
	LOG_MESSAGE,				! Log an event
	NOTIFY_LISTED_OPERATORS;		! Notify interested operators

EXTERNAL
	OCD_VECTOR	: VECTOR,		! OCD list heads
	DEVICE_FAO	: BBLOCK;		! FAO control string descriptor

EXTERNAL LITERAL
	RQCB_K_TYPE,				! RQCB structure type
	MIN_SCOPE,				! Minimum scope value
	MAX_SCOPE;				! Maximum scope value

LOCAL
	MESSAGE_VECTOR	: VECTOR [5,LONG],	! Message info
	DEV_CHAR	: BBLOCK [DIB$K_LENGTH],! Device characteristics buffer
	CHAR_DESC	: BBLOCK [DSC$K_S_BLN],	! Descriptor for above buffer
	NAM_BUF		: BBLOCK [MAX_DEV_NAM],	! Buffer for device name
	NAM_DESC	: BBLOCK [DSC$K_S_BLN],	! Descriptor for above buffer
	MESSAGE		: LONG,			! Message code
	MSG		: REF BBLOCK,		! Pointer to message text
	RQCB		: REF BBLOCK,		! RQCB data structure
	OCD		: REF BBLOCK,		! OCD data structure
	OCD_COUNT	: LONG,			! Count of OCDs in OCD list
	OCD_INDEX	: LONG,			! Index into OCD_VECTOR
	OPER_COUNT	: LONG,			! Count of operators in operator list
	STATUS		: LONG;

!
! Check the message for a minimum length.  Assume minimum
! length for a device name (minus the unit #) is 2 characters.
!
IF .BUFFER_DESC [DSC$W_LENGTH] LSS (2+2+3)
THEN
    RETURN;
!
! The message must be one of the known device messages.  All others
! are ignored.  Set the message code in the message vector to the
! proper value.
!
MSG = .BUFFER_DESC [DSC$A_POINTER];
SELECTONE .MSG [0,0,16,0] OF
    SET
    [ MSG$_DEVONLIN	] : MESSAGE_VECTOR [0] = OPC$_DEVONLINE;
    [ MSG$_DEVOFFLIN	] : MESSAGE_VECTOR [0] = OPC$_DEVOFFLINE;
    [ MSG$_DEVOFFLINX	] : MESSAGE_VECTOR [0] = OPC$_DEVOFFLINX;
    [ MSG$_WRONGVOL	] : MESSAGE_VECTOR [0] = OPC$_WRONGVOL;
    [ MSG$_DEVWRTLCK	] : MESSAGE_VECTOR [0] = OPC$_DEVWRTLCK;
    [ MSG$_MVCOMPLETE	] : MESSAGE_VECTOR [0] = OPC$_MVCOMPLETE;
    [ MSG$_MVABORTED	] : MESSAGE_VECTOR [0] = OPC$_MVABORTED;
    [ MSG$_UDA50MVER	] : MESSAGE_VECTOR [0] = OPC$_UDA50MVER;
    [ MSG$_DUPUNITNO	] : MESSAGE_VECTOR [0] = OPC$_DUPUNITNO;
    [ OTHERWISE		] : RETURN;
    TES;
!
! Format the device name.
!
NAM_DESC [0,0,32,0] = MAX_DEV_NAM;
NAM_DESC [DSC$A_POINTER] = NAM_BUF;
IF NOT $FAO (DEVICE_FAO, NAM_DESC, NAM_DESC, MSG [4,0,0,0], .MSG [2,0,16,0])
THEN
    RETURN;
!
! Get the device characteristics.
!
CHAR_DESC [0,0,32,0] = DIB$K_LENGTH;
CHAR_DESC [DSC$A_POINTER] = DEV_CHAR;
IF NOT $GETDEV (DEVNAM=NAM_DESC, PRIBUF=CHAR_DESC)
THEN
    RETURN;
!
! Allocate an RQCB.  This is necessary 
! to format and later issue the message.
!
IF NOT ALLOCATE_DS (RQCB_K_TYPE, RQCB)
THEN
    RETURN;
!
! Set the operator interest mask depending on the device class.
! Also target the message to DEVICE class operators.
!
RQCB [RQCB_L_ATTNMASK1] = (SELECTONEU .DEV_CHAR [DIB$B_DEVCLASS] OF
			     SET
			     [ DC$_DISK ]	: OPC$M_NM_DISKS;
			     [ DC$_TAPE ]	: OPC$M_NM_TAPES;
			     [ DC$_CARD ]	: OPC$M_NM_CARDS;
			     [ DC$_LP ]		: OPC$M_NM_PRINT;
			     [ OTHERWISE ]	: OPC$M_NM_DEVICE;
			     TES);
RQCB [RQCB_L_ATTNMASK1] = (.RQCB [RQCB_L_ATTNMASK1] OR OPC$M_NM_DEVICE); 
!
! Format the message, log it, and send it to all interested operators.
! Every operator in the data base is a candidate for the message.
!
MESSAGE_VECTOR [1] = 0;				! Use current system time
MESSAGE_VECTOR [2] = NAM_DESC;			! Set addr of dev name descriptor
FORMAT_MESSAGE (.RQCB, MESSAGE_VECTOR);
OCD_INDEX = MAX_SCOPE;
WHILE (.OCD_INDEX GEQ MIN_SCOPE) DO
    BEGIN
    !
    ! Scan the OCD list for each class of operator.
    !
    OCD_COUNT = .OCD_VECTOR [(.OCD_INDEX - 1) * 2 + 1];
    OCD = .OCD_VECTOR [(.OCD_INDEX - 1) * 2];
    WHILE (.OCD_COUNT GTR 0) DO
	BEGIN
	!
	! Notify every operator in the OCD's operator list.
	! Also log the message for each OCD.
	!
	RQCB [RQCB_L_OCD] = .OCD;		! Set OCD address
	LOG_MESSAGE (.RQCB);			! Log the message
	NOTIFY_LISTED_OPERATORS (.RQCB);	! Inform the operators
	OCD_COUNT = .OCD_COUNT - 1;		! Decrement operator count
	OCD = .OCD [OCD_L_FLINK];		! Get next OCD address
	END;
    OCD_INDEX = .OCD_INDEX - 1;
    END;
!
! Zero the pointer to the OCD within the RQCB to prevent
! the OCD from being affected by the RQCB deallocation.
!
RQCB [RQCB_L_OCD] = 0;
DEALLOCATE_RQCB (.RQCB);
RETURN; 	

END;						! End of DEVICE_HANDLER

END						! End of DEVICE
ELUDOM
