	.NLIST
;
;	Version	'V03-001'
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************

;++
;
; FACILITY:  VAX/VMS System Macro Libraries
;
; ABSTRACT:
;
;	This file contains macros that are commonly used by the
;	Executive and drivers.
;
; ENVIRONMENT:
;
;	n/a
;
;--
;
;
; AUTHOR:  The VMS Group	 CREATION DATE:  1-Aug-1976
;
; MODIFICATION HISTORY:
;
;	V03-001	ROW0082		Ralph O. Weber		10-APR-1982
;		Add two new SCS macros; SCAN_RSPID_WAIT and SCAN_RDT.
;
;	V02-032	KTA0081		Kerbey T. Altmann	22-Feb-1982
;		Add some new SCS macros.
;
;	V02-031	JLV0176		Jake VanNoy		9-Feb-1982
;		Add LINKTIME and ECO_LEVEL to DPTAB. Make same
;		macro smarter about DPT$C_LENGTH.
;
;	V02-030	KTA0079		Kerbey T. Altmann	08-Feb-1982
;		Add new argument to LISTEN.
;
;	V02-029	RLRCON		Robert L. Rappaport	13-Jan-1982
;		Correct typographical error in CONNECT.
;
;	V02-028	KTA0061		Kerbey T. Altmann	04-Jan-1982
;		Modify SCS ACCEPT, CONNECT, and DISCONNECT.
;
;	V02-027	KTA0040		Kerbey T. Altmann	30-Oct-1981
;		Add the SCS macros.
;
;	V02-026	STJ0103		Steven T. Jeffreys	17-Jul-1981
;		Made contents of DDT$L_MNTVER default to IOC$MNTVER.
;
;	V02-025	LJK0039		Lawrence J. Kenah	31-Jul-1981
;		Modified RPTEVT macro to optionally use a JSB
;		linkage to SCH$RSE. BSBW is still the default.
;
;	V02-024	JLV0054		Jake VanNoy		31-Jul-1981
;		Changed the way the DDT$W_FDTSIZE is calculated to
;		make up for a shortcoming (bug?) in MACRO.
;
;	V02-023	TCM0001		Trudy C. Matthews	29-Jul-1981
;		Changed all "7ZZ"s to "730"s.
;
;	V02-022	JLV0051		Jake VanNoy		22-Jul-1981
;		Fixed DPT$W_VECTOR macro.
;
;	V02-021	STJ0064		Steve Jeffreys		18-Jul-1981
;		Modified DECREF macro to optionally use JSB for call
;		to MMG$REFCNTNEG.  Defaults to BSBW.
;
;	V02-020	JLV0045		Jake VanNoy		13-July-1981
;		Added DPT$W_VECTOR.
;
;	V02-019	JLV0043		Jake VanNoy		13-July-1981
;		Added DDT$W_FDTSIZE to DDT by adding to DDTAB and 
;		the FUNCTAB macro.
;
;	V02-018	STJ0061		Steven T. Jeffreys	06-Jun-1981
;		Changed DDTAB macro to fill DDT$L_MNTVER.
;		Changed DECREF macro to use general addressing mode.
;
;	V02-017	LJK0030		Lawrence J. Kenah	23-Jun-1981
;		Added PFN_REFERENCE macro and PFN_DISP_xx macros to support 
;		physical memory configurations of more than 32 Mbytes. 
;		Changed DECSHR macro to use PFN_REFERENCE macro.
;
;	V02-016	RLRUBAA		Robert L. Rappaport	29-May-1981
;		Added LOADUBAA macro definition which invokes the
;		alternate LOAD UBA entrypoint used by UNIBUS devices
;		such as the TS11, which require the offset bit in the
;		UBA mapregisters to be clear regardless of whether the
;		buffer is word aligned or not.
;
;	V02-015 JLV0006		Jake VanNoy		23-Apr-1981
;		Added DPT$W_DELIVER and changed DPT$W_DFLTUNITS
;		to DEFUNITS.
;
;	V02-014	LMK0004		Len Kawell		23-Mar-1981
;		Add some NOP's to the TIMEWAIT macro so when it is
;		used to poll a UNIBUS bit, it allows DMA activity
;		to properly occur.
;
;	V02-013 JLV0001		Jake VanNoy		26-Feb-1981
;		Add DFLTUNITS to macro DPTAB.
;
;	V02-012 CAS0003		C. A. Samuelson		29-Dec-1980
;		Add MACRO TIMEWAIT for looping on a bit for a specific
;		amount of time without reading processor clock
;
;	V02-011	RIH0007		Richard I. Hustvedt	15-Dec-1980
;		Change TBIA to use a value of 0, for conformance with SRM.
;
;	V02-010	SRB0005		Steve Beckhardt		16-Sep-1980
;		Added CPUDISP macro for cpu dependant code.
;
;**
;
; GENERATING SYSTEM INTERNAL BUG CHECK
;
; BUG_CHECK ERROR,TYPE
;
;	ERROR  = ONE TO SIX CHARACTER ERROR NAME.
;	TYPE   = "FATAL" OR ANYTHING ELSE.
;
 
	.MACRO	BUG_CHECK ERROR,TYPE=CONT
		.WORD	^XFEFF
		.IIF IDN <TYPE>,<FATAL> , .WORD	BUG$_'ERROR'!4
		.IIF DIF <TYPE>,<FATAL> , .WORD	BUG$_'ERROR'
	.ENDM	BUG_CHECK
 
;
; GENERATE OPERATING BUG CHECK
;
; BUGCHK SUBSYSTEM,ERROR,MODE [,CALLOP=JSB]
;
 
	.MACRO	BUGCHK SUBSYSTEM,ERROR,MODE,CALLOP=BSBW
	.IF IDN	<MODE>,<FATAL>
	CALLOP	EXE$BUGCHKFATAL
	.IFF
	CALLOP	EXE$BUGCHKCONT
	.ENDC
	.ASCIZ	/SUBSYSTEM'_'ERROR/
	.ENDM	BUGCHK
 
;
;	CASE MACRO FOR GENERATING CASE AND CASE TABLE
;
;	CASE	SRC,<DISPATCH LIST>,[TYPE=W/B/L],[LIMIT=#0],[NMODE=S^#]
;
	.MACRO	CASE,SRC,DISPLIST,TYPE=W,LIMIT=#0,NMODE=S^#,?BASE,?MAX
	CASE'TYPE	SRC,LIMIT,NMODE'<<MAX-BASE>/2>-1
BASE:
	.IRP	EP,<DISPLIST>
	.WORD	EP-BASE
	.ENDR
MAX:	
	.ENDM
 
;
; CPU TYPE DISPATCH MACRO:
;
;	THE ADDRESSES IN THE ADDRESS LIST ARE:
;		ADDRESS OF CODE FOR CPU TYPE=1 (11/780)
;		ADDRESS OF CODE FOR CPU TYPE=2 (11/750)
;		ADDRESS OF CODE FOR CPU TYPE=3 (11/730)
;		ADDRESS OF CODE FOR CPU TYPE=4 (?)
;		ETC.
;
;	CPUDISP IS INVOKED TO HANDLE CPU DIFFERENCES IN LINE, E.G.,
;
;		CPUDISP	<10$,20$,30$>	;*DISPATCH ON CPU TYPE*
;	10$:	<11/780 SPECIFIC CODE>
;	20$:	<11/750 SPECIFIC CODE>
;	30$:	<11/730 SPECIFIC CODE>
;					;*END OF CPU-DEPENDENT CODE*
;
;	WHEN THE NEXT CPU IS ADDED, ALL OCCURRENCES OF CPUDISP 
;	MUST BE EXPANDED TO HANDLE FOUR CPU SPECIFIC PATHS.
;
	.MACRO	CPUDISP,ADDRLIST	
	CASE	G^EXE$GB_CPUTYPE,<ADDRLIST>,LIMIT=#PR$_SID_TYP780,TYPE=B
	.ENDM	CPUDISP			
;
; GENERATE DRIVER DISPATCH TABLE
;
; DDTAB	DEVNAM,START,UNSOLIC,FUNCTB,CANCEL,REGDMP,DIAGBF,ERLGBF
;
; FDTSIZE is defined by FUNCTAB macro, it is zeroed here as well so a driver
; can have a DDTAB without a FUNCTAB. It is not done here with a 
; .IF NOT_DEFINED macro as MACRO will then immediately store the zero (on
; the first pass), and the value calculated by the FUNCTAB macro will
; be ignored.
;
	.MACRO	DDTAB	DEVNAM,-
			START=0,-
			UNSOLIC=0,-
			FUNCTB,-
			CANCEL=0,-
			REGDMP=0,-
			DIAGBF=0,-
			ERLGBF=0,-
			UNITINIT=0,-
			ALTSTART=0,-
			MNTVER=+IOC$MNTVER
	.PSECT	$$$115_DRIVER,LONG
'DEVNAM'$DDT::
	GENRADDR START,'DEVNAM'$DDT
	GENRADDR UNSOLIC,'DEVNAM'$DDT
	GENRADDR FUNCTB,'DEVNAM'$DDT
	GENRADDR CANCEL,'DEVNAM'$DDT
	GENRADDR REGDMP,'DEVNAM'$DDT
	.WORD	DIAGBF
	.WORD	ERLGBF
	GENRADDR UNITINIT,'DEVNAM'$DDT
	GENRADDR ALTSTART,'DEVNAM'$DDT
	GENRADDR MNTVER,'DEVNAM'$DDT
	.WORD	FUNCTAB_LEN
FUNCTAB_LEN = 0
	.ENDM	DDTAB
 
;
; DECREMENT PAGE REFERENCE COUNT
;
;	DECREF	EQL,GTR,PFN,MODE,LABEL,CALL
;
;	EQL	= BRANCH LOCATION IF NEW REFCNT = 0
;	GTR	= BRANCH LOCATION IF NEW REFCNT > 0
;	PFN	= REGISTER CONTAINING PFN, DEFAULT TO R0
;	MODE	= ADDRESSING MODE, DEFAULT IS WORD DISPLACEMENT
;	LABEL	= IF PRESENT, USE THE SUPPLIED PARAMETER AS A LABEL.
;		  OTHERWISE CREATE A LOCAL LABEL.
;	CALL	= IF PRESENT, USE A JSB TO CALL MMG$REFCNTNEG.
;		  OTHERWISE USE A BSBW.
;	      
;
	.MACRO	DECREF	EQL,GTR,PFN=R0,MODE=W^,?L1,CALL
	TMP...=0
		DECW	@'MODE'PFN$AW_REFCNT[PFN]
	.IF	NB,EQL
		BEQL	EQL
		TMP...=TMP...+1
	.ENDC
	.IF	NB,GTR
		BGTR	GTR
		TMP...=TMP...+1
	.ENDC
	.IF	LT,<TMP...-2>
		BGEQ	L1
	.ENDC
	.IF	NB,CALL
		JSB	G^MMG$REFCNTNEG
	.IFF
		BSBW	MMG$REFCNTNEG
	.ENDC
	.IF	LT,<TMP...-2>
L1:
	.ENDC
	.ENDM	DECREF
;
; DECREMENT PAGE SHARE COUNT
;
;	DECSHR	EQL,GTR,PFN,IMAGE_FLAG
;
;	EQL = BRANCH LOCATION IF NEW SHRCNT = 0
;	GTR = BRANCH LOCATION IF NEW SHRCNT > 0
;	PFN = REGISTER CONTAINING PFN, DEFAULT TO R0
;	IMAGE_FLAG = Indicator of whether macro is located in nonpaged exec
;			Set to SYS_NONPAGED if so
;			Defaults to NOSYS
;
	.MACRO	DECSHR	EQL,GTR,PFN=R0,IMAGE_FLAG=NOSYS,?L1
	TMP...=0
			PFN_REFERENCE	-
	DECW	<@W^PFN$Ax_SHRCNT[PFN]>,-
			LONG_OPCODE=DECL,-
			IMAGE=IMAGE_FLAG
	.IF	NB,EQL
		BEQL	EQL
		TMP...=TMP...+1
	.ENDC
	.IF	NB,GTR
		BGTR	GTR
		TMP...=TMP...+1
	.ENDC
	.IF	LT,<TMP...-2>
		BGEQ	L1
	.ENDC
		BSBW	MMG$SHRCNTNEG
	.IF	LT,<TMP...-2>
L1:
	.ENDC
	.ENDM	DECSHR
;
; DEVICE DRIVER PROLOGUE TABLE
;
;	DPTAB	END,ADAPTER,FLAGS,UCBSIZE,UNLOAD,MAXUNITS,DEFUNITS,DELIVER
;		NAME
;
;
;	END =	ADDR OF END OF DRIVER
;	ADAPTER = ADAPTER TYPE (UBA,MBA,DRA)
;	FLAGS = DRIVER LOADER FLAGS
;	UCBSIZE = SIZE OF EACH UCB (IN BYTES)
;	UNLOAD = ADDRESS OF A ROUTINE TO CALL IF THE DRIVER IS TO BE UNLOADED
;	MAXUNITS = MAXIMUM NUMBER OF UNITS THAT CAN BE CONNECTED.
;	DEFUNITS = DEFAULT NUMBER OF UNITS TO AUTOCONFIGURE.
;	DELIVER = ADDRESS OF A ROUTINE TO CALL AT AUTOCONFIGURE TO DELIVER UNITS
;	VECTOR = OFFSET TO SET OF VECTORS (USED BY TTDRIVER)
;	NAME = DRIVER NAME
;
;
	.MACRO	DPTAB	END,ADAPTER,FLAGS=0,UCBSIZE,UNLOAD,MAXUNITS=8,-
			DEFUNITS=1,DELIVER,VECTOR,NAME
	.SAVE
	$DPTDEF
	ASSUME	DPT$C_LENGTH EQ 56
	.PSECT	$$$105_PROLOGUE 
DPT$TAB:
	.BLKL	2

	.WORD	END-DPT$TAB
	.BYTE	DYN$C_DPT
	.BYTE	0

	.BYTE	AT$_'ADAPTER'
	.BYTE	FLAGS
	.WORD	UCBSIZE

	.WORD	DPT$INITAB-DPT$TAB
	.WORD	DPT$REINITAB-DPT$TAB

	.IF NB,UNLOAD
	.WORD	UNLOAD-DPT$TAB
	.IFF
	.WORD	0
	.ENDC

	.WORD	MAXUNITS
	.WORD	DPT$C_VERSION
	.WORD	DEFUNITS

	.IF NB,DELIVER
	.WORD	DELIVER-DPT$TAB
	.IFF
	.WORD	0
	.ENDC

	.IF NB,VECTOR
	.WORD	VECTOR-DPT$TAB
	.IFF
	.WORD	0
	.ENDC

	$$$=.
	.ASCIC	/NAME/
	.=$$$+12

	.LONG	0,0		; LINK TIME
	.LONG	0		; ECO LEVEL

	.MDELETE DPTAB
	.ENDM	DPTAB

;
; STORE DPT INITIALIZATION TABLE VALUES
;
;	DPT_STORE STRUC_TYPE,STRUC_OFFSET,OPERATION,EXPRESSION,POS,SIZE
;
;	STRUC_TYPE = STRUC TYPE CODE (DDB,UCB,CRB,IDB)
;		   = "INIT" IF START OF INIT TABLE
;		   = "REINIT" IF START OF RE-INIT TABLE
;		   = "END" IF END OF RE-INIT TABLE
;	STRUC_OFFSET = UNSIGNED OFFSET INTO STRUC
;	OPERATION = TYPE OF INITIALIZATION OPERATION 
;		    B=BYTE,W=WORD,L=LONG,D=RELATIVE TO DRIVER,V=FIELD
;		    IF PRECEDED BY "@" (IE. @B) THEN EXPRESSION 
;		    IS ADDRESS OF DATA
;	EXPRESSION = EXPRESSION TO BE STORED
;	POS = BIT POSITION (FOR OPERATION = V ONLY)
;	SIZE = FIELD SIZE (FOR OPERATION = V ONLY)
;
	.MACRO	DPT_STORE STR_TYPE,STR_OFF,OPER,EXP,POS,SIZE
	.IF IDN <STR_TYPE>,<INIT>
DPT$INITAB:
	.IFF
	.IF IDN <STR_TYPE>,<REINIT>
DPT$REINITAB:
	.IFF
	.IF IDN <STR_TYPE>,<END>
	.BYTE	0
	.RESTORE

	.IFF
	.BYTE	DYN$C_'STR_TYPE
	.BYTE	STR_OFF
	$$OP=0
	.IRPC	C,<OPER>
	.IIF IDN <C>,<@>, $$OP=^X80
	.IIF IDN <C>,<W>, $$OP=$$OP!1
	.IIF IDN <C>,<D>, $$OP=$$OP!2
	.IIF IDN <C>,<L>, $$OP=$$OP!3
	.IIF IDN <C>,<V>, $$OP=$$OP!4
	.ENDR
	.BYTE	$$OP
	.IF EQ	$$OP
	.BYTE	EXP
	.IFF
	.IF EQ $$OP-1
	.WORD	EXP
	.IFF
	.IF EQ $$OP-2
	.WORD	EXP-DPT$TAB
	.IFF
	.LONG	EXP
	.IIF NB,POS, .BYTE POS
	.IIF NB,SIZE, .BYTE SIZE
	.ENDC
	.ENDC
	.ENDC
	

	.ENDC
	.ENDC
	.ENDC


	.ENDM	DPT_STORE
;
; DISABLE INTERRUPTS
;
; DSBINT IPL,DST
;
 
	.MACRO	DSBINT IPL,DST
		.IF B	DST
		MFPR	S^#PR$_IPL,-(SP)
		.IFF
		MFPR	S^#PR$_IPL,DST
		.ENDC
		.IF B	IPL
		MTPR	#31,S^#PR$_IPL
		.IFF
		MTPR	IPL,S^#PR$_IPL
		.ENDC
	.ENDM	DSBINT
 
;
; ENABLE INTERRUPTS
;
; ENBINT SRC
;
 
	.MACRO	ENBINT SRC
		.IF B	SRC
		MTPR	(SP)+,S^#PR$_IPL
		.IFF
		MTPR	SRC,S^#PR$_IPL
		.ENDC
	.ENDM	ENBINT
 
;
; MACRO TO DEFINE SOME OF THE ERROR MESSAGE BUFFER OFFSET VALUES
;
; CALL:		$EMBDEF	<LIST>
;
; WHERE:	LIST IS A SERIES OF 2 CHARACTER CODES FOR THE TYPE
;		OF ERROR MESSAGES THE OFFSETS ARE DESIRED
;
;	EG:	$EMBDEF	<BC,CR,DV>
;
;	WOULD DEFINE CODES FOR BUGCHECK,CRASH, AND DEVICE ERROR MESSAGES.
;
 
	.MACRO	$EMBDEF	LIST=<DV,TS>
	$EMBETDEF			; DEFINE ENTRY TYPE CODES
	$EMBHDDEF			; DEFINE HEADER OFFSETS
	.IRP	Z,<LIST>
	$EMB'Z'DEF
	.ENDR
	.ENDM	$EMBDEF
 
;
; FUNCTION TABLE ENTRY MACRO
;
; FUNCTAB ACTION ROUTINE,FUNCTION CODES
;
; NULL ACTION ROUTINE DOES NOT EXPAND A ACTION ADDRESS
;
	.MACRO	FUNCTAB ACTION,CODES
MASKL	= 0
MASKH	= 0
	.IF NOT_DEFINED FUNCTAB_LEN
FUNCTAB_LEN = 0
	.ENDC

	.IRP	X,<CODES>
	.IF	GT	<IO$_'X&IO$_VIRTUAL>-31
MASKH	= MASKH!<1@<<IO$_'X&IO$_VIRTUAL>-32>>
	.IFF
MASKL	= MASKL!<1@<IO$_'X&IO$_VIRTUAL>>
	.ENDC
	.ENDM
	.LONG	MASKL,MASKH
	FUNCTAB_LEN = FUNCTAB_LEN + 8
	.IF NB ACTION
	GENRADDR ACTION,<.+8>
	FUNCTAB_LEN = FUNCTAB_LEN + 4
	.ENDC
	.ENDM
 
;
; GENERATE RELATIVE ADDRESS FOR DRIVER DISPATCH AND FUNCTION DECISION TABLES
;
; GENRADDR ADDRESS,BASE
;
 
	.MACRO	GENRADDR ADDRESS,BASE
	.IF IDN	<ADDRESS>,<0>
	.LONG	IOC$RETURN
	.IFF
	.IRPC	X,<ADDRESS>
	.IF IDN	<X>,<+>
	.LONG	ADDRESS
	.IFF
	.LONG	ADDRESS-BASE
	.ENDC
	.MEXIT
	.ENDM
	.ENDC
	.ENDM	GENRADDR
 
; 
; TEST IF PROCESS HAS SPECIFIED PRIVILEGE AND BRANCH ON TRUE
;
; IFPRIV PRIV,DEST,PCBREG
;
 
	.MACRO	IFPRIV PRIV,DEST,PCBREG=R4
		.IF DIF	<PRIV>,<R1>
		.IF DIF <PRIV>,<R2>
		BBS	#PRV$V_'PRIV,@PCB$L_PHD(PCBREG),DEST
		.IFF
		BBS	PRIV,@PCB$L_PHD(PCBREG),DEST
		.ENDC
		.IFF
		BBS	PRIV,@PCB$L_PHD(PCBREG),DEST
		.ENDC
	.ENDM	IFPRIV

;
; TEST IF PROCESS DOES NOT HAVE PRIVILEGE AND BRANCH ON TRUE
;
; IFNPRIV PRIV,DEST,PCBREG
;
 
	.MACRO	IFNPRIV PRIV,DEST,PCBREG=R4
		.IF DIF	<PRIV>,<R1>
		.IF DIF	<PRIV>,<R2>
		BBC	#PRV$V_'PRIV,@PCB$L_PHD(PCBREG),DEST
		.IFF
		BBC	PRIV,@PCB$L_PHD(PCBREG),DEST
		.ENDC
		.IFF
		BBC	PRIV,@PCB$L_PHD(PCBREG),DEST
		.ENDC
	.ENDM	IFNPRIV

;
; BRANCH IF RANGE OF ADDRESSES IS NOT READABLE
;
; IFNORD SIZ,ADR,DEST,MODE
;

	.MACRO	IFNORD	SIZ,ADR,DEST,MODE=#0
		PROBER	MODE,SIZ,ADR
		BEQL	DEST
	.ENDM	IFNORD

;
; BRANCH IF RANGE OF ADDRESSES IS READABLE
;
; IFRD SIZ,ADR,DEST,MODE
;
 
	.MACRO	IFRD SIZ,ADR,DEST,MODE=#0
		PROBER	MODE,SIZ,ADR
		BNEQ	DEST
	.ENDM	IFRD
 
;
; BRANCH IF RANGE OF ADDRESSES IS NOT WRITABLE
;
; IFNOWRT SIZ,ADR,DEST,MODE
;

	.MACRO	IFNOWRT	SIZ,ADR,DEST,MODE=#0
		PROBEW	MODE,SIZ,ADR
		BEQL	DEST
	.ENDM	IFNOWRT

;
; BRANCH IF RANGE OF ADDRESS IS WRITABLE
;
; IFWRT SIZ,ADR,DEST,MODE
;

	.MACRO	IFWRT	SIZ,ADR,DEST,MODE=#0
		PROBEW	MODE,SIZ,ADR
		BNEQ	DEST
	.ENDM	IFWRT

;
; CREATE I/O DRIVER FORK PROCESS
;
; IOFORK
;
 
	.MACRO	IOFORK
		JSB	G^EXE$IOFORK
	.ENDM	IOFORK
 
;
; CREATE FORK PROCESS
;
; FORK
;
  
	.MACRO	FORK
		JSB	G^EXE$FORK
	.ENDM	FORK
  
;
; INVALIDATE TRANSLATION BUFFER
;
; INVALID ADDR,REG
;
 
	.MACRO	INVALID ADDR,REG
		.IF B	ADDR
		MTPR	#0,S^#PR$_TBIA
		.IFF
		.IF B	REG
		MTPR	ADDR,S^#PR$_TBIS
		.IFF
		MOVL	ADDR,REG
		MTPR	REG,S^#PR$_TBIS
		.ENDC
		.ENDC
	.ENDM	INVALID

;
; LOAD P0 SPACE LENGTH REGISTER
;
	.MACRO	LDP0LR	SRC
	.LIST	MEB
		MTPR	SRC,S^#PR$_P0LR
	.NLIST	MEB
	.ENDM	LDP0LR
;
; LOAD P1 SPACE LENGTH REGISTER
;
	.MACRO	LDP1LR	SRC
	.LIST	MEB
		MTPR	SRC,S^#PR$_P1LR
	.NLIST	MEB
	.ENDM	LDP1LR
 
;
; LOAD MBA MAP REGISTERS
;
 
	.MACRO	LOADMBA
		JSB	G^IOC$LOADMBAMAP
	.ENDM	LOADMBA
 
;
; LOAD UBA MAP REGISTERS
;
 
	.MACRO	LOADUBA
		JSB	G^IOC$LOADUBAMAP
	.ENDM	LOADUBA
 
;
; LOAD UBA MAP REGISTERS - ALTERNATE ENTRY POINT
;
 
	.MACRO	LOADUBAA
	JSB	G^IOC$LOADUBAMAPA
	.ENDM	LOADUBAA

;+
; LOCK - MACRO TO SET A LOCK BIT AND RETRY IF SET FAILS
;
; INPUTS:
;
;	FLAG = BIT POSITION TO SET
;	FIELD = BASE OF FIELD IN WHICH FLAG IS TO BE SET
;
; OUTPUTS:
;
;	R0 = SUCCESS IF FLAG CHANGED FROM CLEAR TO SET STATE IN
;	     EXE$GL_LOCKRTRY RETRIES.
;	   = FAILURE IF RETRIES EXCEEDED BEFORE FLAG'S STATE COULD
;	     BE CHANGED.
;
;	IF SUCCESS:
;
;	(SP) = PREVIOUS IPL AND CURRENT IPL = 31.
;
;-
	.MACRO	LOCK	FLAG,FIELD,?EXIT,?ERROR,?TRY
	MOVL	G^EXE$GL_LOCKRTRY,R0
TRY:	DSBINT
	BBSSI	FLAG,FIELD,ERROR
	MOVL	#1,R0
	BRB	EXIT
ERROR:	ENBINT
	SOBGTR	R0,TRY
EXIT:
	.ENDM	LOCK


;+
; UNLOCK - MACRO TO CLEAR A LOCK BIT
;
; INPUTS:
;
;	FLAG = BIT POSITION TO CLEAR
;	FIELD = BASE OF FIELD IN WHICH FLAG IS TO BE CLEAR
;
;	(SP) = PREVIOUS IPL
;
; OUTPUTS:
;
;	FLAG CLEARED AND PREVIOUS IPL RESTORED.
;-
	.MACRO	UNLOCK	FLAG,FIELD,?EXIT

	BBCCI	FLAG,FIELD,EXIT
EXIT:
	ENBINT
	.ENDM	UNLOCK

 

;	.MACRO	PFN_DISP_IF_BIGPFN_THEN
;	.MACRO	PFN_DISP_ELSE
;	.MACRO	PFN_DISP_ENDIF
;
;  The following three macros provide a transparent method of making an
;  execution time decision on which code path to execute, depending on
;  the size of physical memory. This series of macros is provided for
;  the case where more than one instruction depends on physical memory
;  size. A single instruction that differs in more than the choice of
;  opcode must also use this macro. When a single instruction that differs 
;  only in its opcode is the issue, the PFN_REFERENCE macro should be used.
;
;  The actual logical construction is as follows
;
;	PFN_DISP_IF_BIGPFN_THEN	; IF FLINK and BLINK are longword arrays THEN
;		block of code with longword references
;	PFN_DISP_ELSE		; ELSE (if FLINK and BLINK are word arrays)
;		block of code with word references (This block is optional.)
;	PFN_DISP_ENDIF
;
;  These macros are currently implemented with byte branch displacements for
;  both the THEN and ELSE pieces. If necessary, the macros could be enhanced
;  to generate the correct branches when word displacements are required.

;	.MACRO	PFN_DISP_IF_BIGPFN_THEN 	END_BIGPFN_CODE,MODE
;
;  The first argument to the PFN_DISP_IF_BIGPFN_THEN macro is the label
;  of the end of the block of code that executes in the event that more
;  than 32 Mbytes of physical memory is present (which implies that FLINK
;  and BLINK are longword arrays). This label may either locate a block
;  of code that executes in the event that the FLINK and BLINK arrays are
;  word arrays (IF-THEN-ELSE construction) or it may locate the end of code 
;  that depends on the size of the PFN link arrays (IF-THEN construction).
;  
;  The second argument allows an addressing mode other than general (G^)
;  to be selected in special cases where the linker's default selection
;  would be incorrect. 

	.MACRO	PFN_DISP_IF_BIGPFN_THEN		END_BIGPFN_CODE=10$,MODE=G^
	TSTW	MODE'MMG$GW_BIGPFN
	BEQL	END_BIGPFN_CODE
.SHOW
	;This code executes if the PFN link arrays are longword arrays.;
.NOSHOW
	.ENDM	PFN_DISP_IF_BIGPFN_THEN

;  The code that executes for large physical memory sizes is sandwiched
;  between the PFN_DISP_IF_BIGPFN_THEN macro and either a PFN_DISP_ELSE
;  macro or a PFN_DISP_ENDIF macro. This is the "then" half of the
;  conditional and contains longword references to the FLINK and BLINK
;  arrays. 

;	.MACRO	PFN_DISP_ELSE	ELSE_CODE,COMMON_CODE
;
;  There are two parameters for this macro. The first parameter is the label
;  where the word code begins. The second parameter is the label where
;  PFN-dependent code ends and common code begins.

	.MACRO	PFN_DISP_ELSE	ELSE_CODE=10$,COMMON_CODE=20$
	BRB	COMMON_CODE
.SHOW
	;This code executes if the PFN link arrays are word arrays.;
.NOSHOW
ELSE_CODE':
	.ENDM	PFN_DISP_ELSE

;  The code that executes for small physical memory sizes is sandwiched between
;  the PFN_DISP_ELSE and PFN_DISP_ENDIF macros. This is the "else" half of the
;  conditional and contains word references to the FLINK and BLINK arrays.

;	.MACRO	PFN_DISP_ENDIF	COMMON_CODE
;
;  The single parameter for this macro is the label where the two code
;  paths rejoin into a single code path. Note that the default parameters
;  to this series of macros assumes an IF-THEN-ELSE construction. If an
;  IF_THEN construction is used, an explicit parameter must be supplied
;  to the PFN_DISP_ELSE macro. 

	.MACRO	PFN_DISP_ENDIF	COMMON_CODE=20$
.SHOW
	;End of code that depends on size of PFN link arrays;
.NOSHOW
COMMON_CODE':
	.ENDM	PFN_DISP_ENDIF
;	.MACRO	PFN_REFERENCE
;
;  The PFN_REFERENCE macro replaces all single instruction references
;  to the PFN array elements whose size depends on physical memory size.
;  These arrays are
;
;	FLINK	Forward Link Array
;	BLINK	Backward Link Array
;	SHRCNT	Global Share Count Array (Overlays FLINK)
;	WSLX	Working Set List Index Array (Overlays BLINK)
;
;  The macro records the address of each such instruction, as well as the
;  opcode that must be used in the event that there is more than 32 Mbytes
;  of physical memory present. As a precautionary measure, a third table
;  contains the original opcode to allow verification while the fixups
;  are taking place. The address and opcode tables are used by
;  INIT to do bootstrap-time fixups in the event that there is more than
;  32 Mbytes present. If INIT detects that there is less than 32 Mbytes
;  present, nothing is done. That is, the default case is a system with
;  less than 32 Mbytes, with the relevant PFN array elements as words.
;
;  Note that opcode fixups can only be done on the nonpaged portion of
;  SYS.EXE. To allow for opcode selection in other places,
;  the macro also provides for an execution time decision in the event that
;  the instruction cannot be fixed up by INIT. This kind of decision must
;  be used by:
;
;	o  paged executive routines
;
;	o  dynamically loaded code (such as machine check handlers)
;
;	o  any external routine or image (including XDELTA)
;
;  The macro also provides for two-byte opcode because they are so easy
;  to include. This avoids lots of problems in the event that two-byte
;  opcodes are used by memory management in the future.
;
;  Parameters:
;
;	WORD_OPCODE	Opcode for word reference (inserted into SYS.EXE)
;
;	OPERANDS	Operands of instruction
;
;	LONG_OPCODE	Opcode for longword reference (stored in table)
;
;	IMAGE		Set to "SYS_NONPAGED" if INIT does opcode fixup. This 
;			setting should only be selected for references in the 
;			nonpaged portion of SYS.EXE.
;
;	MODE		Defaults to G^. This parameter can be set to @# or
;			to L^ when the linker's default selection for G^
;			addressing would be inappropriate, such as in
;			module SHELL.
;
;	OPCODE_SIZE	Set to "TWO_BYTE" for two-byte opcode
;			(The two-byte material in the macro is currently
;			 commented out because there is no example of a
;			 two-byte opcode reference to the PFN data base.)
;
	.MACRO	PFN_REFERENCE	-
		WORD_OPCODE,-
		OPERANDS,-
		LONG_OPCODE,-
		IMAGE=NOSYS,-
		MODE=G^,-
		OPCODE_SIZE=ONE_BYTE,-
		?L_10$,?L_20$

	.IF	IDENTICAL	<IMAGE>,<SYS_NONPAGED>

	$OPDEF
	...PFN = .
	.SAVE_PSECT	LOCAL_BLOCK
	.PSECT	Z$INIT$PFN_FIXUP_TABLE
;		.IF	IDENTICAL	<OPCODE_SIZE>,<TWO_BYTE>
;		.ADDRESS	...PFN	; Store data about low byte of opcode
;		.BYTE	<OP$_'WORD_OPCODE>&^X00FF
;		.BYTE	<OP$_'LONG_OPCODE>&^X00FF
;		.ADDRESS	...PFN + 1 ; Store data about high byte of opcode
;		.BYTE	<<OP$_'WORD_OPCODE>&^XFF00>@-8
;		.BYTE	<<OP$_'LONG_OPCODE>&^XFF00>@-8
;		.IF_FALSE
	.ADDRESS	...PFN
	.BYTE	OP$_'WORD_OPCODE
	.BYTE	OP$_'LONG_OPCODE
;		.ENDC
	.RESTORE_PSECT
	WORD_OPCODE	OPERANDS

	.IF_FALSE

	TSTW		MODE'MMG$GW_BIGPFN	
	BNEQU		L_10$
	WORD_OPCODE	OPERANDS
	BRB		L_20$
L_10$:	LONG_OPCODE	OPERANDS
L_20$:

	.ENDC

	.ENDM	PFN_REFERENCE
;
; PURGE DATA PATH
;

	.MACRO	PURDPR
		JSB	G^IOC$PURGDATAP
	.ENDM	PURDPR

;+
; QRETRY - EXECUTE AN INTERLOCKED QUEUE INSTRUCTION AND RETRY IF FAILURE
;
; INPUTS:
;
;	OPCODE = OPCODE NAME: INSQHI,INSQTI,REMQHI,REMQTI.
;	OPERAND1 = FIRST OPERAND FOR OPCODE.
;	OPERAND2 = SECOND OPERAND FOR OPCODE.
;	SUCCESS = LABEL TO BRANCH TO IF OPERATION SUCCEEDS.
;		IF NOT SPECIFIED, MACRO JUST FALLS THRU.
;	ERROR = LABEL TO BRANCH TO IF OPERATION FAILS.
;		IF NOT SPECIFIED, MACRO JUST FALLS THRU.
;
; OUTPUTS:
;
;	R0 = DESTROYED.
;
;	C-BIT = CLEAR IF OPERATION SUCCEEDED.
;		SET IF OPERATION FAILED - QUEUE LOCKED.
;		(MUST BE CHECKED BEFORE V-BIT OR Z-BIT)
;
;	REMQTI OR REMQHI -
;
;		V-BIT = CLEAR IF AN ENTRY REMOVED FROM QUEUE.
;			SET IF NO ENTRY REMOVED FROM QUEUE.
;
;	INSQTI OR INSQHI -
;
;		Z-BIT = CLEAR IF ENTRY IS NOT FIRST IN QUEUE.
;			SET IF ENTRY IS FIRST IN QUEUE.
;-
	.MACRO	QRETRY	OPCODE,OPERAND1,OPERAND2,SUCCESS,ERROR,?LOOP,?OK
	CLRL	R0
LOOP:
	OPCODE	OPERAND1,OPERAND2
	.IF NB SUCCESS
	BCC	SUCCESS
	.IFF
	BCC	OK
	.ENDC
	AOBLSS	G^EXE$GL_LOCKRTRY,R0,LOOP
	.IF NB ERROR
	BRB	ERROR
	.ENDC
OK:
	.ENDM	QRETRY


;
; I/O REQUEST COMPLETE
;
; REQCOM
;
 
	.MACRO	REQCOM
		JMP	G^IOC$REQCOM
	.ENDM	REQCOM
 
;
; RELEASE ALL CHANNELS
;
; RELCHAN
;
 
	.MACRO	RELCHAN
		JSB	G^IOC$RELCHAN
	.ENDM	RELCHAN
 
;
; RELEASE SECONDARY CHANNEL
;
; RELSCHAN
;
 
	.MACRO	RELSCHAN
		JSB	G^IOC$RELSCHAN
	.ENDM	RELSCHAN
 
;
; RELEASE UNIBUS DATAPATH
;
 
	.MACRO	RELDPR
		JSB	G^IOC$RELDATAP
	.ENDM	RELDPR
 
;
; RELEASE UNIBUS MAP REGISTERS
;
 
	.MACRO	RELMPR
		JSB	G^IOC$RELMAPREG
	.ENDM	RELMPR
 
;
; REQUEST PRIMARY CHANNEL
;
; REQPCHAN PRI
;
 
	.MACRO	REQPCHAN PRI
		.IF NB	PRI
		.IF IDN	<HIGH>,<PRI>
		JSB	G^IOC$REQPCHANH
		.IFF
		JSB	G^IOC$REQPCHANL
		.ENDC
		.IFF
		JSB	G^IOC$REQPCHANL
		.ENDC
	.ENDM	REQPCHAN
 
;
; REQUEST SECONDARY CHANNEL
;
; REQSCHAN PRI
;
 
	.MACRO	REQSCHAN PRI
		.IF NB	PRI
		.IF IDN	<HIGH>,<PRI>
		JSB	G^IOC$REQSCHANH
		.IFF
		JSB	G^IOC$REQSCHANL
		.ENDC
		.IFF
		JSB	G^IOC$REQSCHANL
		.ENDC
	.ENDM	REQSCHAN
 
;
; REQUEST UNIBUS DATAPATH
;
 
	.MACRO	REQDPR
		JSB	G^IOC$REQDATAP
	.ENDM	REQDPR
 
;
; REQUEST UNIBUS MAP REGISTERS
;
 
	.MACRO	REQMPR
		JSB	G^IOC$REQMAPREG
	.ENDM	REQMPR
 
;
; REPORT SYSTEM EVENT
;
; RPTEVT EVENTNAME
;

	.MACRO	RPTEVT,NAME,CALL_TYPE=BSB
		.IF	IDENTICAL	<CALL_TYPE>,<JSB>
		JSB	G^SCH$RSE
		.IF_FALSE
		BSBW	SCH$RSE
		.ENDC
		.BYTE	EVT$_'NAME
	.ENDM	RPTEVT

; SAVE PROCESSOR INTERRUPT PRIORITY LEVEL
;
; SAVIPL DST
;

	.MACRO	SAVIPL	DST=-(SP)
		MFPR	S^#PR$_IPL,DST
	.ENDM	SAVIPL
;
; SET PROCESSOR INTERRUPT PRIORITY LEVEL
;
; SETIPL IPL
;
 
	.MACRO	SETIPL IPL
		.IF NB	IPL
		MTPR	IPL,S^#PR$_IPL
		.IFF
		MTPR	#31,S^#PR$_IPL
		.ENDC
	.ENDM	SETIPL
 
;
; INITIATE SOFTWARE INTERRUPT
;
; SOFTINT IPL
;
 
	.MACRO	SOFTINT IPL
		MTPR	IPL,S^#PR$_SIRR
	.ENDM	SOFTINT
;++
; Macro to wait for a specific bit to become set/clear within a
; specified interval of time.  Uses a processor specific value
; established by system bootstrap to determine an approximate interval
; of time to wait instead of reading the processor clock.
;
; INPUTS:
;
;	TIME - the number of 10 micro-second intervals to wait
;	BITVAL - value of the bit(s) to test, i.e., the operand
;		 specifier of the mask for a BITx instruction
;	SOURCE - the source operand specifier of the location to test
;	CONTEXT - either a "B", "W", or "L" specifying the context of
;		  the reference to the source operand
;	SENSE - whether to test fo the bit to be set or for it to be
;		cleared.  Devault (blank) is for set.  Else, specify
;		".TRUE." or ".FALSE."
;
; OUTPUTS:
;
;	R0 - indicates success of failure status.  Success is defined as
;		the bit being at the specified sense within the specified
;		time interval.
;	R1 - destroyed, all other registers preserved.
;
;--

	.MACRO	TIMEWAIT TIME,BITVAL,SOURCE,CONTEXT,SENSE,?L1,?L2

	MOVZWL	#SS$_NORMAL,R0		; Assume success
	MULL3	TIME,G^EXE$GL_TENUSEC,R1; Calculate the time interval count
L1:	BIT'CONTEXT'	BITVAL,SOURCE	; Test the bit

	.IF BLANK SENSE
	BNEQ	L2			; Conditionally branch on sense
	.IF_FALSE
	.IF IDENTICAL SENSE .TRUE.
	BNEQ	L2
	.IF_FALSE
	BEQL	L2
	.ENDC
	.ENDC

	.BYTE	1[8]			; NOP's to slow loop enough to allow
					; UNIBUS DMA to occur when testing a
					; device register
	SOBGTR	R1,L1			; Decrement interval count
	CLRL	R0			; Count expired, return failure
L2:
	.ENDM

; WAITFOR INTERRUPT OR TIMEOUT AND KEEP CHANNEL
;
; WFIKPCH EXCPT,TIME
;
 
	.MACRO	WFIKPCH EXCPT,TIME
		.IF B	TIME
		ASHL	#16,#1,-(SP)
		.IFF
		PUSHL	TIME
		.ENDC
		JSB	G^IOC$WFIKPCH
		.WORD	EXCPT-.
	.ENDM	WFIKPCH
 
;
; WAITFOR INTERRUPT OR TIMEOUT AND RELEASE CHANNEL
;
; WFIRLCH EXCPT,TIME
;
 
	.MACRO	WFIRLCH EXCPT,TIME
		.IF B	TIME
		ASHL	#16,#1,-(SP)
		.IFF
		PUSHL	TIME
		.ENDC
		JSB	G^IOC$WFIRLCH
		.WORD	EXCPT-.
	.ENDM	WFIRLCH
;
; System Communications Services (SCS) Macros
;
; ACCEPT - Accept a connection request
;
	.MACRO	ACCEPT,	MSGADR=0,DGADR=0,ERRADR,INITCR=#0,MINSCR=#0, -
			INITDG=#0,BLKPRI=#0,CONDAT=0,AUXSTR=0,BADRSP=0,?RETADR
		PUSHAB	B^RETADR
	$PUSHADR BADRSP
	$PUSHADR AUXSTR
	$PUSHADR CONDAT
		MOVZBW	BLKPRI,-(SP)
		MOVW	INITDG,-(SP)
		MOVW	MINSCR,-(SP)
		MOVW	INITCR,-(SP)
	.IF B ERRADR
		.ERROR	99	; Error address parameter is required ;
	.IFF
		PUSHAB	ERRADR
	.ENDC
	$PUSHADR DGADR
	$PUSHADR MSGADR
		.GLOBAL   SCS$ACCEPT
		JMP	G^SCS$ACCEPT
RETADR:
	.ENDM	ACCEPT
;
; ALLOC_DG_BUF - Allocate a datagram buffer
;
	.MACRO	ALLOC_DG_BUF
		JSB	@PDT$L_ALLOCDG(R4)
	.ENDM	ALLOC_DG_BUF
;
; ALLOC_MSG_BUF - Allocate a message  buffer
;
	.MACRO	ALLOC_MSG_BUF
		JSB	@PDT$L_ALLOCMSG(R4)
	.ENDM	ALLOC_MSG_BUF
;
; ALLOC_RSPID - Allocate a response id
;
	.MACRO	ALLOC_RSPID
		.EXTERNAL SCS$ALLOC_RSPID
		JSB	G^SCS$ALLOC_RSPID
	.ENDM	ALLOC_RSPID
;
; CONFIG_PTH - Get path configuration information
;
	.MACRO	CONFIG_PTH,STAADR=0,OUTBUF=0
	$MOVEADR STAADR, R1
	$MOVEADR OUTBUF, R2
		.EXTERNAL SCS$CONFIG_PTH
		JSB	G^SCS$CONFIG_PTH
	.ENDM	CONFIG_PTH
;
; CONFIG_SYS - Get system configuration information
;
	.MACRO	CONFIG_SYS,SYSADR=0,OUTBUF=0
	$MOVEADR SYSADR, R1
	$MOVEADR OUTBUF, R2
		.EXTERNAL SCS$CONFIG_SYS
		JSB	G^SCS$CONFIG_SYS
	.ENDM	CONFIG_SYS
;
; CONNECT - Initiate a virtual circuit conncection
;
	.MACRO	CONNECT,MSGADR=0,DGADR=0,ERRADR,RSYSID=0,RSTADR=0,-
		RPRNAM=0,LPRNAM=0,INITCR=#0,MINSCR=#0,INITDG=#0,-
		BLKPRI=#0,CONDAT=0,AUXSTR=0,BADRSP=0,?RETADR
		PUSHAB	B^RETADR
	$PUSHADR BADRSP
	$PUSHADR AUXSTR
	$PUSHADR CONDAT
		MOVZBW	BLKPRI,-(SP)
		MOVW	INITDG,-(SP)
		MOVW	MINSCR,-(SP)
		MOVW	INITCR,-(SP)
	$PUSHADR LPRNAM
	$PUSHADR RPRNAM
	$PUSHADR RSTADR
	$PUSHADR RSYSID
	.IF B ERRADR
		.ERROR	99	; Error address parameter is required ;
	.IFF
		PUSHAB	ERRADR
	.ENDC
	$PUSHADR DGADR
	$PUSHADR MSGADR
		.EXTERNAL SCS$CONNECT
		JMP	G^SCS$CONNECT
RETADR:
	.ENDM	CONNECT
;
; DEALLOC_DG_BUF - Deallocate a datagram buffer
;
	.MACRO	DEALLOC_DG_BUF
		JSB	@PDT$L_DEALLOCDG(R4)
	.ENDM	DEALLOC_DG_BUF
;
; DEALLOC_MSG_BUF - Deallocate a message  buffer
;
	.MACRO	DEALLOC_MSG_BUF
		JSB	@PDT$L_DEALLOMSG(R4)
	.ENDM	DEALLOC_MSG_BUF
;
; DEALLOC_MSG_BUF_REG - Deallocate a message buffer
;
	.MACRO	DEALLOC_MSG_BUF_REG
		JSB	@PDT$L_DEALRGMSG(R4)
	.ENDM	DEALLOC_MSG_BUF_REG
;
; DEALLOC_RSPID - Deallocate a response id
;
	.MACRO	DEALLOC_RSPID
		.EXTERNAL SCS$DEALL_RSPID
		JSB	G^SCS$DEALL_RSPID
	.ENDM	DEALLOC_RSPID
;
; DISCONNECT - Break a virtual circuit
;
	.MACRO	DISCONNECT,DISTYP
	 .IF NB DISTYP
		MOVL	DISTYP, R0
	 .ENDC
		.EXTERNAL SCS$DISCONNECT
		JSB	G^SCS$DISCONNECT
	.ENDM	DISCONNECT
;
; LISTEN - Listen for incoming CONNECT requests
;
	.MACRO	LISTEN,MSGADR=0,ERRADR,LPRNAM=0,PRINFO=0,?RETADR  
		PUSHAB	B^RETADR
	$PUSHADR PRINFO
	$PUSHADR LPRNAM
	.IF B ERRADR
		.ERROR	99	; Error address parameter is required ;
	.IFF
		PUSHAB	ERRADR
	.ENDC
	$PUSHADR MSGADR
		.GLOBAL   SCS$LISTEN
		JMP	G^SCS$LISTEN
RETADR:
	.ENDM	LISTEN
;
; MAP - Map a buffer for block transfer
;
	.MACRO	MAP
		JSB	@PDT$L_MAP(R4)
	.ENDM	MAP
;
; MAP_BYPASS - Map a buffer for block transfer and bypass
;
	.MACRO	MAP_BYPASS
		JSB	@PDT$L_MAPBYPASS(R4)
	.ENDM	MAP_BYPASS
;
; MAP_IRP - Map a buffer for block transfer, extract
;
	.MACRO	MAP_IRP
		JSB	@PDT$L_MAPIRP(R4)
	.ENDM	MAP_IRP
;
; MAP_IRP_BYPASS - Map a buffer for block transfer, extract
;
	.MACRO	MAP_IRP_BYPASS
		JSB	@PDT$L_MAPIRPBYP(R4)
	.ENDM	MAP_IRP_BYPASS
;
; MRESET - Reset remote port and system
;
	.MACRO	MRESET,RSTADR,FLAG=#0
		MOVL	FLAG,R0
	$MOVEADR RSTADR,R1
		JSB	@PDT$L_MRESET(R4)
	.ENDM	MRESET
;
; MSTART - Start remote port and system
;
	.MACRO	MSTART,RSTADR,FLAG=#1,START=#0
		MOVL	FLAG,R0
	$MOVEADR RSTADR,R1
		MOVL	START,R2
		JSB	@PDT$L_MSTART(R4)
	.ENDM	MSTART
;
; QUEUE_MULT_DGS - Add or subtract buffers from the datagram
;
	.MACRO	QUEUE_MULT_DGS,NUMBUF
	 .IF NB NUMBUF
		MOVL	NUMBUF, R1
	 .ENDC
		JSB	@PDT$L_QUEUEMDGS(R4)
	.ENDM	QUEUE_MULT_DGS
;
; QUEUE_DG_BUF - Queue a datagram buffer for receive
;
	.MACRO	QUEUE_DG_BUF
		JSB	@PDT$L_QUEUEDG(R4)
	.ENDM	QUEUE_DG_BUF
;
; READ_COUNTERS - Read and initialize port counters
;
	.MACRO	READ_COUNTERS,RSTADR=0,LPRNAM
	$MOVEADR RSTADR,R0
	$MOVEADR LPRNAM,R1
		JSB	@PDT$L_READCOUNT(R4)
	.ENDM	READ_COUNTERS
;
; RECYCL_MSG_BUF - Recycle a message buffer,low
;
	.MACRO	RECYCL_MSG_BUF
		JSB	@PDT$L_RCLMSGBUF(R4)
	.ENDM	RECYCL_MSG_BUF
;
; RECYCH_MSG_BUF - Recycle a message buffer,high
;
	.MACRO	RECYCH_MSG_BUF
		JSB	@PDT$L_RCHMSGBUF(R4)
	.ENDM	RECYCH_MSG_BUF
;
; REJECT - Reject a connection request
;
	.MACRO	REJECT,REJTYP
	 .IF NB REJTYP
		MOVL	REJTYP, R0
	 .ENDC
		JSB	@PDT$L_REJECT(R4)
	.ENDM	REJECT
;
; REQUEST_DATA - Request block transfer data
;
	.MACRO	REQUEST_DATA
		.EXTERNAL SCS$ALLOC_RSPID
		JSB	G^SCS$ALLOC_RSPID
		JSB	@PDT$L_ALLOCMSG(R4)
		JSB	@PDT$L_REQDATA(R4)
	.ENDM	REQUEST_DATA
;
; RLS_COUNTERS - Release counters
;
	.MACRO	RLS_COUNTERS
		JSB	@PDT$L_RLSCOUNT(R4)
	.ENDM	RLS_COUNTERS
;
; SCAN_RDT - Scan RDT for CDRP with given CDT
;
	.MACRO	SCAN_RDT,ACTION
		.EXTERNAL SCS$LKP_RDTCDRP
		MOVAB	ACTION, R0
		JSB	G^SCS$LKP_RDTCDRP
	.ENDM	SCAN_RDT
;
; SCAN_RSPID_WAIT - Scan RSPID wait queue for CDRP with given CDT
;
	.MACRO	SCAN_RSPID_WAIT,ACTION
		.EXTERNAL SCS$LKP_RDTWAIT
		MOVAB	ACTION, R0
		JSB	G^SCS$LKP_RDTWAIT
	.ENDM	SCAN_RSPID_WAIT
;
; SEND_DATA - Send block transfer data
;
	.MACRO	SEND_DATA
		.EXTERNAL SCS$ALLOC_RSPID
		JSB	G^SCS$ALLOC_RSPID
		JSB	@PDT$L_ALLOCMSG(R4)
		JSB	@PDT$L_SENDDATA(R4)
	.ENDM	SEND_DATA
;
; SEND_DG_BUF - Send a datagram
;
	.MACRO	SEND_DG_BUF,FLAG
	 .IF NB FLAG
		MOVL	FLAG, R0
	 .ENDC
	JSB	@PDT$L_SENDDG(R4)
	.ENDM	SEND_DG_BUF
;
; SEND_CNT_MSG_BUF - Send a message with byte count
;
	.MACRO	SEND_CNT_MSG_BUF
		JSB	@PDT$L_SNDCNTMSG(R4)
	.ENDM	SEND_CNT_MSG_BUF
;
; SEND_MSG_BUF - Send a message
;
	.MACRO	SEND_MSG_BUF
		JSB	@PDT$L_SENDMSG(R4)
	.ENDM	SEND_MSG_BUF
;
; UNMAP - Unmap a buffer for block transfer
;
	.MACRO	UNMAP
		JSB	@PDT$L_UNMAP(R4)
	.ENDM	UNMAP
;
; Macros for invocation of Machine Check recovery blocks
;
; $PRTCTINI - set start of recovery block
;	LABEL = end of recovery block label (must be same label as $PRTCTEND)
;	MASK = bit mask for types of errors to protect against
;
	.MACRO	$PRTCTINI LABEL,MASK
		PUSHAL	LABEL
		MOVL	MASK,R0
		JSB	G^EXE$MCHK_PRTCT
	.ENDM
 
;
; $PRTCTEND - macro for defining end of current recovery block.
;	LABEL = end of recovery block label (must be same as in $PRTCTINI)
;
	.MACRO	$PRTCTEND LABEL
		RSB
LABEL:
	.ENDM
;
; $PRTCTEST - test to see if recovery block in effect for current error
;	ADDRESS = pointer to PC,PSL pair of error interrupt on stack
;	MASK = bits defining type of error
;
	.MACRO	$PRTCTEST ADDRESS,MASK
		MOVAL	ADDRESS,R1
		MOVL	MASK,R2
		JSB	G^EXE$MCHK_TEST
	.ENDM
 
;
; $BUGPRTCT - Macro to test whether or not recovery block in effect
;		for this BUGCHECK
;		Arguments already on current (Interrupt) stack
;
	.MACRO	$BUGPRTCT
		JSB	G^EXE$MCHK_BUGCHK
	.ENDM
 
	.LIST
