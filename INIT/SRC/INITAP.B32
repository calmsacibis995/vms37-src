MODULE INITAP (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  INIT Utility Structure Level II
!
! ABSTRACT:
!
!	THIS MODULE HANDLES INITIALIZATION OF ANSI MAGNETIC TAPE
!
! ENVIRONMENT:
!
!	VAX/VMS operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  D. H. GILLESPIE,	 CREATION DATE:  10-DEC-1977  18:10
!
! MODIFIED BY:
!
!	V02-013 DMW0018		David Michael Walp	 2-Mar-1982
!		Another correction for the volume invalid problem
!
!	V02-012 DMW0016		David Michael Walp	18-Dec-1981
!		Increase Transtable size to 256
!
!	V02-011 DMW0011		David Michael Walp	21-Aug-1981
!		Correct override typo and new Tape_Own_Prot and 
!		/LABEL for /ANSI
!
!	V02-010 DMW0010		David Michael Walp	18-Jun-1981
!		Cleaned up defaulting of density.
!
!	V02-009 DMW0009		David Michael Walp	19-May-1981
!		Placed Volume Id into the File Set Id of the 'Dummy File'
!
!	V02-008 DMW0008		David Michael Walp	1-May-1981
!		Upcased Volume label and check for illegal ( non ANSI 'a'
!		characters )
!
!       V02-007 DMW0006		David Michael Walp	25-Apr-1981
!		Created routine SET_CHARACTER ( reset parity and
!		format )
!
!       V02-006 DMW0004		David Michael Walp	9-Apr-1981
!		Added switch '/ANSI=VOLUME_ACCESSIBILITY:"x"'
!		Fixed bugs with overide switches and error returns
!		Created FORMAT_VOL1 from old and new code
!		Reformated module
!
!       V02-005 DMW0001		David Michael Walp	10-Dec-1980
!		Replace Check_Prot procedure. Old procedure was
!		confused by the fact that init was installed with sysprv
!		for version 2.
!
!	V02-004	RLRDENS		Robert L. Rappaport	8-Oct-1980
!		At the same time that /DENSITY=1 and /DENSITY=2 support
!		is being added to INITIALIZE, we correct the problem
!		of INITIALIZE returning SS$_VOLINV when the INITIALIZE
!		follows a DISMOUNT/NOUNLOAD in a command procedure.
!
!	V02-003	MCN0001		Maria del C. Nasr,	20-Jun-1980  15:10
!		Change DECFILE112 to DECFILE11A in HDR1, and eliminate binary
!		data from HDR2.  This is part of the implementation of HDR3.
!
!	V0100	ACG00001	Andrew C. Goldstein,  10-Oct-1978  21:27
!		Previous revision history moved to [INIT.SRC]INIT.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:INIDEF.B32';
REQUIRE 'LIBD$:[VMSLIB.OBJ]INITMSG.B32';

FORWARD ROUTINE
	CHECK_PROT,				! check volume protection
	DEFAULT_CHAR    : NOVALUE,		! set default characteristic
						!   of tape drive
	FORMAT_VOLOWNER	: NOVALUE,		! format volume owner field
	FORMAT_VOL1	: NOVALUE,		! format the VOL1 label
	INIT_TAPE	: NOVALUE,		! main control for tape init
	READ_VOLLABELS	: NOVALUE,		! read & verify VOL1 & HDR1 
						!  ANSI labels
	SET_DENSITY	: NOVALUE,		! set the density of the drive
	TAPE_OWN_PRO	: NOVALUE;		! determine protection and
						!  owner of tape

EXTERNAL ROUTINE
	SET_VALID,				! set volume valid bit
	CALDAYNO,				! calculate day number ( chop
						!  hour min sec from binary )
	CONVDATE_J2R,				! convert date ANSI tape JULIAN
						!  to VMS
	CONVDATE_R2J,				! convert VMS date to ANSI 
						!  JULIAN format on tape
!	WRITE_USER_UVL,				! write user volume labels
	LIB$CVT_OTB	: ADDRESSING_MODE(ABSOLUTE);

EXTERNAL
	CHANNEL,				! channel of volume 
	CTL$GQ_PROCPRIV	: REF BBLOCK ADDRESSING_MODE(ABSOLUTE),
	INIT_OPTIONS	: BITVECTOR,		! init option bits
	LABEL_STRING	: VECTOR,		! label descriptor 
	OWNER_UIC,				! value of owner switch
	PROCESS_UIC,				! process uic
	PROTECTION,				! value of protection switch
	VOL_ACC		: BYTE;			! value of label:volume switch

OWN
	ANSI_LABEL	: BBLOCK [80],		! ANSI label
	IO_STATUS	: VECTOR [4,WORD],	! I/O status 
	PRIVILEGE_MASK	: REF BBLOCK,		! process privilege mask
	VOLUME_PROT,				! protection for tape
	VOLUME_UIC,				! owner of tape
	VOL1		: BLOCK [80,BYTE]
			INITIAL(BYTE ( 'VOL1',	! VOL1 skeleton
			REP 33 OF BYTE(' '),
			'D%C',
			REP 10 OF BYTE(' '),
			'1',
			REP 28 OF BYTE(' '),
			'3')),
	
	HDR1		: BLOCK [80,BYTE]
			INITIAL (BYTE ('HDR1',	! HDR1 skeleton
			REP 23 OF BYTE (' '),
			REP 3 OF BYTE ('0'),
			'1',
			REP 7 OF BYTE ('0'),
			'100',
			REP 13 OF BYTE(' '),
			REP 6 OF BYTE('0'),
			'DECFILE11A  ',
			REP 8 OF BYTE(' '))),


	HDR2		: BLOCK[80,BYTE]
			INITIAL (BYTE('HDR2',	! HDR2 skeleton
			'F',
			REP 10 OF BYTE('0'),
			REP 35 OF BYTE(' '),
			'00',
			REP 28 OF BYTE(' ')));


GLOBAL ROUTINE INIT_TAPE : NOVALUE  =

!++
!
! FUNCTIONAL DESCRIPTION:
!	This routine is the main control for tape initialization.  If the
!	current tape is a valid files_11 ANSI tape, then the user must have
!	write privileges or be the owner of the tape.  If the first file has
! 	not expired, then the user must specify override expiration date and
!	have the privilege to do so.   On new tapes the user must specify 
!	to overide both the expiration date and accessilibity char in VOL1
!	and HDR1 and have VOLPRO priv to avoid the run away tape condition.
!
! CALLING SEQUENCE:
!	INIT_TAPE()
!
! INPUT PARAMETERS:
!	none
!
! IMPLICIT INPUTS:
!	CLI parser database
!
! OUTPUT PARAMETERS:
!	none
!
! IMPLICIT OUTPUTS:
!	FILES-11 structure level II ansi magnetic tape initialized
!
! ROUTINE VALUE:
!	none
!
! SIDE EFFECTS:
!	none
!
! USER ERRORS:
!	none
!
!--

BEGIN

LOCAL
	DESCR		: VECTOR [2],		! descriptor
	STATUS,					! system service status
	TODAY		: VECTOR [12,BYTE];	! buffer for today's date

EXTERNAL ROUTINE
	SET_VALID,				! set software volume valid
	CLEAR_VALID;				! clear software volume valid

BIND
	SECONDS = UPLIT (-10000000,-1);		! 1 second in 100 nsec units

!
! Here we have inserted a single QIO (IO$_REWIND) which apparently is not
!  needed but which in fact is here to take care of an anomaly that
!  sometimes occurs when the INITIALIZE command appears in a command file
!  immediately following a DISMOUNT/NOUNLOAD command.
!
!  Under certain circumstances the INITIALIZE fails with a SS$_VOLINV status.
!  The problem is due to a complicated interaction involving QIO dispatching
!  logic, the MAGTAPE ACP, and the INITIALIZE command.  What occurs is the
!  following.
!
!  DISMOUNT, before finishing issues a $QIOW with an I/O function code of
!  IO$_ACPCONTROL!IO$M_DMOUNT.  This request is forwarded to the ACP and
!  DISMOUNT then has its image rundown.
!
!  The ACP then issues a $QIOW with a function code of IO$_REWIND!IO$M_NOWAIT,
!  while in parallel, INITIALIZE is starting up and it proceeds to set the
!  UCB$M_VALID bit in UCB$W_STS (which in this case was still on due to the
!  volume previously having been mounted) and then INITIALIZE issues its own
!  $QIOW with an IO$_REWIND funciton code.
!
!  In some instances, the ACP's REWIND QIO does not get as far as REQCOM
!  until after INITIALIZE's REWIND has been queued.  If this occurs, INIT's
!  queued REWIND is started up before the ACP actually regains control and
!  the driver has no trouble since it finds the UCB$M_VALID bit still on.
!  Unfortunately, as since as the ACP regains control, following the
!  driver's WFIKPCH, the ACP clears the UCB$M_VALID bit.  The next QIO
!  issued by INITIALIZE will fail due to the absence of the UCB$M_VALID
!  bit.
!
!  The solution (pronounced KLUDGE) herein implemented, simply inserts an extra
!  single $QIOW with IO$_REWIND function code, surrounded by explicit
!  settings of the UCB$M_VALID bit, before the real logic of INITIALIZE begins.
!  This $QIOW allows the above potential interaction to occur, and after it is
!  finished, we again set the UCB$M_VALID bit on.
!

KERNEL_CALL(SET_VALID);

STATUS = $QIOW(
	CHAN = .CHANNEL,
	FUNC = IO$_REWIND,
	IOSB = IO_STATUS[0]);

! wait 10 seconds before giving up
!
INCR J FROM 0 TO 9 DO
    BEGIN
    KERNEL_CALL(SET_VALID);
    STATUS = $QIOW(	CHAN = .CHANNEL,
			FUNC = IO$_REWIND,
			IOSB = IO_STATUS);
    IF .STATUS THEN STATUS = .IO_STATUS[0];
    IF .STATUS NEQ SS$_MEDOFL AND .STATUS NEQ SS$_VOLINV THEN EXITLOOP;
    IF $SETIMR( DAYTIM = SECONDS, EFN = 0)
    THEN $WAITFR( EFN = 0);
    END;

! all rewind errors reported to user
!
IF NOT .STATUS THEN ERR_EXIT(.STATUS);

! set the VMS default tape drive characteristics
!
DEFAULT_CHAR();

! check user access to rewrite ( DESTROY ) the tape
!
PRIVILEGE_MASK = CTL$GQ_PROCPRIV;		! process privilege mask

IF NOT (.INIT_OPTIONS[OPT_OVR_EXP]       	! bypass all protection if
AND .INIT_OPTIONS[OPT_OVR_ACC]			! overide expiration and access
AND .PRIVILEGE_MASK[PRV$V_VOLPRO])		! characters and volpro 
						! ( new tape run away problem )
THEN
    BEGIN
    READ_VOLLABELS();				! is it an ANSI tape
    IF (
	(.INIT_OPTIONS[OPT_OVR_EXP]		! does user have privilege to
	OR .INIT_OPTIONS[OPT_OVR_ACC])		! override expiration or access
	AND NOT (.PRIVILEGE_MASK[PRV$V_VOLPRO] 	! character ( volpro priv or 
	OR .VOLUME_UIC EQL .PROCESS_UIC)	! owner of the tape )
	)
    OR
	(
	NOT KERNEL_CALL (CHECK_PROT, .VOLUME_PROT, .VOLUME_UIC)
	)					! does user have VMS write priv
    THEN ERR_EXIT(SS$_NOPRIV);
    END;

!
! format the volume label
!
FORMAT_VOL1 ();

!
! default expiration and creation dates to today's date for HDR1
!
DESCR[0] = 11;
DESCR[1] = TODAY;
$ASCTIM(TIMBUF = DESCR);
CONVDATE_R2J(TODAY,HDR1[HD1$T_CREATEDT]);
CH$MOVE(HD1$S_CREATEDT,HDR1[HD1$T_CREATEDT],HDR1[HD1$T_EXPIREDT]);

!
! write the file set id from the volume label, the MOUNT will place it
!   in the MVL and the MTAACP will use it as the FILE SET ID
! move must be done after VOL1 has been set up, because Legal ANSI 'a'
!   character check is in FORMAT_VOL1
!
CH$MOVE ( VL1$S_VOLLBL, VOL1[VL1$T_VOLLBL], HDR1[HD1$T_FILESETID] );

! rewind the tape
!
STATUS = $QIOW(
	CHAN = .CHANNEL,
	FUNC = IO$_REWIND,
	IOSB = IO_STATUS[0]);

IF .STATUS THEN STATUS = .IO_STATUS[0];	! report problems to user
IF NOT .STATUS THEN ERR_EXIT(.STATUS);

!
! set tape density if users has used /DENSITY qualifier
!
IF .INIT_OPTIONS [OPT_DENSITY] THEN SET_DENSITY ();

!
! now write VOL1 (UVL) HDR1 HDR2 ** EOF1 EOF2 **  in other words the volume 
! label and a dummy empty file ( so the label set are complete )
!
STATUS = $QIOW(
	CHAN = .CHANNEL,
	IOSB = IO_STATUS[0],
	FUNC = IO$_WRITELBLK,
	P1 = VOL1,
	P2 = 80);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);

!
! Give the user the opportunity to write the user volume labels, the first
! 3 characters of which must be 'UVL'.  They should not be longer than 80 char-
! acters
!
!STATUS = WRITE_USER_UVL();
!IF NOT .STATUS THEN ERR_EXIT(.STATUS);

STATUS = $QIOW(					! HDR1
	CHAN = .CHANNEL,
	IOSB = IO_STATUS[0],
	FUNC = IO$_WRITELBLK,
	P1 = HDR1,
	P2 = 80);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);

STATUS = $QIOW(					! HDR2
	CHAN = .CHANNEL,
	IOSB = IO_STATUS[0],
	FUNC = IO$_WRITELBLK,
	P1 = HDR2,
	P2 = 80);
IF .STATUS THEN STATUS  = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);

STATUS = $QIOW(					! Tape Mark
	CHAN = .CHANNEL,
	IOSB = IO_STATUS[0],
	FUNC = IO$_WRITEOF);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);

STATUS = $QIOW(					! Tape Mark
	CHAN = .CHANNEL,
	IOSB = IO_STATUS[0],
	FUNC = IO$_WRITEOF);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);

HDR1[HD1$L_HD1LID] = 'EOF1';			! format trailers
HDR2[HD2$L_HD2LID] = 'EOF2';

STATUS = $QIOW(					! EOF1
	CHAN = .CHANNEL,
	IOSB = IO_STATUS[0],
	FUNC = IO$_WRITELBLK,
	P1 = HDR1,
	P2 = 80);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);
STATUS = $QIOW(					! EOF2
	CHAN = .CHANNEL,
	IOSB = IO_STATUS[0],
	FUNC = IO$_WRITELBLK,
	P1 = HDR2,
	P2 = 80);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);
STATUS = $QIOW(					! Tape Mark
	CHAN = .CHANNEL,
	IOSB = IO_STATUS,
	FUNC = IO$_WRITEOF);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);
STATUS = $QIOW(					! Tape Mark
	CHAN = .CHANNEL,
	IOSB = IO_STATUS,
	FUNC = IO$_WRITEOF);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);

STATUS = $QIOW(
	CHAN = .CHANNEL,
	IOSB = IO_STATUS,
	FUNC = IO$_REWIND);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT(.STATUS);

KERNEL_CALL (CLEAR_VALID);

RETURN 1;
END;						! end of routine INIT_TAPE

ROUTINE DEFAULT_CHAR : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine sets the tape drive default characteristics.
!
! CALLING SEQUENCE:
!	DEFAULT_CHAR ();
!
! INPUT PARAMETERS:
!	NONE
!
! IMPLICIT INPUTS:
!	CHANNEL		- the I/O channel of the tape drive
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	IO_STATUS	- set to the return status of the QIO
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	NONE
!
! USER ERRORS:
!	NONE
!
!--

BEGIN

LITERAL
	ODD_PARITY	= 0;

LOCAL
	CHARACTERISTIC	: VECTOR [4,WORD],	! characteristics to set
	STATUS;

BIND
	!  Set up offsets into the characteristics buffer
	!
	FORMAT		= CHARACTERISTIC[2] : BBLOCK,
	PARITY		= CHARACTERISTIC[2] : BBLOCK,
	BUFFER_SIZE	= CHARACTERISTIC[1] : WORD,
	DENSITY		= CHARACTERISTIC[2] : BBLOCK;

CHARACTERISTIC[0]= CHARACTERISTIC[1]= CHARACTERISTIC[2]= CHARACTERISTIC[3]= 0;

! Now set density
!
DENSITY[MT$V_DENSITY] = MT$K_PE_1600;

! Parity set to odd, we only support 9-tracks and 9-tracks are always odd
!
PARITY [ MT$V_PARITY ] = ODD_PARITY;

! Reset Tape format to FILES-11 ( only supported format )
!
FORMAT [ MT$V_FORMAT ] = MT$K_NORMAL11;

! Set the buffer size to ANSI max ( VMS default )
!
BUFFER_SIZE = 2048;

! write the characteristics to the tape drive
!
STATUS = $QIOW (CHAN = .CHANNEL,
		IOSB = IO_STATUS,
		FUNC = IO$_SETMODE,
		P1 = CHARACTERISTIC);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT (.STATUS);

END;					! end of routine DEFAULT_CHAR

ROUTINE SET_DENSITY : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine sets the density of the tape drive.
!
! CALLING SEQUENCE:
!	SET_DENSITY ();
!
! INPUT PARAMETERS:
!	NONE
!
! IMPLICIT INPUTS:
!	CHANNEL		- the I/O channel of the tape drive
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	IO_STATUS	- set to the return status of the QIO
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	NONE
!
! USER ERRORS:
!	NONE
!
!--

BEGIN

LOCAL
	CHARACTERISTIC	: VECTOR [4,WORD],	! characteristics to set
	STATUS;

BIND
	!  Set up offsets into the characteristics buffer
	!
	BUFFER_SIZE	= CHARACTERISTIC[1] : WORD,
	DENSITY		= CHARACTERISTIC[2] : BBLOCK;


! read the characteristics of the tape drive 
!
STATUS = $QIOW (CHAN = .CHANNEL,
		IOSB = CHARACTERISTIC,
		FUNC = IO$_SENSEMODE);
IF .STATUS THEN STATUS = .CHARACTERISTIC[0];
IF NOT .STATUS THEN ERR_EXIT (.STATUS);

! Set up the buffer to hold the new characteristics.  Get the device
!  independent stuff from the 2nd long word of IO_STATUS, use the default
!  buffersize and zero the notused field
!
CHARACTERISTIC [ 0 ] = 0;
BUFFER_SIZE	     = 2048;

! Now set density to what the user specified.
!
IF .INIT_OPTIONS[OPT_DENS_800]
  THEN DENSITY[MT$V_DENSITY] = MT$K_NRZI_800
  ELSE
    IF .INIT_OPTIONS[OPT_DENS_1600]
      THEN DENSITY[MT$V_DENSITY] = MT$K_PE_1600
      ELSE DENSITY[MT$V_DENSITY] = MT$K_GCR_6250;

! write the characteristics to the tape drive
!
STATUS = $QIOW (CHAN = .CHANNEL,
		IOSB = IO_STATUS,
		FUNC = IO$_SETMODE,
		P1 = CHARACTERISTIC);
IF .STATUS THEN STATUS = .IO_STATUS[0];
IF NOT .STATUS THEN ERR_EXIT (.STATUS);

END;					! end of routine SET_DENSITY

ROUTINE READ_VOLLABELS : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!	this routine reads the first block on the magnetic tape and
!	checks if it is an ANSI tape.  If it is, it then reads the
!	HDR1 record to determine if the first file on the tape has expired.
!
! CALLING SEQUENCE:
!	READ_VOLLABELS()
!
! INPUT PARAMETERS:
!	none
!
! IMPLICIT INPUTS:
!	channel - channel number assigned to device being initialized
!
! OUTPUT PARAMETERS:
!	none
!
! IMPLICIT OUTPUTS:
!	VOLUME_UIC 	- owner of tape
!	VOLUME_PROT	- tape protection
!
! ROUTINE VALUE:
!	none
!
! SIDE EFFECTS:
!	none
!
! USER ERRORS:
!	none
!
!--

BEGIN

LOCAL
	DATE		: VECTOR [2],		! binary date
	DESCR		: VECTOR [2],		! descriptor for today buffer
	REGDATE		: VECTOR [12,BYTE],	! buffer for date in format
						!  DD_MMM_YYYY
	STATUS,					! system service status
	TODAY		: VECTOR [12,BYTE];

! read first block on tape and check status
!
STATUS = $QIOW(
	CHAN = .CHANNEL,
	FUNC = IO$_READLBLK,
	IOSB = IO_STATUS,
	P1 = ANSI_LABEL,
	P2 = 80);
IF .STATUS THEN STATUS = .IO_STATUS[0];

! set up default volume owner and protection, which is the current users UIC
!   and read write allowed.  This will be reset by TAPE_OWN_PROT if this is
!   a VAX/VMS tape
!
VOLUME_UIC  = .PROCESS_UIC;
VOLUME_PROT = 0;


! if first record is Tape Mark then not ANSI tape
! if label is more than 80 characters ignore those characters beyond 80
!
IF (NOT .STATUS) AND (.STATUS NEQ SS$_DATAOVERUN)
THEN
    BEGIN
   
    ! if this is a new tape, the default density may have been changed
    ! by the QIO failure
    !
    IF .STATUS EQL SS$_OPINCOMPL 
    THEN
	BEGIN
	
	! tape must be at begining ( no reads to set density )
	!
	STATUS = $QIOW(	CHAN = .CHANNEL,
			FUNC = IO$_REWIND,
			IOSB = IO_STATUS);
	IF .STATUS THEN STATUS = .IO_STATUS[0];
	IF NOT .STATUS THEN ERR_EXIT (.STATUS);

	DEFAULT_CHAR ();
	END;
	
    RETURN 1;
    END;

! now check if first block is VOL1, foreign
!
IF .ANSI_LABEL[VL1$L_VL1LID] NEQ 'VOL1' THEN RETURN 1;

! determine owner and VMS protection of tape
!
TAPE_OWN_PRO();

! check ANSI accessibility character
!
IF  (.ANSI_LABEL[VL1$B_VOLACCESS] NEQ ' ') AND NOT .INIT_OPTIONS[OPT_OVR_ACC]
    THEN ERR_EXIT (SS$_FILACCERR);

! first record on tape is VOL1.  Now read HDR1 and determine if first
! file has expired.  User volume labels may intervene.
!
WHILE 1 DO
    BEGIN
    STATUS = $QIOW(
	CHAN = .CHANNEL,
	FUNC = IO$_READLBLK,
	IOSB = IO_STATUS[0],
	P1 = ANSI_LABEL,
	P2 = 80);
    IF .STATUS THEN STATUS = .IO_STATUS[0];
    IF NOT .STATUS THEN
	IF .STATUS NEQ SS$_DATAOVERUN THEN RETURN 0;	! ANSI tape, but can't 
							! read HDR1 
    IF .ANSI_LABEL[HD1$L_HD1LID] EQL 'HDR1' THEN EXITLOOP;
    END;

! test if the first file on the tape has expired
!
! convert the JULIAN date on the tape to a VMS date
!
IF CONVDATE_J2R(REGDATE,ANSI_LABEL[HD1$T_EXPIREDT]) 
THEN
    BEGIN
    DESCR[0] = 12;				! set up the descriptor
    DESCR[1] = REGDATE;
    REGDATE[11] = ' ';
    $BINTIM(TIMBUF = DESCR,TIMADR = DATE);	! convert from ASCII to binary
    $GETTIM(TIMADR = TODAY);			! get today's date in binary
    CALDAYNO(DATE,TODAY);			! chop off hours min and sec
    END
ELSE DATE = TODAY = 0; 				! when all else fails

IF (.DATE GTRU .TODAY) AND NOT (.INIT_OPTIONS[OPT_OVR_EXP])
    THEN ERR_EXIT (SS$_FILNOTEXP);

! test if first file has expired
!
IF (.ANSI_LABEL[HD1$B_FILACCESS] NEQ ' ') AND NOT (.INIT_OPTIONS[OPT_OVR_ACC])
    THEN ERR_EXIT (SS$_FILACCERR);

RETURN 0;				! valid to rewrite the ANSI TAPE
END;					! end of routine READ_VOLLABLES

ROUTINE TAPE_OWN_PRO : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!	this routine determines the owner and protection of the volume
!
! CALLING SEQUENCE:
!	TAPE_OWN_PRO()
!
! INPUT PARAMETERS:
!	none
!
! IMPLICIT INPUTS:
!	ANSI_LABEL -	location 38-40 D%C
!			location 41-45 group UIC
!			location 46-50 member UIC
!
! OUTPUT PARAMETERS:
!	none
!
! IMPLICIT OUTPUTS:
!	VOLUME_UIC  - owning UIC of tape
!	VOLUME_PROT - tape protection 
!
! ROUTINE VALUE:
!	none
!
! SIDE EFFECTS:
!	none
!
! USER ERRORS:
!	none
!
!--

BEGIN

LOCAL
	CONV_BUF	: VECTOR [6,BYTE],	! buffer to hold part of UIC
						!  while converting
	P,					! pointer into VOL1 ownership 
						!  field
	VALUE;					! value of binary convertion

MAP	VOLUME_PROT	: BITVECTOR [ 32 ];	! volume protection in bits

! bit numbers for different protections
!
LITERAL
	WORLD_WRITE = 13,
	WORLD_READ  = 12,
	GROUP_WRITE = 9,
	GROUP_READ  = 8;


! if ANSI tape produced by VAX system, decode owner field
!
IF .(ANSI_LABEL[VL1$T_VOLOWNER])<0,24> EQL 'D%C' THEN
    BEGIN

    ! point to the UIC encoded part
    !
    P = ANSI_LABEL[VL1$T_VOLOWNER] + 3;

    ! test for encoding
    !
    IF .(.P)<0,8> NEQ ' ' THEN
	BEGIN

	! decode the group number
	!
	CH$MOVE(5,.P,CONV_BUF);
	IF .(.P) <0,8> GEQ 'A' THEN
	CONV_BUF<0,8> = .(.P)<0,8> - ('A' - '0');

	! not a VMS tape exit with things unchanged
	!
	IF NOT LIB$CVT_OTB(5,CONV_BUF,VALUE) THEN RETURN;

	VOLUME_UIC<16,16> = .VALUE<0,16>;
	END;

    ! point to the member number
    !
    P = .P + 5;
    IF .(.P)<0,8> NEQ ' ' THEN

	! decode the member number
	!
	BEGIN
	CH$MOVE(5,.P,CONV_BUF);
	IF .(.P) <0,8> GEQ 'A' THEN
	CONV_BUF<0,8> = .(.P)<0,8> - ('A' - '0');

	! not a VMS tape reset VOLUME_UIC and exit
	!
	IF NOT LIB$CVT_OTB(5,CONV_BUF,VALUE) THEN
	    BEGIN
	    VOLUME_UIC = .PROCESS_UIC;
	    RETURN;
	    END;

	VOLUME_UIC<0,16> = .VALUE<0,16>;
	END;

    ! now protection must be decoded
    ! if both group and member are blank then all privileges granted
    !
    ! pointer to group
    !
    P = .P -5;
    IF NOT CH$FAIL(CH$FIND_NOT_CH(10,.P,' ')) THEN
	BEGIN

	! decode world protection
	!
	VOLUME_PROT[WORLD_WRITE] = 1;
	IF .(.P)<0,8> LSS 'A' THEN VOLUME_PROT[WORLD_READ] = 1;


	! decode group protection
	!
	! pointer to member field
	!
	P = .P + 5;
	IF NOT CH$FAIL(CH$FIND_NOT_CH(5,.P,' ')) THEN
	    BEGIN
	    VOLUME_PROT[GROUP_WRITE] = 1;
	    IF .(.P)<0,8> LSS 'A' THEN VOLUME_PROT[GROUP_READ] = 1;
	    END;
	END;
    END;

END;						! end of routine TAPE_OWN_PRO

ROUTINE CHECK_PROT(VOL_PROT,VOL_UIC)  =

!++
!
! FUNCTIONAL DESCRIPTION:
!	this routine check volume protection
!
! CALLING SEQUENCE:
!	CHECK_PROT(ARG1,ARG2)
!
! INPUT PARAMETERS:
!	ARG1 - volume protection
!	ARG2 - volume owner UIC
!
! IMPLICIT INPUTS:
!	PROCESS_UIC	- UIC of the current process
!	PRIVILEGE_MASK	- mask of privileges that the user has
!	INIT_OPTIONS	- init options bitvector
!
! OUTPUT PARAMETERS:
!	none
!
! IMPLICIT OUTPUTS:
!	none
!
! ROUTINE VALUE:
!	SS$NORMAL - if users has the needed priviledges
!       SS$NOPRIV - if users does not have the needed priviledges
!
! SIDE EFFECTS:
!	none
!
! USER ERRORS:
!	none
!
!--

BEGIN

MAP
	PROCESS_UIC	: VECTOR [ 2, WORD ],  ! the process UIC
	VOL_PROT	: BITVECTOR,
	VOL_UIC		: VECTOR [ 2, WORD ];

EXTERNAL
	EXE$GL_SYSUIC	: REF BBLOCK ADDRESSING_MODE ( ABSOLUTE );

LITERAL
	NOT_GROUP_WRITE = 9,  ! the group write disable bit
	NOT_WORLD_WRITE = 13; ! the world write disable bit


!  check if the user has write access to the tape
!
IF ( .PRIVILEGE_MASK [ PRV$V_BYPASS ] ) OR        ! user has bypass priviledge

   ( .PRIVILEGE_MASK [ PRV$V_SYSPRV ] ) OR        ! user has sysprv priviledge

   ( .PRIVILEGE_MASK [ PRV$V_VOLPRO ] ) OR        ! user has volpro priviledge

   ( NOT .VOL_PROT [ NOT_WORLD_WRITE ] ) OR        ! the tape is world write

   ( .PROCESS_UIC [ 1 ] LEQ .EXE$GL_SYSUIC ) OR    ! the user's UIC has a
						   ! system group number

   (( .PROCESS_UIC [ 1 ] EQL .VOL_UIC [ 1 ] ) AND  ! (the user's and tape's 
    (( .PROCESS_UIC [ 0 ] EQL .VOL_UIC [ 0 ] ) OR  !  UIC matches) OR (tape's
     ( NOT .VOL_PROT [ NOT_GROUP_WRITE ] )))	   !  and user's group match
						   !  and tape is group write)
   THEN RETURN SS$_NORMAL;

!  user does not needed priviledges return error
!
RETURN SS$_NOPRIV;

END;


ROUTINE FORMAT_VOL1 : NOVALUE  =

!++
!
! FUNCTIONAL DESCRIPTION:
!	This routine formats the volume label one of an ANSI labeled tape.
!
! CALLING SEQUENCE:
!	FORMAT_VOL1 ()
!
! INPUT PARAMETERS:
!	none
!
! IMPLICIT INPUTS:
!	none
!
! OUTPUT PARAMETERS:
!	none
!
! IMPLICIT OUTPUTS:
!	none
!
! ROUTINE VALUE:
!	none
!
! SIDE EFFECTS:
!	The correct infomation gets stuffed into the VOL1 skeleton
!
! USER ERRORS:
!	none
!
!--

BEGIN

LOCAL
	VOLUME_PROT,	! protection for tape
	VOLUME_UIC;	! owner of tape

BIND
	! UPLIT was used instead of CH$TRANSTABLE here, the code
	!  produced is the same (ie the constant string generated).
	!  UPLIT was used because CH$TRANSTABLE generates a warning error
	!  because more then a single character at a time is specified
	!  in the %ASCII. ( BLISS KLUDGE )
	!
	! The table will upcase a..z and return '@' for any non ANSI
	!  'a' characters.
	!
	TRANSLATION_TABLE = UPLIT BYTE (
		%ASCII '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@',
		%ASCII ' !"@@%&''()*+,-./0123456789:;<=>?',
		%ASCII '@ABCDEFGHIJKLMNOPQRSTUVWXYZ@@@@@',
		%ASCII '@ABCDEFGHIJKLMNOPQRSTUVWXYZ@@@@@',
		%ASCII '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@',
		%ASCII '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@',
		%ASCII '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@',
		%ASCII '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

!
! place the label in the new volume
!

! check length of label for volume
!
IF .LABEL_STRING[0] GTRU VL1$S_VOLLBL THEN ERR_EXIT(SS$_MTLBLLONG);

! translate the label into upper case and put in '@' for any non-ANSI
!  a characters found, padded with space in case label from command is
!  less than six characters long
!
CH$TRANSLATE (TRANSLATION_TABLE, .LABEL_STRING[0], .LABEL_STRING[1], ' ',
	      VL1$S_VOLLBL, VOL1[VL1$T_VOLLBL]);

! check for non-ANSI 'a' characters
!
IF NOT CH$FAIL( CH$FIND_CH ( VL1$S_VOLLBL, VOL1[VL1$T_VOLLBL], '@'))
	THEN ERR_EXIT ( INIT$_BADVOLLBL );

! determine owner and protection of new volume
!
IF .INIT_OPTIONS[OPT_PROTECTION] 	! did user specify protection
THEN VOLUME_PROT = .PROTECTION		! protection input by user
ELSE VOLUME_PROT = 0;			! no protection is default

IF .INIT_OPTIONS[OPT_OWNER_UIC] 	! did user specify owner UIC
THEN VOLUME_UIC = .OWNER_UIC		! user input owner
ELSE VOLUME_UIC = .PROCESS_UIC;		! use the user's process UIC

! place the values in the label
!
FORMAT_VOLOWNER(VOL1,.VOLUME_UIC,.VOLUME_PROT);

! put the accessibilty character into place
!
IF .INIT_OPTIONS[OPT_LABEL_QUAL]
THEN VOL1[VL1$B_VOLACCESS] = .VOL_ACC;

END;					! end of routine FORMAT_VOL1

ROUTINE FORMAT_VOLOWNER(VOL_LABEL,OWNER,PROTECTION) : NOVALUE  =

!++
!
! FUNCTIONAL DESCRIPTION:
!	this routine formats the volume owner filed in VOL1
!
! CALLING SEQUENCE:
!	FORMAT_VOLOWNER(ARG1,ARG2,ARG3)
!
! INPUT PARAMETERS:
!	ARG1 - address of VOL1 label
!	ARG2 - owner of tape
!	ARG3 - tape protection
!
! IMPLICIT INPUTS:
!	D%C preinitialized
!
! OUTPUT PARAMETERS:
!	none
!
! IMPLICIT OUTPUTS:
!	none
!
! ROUTINE VALUE:
!	none
!
! SIDE EFFECTS:
!	none
!
! USER ERRORS:
!	none
!
!--

BEGIN

MAP
	VOL_LABEL	: REF BBLOCK,	! address of VOL1 label
	PROTECTION	: BITVECTOR;	! protection to be encoded on tape

LOCAL
	DESCR		: VECTOR [2],	! descriptor
	P;				! pointer

LITERAL
	WORLD_WRITE = 13,
	WORLD_READ = 12,
	GROUP_WRITE = 9,
	GROUP_READ = 8;


! first convert binary owner to ASCII
!
DESCR[0] = 10;
DESCR[1] = VOL_LABEL[VL1$T_VOLOWNER] + 3;
$FAO(
	DESCRIPTOR('!5OW!5OW'), 0,
	DESCR[0],
	.OWNER<16,16>,.OWNER<0,16>);

! now format protection
!
IF NOT .PROTECTION[GROUP_READ] OR NOT .PROTECTION[WORLD_READ] THEN
    BEGIN
    P = VOL_LABEL[VL1$T_VOLOWNER] + 8;
    (.P)<0,8> = .(.P)<0,8> + ('A' - '0');
    END;

! now if group can also write, blank fill member field
!
IF NOT .PROTECTION[GROUP_WRITE] THEN CH$FILL(' ',5,VOL_LABEL[VL1$T_VOLOWNER] + 8);

IF NOT .PROTECTION[WORLD_READ] THEN
    BEGIN
    P = VOL_LABEL[VL1$T_VOLOWNER] + 3;
    (.P)<0,8> = .(.P)<0,8> + ('A' - '0');
    END;
 
IF NOT .PROTECTION[WORLD_WRITE] THEN CH$FILL(' ',10,VOL_LABEL[VL1$T_VOLOWNER] + 3);
END;					!end of routine FORMAT_VOLOWNER

END
ELUDOM
