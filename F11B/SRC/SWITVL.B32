MODULE SWITVL (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 2
!
! ABSTRACT:
!
!	This module contains routines that switch file ACP context from
!	one volume to another.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  8-Nov-1978  13:35
!
! MODIFIED BY:
!
!	B0103	ACG0082		Andrew C. Goldstein,	13-Nov-1979  23:56
!		Remove FLUSH_BUFFERS call due to write-back cacheing
!
!	B0102	ACG0082		Andrew C. Goldstein,	5-Nov-1979  14:52
!		Changes for write-back cacheing
!
!	B0101	ACG0037		Andrew C. Goldstein,  7-May-1979  15:15
!	Range check RVN before indexing into RVT;
!	allow RVN 1 on single volumes
!
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';


FORWARD ROUTINE
	SWITCH_VOLUME	: NOVALUE,	! switch context to specified RVN
	SWITCH_CHANNEL;			! switch channel assignments

GLOBAL ROUTINE SWITCH_VOLUME (NEW_RVN) : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine switches the ACP context to the specified RVN. It
!	assigns the current channel to the new unit.
!
!
! CALLING SEQUENCE:
!	SWITCH_VOLUME (ARG1)
!
! INPUT PARAMETERS:
!	ARG1: relative volume number to switch to
!
! IMPLICIT INPUTS:
!	CURRENT_UCB: UCB address of current volume
!	CURRENT_VCB: VCB address of current volume
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	context switched to new volume
!
!--

BEGIN

LOCAL
	RVN,				! filtered RVN desired
	RVT		: REF BBLOCK,	! address of relative volume table
	UCB		: REF BBLOCK;	! address of new UCB

EXTERNAL
	CURRENT_RVN,			! RVN of current volume
	CURRENT_UCB	: REF BBLOCK,	! address of current UCB
	CURRENT_VCB	: REF BBLOCK;	! address of current VCB


! First check if a volume switch is necessary. Extract the true RVN part
! (removing extended file ID if present), check for zero and compare it
! against the current RVN.
!

RVN = .NEW_RVN<0,16>;
IF .CURRENT_VCB[VCB$V_EXTFID]
THEN RVN = .NEW_RVN<0,8>;

IF .RVN EQL 0 OR .RVN EQL .CURRENT_RVN
THEN RETURN;

! Get the RVT and from it the UCB address we are switching to. Nonexistence
! of either is an error.
!

RVT = .CURRENT_VCB[VCB$L_RVT];
IF .RVT EQL .CURRENT_UCB
THEN
    BEGIN
    IF .RVN EQL 1
    THEN RETURN
    ELSE ERR_EXIT (SS$_NOTVOLSET);
    END;

IF .RVN GTRU .RVT[RVT$B_NVOLS]
THEN ERR_EXIT (SS$_DEVNOTMOUNT);

UCB = .VECTOR [RVT[RVT$L_UCBLST], .RVN-1];
IF (
    IF .UCB EQL 0
    THEN 1
    ELSE NOT .BBLOCK [UCB[UCB$L_DEVCHAR], DEV$V_MNT]
    )
THEN ERR_EXIT (SS$_DEVNOTMOUNT);

IF .UCB[UCB$B_TYPE] NEQ DYN$C_UCB
THEN BUG_CHECK (NOTUCBRVT, FATAL, 'Not UCB pointer in RVT');

! Finally shuffle the channels and pointers about.
!

KERNEL_CALL (SWITCH_CHANNEL, .UCB);

END;					! end of routine SWITCH_VOLUME

GLOBAL ROUTINE SWITCH_CHANNEL (UCB) =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine reassigns the ACP's channels to the specified UCB
!	and fixes up the associated pointers. It must be called in
!	kernel mode.
!
!
! CALLING SEQUENCE:
!	SWITCH_CHANNEL (ARG1)
!
! INPUT PARAMETERS:
!	ARG1: UCB address of new device
!
! IMPLICIT INPUTS:
!	IO_CHANNEL: channel number of primary channel
!	CURRENT_UCB: address of current UCB
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	CURRENT_UCB: contains address of new UCB
!	CURRENT_VCB: address of new VCB
!	CURRENT_RVN: RVN of new volume
!
! ROUTINE VALUE:
!	1
!
! SIDE EFFECTS:
!	channels reassigned
!
!--

BEGIN

MAP
	UCB		: REF BBLOCK;	! UCB address arg

LOCAL
	IO_CCB		: REF BBLOCK;	! address of CCB for IO_CHANNEL

EXTERNAL
	IO_CHANNEL,			! primary channel number
	CURRENT_RVN,			! RVN of current volume
	CURRENT_UCB	: REF BBLOCK,	! current UCB address
	CURRENT_VCB	: REF BBLOCK;	! current VCB address

EXTERNAL ROUTINE
	GET_CCB;			! get CCB address of channel


! Get the CCB addresses for the channels. Then assign the secondary channel
! to the old device and the primary channel to the new. Update the global
! pointers.
!

IO_CCB = GET_CCB (.IO_CHANNEL);

IO_CCB[CCB$L_UCB] = .UCB;

CURRENT_UCB = .UCB;
CURRENT_VCB = .UCB[UCB$L_VCB];

IF .CURRENT_VCB EQL 0
THEN BUG_CHECK (NOTUCBRVT, FATAL, 'Bad UCB pointer in RVT');
IF .CURRENT_VCB[VCB$B_TYPE] NEQ DYN$C_VCB
THEN BUG_CHECK (NOTVCBUCB, FATAL, 'Bad VCB pointer in UCB');

CURRENT_RVN = .CURRENT_VCB[VCB$W_RVN];

RETURN 1;

END;					! end of routine SWITCH_CHANNEL

END
ELUDOM
