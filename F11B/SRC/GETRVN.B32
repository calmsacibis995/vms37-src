MODULE GETRVN (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 2
!
! ABSTRACT:
!
!	This module contains routines to default the RVN of a file ID to
!	the current context.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  13-Nov-1978  18:50
!
! MODIFIED BY:
!
!	B0101	ACG0091		Andrew C. Goldstein,	6-Dec-1979  19:19
!		Set explicit RVN of 1 to 0 if not volume set
!
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';


FORWARD ROUTINE
	APPLY_RVN	: NOVALUE,	! apply current RVN
	DEFAULT_RVN	: NOVALUE;	! use RVN 0 if same as current

GLOBAL ROUTINE APPLY_RVN (RVN) : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine defaults the RVN at the indicated address to the RVN
!	of the current volume if it is zero.
!
!
! CALLING SEQUENCE:
!	APPLY_RVN (ARG1)
!
! INPUT PARAMETERS:
!	ARG1: address of RVN word
!
! IMPLICIT INPUTS:
!	CURRENT_RVN: RVN of current volume
!	CURRENT_VCB: address of VCB in process
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	contents of specified address may be written with current RVN
!
!--

BEGIN

EXTERNAL
	CURRENT_RVN,			! RVN of current volume
	CURRENT_VCB	: REF BBLOCK;	! address of VCB in process


IF .CURRENT_VCB[VCB$V_EXTFID]
THEN
    BEGIN
    IF .(.RVN)<0,8> EQL 0
    THEN (.RVN)<0,8> = .CURRENT_RVN;
    IF .(.RVN)<0,8> EQL 1
    AND .CURRENT_RVN EQL 0
    THEN (.RVN)<0,8> = 0;
    END

ELSE
    BEGIN
    IF .(.RVN)<0,16> EQL 0
    THEN (.RVN)<0,16> = .CURRENT_RVN;
    IF .(.RVN)<0,16> EQL 1
    AND .CURRENT_RVN EQL 0
    THEN (.RVN)<0,16> = 0;
    END;

END;					! end of routine APPLY_RVN

GLOBAL ROUTINE DEFAULT_RVN (RVN) : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine zeroes the RVN at the indicated address if it is
!	the same as the current RVN
!
!
! CALLING SEQUENCE:
!	DEFAULT_RVN (ARG1)
!
! INPUT PARAMETERS:
!	ARG1: address of RVN word
!
! IMPLICIT INPUTS:
!	CURRENT_RVN: RVN of current volume
!	CURRENT_VCB: address of VCB in process
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	contents of specified address may be zeroed
!
!--

BEGIN

EXTERNAL
	CURRENT_RVN,			! RVN of current volume
	CURRENT_VCB	: REF BBLOCK;	! address of VCB in process


IF .CURRENT_VCB[VCB$V_EXTFID]
THEN
    BEGIN
    IF .(.RVN)<0,8> EQL .CURRENT_RVN
    THEN (.RVN)<0,8> = 0;
    END

ELSE
    BEGIN
    IF .(.RVN)<0,16> EQL .CURRENT_RVN
    THEN (.RVN)<0,16> = 0;
    END;

END;					! end of routine DEFAULT_RVN

END
ELUDOM
