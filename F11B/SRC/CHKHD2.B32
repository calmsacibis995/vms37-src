MODULE CHKHD2 (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 2
!
! ABSTRACT:
!
!	This routine verifies that the block given it is in fact a
!	file header. If file number and/or file seqence number are also
!	supplied, they are checked as well.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  13-Dec-1976  16:11
!
! MODIFIED BY:
!
!	V02-003	ACG0156		Andrew C. Goldstein,	12-Mar-1980  15:21
!		Fix header invalidation bug
!
!	B0102	ACG0146		Andrew C. Goldstein,	22-Feb-1980  21:40
!		Change file sequence number check to no such file
!
!	B0101	acg0003		Andrew C. Goldstein,  10-Nov-1978  19:29
!	Add multi-volume support
!
!	B0100	ACG00001	Andrew C. Goldstein,  10-Oct-1978  19:59
!	Previous revision history moved to [F11B.SRC]F11B.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';

GLOBAL ROUTINE CHECK_HEADER2 (HEADER, FILE_ID) =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine verifies that the block given it is in fact a
!	file header. If file number and/or file seqence number are also
!	supplied, they are checked as well.
!
! CALLING SEQUENCE:
!	CHECK_HEADER (ARG1, ARG2)
!
! INPUT PARAMETERS:
!	ARG1: address of header image
!	ARG2: address of file ID
!
! IMPLICIT INPUTS:
!	CURRENT_VCB: address of VCB of volume
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	USER_STATUS contains code if not valid
!
! ROUTINE VALUE:
!	0 if garbage
!	1 if valid and correct file header
!	2 if deleted file header
!	4 if valid header but wrong sequence number
!
! SIDE EFFECTS:
!	NONE
!
!--

BEGIN

MAP
	HEADER		: REF BBLOCK,	! file header arg
	FILE_ID		: REF BBLOCK;	! file ID arg

LOCAL
	MAP_AREA	: REF BBLOCK;	! pointer to header map area

EXTERNAL ROUTINE
	CHECKSUM;			! compute file header checksum

EXTERNAL
	CURRENT_VCB	: REF BBLOCK;	! VCB of volume


! First check the structure level.
!

IF .HEADER[FH2$B_STRUCLEV] NEQ 2
THEN (ERR_STATUS (SS$_FILESTRUCT); RETURN 0);

! Check the area offsets and the retrieval pointer use counts for
! consistency.
!

IF .HEADER[FH2$B_IDOFFSET] LSSU FH2$C_LENGTH/2
OR .HEADER[FH2$B_MPOFFSET] LSSU .HEADER[FH2$B_IDOFFSET]
OR .HEADER[FH2$B_ACOFFSET] LSSU .HEADER[FH2$B_MPOFFSET]
OR .HEADER[FH2$B_RSOFFSET] LSSU .HEADER[FH2$B_ACOFFSET]
OR .HEADER[FH2$B_MAP_INUSE] GTRU .HEADER[FH2$B_ACOFFSET] - .HEADER[FH2$B_MPOFFSET]
THEN (ERR_STATUS (SS$_BADFILEHDR); RETURN 0);

! At this point, we have verified that the block at least once was a
! valid file header.
!
! Look at the file number in the header. If zero, this is a 
! deleted header.
!

IF .HEADER[FH2$W_FID_NUM] EQL 0
AND (
    IF .CURRENT_VCB[VCB$V_EXTFID]
    THEN .HEADER[FH2$B_FID_NMX] EQL 0
    ELSE 1
    )
THEN (ERR_STATUS (SS$_NOSUCHFILE); RETURN 2);

! Now compute the header checksum.
!

IF NOT CHECKSUM (.HEADER)
THEN (ERR_STATUS (SS$_BADCHKSUM); RETURN 2);

! Check file number and file sequence number.
!

IF .HEADER[FH2$W_FID_NUM] NEQ .FILE_ID[FID$W_NUM]
OR (
    IF .CURRENT_VCB[VCB$V_EXTFID]
    THEN .HEADER[FH2$B_FID_NMX] NEQ .FILE_ID[FID$B_NMX]
    ELSE 0
    )
THEN (ERR_STATUS (SS$_FILENUMCHK); RETURN 2);

IF .HEADER[FH2$W_FID_SEQ] NEQ .FILE_ID[FID$W_SEQ]
THEN (ERR_STATUS (SS$_NOSUCHFILE); RETURN 4);

! Header is ok.
!

RETURN 1;

END;					! end of routine CHECK_HEADER

END
ELUDOM
