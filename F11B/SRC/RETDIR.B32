MODULE RETDIR (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 2
!
! ABSTRACT:
!
!	This routine returns the resulting data from a directory
!	operation to the user's buffer packet.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines. This routine must be called
!	in kernel mode.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  3-Jan-1977  15:07
!
! MODIFIED BY:
!
!	V02-001	ACG0208		Andrew C. Goldstein,	12-Nov-1981  23:49
!		Change declaration of directory context block
!		Fix length checks on file name buffer
!
!	V02-000	ACG0167		Andrew C. Goldstein,	16-Apr-1980  19:27
!		Previous revision history moved to F11B.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';

GLOBAL ROUTINE RETURN_DIR (COUNT, STRING, ABD) : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine returns the resulting data from a directory
!	operation to the user's buffer packet.
!
! CALLING SEQUENCE:
!	RETURN_DIR (ARG1, ARG2, ARG3)
!
! INPUT PARAMETERS:
!	ARG1: byte count of result string
!	ARG2: address of result string
!	ARG3: address of buffer descriptors
!
! IMPLICIT INPUTS:
!	DIR_ENTRY: address of directory record
!	DIR_VERSION: address of version entry
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	result data written into buffer packet
!
!--

BEGIN

MAP
	STRING		: REF VECTOR [,BYTE], ! file string arg
	ABD		: REF BBLOCKVECTOR [,ABD$C_LENGTH];
					! descriptor arg

LOCAL
	STRING_DESC	: VECTOR [2];	! FAO output string descriptor

EXTERNAL
	DIR_CONTEXT	: BBLOCK;	! directory context block

DIR_CONTEXT_DEF;


! Build the file name string in the buffer supplied.
!

.COUNT = 0;
STRING_DESC[0] = FILENAME_LENGTH+6;
STRING_DESC[1] = .STRING;
$FAO (DESCRIPTOR ('!AC;!SW'),
	.COUNT,
	STRING_DESC,
	DIR_ENTRY[DIR$B_NAMECOUNT],
	.DIR_VERSION[DIR$W_VERSION]
	);

! If the user provided a result length buffer, give him the length
! of the result string.
!

IF .ABD[ABD$C_RESL, ABD$W_COUNT] GEQ 2
THEN
    BEGIN
    (.ABD[ABD$C_RESL, ABD$W_TEXT] + ABD[ABD$C_RESL, ABD$W_TEXT] + 1)<0,16> = ..COUNT;
    END;

! If the user provided a result string buffer, return as much of the
! result string as will fit (zero filling the buffer).
!

CH$COPY (..COUNT, .STRING, 0,
    .ABD[ABD$C_RES, ABD$W_COUNT],
    .ABD[ABD$C_RES, ABD$W_TEXT] + ABD[ABD$C_RES, ABD$W_TEXT] + 1);

END;					! end of routine RETURN_DIR

END
ELUDOM
