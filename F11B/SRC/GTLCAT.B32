MODULE GTLCAT (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 2
!
! ABSTRACT:
!
!	This routine extracts the compatibility mode placement control data
!	from the attribute list and stores it in the standard place in the FIB.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  8-Dec-1978  15:52
!
! MODIFIED BY:
!
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';

GLOBAL ROUTINE GET_LOC_ATTR (ABD, FIB) : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine extracts the compatibility mode placement control data
!	from the attribute list and stores it in the standard format in the FIB.
!
!
! CALLING SEQUENCE:
!	GET_LOC_ATTR (ARG1, ARG2)
!
! INPUT PARAMETERS:
!	ARG1: address of buffer descriptor vector
!
! IMPLICIT INPUTS:
!	IO_PACKET: address of I/O packet
!
! OUTPUT PARAMETERS:
!	ARG2: address of user FIB
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	NONE
!
!--

BEGIN

MAP
	ABD		: REF BBLOCKVECTOR [,ABD$C_LENGTH],
					! descriptor vector arg
	FIB		: REF BBLOCK;	! FIB arg

! Format of compatibility mode attribute data.
!

MACRO
	LOC_CODE	= 0, 0, 8, 0%,	! attribute code byte
	LOC_CTRL	= 1, 0, 8, 0%,	! placement control bits
	LOC_VBN		= 1, 0, 1, 0%,	! VBN format
	LOC_APPROX	= 1, 1, 1, 0%,	! use approximate placement
	LOC_LBN		= 1, 2, 1, 0%,	! LBN format
	LOC_CYL		= 1, 3, 1, 0%,	! cylinder format
	LOC_RVN		= 2, 0, 8, 0%,	! relative volume number
	LOC_ADDR	= 3, 0,32, 0%;	! address value

! Format conversion table. This table is indexed into by the control bits.
!

BIND
	ALIGN_TABLE	= UPLIT BYTE   (0,
					FIB$C_VBN,
					0,
					FIB$C_VBN,
					FIB$C_LBN,
					FIB$C_VBN,
					FIB$C_LBN,
					FIB$C_VBN,
					FIB$C_CYL,
					0,
					FIB$C_CYL,
					0,
					FIB$C_CYL,
					0,
					FIB$C_CYL
					) : VECTOR [,BYTE];

LOCAL
	P		: REF BBLOCK;	! pointer to attribute text

EXTERNAL
	IO_PACKET	: REF BBLOCK;	! address of I/O packet


! Loop, scanning the attribute list entries for the placement attribute code.
! when found, reformat the data into the FIB.
!

INCR J FROM ABD$C_ATTRIB TO .IO_PACKET[IRP$W_BCNT]
DO
    BEGIN
    P = .ABD[.J, ABD$W_TEXT] + ABD[.J, ABD$W_TEXT];
    IF .P[LOC_CODE] EQL ATR$C_ALCONTROL
    THEN
	BEGIN
	IF .ABD[.J, ABD$W_COUNT] LSSU 6
	THEN ERR_EXIT (SS$_BADPARAM);

	IF .P[LOC_CTRL] GTRU 14
	THEN ERR_EXIT (SS$_BADPARAM);
	FIB[FIB$B_ALALIGN] = .ALIGN_TABLE[.P[LOC_CTRL]];
	IF .FIB[FIB$B_ALALIGN] EQL 0
	THEN ERR_EXIT (SS$_BADPARAM);

	FIB[FIB$W_LOC_RVN] = .P[LOC_RVN];
	FIB[FIB$L_LOC_ADDR] = ROT (.P[LOC_ADDR], 16);

	IF .P[LOC_CYL]
	THEN FIB[FIB$V_ONCYL] = 1;
	IF NOT .P[LOC_APPROX]
	THEN FIB[FIB$V_EXACT] = 1;
	RETURN;
	END;

    END;				! end of attribute scan loop

END;					! end of routine GET_LOC_ATTR

END
ELUDOM
