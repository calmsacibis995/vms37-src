MODULE DIRACC (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 2
!
! ABSTRACT:
!
!	This routine "accesses" a directory and readies it for processing.
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  2-Jan-1977  22:53
!
! MODIFIED BY:
!
!	V02-005	ACG0229		Andrew C. Goldstein,	23-Dec-1981  20:55
!		Count directory LRU hits and misses
!
!	V02-004	ACG33975	Andrew C. Goldstein,	13-Mar-1981  15:46
!		Fix losing directory FCB's when LRU is disabled
!
!	V02-003	ACG0167		Andrew C. Goldstein,	16-Apr-1980  19:25
!		Previous revision history moved to F11B.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';


FORWARD ROUTINE
	DIR_ACCESS	: NOVALUE,	! access a directory
	CREATE_DFCB;			! create directory FCB

GLOBAL ROUTINE DIR_ACCESS (FIB, WRITE) : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine "accesses" a directory and readies it for processing.
!
! CALLING SEQUENCE:
!	DIR_ACCESS (ARG1, ARG2)
!
! INPUT PARAMETERS:
!	ARG1: address of FIB
!	ARG2: 0 if read only
!	      1 if read/write
!
! IMPLICIT INPUTS:
!	NONE
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	DIR_FCB: address of FCB for directory
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	FCB may be created
!	directory LRU may be altered
!	directory file header may be read
!
!--

BEGIN

MAP
	FIB		: REF BBLOCK;	! FIB argument

LOCAL
	TEMP,				! temp to save header address
	FCB		: REF BBLOCK,	! FCB address
	HEADER		: REF BBLOCK;	! directory header address

EXTERNAL
	DIR_FCB		: REF BBLOCK,	! global FCB address
	FILE_HEADER	: REF BBLOCK,	! global file header address
	PMS$GL_DIRHIT	: ADDRESSING_MODE (GENERAL),
					! count of directory LRU hits
	PMS$GL_DIRMISS	: ADDRESSING_MODE (GENERAL);
					! count of directory LRU misses

EXTERNAL ROUTINE
	SWITCH_VOLUME,			! switch to correct volume
	SEARCH_FCB,			! search FCB list
	READ_HEADER,			! read file header
	CHECK_PROTECT,			! check file protection
	MAKE_DIR_FCB;			! put directory FCB in LRU


! Switch context to the volume of the specified RVN.
!

SWITCH_VOLUME (.FIB[FIB$W_DID_RVN]);

! First find the FCB of the directory, if any exists.
!

HEADER = 0;				! assume no header
IF (FCB = .DIR_FCB) EQL 0
THEN
    BEGIN
    FCB = SEARCH_FCB (FIB[FIB$W_DID]);

! If there was no FCB, or it is an accessed file, read the header.
! If the FCB is in the LRU, check the sequence number since no
! one else does. If there was no FCB, create one.
!

    IF (IF .FCB EQL 0 THEN 1 ELSE (NOT .FCB[FCB$V_DIR]))
    THEN
	BEGIN
	PMS$GL_DIRMISS = .PMS$GL_DIRMISS + 1;
	TEMP = .FILE_HEADER;
	HEADER = READ_HEADER (FIB[FIB$W_DID], .FCB);
	FILE_HEADER = .TEMP;		! restore main header address
	END
    ELSE
	BEGIN
	PMS$GL_DIRHIT = .PMS$GL_DIRHIT + 1;
	IF .FCB[FCB$W_FID_SEQ] NEQ .FIB[FIB$W_DID_SEQ]
	THEN ERR_EXIT (SS$_FILESEQCHK);
	END;

    IF .FCB EQL 0
    THEN FCB = KERNEL_CALL (CREATE_DFCB, .HEADER);
    DIR_FCB = .FCB;				! store global FCB address
    END;

! Check directory file protection.
!

CHECK_PROTECT (.WRITE, 0, .FCB);

! If we read a header, check the file attributes to make sure that
! this is a real directory file.
!

IF .HEADER NEQ 0
THEN
    BEGIN
    IF NOT .HEADER[FH2$V_DIRECTORY]
    OR NOT .HEADER[FH2$V_CONTIG]
    OR .BBLOCK [HEADER[FH2$W_RECATTR], FAT$B_RTYPE] NEQ FAT$C_VARIABLE
    OR .BBLOCK [HEADER[FH2$W_RECATTR], FAT$B_RATTRIB] NEQ FAT$M_NOSPAN
    OR .HEADER[FH2$W_SEG_NUM] NEQ 0
    OR .HEADER[FH2$W_EX_FIDNUM] NEQ 0
    OR .HEADER[FH2$W_EX_FIDSEQ] NEQ 0
    OR .HEADER[FH2$W_EX_FIDRVN] NEQ 0
    THEN ERR_EXIT (SS$_BADIRECTORY);
    END;

! Arbitrate the access interlocks.
!

IF .FCB[FCB$V_EXCL]			! check for exclusive access
    THEN ERR_EXIT (SS$_ACCONFLICT);
IF .WRITE				! if write access desired
AND .FCB[FCB$W_LCNT] NEQ 0		! and file locked against writers
    THEN ERR_EXIT (SS$_ACCONFLICT);

! Make the final diddles for the FCB to be useful for directory searches.
!

KERNEL_CALL (MAKE_DIR_FCB, .FCB);

RETURN 1;

END;					! end of routine DIR_ACCESS

ROUTINE CREATE_DFCB (HEADER) =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine creates a directory FCB and initializes it according to
!	the given file header.
!
! CALLING SEQUENCE:
!	CREATE_DFCB (ARG1)
!
! INPUT PARAMETERS:
!	ARG1: address of file header
!
! IMPLICIT INPUTS:
!	NONE
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	ADDRESS OF FCB
!
! SIDE EFFECTS:
!	FCB created and initialized
!
!--

BEGIN

MAP
	HEADER		: REF BBLOCK;	! file header argument

LOCAL
	FCB		: REF BBLOCK;	! address of FCB created

EXTERNAL ROUTINE
	ALLOCATE,			! allocate dynamic memory
	INIT_FCB2;			! initialize contents of FCB

! Allocate an FCB sized and typed block, including the area for
! the directory index. Then use the common routine to init it.
!

FCB = ALLOCATE (FCB$C_LENGTH+FCB$S_DIRIDX, FCB_TYPE);
FCB[FCB$L_WLFL] = FCB[FCB$L_WLFL];	! init null window list
FCB[FCB$L_WLBL] = FCB[FCB$L_WLFL];
FCB[FCB$L_STVBN] = 1;			! init start VBN to 1
FCB[FCB$W_DIRSEQ] = .FCB[FCB$W_DIRSEQ] + 1;
CH$FILL (-1, FCB$S_DIRIDX, FCB[FCB$B_DIRIDX]);
INIT_FCB2 (.FCB, .HEADER);
RETURN .FCB;

END;					! end of routine CREATE_FCB

END
ELUDOM
