	.TITLE	CNVCLIATB - CONVERT ASCII TO BINARY
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;++
; FACILITY: COMMAND LANGUAGE INTERPRETER UTILITIES
;
; ABSTRACT:
;
; THIS SUBROUTINE CONVERTS AN UNSIGNED ASCII NUMERIC STRING TO
; AN UNSIGNED, SINGLE PRECISION VALUE.
;
; ENVIRONMENT: USER MODE
;
; AUTHOR: C. A. MONIA	, CREATION DATE: 15-AUG-1977
;
; MODIFIED BY:
;
;	, : VERSION
; 01	- 
;
; 101	T. Halvorsen	27-Jun-1979
;	Change PSECT name to avoid conflicting psects warning with RTL
;--
	.PAGE
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
 
	$CLIMSGDEF			; DEFINE CLI STATUS VALUES
 
;
; OWN STORAGE:
;
; ASCII TO BINARY CONVERSION TABLE
;
 
	.PSECT	CNVCLIATB,EXE,RD,NOWRT
 
CVTABL:	.ASCII	/0123456789ABCDEF/	; ALL LEGAL NUMERICS
	.PAGE
	.SBTTL	CNVCLIATB - CONVERT ASCII TO SINGLE-PRECISION BINARY
 
;++
; FUNCTIONAL DESCRIPTION:
;
; THIS SUBROUTINE CONVERTS AN ASCII STRING TO A SINGLE PRECISION
; BINARY VALUE. THE STRING IS CONSIDERED AS AN UNSIGNED VALUE.
;
; THE CONVERSION RADIX IS ONE OF THE FOLLOWING:
;
;	LIB$CVTCLIDTB - CONVERT DECIMAL TO BINARY
;	LIB$CVTCLIOTB - CONVERT OCTAL TO BINARY
;	LIB$CVTCLIHTB - CONVERT HEXADECIMAL TO BINARY
;
; CALLING SEQUENCE:
;
;	JSB	LIB$CVTCLIXXX
;
; INPUT PARAMETERS:
;
;	R2,R3 = DESCRIPTOR FOR STRING TO BE CONVERTED
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	R0 = CLI$_NORMAL, SUCCESS:
;
;		R1 CONTAINS THE DOUBLE PRECISION RESULT
;
;	R0 = CLI$_IVCHAR, INVALID NUMERIC CHARACTER FOUND
;	R0 = CLI$_IVVALU, VALUE EXCEEDS A SINGLE PRECISION INTEGER
;
; IMPLICIT OUTPUTS:
;
;	R3 = ADDRESS OF BYTE FOLLOWING LAST CHARACTER OR ONE BYTE
;	     PAST AN INVALID CHARACTER
;
; COMPLETION CODES:
;
;	SEE ABOVE
;
; SIDE EFFECTS:
;
;	R2 IS DESTROYED
;
;--
 
	.ENABL	LSB
 
LIB$CVTCLIDTB::				;
	MOVL	#10,R0			; GET RADIX
	BRB	10$			;
LIB$CVTCLIOTB::				;
	MOVL	#8,R0			; GET RADIX
	BRB	10$			;
LIB$CVTCLIHTB::				;
	MOVL	#16,R0			; GET RADIX
10$:					;
	PUSHR	#^M<R0,R4,R5,R6>	; SAVE REGISTERS AND RADIX
	CLRL	R5			; CLEAR RESULT
20$:					;
	MOVL	#CLI$_NORMAL,R4		; ASSUME SUCCESS
	DECW	R2			; DECREMENT BYTE COUNT
	BLSSU	30$			; IF LSSU DONE
	MOVL	#CLI$_IVCHAR,R4		; ASSUME INVALID CHARACTER
	MOVZBL	(R3)+,R0		; GET NEXT CHARACTER
	BBC	#6,R0,25$		; BRANCH IF 0 - 9
	BBCC	#5,R0,25$		; CONVERT TO UPPER CASE ALPHA
25$:					;
	LOCC	R0,(SP),CVTABL		; SEARCH FOR NUMERIC CHARACTER
	BEQL	30$			; IF EQL INVALID CHARACTER
	SUBL3	R0,(SP),R0		; CONVERT INDEX TO NUMBER
	MOVL	#CLI$_IVVALU,R4		; ASSUME OVERFLOW
	EMUL	R5,(SP),R0,R5		;  COMPUTE BINARY VALUE
	TSTL	R6			; TEST FOR OVERFLOW
	BEQL	20$			; IF EQL CONTINUE
30$:					;
	MOVQ	R4,R0			; GET STATUS AND RESULT
	POPR	#^M<R2,R4,R5,R6>	; RESTORE REGISTERS
	RSB				;
 
	.END
