	.TITLE	CANCEL - CLI COMMAND TO CANCEL SCHEDULED WAKE REQUESTS
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	STARLET DCL CLI
;
; ABSTRACT:	CANCEL SCHEDULED WAKE REQUESTS FOR A PROCESS
;
;
; ENVIRONMENT:	NATIVE MODE USER CODE
;
; AUTHOR:	C. A. MONIA , CREATION DATE:	10-OCT-1977
;
; MODIFIED BY:
;
;	V001	TMH0001		Tim Halvorsen		04-Feb-1982
;		Remove reference to CLI$K_ symbols at assembly time.
;--
	.PAGE
	.SBTTL	DECLARATIONS
;
; MACRO LIBRARY CALLS
;
 
	$CLIDEF				; DEFINE CANCEL RUN PARAMETERS
	$STSDEF				; DEFINE FORMAT OF STATUS LONGWORD
 
;
; MACROS
;
 
	.MACRO	CALLBAK	A1,A2
$$$=2
	.IF NB	A2
	PUSHAB	W^A2
$$$=$$$+1
	.ENDC
	PUSHAB	CLIWORK
	PUSHAB	W^A1
	CALLS	#$$$,@CLI$A_UTILSERV(AP)
	.ENDM
 
;
; CALLBACK FOR VALUE CONVERSION
;
 
	.MACRO	CONVERTVAL	VALDESC
	PUSHAB	@4(AP)
	PUSHAB	W^CLIWORK
	PUSHAB	W^VALDESC
	CALLS	#3,@CLI$A_UTILSERV(AP)
	.ENDM
 
;
; INSERT SEVERITY FIELD IN ERROR CODE
;
 
	.MACRO	INSEV	SEV,LOC
	INSV	#STS$K_'SEV,-	
		#STS$V_SEVERITY,-
		#STS$S_SEVERITY,-
		LOC		
	.ENDM

;
; ERROR MESSAGE CALL FOR SYNTAX ERRORS
;
 
	.MACRO	SYNTAXERR	CODE=R0,DESCR=0
	PUSHL	CODE
	PUSHAB	DESCR
	CALLS	#2,ERREXIT
	.ENDM
 
;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;

	.PSECT	RWDATA	WRT,RD,BYTE
 
;
; DEFINE REQUEST DESCRIPTOR FOR ASCII CONVERSION
;
 
ASCVAL:	$CLIREQDESC	-		;
		ERRACT=ERREXIT,-	; ERROR ACTION ROUTINE
		RQTYPE=CLI$K_ASCIIVAL	; ASCII CONVERSION
 
;
; DEFINE QUALIFIER LIST FOR CANCEL COMMAND
;
 
CANQUAL:				;
	$CLIQUALDESC	-		;
		QDCODE=CLI$K_CANC_IDEN,- ; /IDENTIFICATION QUALIFIER
		TRUACT=IDEN		; ACTION ROUTINE
	$CLIQUALDESC	-		;
		QDCODE=END_LIST		; TERMINATE QUALIFIER LIST
 
;
; DEFINE ARGUMENT BLOCK FOR CALL TO CANCEL SCHEDULED WAKE REQUESTS
;
 
CANWAK:	$CANWAK				;
;		PIDADR=PID,-		; ADDRESS OF PROCESS I/D
;		PRCNAM=PRODESC+CLI$W_RQSIZE ; ADDRESS OF PROCESS NAME DESCR.
 
;
; DEFINE COMMAND QUALFIER DESCRIPTOR
;
 
CMDESC:	$CLIREQDESC	-		;
		ERRACT=ERREXIT,-	; ERROR ACTION ROUTINE
		RQTYPE=CLI$K_GETQUAL,-	; GET COMMAND QUALIFIER
		QUALST=CANQUAL		; ADDRESS OF QUALIFIER DESCRIPTOR
 
;
; ALLOCATE CLI WORK SPACE
;
 
CLIWORK:.BLKB	CLI$K_WORKAREA		;
 
;
; DEFINE REQUEST DESCRTIPTOR TO FIND COMMAND LINE
;
 
CMLDSC:	$CLIREQDESC	-		;
		RQTYPE=CLI$K_GETCMD	;
 
 
;
; DEFINE REQUEST DESCRIPTOR TO INITIALIZE THE PARSE
;
 
INIPRS:	$CLIREQDESC	-		;
		ERRACT=ERREXIT,-	; ERROR ACTION ROUTINE
		RQTYPE=CLI$K_INITPRS	; INITIALIZE THE PARSE
 
;
; DEFINE REQUEST DESCRIPTOR FOR PARSE COMPLETION
;
 
PARSDON:$CLIREQDESC	-		;
		ERRACT=ERREXIT,-	;
		RQTYPE=CLI$K_ENDPRM1	;
 
;
; DEFINE REQUEST DESCRIPTOR BLOCK FOR PROCESS NAME
;
 
PRODESC:$CLIREQDESC	-		;
		ERRACT=ERREXIT,-	; ERROR ACTION ROUTINE
		PRSACT=PROCNAM,-	; ACTION ROUTINE FOR PROCESS NAME
		RQTYPE=CLI$K_INPUT1	; REQUEST TYPE IS INPUT FILE 1
 
;
; ALLOCATE PROCESS I/D LONGWORD
;
 
PID:	.BLKL	1			;
 
;
; ALLOCATE STATUS FLAG LONGWORD
;
 
STSFLG:	.BLKL	1			;
	.PAGE
	.SBTTL	ARGUMENTS FOR ERROR MESSAGE OUTPUT
 
;
; FACILITY NAME
;
 
CANNAM:	.ASCII	/CANCEL/
$$$L=.-CANNAM
 
;
; DESCRIPTOR FOR FACILITY NAME
;
 
FACNAM:	.LONG	$$$L			;
	.LONG	CANNAM			;
 
;
; SYSTEM SERVICE ERROR MESSAGE ARGUMENT LIST
;
 
SRVMSG:	$PUTMSG	FACNAM=FACNAM,-		; FACILITY NAME
		MSGVEC=SRVERR		; MESSAGE VECTOR
 
 
;
; SYSTEM SERVICE ERROR ARGUMENT LIST
;
 
SRVERR:	.LONG	2			;
SRVCOD:	.BLKL	1			; ERROR CODE
	.LONG	0			; NO FAO ARGUMENTS
 
 
	.PAGE
	.SBTTL	CANCEL - CANCEL SCHEDULED WAKE REQUESTS
;++
; FUNCTIONAL DESCRIPTION:
;
; THIS PROCEDURE IS CALLED BY THE CLI INTERPRETER TO CANCEL SCHEDULED
; WAKE REQUESTS FOR A PROCESS
;
;
; CALLING SEQUENCE:
;
;	NONE
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--
 
	.PSECT	CANCEL	NOWRT,EXE
 
CANCEL:					;
	.WORD	0			; NO REGISTERS SAVED
	CALLG	(AP),CAN_PARSCMD	; PARSE INPUT
	$CANWAK_G	CANWAK		; CANCEL SCHEDULED WAKE REQUESTS
	BLBC	R0,CAN_ERR_CANWAK	; IF LBC, ERROR FROM SERVICE
	RET				;
	.PAGE
	.SBTTL	ERROR FROM CANCEL SERVICE
 
;
; ERROR FROM CANCEL SCHEDULED WAKE REQUEST SYSTEM SERVICE
;
 
CAN_ERR_CANWAK:				;
	INSEV	ERROR,R0		; SET SEVERITY TO ERROR
	MOVL	R0,SRVCOD		; SET ERROR CODE
	$PUTMSG_G	SRVMSG		; REPORT SYSTEM SERVICE ERROR
	BLBC	R0,10$			; IF LBC DISPLAY ERROR ON EXIT
	MOVL	SRVCOD,R0		; GET ERROR CODE
	BBSS	#STS$V_INHIB_MSG,R0,10$	; INHIBIT CLI STATUS MESSAGE
10$:					;
	RET				;
	.PAGE
	.SBTTL	PARSE CANCEL COMMAND
 
;
; PARSE CANCEL COMMAND
;
 
CAN_PARSCMD::				;
	.WORD	0			; NULL ENTRY MASK
	CLRL	CANWAK+CANWAK$_PIDADR	; RESET ADDRESS OF PID
	CLRL	CANWAK+CANWAK$_PRCNAM	; RESET ADDRESS OF PROCESS NAME
	CALLBAK	INIPRS			; INITIALIZE RESULT PARSE
	CALLBAK	CMLDSC,STSFLG		; GET COMMAND LINE
	CALLBAK	CMDESC,STSFLG		; GET COMMAND QUALIFIERS
	CALLBAK	PRODESC,STSFLG		; PARSE PROCESS NAME
	CALLBAK	PARSDON			; COMPLETE THE PARSE
	RET				;
	.PAGE
	.SBTTL	PROCNAM - PROCESS NAME PRESENT ROUTINE
 
;
; THIS ROUTINE IS CALLED BY THE RESULT PARSER WHENEVER THE PROCESS
; NAME IS SUPPLIED IN THE CANCEL COMMAND
;
 
PROCNAM:				;
	.WORD	0			; NULL ENTRY MASK
	MOVAB	PRODESC+CLI$W_RQSIZE,CANWAK+CANWAK$_PRCNAM ; POINT TO NAME
	RET				;
	.PAGE
	.SBTTL	ACTION ROUTINES FOR CANCEL SCHEDULED WAKE REQUEST
 
;
; ACTION ROUTINE FOR IDENTIFICATION QUALIFIER
;
 
IDEN:					;
	.WORD	^M<R0,R1,R2>		;
	CONVERTVAL	ASCVAL		; CONVERT AN ASCII VALUE
	PUSHAB	PID			; PUSH ADDRESS OF PROCESS I/D
	PUSHAB	ASCVAL+CLI$W_RQSIZE	; PUSH ADDRESS OF STRING DESCR.
	CALLS	#2,LIB$CVT_HEXBIN	; CONVERT TO BINARY
	BLBC	R0,10$			; REPORT ERROR
	MOVAB	PID,CANWAK+CANWAK$_PIDADR ; POINT TO PROCESS I/D
	RET				;
10$:					;
	SYNTAXERR	R0,ASCVAL	;
	.PAGE
	.SBTTL	ACTION ROUTINE ERROR EXIT HANDLERS
 
;
; ERROR EXIT HANDLER
;
; THIS PROCEDURE IS CALLED TO PRINT AN ERROR MESSAGE AND EXIT WHENEVER A
; SYNTAX ERROR IS DETECTED IN A RUN COMMAND
;
; CALLING SEQUENCE:
;
;	CALL	ERREXIT(DESCRIPTOR,CODE)
;
; PARAMETER DESCRIPTIONS:
;
;	DESCRIPTOR=ADDRESS OF QUALIFIER DESCRIPTOR, CLI$W_RQSIZE IS THE
;	           OFFSET TO A QUAD-WORD STRING DESCRIPTOR FOR THE ERRONEOUS
;		   SUBSTRING.
;
;	CODE=CLI ERROR CODE
;
;
; IMPLICIT INPUTS:
;
;	CMLDSC - COMMAND LINE QUALIFIER DESCRIPTOR
;
;	FACNAME - ADDRESS OF FACILITY NAME
;
 
	.ENABL	LSB
 
ERREXIT:				;
	.WORD	^M<R2>			;
	MOVL	8(AP),R2		; GET ERROR CODE
	INSEV	ERROR,R2		; SET SEVERITY TO ERROR
	PUSHL	R2			; PUSH ERROR CODE
	PUSHAB	CMLDSC			; PUSH ADDRESS OF COMMAND LINE DESCRIPTOR
	PUSHAB	@4(AP)			; PUSH ERROR SEGMENT ADDRESS
	PUSHAB	FACNAM			; PUSH ADDRESS OF FACILITY NAME
	CALLS	#4,LIB$PUTCLIMSG	; OUTPUT CLI MESSAGE
	BLBC	R0,10$			; IF LBC DISPLAY STATUS ON EXIT
ERREXIT1:				;
	MOVL	R2,R0			; GET ERROR CODE
	BBSS	#STS$V_INHIB_MSG,R0,10$	; INHIBIT STATUS DISPLAY
10$:					;
	INSEV	ERROR,R0		; SET APPROPRIATE SEVERITY
	$EXIT_S	R0			; EXIT WITH STATUS
 
	.DSABL	LSB
 
	.END	CANCEL
