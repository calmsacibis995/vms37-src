MODULE setdir	(
		IDENT = 'V03-000',
    		ADDRESSING_MODE(EXTERNAL=GENERAL,
    				NONEXTERNAL=LONG_RELATIVE)
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Set Directory Command
!
! ABSTRACT:
!
!	This module processes the Set Directory command.
!
! ENVIRONMENT:
!
!	Vax native, privileged user mode
!
!--
!
! AUTHOR:	Gerry Smith	 	CREATION DATE: 3-Nov-1981
!
! MODIFIED BY:
!
!	V03-002 GAS0026		Gerry Smith		18-Dec-1981
!		Use shared message file, and lower fatal messages to
!		simple error messages.
!
!	V03-001	GAS0024		Gerry Smith		14-Dec-1981
!		Fix /LOG logic
!
!
!**

LIBRARY	'SYS$LIBRARY:LIB';
LIBRARY 'SYS$LIBRARY:CLIMAC';
LIBRARY 'SYS$LIBRARY:TPAMAC';

STRUCTURE
    BBLOCK [O, P, S, E; N] =
    	[N]
    	(BBLOCK + O)<P,S,E>;

FORWARD ROUTINE
    set$directory,			! Main routine for directory
    set_attributes,			! Routine to set directory attributes
    get_dir;				! Routine to get each directory
    

EXTERNAL ROUTINE
    lib$file_scan,			! Routine to get next directory
    handler,				! Condition handler
    check_privilege : NOVALUE,		! Routine to check for privilege
    confirm_action,			! Routine to ask for confirmation
    search_error,			! Where to go if file search fails
    file_error,				! Where to go if file error occurs
    cli_error,				! Where to go if CLI error
    vrsn_act,				! VERSION_LIMIT action routine
    owner_act,				! OWNER_UIC action routine
    lib$tparse,				! Parser
    sys$fao;				! Formatted ASCII output

!
! External data references
!
EXTERNAL
!
! Data
!
    setfile$flags : BITVECTOR[32],		! Qualifier flags word
    uic_value,					! Owner UIC
    group,					! UIC group number
    member,					! UIC member number
    vrsn_value,					! Version limit
    worst_error,				! Worst error encountered
!
! RMS structures
!
    file_result : VECTOR[nam$c_maxrss,BYTE],	! Resultant name string
    file_expanded : VECTOR[nam$c_maxrss,BYTE],	! Expanded name string
    file_rlf : BBLOCK[nam$c_bln],		! Related file name block
    file_nam : BBLOCK[nam$c_bln],		! File name block
    file_fab : BBLOCK[fab$c_bln],		! File FAB
!
! SET dispatcher externals
!
    set$l_status,			! Status return for SET dispatcher
    set$a_cliwork;			! CLI work area in SET dispatcher

!
! External literal references
!
EXTERNAL LITERAL
    qual_confirm,			! CONFIRM bit
    qual_log,				! LOG bit
    qual_owner,				! OWNER_UIC bit
    qual_vrsn,				! VERSION_LIMIT bit
!
! Error messages
!
    set$_facility,			! Facility code
    set$_closeerr,			! Could not close file
    set$_entered,			! File entered in a directory
    set$_enterr,			! Error entering file
    set$_modified,			! File/directory modified
    set$_notdir,			! Not a directory
    set$_notods2,			! Not an ODS2 structure
    set$_readerr,			! Could not read file
    set$_remerr,			! Could not remove file
    set$_removed,			! Directory entry removed
    set$_writeerr;			! Could not write to file

!
! Literal data definitions
!
LITERAL
    true = 1,
    false = 0;

!
! Define the CLI codes for SET DIRECTORY
!

$CLIQUALCODES  (stdi,
		confirm,
		log,
		owner_uic,
		version_limit);

!
! CLI command qualifier list
!
OWN
    conf_desc:				! CONFIRM
	$CLIQUALDESC(QDCODE = confirm,
		     SETLST = qual_confirm),
    log_desc:				! LOG
	$CLIQUALDESC(QDCODE = log,
		     SETLST = qual_log),
    own_desc:				! OWNER_UIC
	$CLIQUALDESC(QDCODE = owner_uic,
		     TRUACT = owner_act,
		     SETLST = qual_owner,
		     QDFLGS = qdexpa),
    vrsn_desc:				! VERSION_LIMIT
	$CLIQUALDESC(QDCODE = version_limit,
		     TRUACT = vrsn_act,
		     SETLST = qual_vrsn,
		     QDFLGS = qdexpa),
    end_desc:
	$CLIQUALEND,

    qual_desc:				! Block that asks for qualifiers
	$CLIREQDESC(RQTYPE = getqual,
		    RQFLGS = absadr,
		    QUALST = conf_desc,
		    ERRACT = cli_error),

    inp_desc:				! Block that gets next dir spec
	$CLIREQDESC(RQTYPE = input1,
		    RQFLGS = absadr,
		    ERRACT = cli_error);

!
! TPARSE table for directories
!
$INIT_STATE (dir_state,dir_keys);

$STATE	(dirstart,
	(tpa$_octal,,,,group));			! Get the group number

$STATE	(,
	(','));					! Get rid of the comma

$STATE	(,
	(tpa$_octal,tpa$_exit,,,member));	! Get the member number

GLOBAL ROUTINE set$directory (option_block,callback) =
!++
!
! Functional description
!
!	This is the main control module for SET DIRECTORY.  It calls
!	LIB$FILE_SCAN to perform the necessary functions on the directory
!	specified in the call to SET.
!
! Calling sequence
!
!	CALL set$directory(option_block,callback)
!
! Input parameters
!
!	option_block = option descriptor block from the CLI
!	callback = address of CLI callback routine
!
! Output parameters
!	none
!
! Implicit outputs
!	none
!
! Routine value
!	none
!
! Side effects
!	none
!
!--
BEGIN


LOCAL
    status;

!
! Establish a condition handler, that will remember the most severe
! error, for return to the set command dispatcher.
!
ENABLE handler;

!
! Check that the image is running with appropriate privilege.
!
check_privilege();

!
! Get the command qualifiers.  By calling the CLI, the appropriate action
! routine for each qualifier will be called, and the corresponding bit in
! setfile$flags will be set.
!

IF NOT (status = (.callback)(qual_desc,
			     set$a_cliwork,
			     setfile$flags))
THEN SIGNAL_STOP(.status);

!
! For each directory specified, find the directory and perform the
! operations requested.
!

file_fab[fab$l_ctx] = false;		! Initialize for get_dir routine
inp_desc[cli$v_concatinp] = true;

WHILE get_dir(.callback)
DO
    BEGIN
    lib$file_scan(			! For each directory found
		  file_fab,		! Use this fab
		  set_attributes,	! Go here if directory found
		  search_error)		! Or here if error
    END;

!
! Retrieve the worst error and return to the SET command dispatcher.
!
set$l_status = .worst_error OR sts$m_inhib_msg;
RETURN true;
END;

ROUTINE set_attributes (fab) =
!++
!
! This is the routine that accesses the directory and sets the
! specified attributes.  If an error occurs while attempting to set
! the attributes, a message telling the user is issued, and any other
! directories are processed.
!
!--
BEGIN

MAP
    fab : REF BBLOCK;			! Define the fab

LOCAL
    atr : BLOCKVECTOR[3,8,BYTE],	! Attribute control block
    ptr,				! Pointer to attribute block
    status,				! Status return
    channel : WORD,			! Channel number
    desc : BBLOCK[dsc$c_s_bln],		! General descriptor
    dir_desc : BBLOCK[dsc$c_s_bln],	! Descriptor for directory
    string : VECTOR[nam$c_maxrss,BYTE],	! String to build directory
    fib : BBLOCK[fib$c_extdata],	! A FIB for the QIO 
    header : BBLOCK[512],		! The file header
    iosb : VECTOR[4,WORD];		! I/O status block

BIND
    recattr = header[fh2$w_recattr] : BBLOCK[atr$s_recattr],

    nam = .fab[fab$l_nam] : BBLOCK;	! Define the name block

!
! Build a descriptor to point to the directory.
!
CH$MOVE(.nam[nam$b_rsl],			! Move this many characters
	.nam[nam$l_rsa],			! From the nam
	string);				! To the temp string
dir_desc[dsc$a_pointer] = string;		! Point to the string
dir_desc[dsc$w_length] = .nam[nam$b_rsl];	! Fill in the size
!
! Move the final bracket over, to get the name of the final directory
!
string[.nam[nam$b_dev]
       + .nam[nam$b_dir]
       + .nam[nam$b_name]] = .string[.nam[nam$b_dev]
				     + .nam[nam$b_dir]
				     - 1] ;
string[.nam[nam$b_dev]	
       + .nam[nam$b_dir] - 1] = '.' ;	! Replace bracket with dot
dir_desc[dsc$w_length] = .nam[nam$b_dev]
			+ .nam[nam$b_dir]
			+ .nam[nam$b_name] + 1;	! Update string length
!
! If the directory looks like [000000.something], get rid of the leading
! zeroes and dot.
!
IF NOT CH$FAIL(status = CH$FIND_SUB(.dir_desc[dsc$w_length],
				    .dir_desc[dsc$a_pointer],
				    6,
				    UPLIT('000000')))

THEN
    BEGIN
    CH$MOVE(.dir_desc[dsc$w_length] - (.status + 7 - .dir_desc[dsc$a_pointer]),
	    .status + 7,
	    .status);
    dir_desc[dsc$w_length] = .dir_desc[dsc$w_length] - 7;
    END;
    
!
! Assign a channel to the file's device
!
desc[dsc$w_length] = .nam[nam$b_dev];	! Set up the descriptor
desc[dsc$a_pointer] = .nam[nam$l_dev];	! to point to the device name

IF NOT (status = $ASSIGN(
    			DEVNAM = desc,
    			CHAN = channel))
THEN 
    BEGIN
    file_error( set$_facility^16 + shr$_openin + sts$k_error,
		.status,.fab);		! Tell user why the assign failed
    RETURN true;			! And continue with other files
    END;

!
! Access the directory, reading its header
!
desc[dsc$w_length] = fib$c_extdata;	! Re-use descriptor to point to FIB
desc[dsc$a_pointer] = fib;

CH$FILL(0,fib$c_extdata,fib);		! Zero out the FIB

fib[fib$l_acctl] = fib$m_write OR	! Set up the FIB
    		   fib$m_noread OR
    		   fib$m_nowrite;

fib[fib$w_fid_num] = .nam[nam$w_fid_num];	! Put in the file id
fib[fib$w_fid_seq] = .nam[nam$w_fid_seq];
fib[fib$w_fid_rvn] = .nam[nam$w_fid_rvn];

atr[0,atr$w_type] = atr$c_header;		! Get the file header
atr[0,atr$w_size] = atr$s_header;
atr[0,atr$l_addr] = header;
atr[1,0,0,32,0] = 0;

status = $QIOW(	CHAN = .channel,		! Access the file, reading
		FUNC = IO$_ACCESS,		! the file header
		IOSB = iosb,
		P1 = desc,
		P5 = atr);
IF .status THEN status = .iosb[0];
IF NOT .status 					! If there was an error,
THEN file_error(set$_readerr,.status,.fab)	! tell the user what it was
ELSE
!
! If /CONFIRM was set, ask the user if the modifications are to be made.
!
IF
    BEGIN
    IF .setfile$flags[qual_confirm]
    THEN confirm_action($descriptor('modify directory'),
				    dir_desc)
    ELSE true
    END
THEN
    BEGIN
!
! Check to see that this is a directory file.  This check is only good for
! an ODS2 volume.
!
    IF .header[fh2$b_struclev] EQL 2 
    AND NOT .header[fh2$v_directory]
    THEN
	BEGIN
	desc[dsc$w_length] = .nam[nam$b_rsl];
	desc[dsc$a_pointer] = .nam[nam$l_rsa];
	SIGNAL (set$_writeerr,				! Error writing to
		1,
		desc,					! This file
		set$_notdir);				! For this reason
	END
    ELSE
	BEGIN
!
! See what qualifiers were set.  Build the attribute control blcok
! as we go.
!
	ptr = 0;			! Nothing on control block

	IF .setfile$flags[qual_vrsn]
	THEN
	    BEGIN

!
! Change the version limit for the directory
!
	    atr[0,atr$w_type] = atr$c_recattr;
	    atr[0,atr$w_size] = atr$s_recattr;
	    atr[0,atr$l_addr] = recattr;
	    recattr[fat$w_versions] = .vrsn_value;
	    ptr = .ptr + 1;				! Update the pointer
	    END;

	If .setfile$flags[qual_owner]
	THEN
	    BEGIN
!
! Change the owner uic
!
	    atr[.ptr,atr$w_type] = atr$c_uic;
	    atr[.ptr,atr$w_size] = atr$s_uic;
	    atr[.ptr,atr$l_addr] = uic_value;
	    ptr = .ptr + 1;				! Update the pointer
	    END;

!
! Look to see if the pointer has been changed.  If yes, modify the header.
!
	IF .ptr NEQ 0
	THEN
	    BEGIN
	    atr[.ptr,0,0,32,0] = 0;		! Put a zero at end of list
	    status = $QIOW( CHAN = .channel,	! Make the modifications
			    FUNC = IO$_MODIFY,
			    IOSB = iosb,
			    P1 = desc,
			    P5 = atr);
	    IF .status THEN status = .iosb[0];
	    IF NOT .status			! If the modify failed, tell user
	    THEN file_error(set$_writeerr,.status,.fab)
	    ELSE
		IF .setfile$flags[qual_log]		! If /LOG, tell user
		THEN SIGNAL(set$_modified,1,dir_desc);
	    END;				! End of header modify
	END;					! End of qualifier scan
!
! Deassign the channel
!
    IF NOT (status = $DASSGN(CHAN = .channel))
    THEN file_error(set$_closeerr, .status, .fab);
    END;

RETURN true;
END;

ROUTINE get_dir (callback) =
!++
!
! This routine is called to get each directory spec from the command line.
! When there are no more directories, a value of FALSE is returned.
!
!--
BEGIN

LOCAL
    desc : BBLOCK[dsc$c_s_bln],		! General descriptor
    endchar : BYTE,			! End-of-directory character
    eos,				! End of string
    ptr,				! Pointer in string
    str_ptr,				! Pointer to rest of string
    str_len,				! Length of rest of string
    temp_string : VECTOR[nam$c_maxrss,BYTE],	! The temporary string
    temp,
    status;
!
! Check to see if there are any more directory specs
!
IF NOT .inp_desc[cli$v_concatinp]	! If no more directories
THEN 
    IF NOT .file_fab[fab$l_ctx]		! And no outstanding ellipses
	THEN RETURN false;		! Then return and say all done.

!
! Check here to see if a trailing ellipse is being treated.  If so,
! then FILE_FAB[FAB$L_CTX] will be set to TRUE, and there's no need
! to search and see if such a trailing ellipse is present.  However,
! if the value is set to FALSE, then this is a new directory spec.
!
IF NOT .file_fab[fab$l_ctx]		! If not processing an ellipse
THEN
    BEGIN
    (.callback)(inp_desc,		! then get the next directory
		set$a_cliwork,
		setfile$flags);

    file_fab[fab$l_fna] = .inp_desc[cli$a_rqaddr];
    file_fab[fab$b_fns] = .inp_desc[cli$w_rqsize];

!
! Since this is a new entry, it must be checked for a trailing ellipse.
!
    CH$MOVE( .inp_desc[cli$w_rqsize],	! Move this many chars
	     .inp_desc[cli$a_rqaddr],	! From the CLI area
	     temp_string);			! To the temp string
    str_ptr = temp_string;		! Set up pointer
    str_len = .inp_desc[cli$w_rqsize];	! and length.
!
! Look for ellipses.
!
    WHILE NOT CH$FAIL(temp = CH$FIND_SUB(.str_len, .str_ptr, 3, UPLIT('...')))
    DO
	BEGIN
	str_ptr = .temp + 3;		! Update pointer
	str_len = .str_len - (.temp - .str_ptr) - 3;
	END;
!
! After the final ellipse, check to see if it is at the end of the
! directory specification.  If so, then change the context field of
! the fab, and insert an end bracket at the beginning of the ellipse.
!
    IF (.str_ptr EQL temp_string + .file_fab[fab$b_fns] -1)
    THEN
	BEGIN
	file_fab[fab$l_ctx] = true;		! Show that there's a trailing ellipse
	CH$WCHAR(..str_ptr, .str_ptr - 3);	! Put the end bracket in place
	file_fab[fab$l_fna] = temp_string;	! Set up FAB fields
	file_fab[fab$b_fns] = .str_ptr - 3 - temp_string + 1;
	END;
    END
!
! If here, then the trailing ellipse has been processed, and this is the 
! second time thru.  Restore the original file name.
!
ELSE
    BEGIN
    file_fab[fab$l_fna] = .inp_desc[cli$a_rqaddr];	! Original filename
    file_fab[fab$b_fns] = .inp_desc[cli$w_rqsize];	! Original length
    file_fab[fab$l_ctx] = false;			! Ellipse processed
    END;

!
! Parse the input string
!
$NAM_INIT (NAM = file_rlf);				! Re-init the RLF
IF (.file_nam[nam$b_dev] NEQ 0)				! If a device was
THEN							! specified, then
    BEGIN
    file_fab[fab$l_dna] = .file_nam[nam$l_dev];		! Make device sticky
    file_fab[fab$b_dns] = .file_nam[nam$b_dev];
    END;
IF NOT (status = $PARSE(FAB = file_fab))
THEN
    BEGIN
    desc[dsc$w_length] = .file_fab[fab$b_fns];
    desc[dsc$a_pointer] = .file_fab[fab$l_fna];
    SIGNAL_STOP(set$_facility^16 + shr$_syntax + sts$k_error,
		1, desc, .status);
    END;

!
! Check the parsed string for legality, i.e. nothing after the directory
!
IF (.file_nam[nam$b_name] NEQ 0 OR
    .file_nam[nam$b_type] NEQ 1 OR
    .file_nam[nam$b_ver]  NEQ 1 )
THEN 
    BEGIN
    desc[dsc$w_length] = .file_fab[fab$b_fns];
    desc[dsc$a_pointer] = .file_fab[fab$l_fna];
    SIGNAL_STOP(set$_facility^16 + shr$_syntax + sts$k_error,
		1, desc, ss$_badirectory);
    END;
!
! Determine what the directory terminator character was, and save it.
!
endchar = .(.file_nam[nam$l_dir] + .file_nam[nam$b_dir] - 1);

!
! The directory string must now be analyzed and manipulated so that the
! final directory entry becomes a file.  First, initialize some pointers.
!
desc[dsc$w_length] = .file_nam[nam$b_esl] - 2;
desc[dsc$a_pointer] = .file_nam[nam$l_esa];
str_ptr = .desc[dsc$a_pointer];
str_len = .desc[dsc$w_length];
ptr = 0;
eos = .desc[dsc$a_pointer] + .desc[dsc$w_length] -1;

!
! Look for wildcard ellipses
!
WHILE NOT CH$FAIL(temp = CH$FIND_SUB(.str_len, .str_ptr, 3, uplit('...')))
DO
    BEGIN
!
! Make PTR point to the beginning of the "...", and advance the string
! pointer to the character just past the "..."
!
    ptr = .temp;
    str_len = .str_len - (.temp - .str_ptr) -3;
    str_ptr = .temp + 3;
    END;

!
! If there was any occurrence of "...", point just past it.
!
IF .ptr NEQ 0 THEN ptr = .ptr + 3;

!
! Find the last directory in the specification
!
WHILE NOT CH$FAIL( temp = CH$FIND_CH(.str_len, .str_ptr, '.'))
DO
    BEGIN
!
! Make PTR point to the ".", and advance the string pointer to 
! the first character after the "."
!
    ptr = .temp;
    str_len = .str_len - (.temp - .str_ptr) - 1;
    str_ptr = .temp + 1;
    END;

IF .ptr NEQ 0
THEN
    BEGIN
!
! If here, then either a trailing ellipse, or a final sub-directory
! was specified.  If the pointer is at the bracket, then there is a 
! trailing ellipse, in which case only a "*" is required.
!
    IF .ptr EQL .eos
    THEN
	BEGIN
	CH$A_WCHAR('*', ptr);		! Stick an asterisk after the bracket.
	ptr = .ptr + 1;			! Adjust the pointer.
	END
!
! If the pointer is inside the bracket, then the last directory name
! must be moved out of the brackets.
!
    ELSE
	BEGIN
!
! Check to see if the directory is [main.sub] or [main...sub]
!
	IF .ptr EQLU .str_ptr
	THEN
	    BEGIN			! [main...sub] form
	    str_len = .eos - .ptr;
	    CH$MOVE(.str_len, .ptr, .ptr+1);
	    CH$WCHAR(.endchar, .ptr);
	    ptr = .ptr + .str_len + 1;
	    END				! end of [main...sub] processing
	ELSE
	    BEGIN			! [main.sub] form
	    str_len = .eos - .str_ptr;
	    CH$WCHAR_A(.endchar, ptr);
	    ptr = .ptr + .str_len;
	    END;			! end of [main.sub] processing
	END;
    END					! End of non-zero pointer stuff
ELSE
    BEGIN
!
! If the pointer is still zero, then there is either a wildcard, a main
! directory, or a [g,m] directory.  In all such cases, a main directory
! of [000000] must be fabricated.
!
    status = CH$FIND_CH(.str_len, .str_ptr, ',');	! Save for later
!
! Move the string out seven spaces and insert "000000]"
!
    str_ptr = .desc[dsc$a_pointer] + .file_nam[nam$b_dev] + 1;
    temp = CH$MOVE(.eos - .str_ptr, .str_ptr, .str_ptr + 7);
    str_ptr = CH$MOVE(6, UPLIT('000000'), .str_ptr);
    CH$MOVE(1, endchar, .str_ptr);
!
! If no comma was found, then all that is required is to update the 
! pointer.
!
    IF CH$FAIL(.status) THEN ptr = .temp
!
! Otherwise, it's a [g,m] directory.  Convert it.
!
    ELSE
	BEGIN

	LOCAL tparse_block : BBLOCK[tpa$k_length0];	! Define a TPARSE block

	CH$FILL(0, tpa$k_length0, tparse_block);	! Zero it.
	tparse_block[tpa$l_count] = tpa$k_count0;	! Fill in size

	tparse_block[tpa$l_stringcnt] = .eos - .str_ptr;
	tparse_block[tpa$l_stringptr] = .str_ptr + 7;
	IF NOT (status = lib$tparse(tparse_block,
				    dir_state,
				    dir_keys))
	THEN SIGNAL_STOP(.status)
	ELSE
	    BEGIN
	    LOCAL temp_desc : BBLOCK[dsc$c_s_bln];
	    temp_desc[dsc$w_length] = 6;
	    temp_desc[dsc$a_pointer] = .str_ptr + 7;
	    IF NOT (status = SYS$FAO($descriptor('!2(3OW)'),
				     temp_desc,
				     temp_desc,
				     .group,
				     .member))
	    THEN SIGNAL_STOP(.status)
	    ELSE ptr = .str_ptr + 14;
	    END;
	END;
    END;
ptr = CH$MOVE(4, UPLIT('.DIR'), .ptr);
file_fab[fab$b_fns] = .ptr - .desc[dsc$a_pointer];
file_fab[fab$l_fna] = .desc[dsc$a_pointer];

RETURN true;
END;

END
ELUDOM
