	.TITLE	SETDAY - SET DAY TYPE COMMAND
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	PRIVILEGED IMAGE
;
; ABSTRACT:
;
;	This image implements the set day command
;
; ENVIRONMENT:
;
;	This image runs in user mode but requires the privilege to
; change mode to kernel to set the flag bits in system space.
; The user is required to have oper privilege.
;
; AUTHOR: Herb Jacobs, CREATION DATE: 12-Dec-1981
;
; MODIFIED BY:
;
;--

	.SBTTL	DECLARATIONS
	.DEFAULT DISPLACEMENT,WORD
;
; INCLUDE FILES:
;
	$CLIDEF				;CLI DEFINITIONS
	$PRVDEF				;PRIVILEGE BIT DEFINITIONS
;
; MACROS:
;
;
	.MACRO	CALLBAK A1,A2
	.IF NB	A2
	PUSHAB	A2
	.IFF
	PUSHAB	W^WORK_L_BITLIS		; address of bit list (in case needed)
	.ENDC
	PUSHAB	W^SET$A_CLIWORK		; CLI common work area
	PUSHAB	A1
	CALLS	#3,@CLI$A_UTILSERV(AP)	; request the service of the CLI
	.ENDM

;
; EQUATED SYMBOLS:
;
;
; OWN STORAGE:
;
	.PSECT	SET$RWDATA	LONG,RD,WRT,NOEXE

;
; DEFINE THE CALL BACK DATA STRUCTURES
;
INPDESC:
	$CLIREQDESC	-
		RQTYPE=CLI$K_GETQUAL,-	;GET QUALIFIERS
		QUALST=INPQUAL		;QUALIFIER LIST
INPQUAL:
	$CLIQUALDEF	-
		QDCODE=CLI$K_STDY_PRIM,- ; /PRIMARY
		TRUACT=SETPRIMARY
	$CLIQUALDEF	-
		QDCODE=CLI$K_STDY_SECO,- ; /SECONDARY
		TRUACT=SETSECONDARY
	$CLIQUALDEF	-
		QDCODE=CLI$K_STDY_DEFA,- ; /DEFAULT
		TRUACT=SETDEFAULT

	$CLIQUALDEF	END_LIST	;END OF QUALIFIER LIST
WORK_L_BITLIS:
	.BLKL	1			; BIT LIST

	.SBTTL	SET DAY description
;++
; FUNCTIONAL DESCRIPTION:
;
;	This command is provided so that a system manager or operator
;	can indicate to VMS that today is not a normal day, but rather
;	a special day, such as a holiday.  This information is used
;	in conjunction with login to allow control over system access
;	on an individual user basis.
;
; CALLING SEQUENCE:
;
;	NONE
;
; INPUT PARAMETERS:
;
;	CLI$A_UTILSERV(AP)	=	CALL BACK ADDRESS FOR RESULT PARSER
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	EXE$GL_FLAGS is updated to represent new state of DAY
;
; COMPLETION CODES:
;
;	NOPRIV - If user missing OPER privilege
;
; SIDE EFFECTS:
;
;	NONE
;
;--

	.PSECT	SET$CODE BYTE,RD,NOWRT,EXE


	.ENTRY	-
SET$DAY,	^M<R2,R3,R4,R5>		; ENTRY MASK

;
; Parse qualifiers and return associated values
;

	MOVZWL	#SS$_NOOPER,R0		; assume no privilege
	BBC	#PRV$V_OPER,G^CTL$GQ_PROCPRIV,10$; branch if not privileged
	CALLBAK	<INPDESC>		; process the qualifiers
					; set status from action routines
10$:	MOVL	R0,SET$L_STATUS		; save status
	RET

	.SBTTL	ACTION ROUTINES FOR QUALIFIERS
;++
; FUNCTIONAL DESCRIPTION:
;
;	THESE ROUTINES ARE CALLED BY THE RESULT PARSER WHEN
; THE APPROPRIATE QUALIFIERS ARE FOUND.
;
; CALLING SEQUENCE:
;
;	CALLG	ARGLIST,SETPRIMARY
;	CALLG	ARGLIST,SETSECONDARY
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	EXE$GL_FLAGS is updated
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--
	.ENABLE	LSB

SETPRIMARY:
	.WORD	^M<>
	$CMKRNL_S B^10$			;set day as primary
	RET

10$:	.WORD	^M<>
	BBSC	#EXE$V_EXPLICITP,G^EXE$GL_FLAGS,20$ ;0= primary day
20$:	BBSS	#EXE$V_EXPLICITS,G^EXE$GL_FLAGS,30$ ;Set activated bit
30$:	MOVZWL	#SS$_NORMAL,R0		;success indication
	RET

SETSECONDARY:
	.WORD	^M<>
	$CMKRNL_S B^40$			;set day as secondary
	RET

40$:	.WORD	^M<>
	BBSS	#EXE$V_EXPLICITP,G^EXE$GL_FLAGS,50$ ;1= secondary day
50$:	BRB	20$			;set activate flag and return

SETDEFAULT:
	.WORD	^M<>
	$CMKRNL_S B^60$			;set day back to default mode
	RET

60$:	.WORD	^M<>
	BBCC	#EXE$V_EXPLICITS,G^EXE$GL_FLAGS,70$ ;clear activated bit
70$:	MOVZWL	#SS$_NORMAL,R0		;success indication
	RET

	.DISABLE LSB

	.END
