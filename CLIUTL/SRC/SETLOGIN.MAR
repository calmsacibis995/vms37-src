	.TITLE	SETLOGIN - SET SYSTEM LOGIN PARAMETERS
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	CLI UTILITY "SET"
;
; ABSTRACT:	SET THE SYSTEM LOGIN PARAMETERS
;
; AUTHOR:	LEN KAWELL 10-MAY-1978
;
; MODIFIED BY:
;
;	V002	TMH0002		Tim Halvorsen		04-Feb-1982
;		Remove reference to CLI$K_ symbols at assembly time.
;--
;
; LOCAL MACROS
;
	.MACRO	CALLBAK	P1,P2
	.IF B <P2>
	PUSHAB	W^BITS			; ADDRESS OF BIT MASK
	.IFF
	PUSHAB	P2			; ADDRESS OF THIRD ARGUEMENT
	.ENDC
	PUSHAB	W^SET$A_CLIWORK	; COMMON WORK AREA
	PUSHAB	P1			; THE REQUEST DESCRIPTOR
	CALLS	#3,@CLI$A_UTILSERV(AP)	; REQUEST THE SERVICE OF THE CLI
	.ENDM				;
 
;
; EXTERNAL SYMBOLS
;
	$PRVDEF				; DEFINE PRIVILEGES
	$UCBDEF				; DEFINE UCB
;
; LOCAL STORAGE
;
	.PSECT	SET$RWDATA,NOEXE,LONG
 
BITS:	.LONG	0			; BIT ARRAY FOR CLI CALL BACK

NUM_VAL:
	$CLIREQDESC	RQTYPE=CLI$K_NUMERVAL

QUAL_REQ:
	$CLIREQDESC	RQTYPE=CLI$K_GETQUAL,QUALST=QUALS
QUALS:

INTE_QUAL:
	$CLIQUALDESC	QDCODE=CLI$K_STLG_INTE,TRUACT=INTERACTIVE,-
			QDFLGS=<CLI$V_QDEXPA>
	$CLIQUALDESC	END_LIST

	.PAGE
	.PSECT	SET$CODE,NOWRT
;++
; SET$LOGIN - SET SYSTEM LOGIN PARAMETERS
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE SETS THE SYSTEM LOGIN COUNTS.
; IT IS ENTERED AS A RESULT OF A DISPATCH FROM THE SET COMMAND PROCESSOR.
;
; INPUTS:
;
;	THE CLI DATA BASE IS SET UP. 
;
; OUTPUTS:
;
;	SET$L_STATUS = STATUS OF THE OPERATION
;--
SET$LOGIN::				; SET LOGIN PARAMETERS
	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9>
	MOVZWL	#SS$_NORMAL,W^SET$L_STATUS; ASSUME SUCCESS
	CALLBAK	W^QUAL_REQ		; CALL CLI FOR QUALIFIERS
	RET				; RETURN
;
; ACTION ROUTINES
;

;
; ACTION ROUTINE TO SET NUMBER OF INTERACTIVE USERS
;
INTERACTIVE:
	.WORD	0
	MOVZWL	#SS$_NOPRIV,W^SET$L_STATUS; ASSUME NO PRIVILEGE
	BBC	#PRV$V_OPER,G^CTL$GQ_PROCPRIV,60$; BR IF NOT PRIVILEGED
	MOVZWL	#SS$_NORMAL,W^SET$L_STATUS ; SET SUCCESS
	TSTW	W^CLI$W_QDVALSIZ+INTE_QUAL; ANY VALUE
	BEQL	10$			; IF EQL THEN OUTPUT MESSAGE ONLY
	CALLBAK	W^NUM_VAL,W^INTE_QUAL	; GET THE VALUE
	TSTW	W^NUM_VAL+CLI$L_RQVALU+2; VALUE IN RANGE?
	BNEQ	50$			; IF NEQ THEN NO
	$CMKRNL_S 100$			; CONTINUE
;
; OUTPUT MESSAGE
;
10$:	MOVZWL	G^SYS$GW_IJOBCNT,-(SP)	; OUTPUT COUNT
	MOVZWL	G^SYS$GW_IJOBLIM,-(SP)	; OUTPUT CURRENT LIMIT
	PUSHL	S^#SET$_LOGIN		;
	PUSHAB	4(SP)			;
	CALLS	#3,W^SHOW$PRINT_MSG	; OUTPUT
	RET
;
; BAD PARAMETER VALUE
;
50$:	MOVZWL	#SS$_BADPARAM,W^SET$L_STATUS; SET UP EXIT STATUS
60$:	RET
;
; KERNEL MODE HANDLER TO CHANGE LOGIN LIMIT
;
100$:	.WORD	^M<R2,R3,R4,R5>
	MOVW	W^NUM_VAL+CLI$L_RQVALU,G^SYS$GW_IJOBLIM
	BEQL	110$			; IF EQL THEN CONTINUE
	BICW	#^X08000,G^SYS$GL_JOBCTLMB+UCB$W_DEVSTS; ENABLE TERMINALS
110$:	MOVL	#1,R0
	RET

	.END
