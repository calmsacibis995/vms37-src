	.TITLE	RUNPARSFIL - PARSE A FILE NAME STRING
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	STARLET DCL CLI
;
; ABSTRACT:	FORM A FULLY QUALIFIED FILE STRING
;
;
; ENVIRONMENT:	NATIVE MODE USER CODE
;
; AUTHOR:	C. A. MONIA, CREATION DATE: 27-SEP-1977
;
; MODIFIED BY:
;
;	01	RIH0017		Richard I. Hustvedt	11-JUN-1979
;		Strip trailing semi-colon from expanded name string so that
;		known files can be correctly referenced.  A semi-colon in
;		the input name string for the image activator will cause
;		the known file table to be ignored.
;--
	.PAGE
	.SBTTL	DECLARATIONS
 
;
; MACROS
;
; DEFINE DATA STRUCTURE
;
 
	.MACRO	$DSECT
	.PSECT	$ABS$,ABS
.=0
	.ENDM
 
;
; EQUATED SYMBOLS:
;
; ARGUMENT LIST OFFSETS
;
 
	$DSECT
 
	.BLKL	1
INFIL:	.BLKL	1			; ADDRESS OF INPTUT FILE STRING DESCRIPTOR
OUTFIL:	.BLKL	1			; ADDRESS OF OUTPUT FILE STRING DESCRIPTOR
DFNAM:	.BLKL	1			; ADDRESS OF DEFAULT NAME STRING
	
;
; OWN STORAGE:
;
 
	.PSECT	RUN_PARSFILDAT
 
;
; FILE ACCESS BLOCK (FAB)
;
 
FAB:	$FAB	-			;
		NAM=NAM			; ADDRESS OF NAMEBLOCK
 
;
; NAME BLOCK
;
 
NAM:	$NAM	-			;
		RSS=64,-		; LENGTH OF RESULTANT STRING
		ESS=64			; LENGTH OF EXPANDED STRING
	.PAGE
	.SBTTL	RUN_PARSFIL - PARSE AN EXECUTABLE FILE SPEC.
;++
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE IS CALLED BY THE RUNDET CLI COMMAND PROCESSOR TO EXPAND
; THE EXECUTABLE FILE SPECIFICATION.
;
; CALLING SEQUENCE:
;
;	CALL RUN_PARSFIL( INFIL,OUTFIL,DFNM )
;
; INPUT PARAMETERS:
;
;	INFIL = ADDRESS OF INPUT FILE STRING DESCRIPTOR. THE STRING RE-
;		PRESENTS THE FILE NAME AS TYPED BY THE USER.
;
;	OUTFIL = ADDRESS OF OUTPUT FILE STRING DESCRIPTOR. AT LEAST
;		 64 BYTES OF STORAGE MUST BE ALLOCATED TO CONTAIN
;		 THE NAME STRING.
;
;	DFNM = ADDRESS OF DEFAULT FILE NAME DESCRIPTOR
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	R0 LBS = SUCCESS
;
;	FIRST WORD OF OUTPUT FILE STRING DESCRIPTOR CONTAINS THE
;	LENGTH OF THE FULLY QUALIFIED NAME STRING.
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	R0 = 1, SUCCESSFUL COMPLETION
;
;	R0 = 0, A STRING LENGTH EXCEEDED 64 BYTES
;
;	R0 = COMPLETION CODES RETURNED BY RMS ROUTINE SYS$PARSE
;
; SIDE EFFECTS:
;
;	NONE
;
;--
 
RUN_PARSFIL::				;
	.WORD	^M<R1,R2>		; SAVE R1 - R2
	CLRL	R0			; ASSUME BAD PARAMETERS
	MOVQ	@INFIL(AP),R1		; GET STRING DESCRIPTOR FOR INPUT FILE
	CMPW	R1,#64			; CHECK LENGTH
	BGTRU	10$			; IF GTRU ERROR
	MOVB	R1,FAB+FAB$B_FNS	; SETUP SIZE OF INFILE NAME STRING
	MOVL	R2,FAB+FAB$L_FNA	; SETUP ADDRESS OF STRING
	MOVQ	@DFNAM(AP),R1		; GET DESCRIPTOR FOR DEFAULT NAME STRING
	CMPW	R1,#64			; CHECK SIZE OF STRING
	BGTRU	10$			; IF GTRU ERROR
	MOVB	R1,FAB+FAB$B_DNS	; SETUP SIZE OF DEFAULT NAME STRING
	MOVL	R2,FAB+FAB$L_DNA	; SETUP ADDRESS OF STRING
	MOVQ	@OUTFIL(AP),R1		; GET ADDRESS OF OUTPUT BUFFER IN R2
	MOVL	R2,NAM+NAM$L_RSA	; SET ADDRESS OF RESULTANT STRING
	MOVL	R2,NAM+NAM$L_ESA	; SET ADDRESS OF EXPANDED STRING
	CLRQ	-(SP)			; NO AST'S WANTED
	MOVAL	FAB,-(SP)		; PUSH ADDRESS OF FAB
	CALLS	#3,@#SYS$PARSE		; PARSE INFILENAME
	BLBC	R0,20$			; IF LBC, ERROR
	MOVZBL	NAM+NAM$B_ESL,R1	; GET LENGTH OF STRING
	CMPB	#^A/;/,-(R2)[R1]	; IS LAST CHARACTER A SEMICOLON?
	BNEQ	10$			; NO, EXPLICIT VERSION
	DECL	R1			; YES, DROP NULL VERSION NUMBER
10$:	MOVW	R1,@OUTFIL(AP) 		; SET LENGTH OF STRING
20$:					;
	RET				;
 
	.END
