	.TITLE	SET$OPT_DISP - SET OPTION DISPATCHER
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SET COMMAND
;
; ABSTRACT:	SET COMMAND OPTION DISPACTHER :
;			THIS MODULE DECODES THE OPTION SPECIFIED IN THE
;			COMMAND LINE AND DISPATCHES TO THE APPROPRIATE
;			ROUTINE TO PROCESS THE OPTION.
;			SEE FUNCTIONAL DESCRIPTION BELOW FOR A SPECIFICATION
;			OF THE CALLING INTERFACE.
;
;
; ENVIRONMENT:
;
;	RUNS IN USER MODE BUT REQUIRES PRIVILEGE TO CHANGE MODE TO KERNEL
;	FOR CERTAIN SET COMMAND OPTIONS.
;
;
;	AUTHOR: BEATRICE U. WALTHER,	CREATION DATE 10-OCT-1977
;
; MODIFIED BY:
;
;	V007	HRJ0037		Herb Jacobs	13-Dec-1981
;		Add SET DAY command.
;
;	V006	GAS0010		Gerry Smith	22-Sep-1981
;		Add SET DIRECTORY command.
;
;	V005	GAS0007		Gerry Smith	08-Sep-1981
;		Add SET FILE command.
;
;	V004	TMH0001		Tim Halvorsen	21-Jan-1980
;		Undo some of V003 except for having SET PROT dispatch to SETPRO
;		rather than SETDEV (SET PROT/DEV).  SETPRO subsequently
;		dispatches to SETDEV.
;
;	V003	TMH0001		Tim Halvorsen	05-Dec-1979
;		Fix qualifier table so that set$prot is not in the option table
;		Add SET PROTECTION command.
;
;	V0102	CNH0010		Chris Hume	13-Jul-1979	11:00
;		Added SET PASSWORD qualifier.
;
;	V0101	LMK0001		LEN KAWELL	2-MAR-79
;		ADDED SET TIME QUALIFIER.
;--

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;

	$CLIDEF				; CLI DEFINITIONS

;
; MACROS:
;

;
; OWN STORAGE :
;

	.PSECT	SET$RWDATA	LONG,RD,WRT,NOEXE
;
;
; DEFINE CLI CALL BACK DATA STRUCTURES
;

SET$A_CLIWORK::				; CLI RESULT PARSE WORK AREA
	.BLKB	CLI$K_WORKAREA
	.ALIGN	LONG
SET_INITPRS:				; CLI REQUEST DESCRIPTOR BLOCK
	$CLIREQDESC	-		; FOR INIT PARSE
		RQTYPE=CLI$K_INITPRS
SET_OPTDESC:				; CLI REQUEST DESCRIPTOR BLOCK
	$CLIREQDESC	-		; FOR GET OPTION
		RQTYPE=CLI$K_GETOPT,-	;
		QUALST=SET_OPTQUAL	; POINT TO LIST OF OPTIONS
SET_OPTQUAL:				; DEFINE LIST OF OPTIONS
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_ACCO,-	; SET ACCOUNTING
		TRUACT=SET$ACCOUNTING	;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_PRIN,-	; SET PRINTER
		TRUACT=SET$PRINT	;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_QUEU,-	; SET QUEUE
		TRUACT=SET$QUEUE	;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_TERM,-	; SET TERMINAL
		TRUACT=SET$TERM		;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_WORK,-	; SET WORKING_SET
		TRUACT=SET$WORKING_S	;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_RMS_,-	; SET RMS_DEFAULT
		TRUACT=SET$RMS		;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_CARD,-	; SET CARD READER
		TRUACT=SET$CARD		;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_DEVI,-	; SET DEVICE
		TRUACT=SET$DEV		;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_LOGI,-	; SET LOGINS
		TRUACT=SET$LOGIN	;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_PROC,-	; SET PROCESS
		TRUACT=SET$PROCESS	;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_MAGT,-	; SET MAGTAPE
		TRUACT=SET$MAGTAPE	;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_FILE,-	; SET FILE
		TRUACT=SET$FILE		;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_DIRE,- ; SET DIRECTORY
		TRUACT=SET$DIRECTORY	;
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_VOLU,- ; SET VOLUME
		TRUACT=SET$VOLUME	;
SET$T_TIMEQUAL::			; LABEL FOR SETIME MODULE
	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_TIME,-	; SET TIME
		TRUACT=SET$TIME		;

	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_PASS,-	; SET PASSWORD
		TRUACT=SET$PASSWORD	;
 
SET$T_PRQUAL::				; LABEL FOR SETDEV MODULE
	$CLIQUALDEF	-
		QDCODE=CLI$K_SET_PROT,-	; SET PROTECTION
		TRUACT=SET$PROTECTION

	$CLIQUALDEF	-		;
		QDCODE=CLI$K_SET_DAY,-	; SET DAY
		TRUACT=SET$DAY		;

	$CLIQUALDEF	END_LIST	; END OF OPTION LIST

SET$L_STATUS::
	.BLKL	1			; SPACE FOR STATUS CODE

	.SBTTL	SET COMMAND OPTION DISPATCHER
;++
; FUNCTIONAL DESCRIPTION:
;
;	THIS ROUTINE DECODES THE SET OPTION AND DISPATCHES DIRECTLY
;	TO THE SPECIFIC ROUTINE.
;
; CALLING SEQUENCE:
;
;	THIS ROUTINE IS CALLED BY THE CLI AS A RESULT OF THE DCL SET COMMAND
;
; INPUT PARAMETERS:
;
;	CLI$A_UTILSERV(AP)	=	CALL BACK ADDRESS FOR RESULT PARSER
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	DISPATCHES TO THE APPROPRIATE MODULE TO PROCESS THE OPTION.
;
; CALLING INTERFACE WITH THE OPTION PROCESSING MODULES:
;
;	A ROUTINE DISPATCHED TO BY THIS DISPATCHER IS PASSED THE
;	ADDRESS OF THE CLI UTILITY SERVICE ROUTINE EXACTLY IN THE SAME WAY
;	AS A ROUTINE CALLED DIRECTLY BY THE CLI. (CLI$A_UTILSERV(AP))
;	THEREFORE THE RETURN CALL TO THE CLI TO REQUEST ADDITIONAL PARSING
;	CAN BE DONE BY THE OPTION PROCESSING MODULE IN THE SAME WAY.
;
;	HOWEVER:
;		- THE CLI WORKING AREA HAS ALREADY BEEN DEFINED IN THE
;		   DISPATCHER AND HAS TO BE REFERENCED BY GLOBAL SYMBOL :
;			 	SET$A_CLIWORK
;		- THE OPTION PROCESSING MODULE SHOULD NOT REQUEST PARSE
;		  INITIALIZATION (INITPRS) AS THIS HAS ALREADY BEEN DONE
;		   IN THE DISPATCHER
;
; MESSAGE HANDLING FACILITY:
;
;	ALL SET OPTION PROCESSING MODULES SHOULD USE THE COMMON OUTPUT
;	FACILITIES PROVIDED IN MODULE SHOW$MSG_UTIL.
;
;	CALLABLE ENTRY POINTS ARE:
;		SHOW$PRINT_MSG = FORMATS AND PRINTS A MESSAGE, GIVEN A MESSAGE
;				 ID AND AN ARGUMENT LIST
;		SHOW$PRINT_LINE = PRINTS A LINE ON SYS$OUTPUT:
;
;	IT IS ALSO RECOMENDED TO USE THE COMMON OUTPUT LINE BUFFER WHERE APPLICABLE,
;	THE DESCRIPTOR OF WHICH IS ADDRESSED BY GLOBAL SYMBOL:
;		SHOW$GQ_OUT_DSC
;
; COMPLETION CODES:
;
;	AS RETURNED BY OPTION PROCESSING MODULE IN GLOBAL LOCATION:
;			SET$L_STATUS

;
; SIDE EFFECTS:
;
;	NONE
;
;--

	.PSECT	SET$CODE BYTE,RD,NOWRT,EXE

	.ENTRY	SET$OPT_DISP,0

;
; initialize parsing
;

	PUSHAB	W^SET$A_CLIWORK		; ADDRESS OF CLI WORK AREA
	PUSHAB	W^SET_INITPRS		; REQUEST INIT PARSE
	CALLS 	#2,@CLI$A_UTILSERV(AP)	; AND CALL THE CLI SERVICE ROUTINE

;
; dispatch to option
;

	MOVL	#SS$_NORMAL,W^SET$L_STATUS
					; ASSUME SUCCESS
	PUSHAB	W^SET$A_CLIWORK		; CLI WORK AREA
	PUSHAB	W^SET_OPTDESC		; REQUEST PARSING OF COMMAND OPTION
	CALLS 	#2,@CLI$A_UTILSERV(AP)	; CALL CLI

;
; exit
;

	MOVL	W^SET$L_STATUS,R0	; reload status returned by action routine
	RET				; ALL DONE

	.END	SET$OPT_DISP
