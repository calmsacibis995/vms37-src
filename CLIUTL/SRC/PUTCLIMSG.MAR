	.TITLE	PUTCLIMSG - PRINT CLI ERROR MESSAGE
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: COMMAND LANGUAGE INTERPRETER UTILITIES
;
; ABSTRACT:
;
; THIS ROUTINE PRINTS A MESSAGE FOR ALL SYNTAX ERRORS DETECTED BY A CLI
; UTILITY.
;
; ENVIRONMENT: USER MODE
;
; AUTHOR: C. A. MONIA	, CREATION DATE: 16-JUNE-1978
;
; MODIFIED BY:
;
;	, : VERSION
; 01	- 
;	001	TMH0001		T. HALVORSEN	06-FEB-1980
;		FIX PUTMSG CALL TO USE DEFAULT FLAGS RATHER THAN EXPLICIT 7
;		WHICH CAUSED FACILITY NAME NOT TO BE OUTPUT AS INTENDED.
;--
	.PAGE
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

	$CLIDEF				; DEFINE CLI RUN PARAMETERS
	$CLIMSGDEF			; DEFINE CLI STATUS CODES
	$STSDEF				; DEFINE RETURNED STATUS FORMAT
 
;
; MACROS:
;

;
; EQUATED SYMBOLS:
;
; DEFINE ARGUMENT LIST OFFSETS
;
 
	.SAVE
	.PSECT	$ABS$,ABS
 
.=0
COUNT:	.BLKL	1			; ARGUMENT COUNT
FACILITY:.BLKL	1			; ADDRESS OF FACILITY NAME DESCRIPTOR
ERRDESCR:.BLKL	1			; QUALIFIER DESCRIPTOR FOR SYNTAX ERROR
LINDESCR:.BLKL	1			; QUALIFIER DESCRIPTOR FOR COMMAND LINE
CODE:	.BLKL	1			; CLI ERROR CODE
 
	.RESTORE
;
; OWN STORAGE:
;

	.PAGE
	.SBTTL	LIB$PUTCLIMSG - PRINT SYNTAX ERROR MESSAGE THEN EXIT
;++
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE IS CALLED WHENEVER A CLI UTILITY DETECTS A SYNTAX ERROR NOT
; OTHERWISE DISCOVERED BY THE COMMAND LANGUAGE INTERPRETER. IT DISPLAYS THE
; ERROR MESSAGE FOLLOWED BY A LINE CONTAINING THE BRACKETED ERROR TEXT.
;
; CALLING SEQUENCE:
;
;	CALL	LIB$PUTCLIMSG ([FACNAM],ERRDESCR,LINDESCR, CODE)
;
; INPUT PARAMETERS:
;
;	FACNAM - ADDRESS OF STRING DESCRIPTOR FOR THE OPTIONAL FACILITY NAME
;
;	ERRDESCR - ADDRESS OF A CLI QUALIFIER DESCRIPTOR. OFFSET CLI$W_RQSIZE
;		     IS THE QUADWORD STRING DESCRIPTOR FOR THE PORTION OF THE
;		     COMMAND LINE CONTAINING THE SYNTAX ERROR.
;
;	LINDESCR - ADDRESS OF A CLI QUALIFIER DESCRIPTOR FOR THE COMMAND LINE.
;
;	CODE - CLI ERROR CODE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	COMPLETION CODES ARE THOSE RETURNED BY SYS$PUTMSG
;
; SIDE EFFECTS:
;
;	NONE
;
;--
 
	.PSECT	_PUTCLIMSG,NOWRT
 
	.ENTRY	LIB$PUTCLIMSG,0		;
	MOVL	#CLI$W_RQSIZE,R1	; GET OFFSET TO LINE DESCRIPTOR
	MOVAB	@ERRDESCR(AP)[R1],R0	; GET ADDRESS OF ERRONEOUS SEGMENT
	ADDL3	(R0),4(R0),-(SP)	; PUSH ADDRESS OF END OF SEGMENT
	MOVAB	@LINDESCR(AP)[R1],R1	; GET ADDRESS OF LINE SEGMENT
	ADDL3	(R1)+,(R1),R1		; COMPUTE END OF SEGMENT
	SUBL3	(SP),R1,-(SP)		; COMPUTE SEGMENT LENGTH
	MOVZBL	#80,R1			; GET MAX LENGTH OF OUTPUT LINE
	CMPL	(SP),R1			; LENGTH EXCEED MAXIMUM?
	BLEQU	10$			; IF LEQU NO
	MOVL	R1,(SP)			; TRUNCATE LINE
10$:					;
	PUSHAB	(SP)			; PUSH ADDRESS OF DESCRIPTOR
	PUSHAB	(R0)			; PUSH ADDRESS OF BAD SEGMENT
	PUSHL	#^X00010002		; FAO COUNT=2, FLAGS = TEXT ONLY
	PUSHL	#CLI$_BADTEXT		; PUSH CODE FOR BAD TEXT BLOCK
	PUSHL	#0			; FAO COUNT=0, USE DEFAULT FLAGS
	PUSHL	CODE(AP)		; PUSH ERROR CODE
	PUSHL	#6			; PUSH LENGTH OF LIST
	MOVAB	(SP),R0			; COPY CURRENT SP
	$PUTMSG_S	FACNAM=@FACILITY(AP),- ; OUTPUT THE MESSAGE
			MSGVEC=(R0)	; ...
	RET				;
 
	.END
