	.TITLE	SHOW$WORKING_S - SHOW WORKING SET PARAMETERS
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SHOW COMMAND
;
; ABSTRACT:
;
;	THIS IMAGE IMPLEMENTS THE SHOW WORKING SET 
;	 COMMAND OPTION
;
; ENVIRONMENT:
;
;	RUNS IN USER MODE
;
; AUTHOR : BEATRICE U. WALTHER,  CREATION DATE: 10-OCT-1977
;
; MODIFIED BY:
;
;	V02-002	HRJ0022		Herb Jacobs		5-Jun-1981
;		Added extent and adjust display to show work.
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$PHDDEF				;PROCESS HEADER DEFINITIONS
	$JPIDEF				;JPI DEFINITIONS
	$PCBDEF				;PCB DEFINITIONS
;
; MACROS:
;
;
 
;
; EQUATED SYMBOLS:
;
;
; OWN STORAGE:
;
	.PSECT	SHOW$RWDATA	LONG,RD,WRT,NOEXE

SHOW_WORK_LIST1:
WORK_DEFAULT:
	.BLKL	1			; SPACE FOR WORKING SET DEFAULT SIZE
WORK_QUOTA:
	.BLKL	1			; SPACE FOR WORKING SET QUOTA
WORK_EXTENT:
	.BLKL	1			; SPACE FOR WORKING SET EXTENT
SHOW_WORK_LIST2:
WORK_AUTHORIZE:
	.BLKL	1			; SPACE FOR MAXIMUM AUTHORIZED QUOTA
WORK_AUTHOREXT:
	.BLKL	1			; SPACE FOR MAXIMUM AUTHORIZED EXTENT
PCBFLAGS:
	.BLKL	1			; SPACE FOR PCB FLAGS LONGWORD

	.SBTTL	SHOW$WORKING_S	- SHOW WORKING SET PARAMETERS
;++
; FUNCTIONAL DESCRIPTION:
;	This routine retrieves the WORKING SET parameters from the current
;	process header, formats and prints them on SYS$OUTPUT:
;
;
; CALLING SEQUENCE:
;
;	CALLS 	SHOW$WORKING_S
;	This routine is called by the CLI as a result of parsing option
;	WORKING_SET of the SHOW command. (i.e , this routine is specified as
;	a true action routine  address in the callback to the CLI in the
;	SHOW option dispatcher.
;
; INPUT PARAMETERS:
;
;	4(AP)=	ADDRESS OF PARAMETER DESCRIPTOR THAT CAUSED THE CALL
;		TO THIS ROUTINE
;	8(AP)=	CLI$A_UTILSERV(AP) = ADDRESS OF CLI UTILITY SERVICE ROUTINE
;
; NOTE:		NO INPUT PARAMETERS ARE USED IN THIS OPTION
;		 AS IT REQUIRES NO QUALIFIERS
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	DISPLAYS THE CURRENT PROCESS'S WORKING SET PARAMETERS ON SYS$OUTPUT:
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--

	.PSECT	SHOW$CODE BYTE,RD,NOWRT,EXE
	.ENTRY	SHOW$WORKING_S,^M<R2,R3>	; SHOW WORKING SET ROUITNE
	$GETJPI_S ITMLST=B^PCBSTS		; get working set parameters
	BLBC	R0,30$				; branch if error
	PUSHL	#SHOW$_WORKSET			; MESSAGE IDENTIFIER
	PUSHAL	W^SHOW_WORK_LIST1		; ARGUMENT LIST FOR $FAO
	CALLS 	#2,W^SHOW$PRINT_MSG		; PRINT MESSAGE
	BBC	#PCB$V_DISAWS,W^PCBFLAGS,10$	; branch if adjustment allowed
	PUSHL	#SHOW$_WORKSET1			; set to print noadjust message
	BRB	20$				; go print message
10$:	PUSHL	#SHOW$_WORKSET2			; set to print adjust message
20$:	PUSHAL	W^SHOW_WORK_LIST2		; ARGUMENT LIST FOR $FAO
	CALLS 	#2,W^SHOW$PRINT_MSG		; PRINT MESSAGE
30$:	MOVL	R0,W^SHOW$L_STATUS		; store status
	RET					; RETURN

pcbsts:	.word	4				; length of buffer
	.word	jpi$_sts			; get pcb flags word
	.address pcbflags			; area to return flags to
	.long	0				; no length return
	.word	4				; length of buffer
	.word	jpi$_dfwscnt			; get default working set size
	.address work_default			; area to return flags to
	.long	0				; no length return
	.word	4				; length of buffer
	.word	jpi$_wsquota			; get working set quota
	.address work_quota			; area to return flags to
	.long	0				; no length return
	.word	4				; length of buffer
	.word	jpi$_wsextent			; get working set extent
	.address work_extent			; area to return flags to
	.long	0				; no length return
	.word	4				; length of buffer
	.word	jpi$_wsauth			; get authorized quota
	.address work_authorize			; area to return flags to
	.long	0				; no length return
	.word	4				; length of buffer
	.word	jpi$_wsauthext			; get authorized extent
	.address work_authorext			; area to return flags to
	.long	0				; no length return
	.long	0				; end of list

	.END
