	.TITLE	LNK_ELAPSTIMS
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	LINKER
;
; ABSTRACT:	COMPUTE AN ELAPSED TIME FROM TWO ABSOLUTE TIMES
;
;
; ENVIRONMENT:
;
; AUTHOR:	T.J. PORTER, CREATION DATE: 27-JUN-77
;
; MODIFIED BY:
;
;	V02-001	BLS0035		Benn Schreiber		19-Jan-1981
;		Change to longword displacement
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

;
; MACROS:
;

;
; EQUATED SYMBOLS:
;
TIMEADDR1 = 4			; FIRST ARGUMENT IS TIME 1 ADDRESS
TIMEADDR2 = 8			; SECOND IS ADDRESS OF TIME 2
;
; OWN STORAGE:
;
	.PSECT	$OWN$,NOEXE,QUAD
ELAPSED:
	.QUAD	0			; THE COMPUTED ELAPSED TIME


	.SBTTL	LNK$CALCELAPS
;++
; FUNCTIONAL DESCRIPTION:
;
;	THIS ROUTINE IS CALLED TO CALCULATE THE ELAPSED TIME BETWEEN TWO
;	ABSOLUTE TIME VALUES. THE ADDRESS OF THE RESULT IS RETURNED AS THE VALUE OF THE ROUTINE.
;	THE ELAPSED TIME IS IN THE "DELTA TIME" FORMAT OF THE SYSTEM AND
;	IS THEREFORE SUITABLE FOR DISPLAYING THROUGH SYSTEM SERVICES
;	SUCH AS FAO %T.
;
; CALLING SEQUENCE:
;
;	LNK$CALCELAPS(TIMEADDR1,TIMEADDR2)
;
;	WHERE:	TIMEADDR1 = ADDRESS OF A QUADWORD CONTAINING THE
;				START TIME
;		TIMEADDR2 = ADDRESS OF A QUADWORD CONTAINING THE
;				END TIME.
;
;
; INPUT PARAMETERS:
;
;	AS ABOVE
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	THE ROUTINE HAS AS ITS VALUE (I.E. CONTENT RETURNED IN R0)
;	THE ADDRESS OF AN OWN QUADWORD CONTAINING THE VALUE
;		TIME1 - TIME2
;	WHICH IS (PRESUMABLY) NEGATIVE.
;
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--

	.PSECT	$CODE$,NOWRT,LONG
LNK$CALCELAPS::
	.WORD	0			;
	MOVAB	L^ELAPSED,R0		; GET ADDRESS OF QUADWORD TO RECEIVE RESULT
	MOVL	TIMEADDR2(AP),R1	; GET ADDRESS OF TIME 2
	MOVQ	@TIMEADDR1(AP),(R0)	; INITIALIZE RESULT WITH TIME 1
	SUBL	(R1)+,(R0)		; SUBTRACT LOW ORDER HALF OF
	SBWC	(R1),4(R0)		; TIME 2 THEN HIGH ORDER HALF WITH CARRY
	RET				; FROM CORRESPONDING HALVES OF TIME 1
	.END
