	.TITLE	GETNM - GET FILE NAME DESCRIPTOR
	.IDENT	/V03000/
 
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; SPECIAL GETNM FOR VAX
;
;+
; GETNM - GET FILE NAME DESCRIPTOR
;
; THIS ROUTINE IS CALLED TO BUILD A FILENAME DATASET DESCRIPTOR FOR A FILE
; SPECIFIER.
;
; INPUTS:
;
;	R1 = ADDRESS OF FIRST BYTE IN FILE SPECIFIER STRING.
;	R3 = ADDRESS OF DATASET DESCRIPTOR.
;	R4 = ADDRESS OF OUTPUT BUFFER FOR FILE NAME STRING.
;
; OUTPUTS:
;
;	C = 0 IF FILE SPECIFIER WAS PARSED SUCCESSFULLY.
;	C = 1 IF A SYNTAX ERROR WAS ENCOUNTERED.
;-
 
GETNM::				;GET FILE NAME DESCRIPTOR
	MOV	R4,-(SP)	;SAVE OUTPUT BUFFER POINTER
	MOV	#33.,R0		;SET MAXIMUM LENGTH OF FILE SPECIFIER
	CLR	VERFLG		;CLEAR VERSION FLAG
10$:	MOVB	(R1)+,R2	;GET NEXT CHARACTER FROM FILE SPECIFIER
	CMPB	#141,R2		;POSSIBLY LOWER CASE?
	BHI	20$		;IF HI NO
	CMPB	#172,R2		;LOWER CASE LETTER?
	BLO	20$		;IF LO NO
	BICB	#40,R2		;CONVERT LOWER CASE TO UPPER CASE
20$:	MOVB	R2,(R4)+	;STORE CHARACTER IN OUTPUT BUFFER
	CMPB	#15,R2		;CARRIAGE RETURN?
	BEQ	40$		;IF EQ YES
	CMPB	#33,R2		;ESCAPE?
	BEQ	40$		;IF EQ YES
	CMPB	CATEN,R2	;CONCATENATION CHARACTER?
	BEQ	40$		;IF EQ YES
	DEC	R0		;ANY MORE SPACE IN OUTPUT BUFFER?
	BGT	10$		;IF GT YES
30$:	MOV	(SP)+,R4	;REMOVE BUFFER ADDRESS FROM STACK
	BR	120$		;
40$:	CLRB	-(R4)		;CONVERT OUTPUT STRING TO ASCIZ
	CMP	R4,(SP)		;NULL FILE SPECIFIER?
	BEQ	30$		;IF EQ YES
	CMPB	#' ,-1(R4)	;PREVIOUS CHARACTER A BLANK?
	BEQ	40$		;IF EQ YES
	MOV	(SP)+,R4	;RESTORE ADDRESS OF OUTPUT BUFFER
	CLR	(R3)		;CLEAR LENGTH OF DEVICE NAME
	MOV	R4,2(R3)	;SET ADDRESS OF DEVICE NAME
	MOV	R4,R0		;COPY ADDRESS OF OUTPUT BUFFER
50$:	MOVB	(R0)+,R1	;GET NEXT CHARACTER
	BEQ	60$		;IF EQ END OF STRING
	CMPB	#':,R1		;DEVICE NAME TERMINATOR?
	BNE	50$		;IF NE NO
	MOV	R0,(R3)		;CALCULATE LENGTH OF DEVICE NAME
	SUB	R4,(R3)		;
	DEC	(R3)		;REDUCE BY ONE TO ADJUST FOR COLON
	BEQ	120$		;IF EQ NULL DEVICE NAME
	MOV	R0,R4		;UPDATE ADDRESS IN OUTPUT BUFFER
60$:	CLR	4(R3)		;CLEAR LENGTH OF DIRECTORY STRING
	MOV	R4,6(R3)	;SET ADDRESS OF DIRECTORY STRING
	CMPB	#'[,(R4)	;DIRECTORY SPECIFIED?
	BEQ	70$		;IF EQ YES
	CMPB	#'<,(R4)	;DIRECTORY SPECIFIED?
	BNE	80$		;IF NE NO
70$:	INC	4(R3)		;INCREMENT LENGTH OF DIRECTORY STRING
	MOVB	(R4)+,R1	;GET NEXT CHARACTER
	BEQ	120$		;IF EQ END OF STRING
	CMPB	#'],R1		;END OF DIRECTORY STRING?
	BEQ	80$		;IF EQ YES
	CMPB	#'>,R1		;END OF DIRECTORY STRING?
	BNE	70$		;IF NE NO
80$:	CLR	R0		;INDICATE NO TYPE FOUND
	CLR	10(R3)		;CLEAR LENGTH OF FILE NAME STRING
	MOV	R4,12(R3)	;SET ADDRESS OF FILE NAME STRING
90$:	MOVB	(R4)+,R1	;GET NEXT CHARACTER
	BEQ	130$		;IF EQ END OF STRING
	CMPB	#'.,R1		;FILE TYPE SPECIFIED?
	BNE	100$		;IF NE NO
	TST	R0		;TYPE ALREADY ENCOUNTERED?
	BNE	120$		;IF NE YES
	INC	R0		;SET TYPE ENCOUNTERED INDICATOR
100$:	CMPB	#';,R1		;FILE VERSION SPECIFIED?
	BNE	110$		;IF NE NO
	MOV	10(R3),VERFLG	;SET FILE VERSION CHARACTER COUNT
110$:	INC	10(R3)		;INCREMENT LENGTH OF FILE NAME STRING
	BR	90$		;
120$:	SEC			;SET FAILURE INDICATION
	RETURN			;
130$:	TST	R0		;FILE TYPE SPECIFIED?
	BNE	140$		;IF NE NO
	MOVB	#'.,-(R4)	;SET EXPLICIT FILE TYPE
	INC	10(R3)		;INCREMENT FILE NAME STRING LENGTH
140$:	RETURN			;
 
	.END
