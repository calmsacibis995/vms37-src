$ !
$ !		[ VMSLIB.COM ] V M S M L B . C O M
$ !
$ !	Build the user interface macro libraries:
$ !		STARLET.MLB
$ !		STARLET.L32
$ !		STARLET.REQ
$ !		TPAMAC.L32
$ !		TPAMAC.REQ
$ !		CLIMAC.L32
$ !		CLIMAC.REQ
$ !		RMSDEF.REQ
$ !		RMSMSG.REQ
$ !
$ !	Build STARLET.MLB
$ !
$ SET NOON
$ DELETE LIB$:STARLET.MLB;*
$ LIB/MACRO /SQUEEZE /CREATE=(BLOCKS=500, MODULES=750) LIB$:STARLET -
	SRC$:UTLDEFM, STARLET, TPAMAR, EODEFM, -
	LIB$:STARDEF, SSMSG, SHRMSG, OPDEF, -
	LIB$:OPCDEF, OPCMSG, SRMDEF, SCRDEF, -
	LIB$:LBRUSR, OBJFMT, -
	LIB$:DSTDEF
$ LIB/MACRO /SQUEEZE LIB$:STARLET -
	SHRLIB$:RMS32MAC, SHRLIB$:RMSCALLS, -		! From RMS
	SHRLIB$:RMSUSR, RMSDEF, RMSFALMSG, -		! From RMS
	SHRLIB$:BASMSG, COBMSG, FORMSG, LIBMSG, -	! From RTL
	SHRLIB$:MTHMSG, OTSMSG, STRMSG, -		! From RTL
	SHRLIB$:RTLMACMAR, -				! From RTL
	SHRLIB$:LIBCLIDEF, LIBDCFDEF, -			! From RTL
	SHRLIB$:CLIMAC, -				! From CLIUTL
	SHRLIB$:CLIMSG, -				! From DCL
	SHRLIB$:CRF, CRFMACROS				! From CRF
$ !
$ !	Build STARLET.L32
$ !
$ DELETE LIB$:STARLET.REQ;*
$ COPY -
	SRC$:UTLDEFB.B32, STARLE.REQ, -
	LIB$:STARDEF.B32, -
	LIB$:SSMSG.B32, SHRMSG, SRMDEF, OPCDEF, OPCMSG, -
	LIB$:SCRDEF.B32, LBRUSR, DSTDEF, -
	LIB$:OBJFMT.B32, -
	SRC$:EODEFB.B32, -
	LIB$:OPDEF.B32 -
  LIB$:STARLET.REQ
$
$ APPEND -
	SHRLIB$:RMSDEF.B32, RMSFALMSG, RMSUSR, -	! From RMS
	SHRLIB$:RMSMAC.REQ, -				! From RMS
	SHRLIB$:CRF.B32, CRFMAC.REQ, -			! From CRF
	SHRLIB$:RTLMACB32.REQ, -			! From RTL
	SHRLIB$:LIBCLIDEF.REQ, LIBDCFDEF.REQ, -		! From RTL
	SHRLIB$:MSGDEF.B32 -				! From MSGFIL
  LIB$:STARLET.REQ
$
$ DELETE LIB$:STARLET.L32;*
$ BLISS/NOLIST/LIBRARY=LIB$:STARLET LIB$:STARLET.REQ
$ DELETE LIB$:TPAMAC.L32;*
$ BLISS/NOLIST/LIBRARY=LIB$:TPAMAC SRC$:TPAMAC.REQ
$ DELETE LIB$:CLIMAC.L32;*
$ BLISS/NOLIST/LIBRARY=LIB$:CLIMAC SRC$:CLIMAC.REQ
