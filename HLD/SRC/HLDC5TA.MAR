	.TITLE	HLDC5TA - HLD RAD50 CONVERSION
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: DECNET HOST LOADER (HLD)
;
; ABSTRACT:
;
;	HLD IS A COMPONENT OF DECNET/VAX-11. IT PROVIDES ACCESS TO
;	RSX11S TASK IMAGES STORED ON A VAX SYSTEM.
;
; ENVIRONMENT:
;
;	THE HLD IMAGE EXECUTES IN THE CONTEXT OF A PROCESS CREATED BY
;	NETACP. IT RUNS IN USER MODE AND REQUIRES NETWORK PRIVILEGE.
;
; AUTHOR: SCOTT G. DAVIS,	CREATION DATE: 22-MAY-79
;
; MODIFICATIONS:
;
;--
;
;	MACROS
;
	.PAGE
	.PSECT	HLD$CODE	NOSHR,EXE,RD,NOWRT

;++
; FUNCTIONAL DESCRIPTION:
;
;	HLD$C5TA CONVERTS A LONGWORD OF RAD50 TASK NAME TO 6 ASCII CHARACTERS
;
; CALLING SEQUENCE:
;
;	BSBW	HLD$C5TA
;
; INPUT PARAMETERS:
;
;	R1 CONTAINS THE ADDRESS OF RAD50 VALUE OF THE TASK NAME
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	R2,R3,R4,R10,R11 DESTROYED
;	HLD$AT_TSKBUF IS FILLED WITH THE ASCII TASK NAME
;
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	NONE
;
;--

HLD$C5TA::
	CLRL	R11			; RESET HIGH ORDER DIVIDEND
	MOVAB	W^HLD$AT_TSKBUF-2,R3	; INIT TASK NAME BFR PTR
	BSBB	CVT			; CONVERT 1ST 3 CHARS
					; FALL THROUGH TO CONVERT 2ND 3 CHARS
CVT:
	MOVZWL	(R1)+,R10		; GET NEXT WORD (3 CHARS)
	ADDL	#6,R3			; POINT BEYOND END OF 3 CHAR PIECE
	MOVL	#3,R4			; SET UP TO LOOP
10$:
	EDIV	#^O50,R10,R10,R2	; REMAINING ENDMOST CHARACTER TO R2
	TSTW	R2			; BLANK?
	BEQL	40$			; IF EQL YES
	CMPW	R2,#^O33		; TEST MIDDLE OF RANGE
	BLSS	30$			; IF LSS ALPHA
	BEQL	20$			; IF EQL DOLLAR SIGN
	ADDW	#^O22-^O11,R2		; ADJUST
20$:	SUBW	#^O100-^O11,R2		;
30$:	ADDW	#^O100-^O40,R2		;
40$:	ADDW	#^O40,R2		;
	MOVB	R2,-(R3)		; STORE CHAR IN BUFFER
	SOBGTR	R4,10$			; LOOP
	RSB				; RETURN

	.END
