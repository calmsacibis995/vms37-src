MODULE DBGBP2 ( IDENT = 'V03-000') =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	DEBUG (DBG)
!
! ABSTRACT:
!	This module contains the lexical scanner and reduction routines
!	for BASIC+2/DEBUG.
!
! ENVIRONMENT:	VAX/VMS, user mode, interrupts disabled.
!
! Version:	1.15
!
! History:
!	Author:
!		Carol Peters, 25 July 1977
!
!	Modified by:
!		MCC	Mike Candela
!		DAR	Dale Roedger
!		JBD	Bruce Dawson
!		KAN	Ken Nappa
!		RT	Rich Title
!
! 1.01	21-SEP-78	MCC	Added dbg$for_reductn from DBGPAR.
! 1.02	26-SEP-78	DAR	Removed require file SYSLIT and added FORTRN.REQ
! 1.03	18-OCT-78	MCC	Changed semantic stack from VECTOR to BLOCKVECTOR
!				to accomodate BLISS fields.
! 1.04	25-OCT-78	DAR	Changed DBG$FOR_REDUCTN to make explicit calls
!				to DBG$ADD_ARG instead of using "LINK_XX" macros.
! 1.05	30-MAR-79	MCC	Changed dbg$for_reductn to support LOG files
! 1.06	13-JUL-79	MCC	Changed dbg$for_reductn to include actions
!				for indirect command files.
! 1.07	12-SEP-79	JBD	Made this file support BASIC+2, not FORTRAN.
! 1.08	25-OCT-79	JBD	Now support new grammar for statement numbers.
! 1.09	29-NOV-79	BAO	Added support for TYPE[/OVERRIDE]
! 1.10	4-DEC-79	JBD	Made dots legal in symbol names.
! 1.11	6-DEC-79	JBD	Made Percent legal in symbol names also. (Sigh,
!				LEX_TYPE_TBL has first chars, not ensuing chars.)
! 1.12	17-DEC-79	JBD	Made %LINE 12:%LINE 20 work. Result: Another hack
!				to [BP2SAVLNO] to check for ":".
! 1.13	18-DEC-79	JBD	Corrected grammar bug. [BP2OVROPR] removed.
! 1.14	21-JAN-80	KAN	Re-installed [BP2OVROPR], but made it act like
!				[BP2OVROPT]. This was the intent of the grammar
!				change of version 1.13, but the current debugger
!				is running with the previous parse tables.
! 1.15	30-JAN-80	MCC	Added support for HELP
! 3.00	13-Nov-81	 RT	Fixed a bug in handling of nested subscripts
! 3.01	21-Nov-81	 RT	Changed SET MODULE so that it accepts module
!				names that begin with a number.
!--

! TABLE OF CONTENTS:
!
FORWARD ROUTINE

	dbg$bp2_get_lex,	! extracts a BASIC+2 lexeme from the input buffer
	dbg$bp2_reductn;	! BASIC+2 action routines
!
! INCLUDE FILES:
!

REQUIRE 'src$:dbgprolog.req';
REQUIRE 'SRC$:DBGGEN.REQ';
REQUIRE 'src$:COMTER.REQ';			
REQUIRE 'SRC$:BSTRUC.REQ';
REQUIRE 'SRC$:scalit.REQ';
REQUIRE 'SRC$:SYSSER.REQ';
REQUIRE 'src$:BASIC2.REQ';		! BASIC+2/DEBUG Parse tables.
! REQUIRE 'SRC$:REDMAC.REQ' is required in MACRO section



!
! MACROS:
!
REQUIRE 'src$:REDMAC.REQ';


!
! EQUATED SYMBOLS:
!


!
! OWN STORAGE:	NONE

!
! EXTERNAL REFERENCES:
!
EXTERNAL ROUTINE
	dbg$bld_invoc_num,		! Add an invocation number from a string
					! descriptor
	DBG$PARS_LINNUM,		! Parse a line number
	dbg$for_add,			! FORTRAN add
	dbg$for_sub,			! FORTRAN subtract
	dbg$for_mul,			! FORTRAN multiply
	dbg$for_div,			! FORTRAN divide
	dbg$for_neg,			! unary dbg$negation
    	dbg$push_nest_stack: NOVALUE,	! Pushes onto DBG$GL_NEST_STACK (see
    					!     DBGREDUC for more details)
    	dbg$pop_nest_stack: NOVALUE,	! Pops from DBG$GL_NEST_STACK (see 
    					!     DBGREDUC for more details. This 
    					!     and the preceding routine are
    					!     used to handle nested subscripts
	dbg$get_filesp,			! Extract a filespec from input string
	dbg$freerelease,		! Release dynamically allocated storage
	dbg$radx_convrt,		! converts ASCII strings to binary numbers
	dbg$cis_getfsp,			! Get filespec for indirect com file
	dbg$verify_out,			! decides whether to VERIFY input
	DBG$SHOW_TYPE,			! handles the SHOW TYPE command.
	dbg$get_help,			! Invoke librarian for HELP
	dbg$cis_openicf;		! Open indirect command file

EXTERNAL
	dbg$char_table : VECTOR [, BYTE],	! Character type table
	dbg$token_table : VECTOR [, BYTE],	! Table of token types.
	dbg$gb_def_mod : VECTOR [, BYTE],	! mode structure
    	dbg$gb_set_module_flag: BYTE,		! TRUE during a SET MODULE
    						!    or CANCEL MODULE command.
	dbg$gl_get_lex,				! holds name of get lexeme routine
	dbg$gl_gbltyp,				! the type override field.
	dbg$gw_gbllngth : WORD,			! The length field for use with 
						! type ASCII.
	dbg$gl_log_buf,				! Addr of buffer containing LOG filespec
	dbg$gl_stk : semantic_stack;		! semantic stack for tokens, etc.

GLOBAL ROUTINE dbg$bp2_get_lex (input_stg_desc, lexeme_stg_desc) =

!++
! Functional description:
!	Using the character pointer for the input line, extracts a lexeme
!	from the input line. A lexeme is defined as an operator, an
!	alphanumeric string, a numeric string, or an
!	illegal string. Blanks and comments are absorbed.
!	The lexeme is returned in the lexeme buffer in the
!	same form as in the input string, except for numeric
!	strings, in which case the string is converted to a
!	binary number and that is returned in the lexeme buffer.
!	A token equivalent of the lexeme is the value of the
!	routine.
!
! Formal parameters:
!	input_stg_desc	- string descriptor to the input buffer.
!	lexeme_stg_desc	- varying string descriptor to the lexeme buffer
!
! Implicit inputs:
!	DBG$CHAR_TABLE	- a 128 byte VECTOR that maps each ascii character
!			  onto a dense list of equivalents.
!	DBG$TOKEN_TABLE	- a 21 byte VECTOR that maps each operator onto
!			  its token equivalent.
!
! Outputs:
!	input_stg_desc	- the field dsc$a_pointer is updated to point to
!			  the next byte to be read in the input stream.
!			  This byte is the delimiter of the lexeme found.
!			  The field dsc$w_length contains the length of
!			  the yet unread input line.
!	lexeme_stg_desc	- the field dsc$w_length holds the actual length
!			  in bytes of the lexeme found. The lexeme buffer
!			  addressed by the field dsc$a_pointer holds the
!			  lexeme string or value.
!
! Implicit outputs:
!	The ASCII representation of the lexeme is written into the
!	string addressed by the dsc$a_pointer field of lexeme_stg_desc.
!
! Routine value:
!	The type of lexeme found, alpha_str_token, digit_str_token,
!	eol_token or "operator"_token.
!
! Side effects:
!	Signal if an input character is invalid,
!	or the input string has a negative length.
!--

	BEGIN

	LITERAL
		single_prec	= 1,
		double_prec	= 3,
		table_offset	= 9,
		operator_max	= 29;

	LITERAL
		max_state_index	= 4,			! index ranges from 0 to 4
		invalid_state	= 0,			! invalid character seen
		alpha_state	= 1,			! alphabetic string expected
		numeric_state	= 2,			! numeric string expected
		eocommand_state	= 3,			! logical end of line or error seen
		radix_state	= 4,			! radix setting expected
		unspec_state	= 5;			! unspecified state, probably special character operator

	BIND
		! Initial valid chars for each state

		lex_type_tbl	= UPLIT (

		mask (illegal),
		mask (alpha, alpha_low, alpha_and_hex, alphalo_and_hex,
			percent_sign), 
		mask (numeric),
		mask (ind_comment, end_of_line),
		mask (quote)

		) : VECTOR;

	BIND
		lex_state_tbl	= UPLIT BYTE (

		invalid_state,
		alpha_state,
		numeric_state,
		eocommand_state,
		radix_state

		) : VECTOR [, BYTE];


	MAP
		input_stg_desc	: REF BLOCK [, BYTE],	! input string descriptor
		lexeme_stg_desc	: REF BLOCK [, BYTE];	! lexeme string descriptor

	LOCAL
		input_ptr,				! character pointer for input
		lexeme_ptr,				! character pointer for lexeme
		previous_radix,				! current local radix
		special_radix,				! special radix setting
		state_index,				! index into lex_state_tbl
		state,					! current state of lexical processor
		char,					! holds a single character
		count;					! counts characters used

	LABEL
		alpha_block;			! label for alpha case in the select

	!++
	! See whether there is any input line left. If not, signal
	! internal error.
	!--
	IF .input_stg_desc [dsc$w_length] LSS 0
	THEN SIGNAL (dbg$_parseerr);

	!++
	! Make the string pointers into formal BLISS character
	! pointers.
	!--
	input_ptr = ch$ptr (.input_stg_desc [dsc$a_pointer]);
	lexeme_ptr = ch$ptr (.lexeme_stg_desc [dsc$a_pointer]);

	previous_radix = .dbg$gb_mod_ptr [mode_radix];		! Save radix in case of local override.

	special_radix = FALSE;

	count = -1;
	DO						! Loop to skip leading blanks.
	    BEGIN
	    char = ch$rchar_a (input_ptr);
	    count = .count + 1;
	    END
	UNTIL .dbg$char_table[.char] NEQ blanks;

	input_ptr = ch$plus (.input_ptr, -1);		! Adjust ptr to first non-blank.
	input_stg_desc [dsc$w_length] = .input_stg_desc [dsc$w_length] - .count;


	!++
	! Convert the mapping of the first significant character
	! into a lexical state. This state drives the later CASE
	! processing.
	!--
	state_index = 0;
	REPEAT
		BEGIN
		IF .lex_type_tbl [.state_index] ^ .dbg$char_table [.char] LSS 0
		THEN
			BEGIN
			state = .lex_state_tbl [.state_index];
			EXITLOOP
			END
		ELSE
			BEGIN
			state_index = .state_index + 1;
			IF .state_index GTR max_state_index
			THEN
				BEGIN
				state = unspec_state;
				EXITLOOP
				END;
			END;
		END;

    	! See whether we are in a SET MODULE or CANCEL MODULE command. If so,
    	! and we are looking at a numeric character, change state to alphabetic
    	! to allow for module names that begin with numbers.
    	!
    	IF .dbg$gb_set_module_flag
    	THEN
    	    IF .char GEQ %C'0' AND .char LEQ %C'9' 
    	    THEN
    		state = alpha_state;


	REPEAT CASE .state FROM 0 to max_state_index + 1 OF	! analyze current state

		SET

		[invalid_state]:			! if illegal, just signal
			SIGNAL (dbg$_invchar);

		[alpha_state]:				! alphanumeric string
alpha_block:		BEGIN

			! If first char is a dot, then we may be in line or
			! statement number mode, in which case, we should go
			! to the unspecified state. If not in those modes, then
			! assume we have a number.
			IF .CHAR EQL %C'.'
			THEN
				BEGIN
				IF .DBG$GB_MOD_PTR[MODE_FORTRAN] EQL LABEL_MODE
				OR .DBG$GB_MOD_PTR[MODE_FORTRAN] EQL LINE_MODE
				THEN STATE = UNSPEC_STATE
				ELSE STATE = NUMERIC_STATE;
				LEAVE ALPHA_BLOCK;
				END;

			!++
			! Now read the input buffer until a non-alpha
			! and non-numeric
			! character is encountered. Store each
			! character found in the buffer for the lexeme
			! unless the length of that buffer is expended.
			!--

			count = 0;

			DO
				BEGIN
				IF (.char GEQU %C'a')
				THEN char = .char - upper_case_dif;
				count = .count + 1;
				IF .count LEQ sym_max_length
				THEN ch$wchar_a (.char, lexeme_ptr);
				char = ch$a_rchar (input_ptr);
				END
			WHILE	(oneof (.dbg$char_table [.char], alpha, alpha_low, numeric,
				    percent_sign, period, alpha_and_hex, alphalo_and_hex));

			IF .count GTR sym_max_length
			THEN
				BEGIN
				SIGNAL (dbg$_stgtrunc);
				lexeme_stg_desc [dsc$w_length] = sym_max_length;
				END
			ELSE lexeme_stg_desc [dsc$w_length] = .count;
			input_stg_desc [dsc$a_pointer] = .input_ptr;
			input_stg_desc [dsc$w_length] = .input_stg_desc [dsc$w_length] - .count;


			RETURN alpha_str_token
			END;

		[numeric_state]:			! numeric string
			BEGIN

			!++
			! A number can begin with
			! or contain a decimal point (period). See
			! whether there is a leading decimal point,
			! and if so, set a flag indicating that.
			! This will allow trailing zeroes to be ignored.
			!--

			LOCAL
				period_present,
				floating_flag;

			IF .char EQL asc_period
			THEN period_present = TRUE
			ELSE period_present = FALSE;
			floating_flag = 0;

			!++
			! Now read the input buffer until a non-numeric
			! character is encountered. Ignore all leading
			! zeroes unless a decimal point was present.
			! Store each character found in the buffer for
			! the lexeme unless the length of that buffer
			! is expended.
			!--
			count = 0;

			IF NOT .period_present
			THEN
				WHILE (.char EQL %C'0')
				DO
					BEGIN
					count = .count + 1;
					char = ch$a_rchar (input_ptr);
					END;

			!++
			! If the entire number was zero, put a single
			! zero in the lexeme buffer and return.
			!--
			input_stg_desc [dsc$w_length] = .input_stg_desc [dsc$w_length] - .count;
			count = 0;
			IF .dbg$char_table [.char] NEQ numeric
				AND .dbg$char_table [.char] NEQ period
				AND NOT .period_present
				AND NOT (oneof (.dbg$char_table [.char], alpha_and_hex, alphalo_and_hex))
			THEN
				BEGIN
				IF .dbg$gb_mod_ptr [mode_fortran] NEQ line_mode
							AND
				   .dbg$gb_mod_ptr [mode_fortran] NEQ label_mode
				THEN
				    ch$wchar (0, .lexeme_ptr)
				ELSE
				    ch$wchar ('0', .lexeme_ptr);

				lexeme_stg_desc [dsc$w_length] = 1;
				input_stg_desc [dsc$a_pointer] = .input_ptr;
				RETURN digit_str_token
				END;

			!++
			! This is the normal store and pick up next
			! numeric character.
			!--
			IF (NOT .period_present) AND (.dbg$char_table [.char] NEQ period)
			THEN
				DO
					BEGIN
					IF .dbg$char_table[.char] EQL alphalo_and_hex
					THEN char = .char - upper_case_dif;
					count = .count + 1;
					IF .count GTR num_max_length
					THEN
						BEGIN


						ch$move (num_max_length - 1,
							ch$plus (ch$ptr (.lexeme_stg_desc [dsc$a_pointer]), 1),
							ch$ptr (.lexeme_stg_desc [dsc$a_pointer]));
						ch$wchar (.char, .lexeme_ptr-1);
						END
					ELSE ch$wchar_a (.char, lexeme_ptr);
					char = ch$a_rchar (input_ptr);


					END
				WHILE (oneof (.dbg$char_table [.char], numeric,
						alpha_and_hex, alphalo_and_hex));

			!++
			! FORTRAN may have a decimal point following the
			! string of digits. Look for the decimal point.
			! If it is present as the next character, set
			! the period present flag.
			!--
			IF .char EQL asc_period
			THEN
				IF .DBG$GB_MOD_PTR[MODE_FORTRAN] EQL LABEL_MODE
				OR .DBG$GB_MOD_PTR[MODE_FORTRAN] EQL LINE_MODE
				THEN
					BEGIN
					INPUT_STG_DESC[DSC$A_POINTER] = .INPUT_PTR;
					INPUT_STG_DESC[DSC$W_LENGTH] = .INPUT_STG_DESC[DSC$W_LENGTH] - .COUNT;
					LEXEME_STG_DESC[DSC$W_LENGTH] = .COUNT;
					! Check for an invalid number
					IF .DBG$GB_MOD_PTR[MODE_RADIX] NEQ .PREVIOUS_RADIX
					OR .PERIOD_PRESENT
					THEN SIGNAL (DBG$_INVNUMBER, 1, .LEXEME_STG_DESC);

					IF .COUNT GTR .LEXEME_STG_DESC[DSC$W_MAXLEN]
					THEN
						BEGIN
						LEXEME_STG_DESC[DSC$W_LENGTH] = .LEXEME_STG_DESC[DSC$W_MAXLEN];
						SIGNAL (DBG$_NUMTRUNC);
						END;
					! Say we read a digit string.
					RETURN DIGIT_STR_TOKEN;
					END
				ELSE
					IF NOT .period_present
					THEN
						BEGIN
						period_present = TRUE;
						DO
							BEGIN
							count = .count + 1;
							IF .count LEQ .lexeme_stg_desc [dsc$w_maxlen]
							THEN ch$wchar_a (.char, lexeme_ptr);
							char = ch$a_rchar (input_ptr);
							END
						WHILE (.dbg$char_table[.char] EQL numeric);
						END;

			!++
			! Now see if this is a number of the form
			! 'nnn'X or 'nnn'O, in which case, look for
			! the closing quote and the radix indicator.
			!--
			IF ((.special_radix) AND (NOT .period_present))
			THEN
				BEGIN

				!++
				! Processing a numeric string that is
				! enclosed in quotes and followed by a
				! letter indicating radix. Look for the
				! closing quote and the radix letter.
				!--
				IF .char EQL asc_quote
				THEN
				    BEGIN
				    char = ch$rchar (ch$plus (.input_ptr, 1));
				    IF .dbg$char_table[.char] EQL alpha_low
				    THEN char = .char - upper_case_dif;
				    SELECTONE .char OF
					SET
					[%C'O']: dbg$gb_mod_ptr[mode_radix] = octal_radix;
					[%C'X']: dbg$gb_mod_ptr[mode_radix] = hex_radix;
					[otherwise]: dbg$gb_mod_ptr[mode_radix] = decimal_radix;
					TES;
					input_ptr = ch$plus (.input_ptr, 2);
					input_stg_desc[dsc$w_length] = .input_stg_desc[dsc$w_length] - 2;
				    END;
				END
			ELSE
				BEGIN

				!++
				! Now allow for a D or E form of floating
				! number, followed by an optional positive
				! or negative sign, followed by more digits.
				!--
				IF (.char GEQU %C'a')
				THEN char = .char - upper_case_dif;
				IF (.char EQL %C'D') OR (.char EQL %C'E')
				THEN
					BEGIN
					IF .char EQL %C'D'
					THEN floating_flag = double_prec
					ELSE floating_flag = single_prec;


					ch$wchar_a (.char, lexeme_ptr);
					count = .count + 1;
					char = ch$a_rchar (input_ptr);
					END;
				IF .period_present AND .floating_flag
				THEN
					BEGIN
					IF (.char EQL %C'+') OR (.char EQL %C'-')
					THEN
						BEGIN
						ch$wchar_a (.char, lexeme_ptr);
						count = .count + 1;
						char = ch$a_rchar (input_ptr);
						END;
					END;
				IF .period_present OR .floating_flag
				THEN	WHILE (.dbg$char_table[.char] EQL numeric)
					DO
					BEGIN
					count = .count + 1;
					IF .count LEQ .lexeme_stg_desc [dsc$w_maxlen]
					THEN ch$wchar_a (.char, lexeme_ptr);
					char = ch$a_rchar (input_ptr);
					END;
				END;

			!++
			! Convert the number, restore the old radix,
			! and return the numeric lexeme.
			!--


			!++
			! See whether the user has just said %LABEL or
			! the %LABEL mode is in effect, in which case
			! a string must be returned instead of the
			! actual binary number.
			!--

			lexeme_stg_desc[dsc$w_length] = .count;
			IF (NOT .period_present) AND (NOT .floating_flag)
				AND (.dbg$gb_mod_ptr [mode_fortran] NEQ label_mode)
				AND (.dbg$gb_mod_ptr [mode_fortran] NEQ line_mode)
			THEN
				BEGIN
				.lexeme_stg_desc[dsc$a_pointer] = dbg$radx_convrt (.lexeme_stg_desc);
				dbg$gb_mod_ptr [mode_radix] = .previous_radix;
				lexeme_stg_desc [dsc$w_length] = 4;


				END
			ELSE
				BEGIN
				IF .count GTR .lexeme_stg_desc [dsc$w_maxlen]
				THEN
					BEGIN
					lexeme_stg_desc [dsc$w_length] = .lexeme_stg_desc [dsc$w_maxlen];
					SIGNAL (dbg$_numtrunc);
					END;
				END;

			input_stg_desc [dsc$a_pointer] = .input_ptr;
			input_stg_desc [dsc$w_length] = .input_stg_desc [dsc$w_length] - .count;


			IF (.dbg$gb_mod_ptr [mode_fortran] EQL label_mode)
				OR (.dbg$gb_mod_ptr [mode_fortran] EQL line_mode)
			THEN
				BEGIN
				IF .dbg$gb_mod_ptr [mode_radix] NEQ .previous_radix
				    OR .period_present
				THEN
				    BEGIN
				    SIGNAL (dbg$_invnumber, 1, .lexeme_stg_desc);
				    RETURN (0);
				    END;
				RETURN digit_str_token;
				END
			ELSE	IF .floating_flag EQL double_prec
				THEN RETURN double_fl_token
				ELSE	IF .floating_flag OR .period_present
					THEN RETURN single_fl_token
					ELSE RETURN digit_str_token
			END;

		[eocommand_state]:			! logical end of line
			BEGIN

			!++
			! The length of the input line should be set to
			! zero here. Reduce it one so that it is less than
			! zero. This will cause an error if this same
			! input line ever comes back to the lex routine.
			!--
			lexeme_stg_desc [dsc$w_length] = 0;
			input_stg_desc [dsc$a_pointer] = ch$plus (.input_ptr, 1);
			input_stg_desc [dsc$w_length] = .input_stg_desc [dsc$w_length] - 1;
			RETURN eol_token
			END;

		[radix_state]:				! up arrow, quote, percent sign
			BEGIN		! BASIC+2 handling

			!++
			! A quote can occur as the beginning of an
			! alphabetic string, or as a special character
			! that begins a numeric string that ends in another
			! quote and then a radix-indicator letter.
			! See whether this is a quote followed by a
			! number. If so, save a state variable, 
			! and proceed to collect the number.
			!--
			LOCAL
				temp_ptr;

			temp_ptr = ch$plus(.input_ptr, 1);
			char = ch$rchar_a(temp_ptr);
			IF (oneof (.dbg$char_table[.char], numeric, alpha_and_hex,
					alphalo_and_hex))
			THEN
				BEGIN
				DO
				char = ch$rchar_a(temp_ptr)
				UNTIL (oneof (.dbg$char_table[.char], quote, end_of_line));
				IF (.dbg$char_table[.char] EQL quote)
				THEN
				    BEGIN
				    char = ch$rchar(.temp_ptr);
				    IF (.char GEQU %C'a')
				    THEN char = .char - upper_case_dif;
				    IF ((.char EQL %C'O') OR (.char EQL %C'X')
					OR (.char EQL %C'D'))
				    THEN state = numeric_state;
				    END;
				END;
			IF (.state EQL numeric_state)
			THEN
				BEGIN
				char = ch$a_rchar(input_ptr);		! update ptr and get first no.
				input_stg_desc[dsc$a_pointer] = .input_ptr;
				input_stg_desc[dsc$w_length] = .input_stg_desc[dsc$w_length] - 1;
				special_radix = TRUE;
				END
			ELSE
				BEGIN
				char = ch$rchar(.input_ptr);
				state = unspec_state;
				END;

			END;

		[unspec_state]:				! special character like operator or illegal
			BEGIN

			!++
			! Most likely, this is a single character
			! operator. Write its ASCII value into the
			! lexeme buffer, and return its equivalent
			! token.
			!--
			LOCAL
				index;

			index = .dbg$char_table[.char];
			IF ((.index GEQ table_offset) AND (.index LEQ operator_max))
			THEN
				BEGIN
				ch$wchar (.char, .lexeme_ptr);
				lexeme_stg_desc[dsc$w_length] = 1;
				input_stg_desc[dsc$a_pointer] = ch$plus (.input_ptr, 1);
				input_stg_desc[dsc$w_length] = .input_stg_desc[dsc$w_length] - 1;
				RETURN .dbg$token_table[.index - table_offset]
				END;

			!++
			! This doesn't seem to be anything about which
			! we know. SIGNAL invalid character.
			!--
			SIGNAL (dbg$_invchar);
			END;			! loop back to CASE statement.

		TES;

	RETURN 1;
	END;					! end of dbg$bp2_get_lex

GLOBAL ROUTINE dbg$bp2_reductn (action_key, stack, lex_stg_desc, lahead_stg_desc, parse_stg_desc) =
!++
! Functional description:
!	Does the action associated with a reduction. The action is chosen
!	based on the action_key, which is the name of an action routine
!	as specified in the semantics table.
!
! Formal Parameters:
!	action_key	- name of the action routine
!	stack		- top of stack in the context of the reduction
!
! Implicit inputs:
!	the name of the parse stack : dbg$gl_stk
!
! Outputs:
!	none
!
! Routine value:
!	TRUE or FALSE
!
! Side effects:
!	The top of stack is often changed. arguments are put into
!	linked lists, context values are altered.
!--

    BEGIN
    LITERAL
	LINE_NUM = 0,
	STMT_NUM = 1;
    MAP
	lex_stg_desc	: REF BLOCK [ ,BYTE],
	lahead_stg_desc	: REF BLOCK [ ,BYTE],
	parse_stg_desc	: REF BLOCK [ ,BYTE];


    CASE .action_key FROM 1 TO bp2none OF

	SET

	[bp2comlin]:	BEGIN
			dbg$verify_out (.parse_stg_desc);
			IF NOT dbg$perform_cmd(.stack) 
			THEN 
				RETURN false;
			END;

	[bp2addexa]:	dbg$add_arg (dbg$gl_stk[.stack+2, stk$v_base], 0);
	[bp2addexp]:	dbg$for_add (dbg$gl_stk [.stack, stk$v_val1], dbg$gl_stk [.stack, stk$v_type],
			     dbg$gl_stk [.stack + 2, stk$v_val1], dbg$gl_stk [.stack + 2, stk$v_type]);
	[bp2addran]:	dbg$add_arg (dbg$gl_stk[.stack+2, stk$v_base],
					dbg$gl_stk[.stack+4, stk$v_base]);
	[bp2aftcnt]:	dbg$get_step_count (.stack + 1, .lex_stg_desc);
	[bp2andopr]:	dbg$logical_and (.stack);
	[bp2arrnam]:	BEGIN
    			dbg$push_nest_stack();
    			dbg$redu_path_addr (.stack);
    			END;
	[bp2ascclr]:	IF .dbg$gl_gbltyp EQL dsc$k_dtype_t
			THEN dbg$gl_gbltyp = -1;
	[bp2asclen]:	dbg$save_ascii_leng (.lex_stg_desc);
	[bp2breacb]:	dbg$gl_list[2] = dbg$get_bpt_act(.parse_stg_desc);
	[bp2canarg]:	dbg$cancel_parm (.stack);
	[bp2canind]:	dbg$set_over_adr();
    	[bp2canmod]:	dbg$gl_context [dbg$k_module] = TRUE;
	[bp2canovr]:	dbg$gl_gbltyp = -1;
	[bp2canswi]:	dbg$can_switch (.stack);
	[bp2calsub]:	dbg$add_arg (dbg$gl_stk[.stack+1, stk$v_base], 0);
	[bp2defarg]:	dbg$add_arg (dbg$gl_stk[.stack, stk$v_base],
					dbg$gl_stk[.stack+2, stk$v_base]);
	[bp2deflst]:	dbg$add_arg (dbg$gl_stk[.stack+2, stk$v_base],
					dbg$gl_stk[.stack+4, stk$v_base]);
	[bp2defnam]:	dbg$save_name (.stack, .lex_stg_desc);
	[bp2deploc]:	BEGIN
			dbg$add_arg (dbg$gl_stk[.stack, stk$v_base], 0);
			dbg$set_overs (override_mode, noimmed_n_token);
			END;
	[bp2dgptho]:	BEGIN
			IF .dbg$gb_mod_ptr [mode_fortran] EQL line_mode
					    OR
			   .dbg$gb_mod_ptr [mode_fortran] EQL label_mode
			THEN
			    dbg$bld_invoc_num(.lahead_stg_desc)
			ELSE
			    dbg$bld_num_pth (.lahead_stg_desc);
			END;
	[bp2digpat]:	dbg$bld_num_pth(.lex_stg_desc);
	[bp2digstr]:	dbg$trans_name(.stack, .lahead_stg_desc);

	[bp2dimadd]:	dbg$add_dimension (.stack + 2);
	[bp2dimone]:	dbg$add_dimension (.stack);
	[bp2divexp]:	dbg$for_div (dbg$gl_stk [.stack, stk$v_val1], dbg$gl_stk [.stack, stk$v_type],
			     dbg$gl_stk [.stack + 2, stk$v_val1], dbg$gl_stk [.stack + 2, stk$v_type]);
	[bp2dfltyp]:	dbg$set_default_typ();
	[bp2eqvexp]:	dbg$equivalence (.stack);
	[bp2exaone]:	dbg$add_arg (dbg$gl_stk[.stack, stk$v_base], 0);
	[bp2expran]:	dbg$add_arg (dbg$gl_stk[.stack, stk$v_base],
					dbg$gl_stk[.stack+2, stk$v_base]);
	[bp2indcom]:	dbg$cis_openicf();
	[bp2insclr]:	IF .dbg$gl_gbltyp EQL dsc$k_dtype_zi
			THEN dbg$gl_gbltyp = -1;
	[bp2gethlp]:	dbg$get_help( .parse_stg_desc);
	[bp2geticf]:	dbg$cis_getfsp(.lex_stg_desc, .parse_stg_desc);
	[bp2getfsp]:	BEGIN
			MAP dbg$gl_log_buf : REF VECTOR [,BYTE];
			dbg$gl_log_buf = dbg$get_filesp(.lex_stg_desc, .parse_stg_desc);
			END;
	[bp2loctyp]:	dbg$set_local_type();
    	[bp2module]:	! We have just processed a SET_MODULE -> SET MODULE
    			! or a CANCEL_MODULE -> CANCEL MODULE reduction
    			dbg$gb_set_module_flag = TRUE;
	[bp2mulexp]:	dbg$for_mul (dbg$gl_stk [.stack, stk$v_val1], dbg$gl_stk [.stack, stk$v_type],
			     dbg$gl_stk [.stack + 2, stk$v_val1], dbg$gl_stk [.stack + 2, stk$v_type]);

	[bp2negexp]:	dbg$for_neg (dbg$gl_stk [.stack + 1, stk$v_val1], dbg$gl_stk [.stack + 1, stk$v_type],
			     dbg$gl_stk [.stack, stk$v_val1], dbg$gl_stk [.stack, stk$v_type]);

	[bp2notopr]:	dbg$complement (.stack);
	[bp2oroper]:	dbg$logical_or (.stack);
	[bp2ovrins]:	dbg$gl_gbltyp = dsc$k_dtype_zi;
	[bp2ovrasc]:	BEGIN
			    dbg$gl_gbltyp = dsc$k_dtype_t;
			    dbg$gw_gbllngth = 4;
			END;
	[bp2ovropl]:	dbg$set_overr_mode (.stack + 2);
	[bp2ovropt]:	dbg$set_overr_mode (.stack);
	[bp2ovrstl]:	dbg$set_overr_step (.stack + 2);
	[bp2ovrstp]:	dbg$set_overr_step (.stack + 1);
	[bp2ovrtyp]:	dbg$set_over_typ();
	[bp2posexp]:	dbg$pop_stack (.stack);
	[bp2quotec]:	BEGIN
			dbg$gl_stk [.stack, stk$v_val1] = dbg$extract_str (.parse_stg_desc);
			dbg$gl_stk [.stack, stk$v_val2] = .dbg$gl_stk [.stack, stk$v_val1];
			END;
	[bp2refarr]:	BEGIN
    			dbg$reduce_array (.stack);
    			dbg$pop_nest_stack();
    			END;
	[bp2rempar]:	dbg$pop_stack (.stack);
	[bp2savlno]:	! If path was %LINE FOO\10, then lexeme has line num,
			! else lahead does.
			IF ch$rchar (.lahead_stg_desc [dsc$a_pointer]) EQL '\'
			THEN
			    dbg$pars_linnum (.lex_stg_desc, line_num)
			ELSE
			    dbg$pars_linnum (.lahead_stg_desc, line_num);
	[bp2savmod]:	dbg$add_module (.stack, .lex_stg_desc);
	[bp2savone]:	dbg$sav_pth_nam();
	[bp2savsn]:	DBG$PARS_LINNUM (.LEX_STG_DESC, STMT_NUM);
	[bp2savtyp]:	dbg$save_type (.stack);
	[bp2savpat]:	dbg$build_path (.lex_stg_desc, FALSE);
	[bp2setbre]:	dbg$break_setup (.stack, .dbg$gl_stk [.stack + 2, stk$v_val1], 1);
	[bp2setbra]:	dbg$break_setup (.stack, .dbg$gl_stk [.stack + 3, stk$v_val1],
					.list_elem_exp1(.dbg$gl_head_lst));
	[bp2settrc]:	dbg$gl_context[dbg$k_trce_call] = TRUE;
	[bp2settrb]:	dbg$gl_context[dbg$k_trce_brch] = TRUE;
	[bp2setdec]:	dbg$set_dec_overs() ;
	[bp2setind]:	dbg$set_over_noadr();
	[bp2setlan]:	dbg$save_lang_name (.stack+2, .lex_stg_desc);
	[bp2setlin]:	dbg$set_line_dec (.stack);
	[bp2setlog]:	dbg$gl_context[dbg$k_log] = TRUE;
	[bp2setmdl]:	dbg$gl_context[dbg$k_module] = TRUE;
	[bp2setmod]:	dbg$gl_context[dbg$k_mode] = TRUE;
	[bp2setout]:	dbg$gl_context[dbg$k_output] = TRUE;
	[bp2setsco]:	dbg$gl_context[dbg$k_scope] = TRUE;
	[bp2setstp]:	dbg$gl_context[dbg$k_step] = TRUE;
	[bp2shoarg]:	dbg$show_param (.stack);
	[bp2shocal]:	dbg$gl_context[dbg$k_traceback] = TRUE;
	[bp2shcall]:	BEGIN
			dbg$get_step_count (.stack + 1, .lex_stg_desc);
			dbg$gl_context[dbg$k_traceback] = TRUE;
			END;
	[bp2shotyp]:	dbg$show_type(default);
	[bp2shovtp]:	dbg$show_type(override);
	[bp2staloc]:	dbg$add_arg (dbg$gl_stk[.stack+1, stk$v_base], 0);
	[bp2stcoun]:	dbg$get_step_count (.stack + 1, .lex_stg_desc);
	[bp2stpcnt]:	dbg$get_step_count (.stack + 2, .lex_stg_desc);
	[bp2subexp]:	dbg$for_sub (dbg$gl_stk [.stack, stk$v_val1], dbg$gl_stk [.stack, stk$v_type],
			     dbg$gl_stk [.stack + 2, stk$v_val1], dbg$gl_stk [.stack + 2, stk$v_type]);

	[bp2substr]:	dbg$char_substring(.stack);
	[bp2svolpt]:	dbg$build_path(.lahead_stg_desc, FALSE);
	[bp2trnlab]:	dbg$reduce_label (.stack, .lahead_stg_desc);
	[bp2trnnam]:	dbg$reduce_pathname (.stack, .lahead_stg_desc);
	[bp2uqunam]:	dbg$translate_name (.stack, .lex_stg_desc);
	[bp2xorexp]:	dbg$exclusive_or (.stack);
	[bp2none]:	0;

	[inrange, outrange]:	RETURN FALSE;

	TES;

    RETURN TRUE
    END;


END							! End of module
ELUDOM
