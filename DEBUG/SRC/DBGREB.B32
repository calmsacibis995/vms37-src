MODULE DBGREB (	IDENT = 'V03-000') =
BEGIN

!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:
!	DEBUG
!
! ABSTRACT:
!	RST routines used primarily to rebuild RST data structures.
!
!
! ENVIRONMENT:
!	This module runs on VAX under VAX/VMS, user mode, non-AST level.
!
! Author:
!	Kevin Pammett, August 12, 1977.
!
! Version:	1.03
!
! MODIFIED BY:
!		Bruce Olsen, 	31 AUG 1979
!
!  1.01	20-SEP-78	DAR	Changed diagnostics to %IF form.
!  1.02	22-SEP-78	DAR	Deleted require file LISTEL.
!  1.03 31-AUG-79	BAO 	Deleted the automatic CANCEL SCOPE which may result from a CANCEL MODULE.
!--

! TABLE OF CONTENTS:
!

FORWARD ROUTINE
	DBG$SET_MODULE : NOVALUE,	! Add a <module list> to the RST.
	DBG$CANC_MODULE : NOVALUE;	! Delete a <module list> from the RST.


!
! INCLUDE FILES:
!

REQUIRE 'src$:dbgprolog.req';
REQUIRE 'SRC$:DBGGEN.REQ';
REQUIRE 'SRC$:BSTRUC.REQ';
REQUIRE 'SRC$:SYSSER.REQ';

!
! MACROS:
!

!
! EQUATED SYMBOLS:
!

!
! EXTERNAL REFERENCES:
!

EXTERNAL

	DBG$GL_HEAD_LST;		! pointer to expression arg list.

EXTERNAL ROUTINE
	dbg$rst_setmod,
	dbg$rst_canmod,
	dbg$rst_char_addr,
	dbg$rst_name_count;

GLOBAL ROUTINE DBG$SET_MODULE( module_to_set ) : NOVALUE =
!++
! Functional Description:
!
!	Add a module, a list of modules, or /ALL modules
!	to the RST.
!
! Formal Parameters:
!
!	MODULE_TO_SET	-an MC pointer to the module we are
!			 to add, or 0 => use the command
!			 argument list or do SET /ALL.
!
! Implicit Inputs:
!
!	The expression list contains MC_PTRs to the
! indicated modules.  If SET /ALL was given, then
! the MC chain points to all modules we will attempt
! to SET.
!
! Implicit Outputs:
!
!	The modules are added.
!
! Return Value:
!
!	NOVALUE - we SIGNAL our way out if something goes wrong.
!
! Side Effects:
!
!	The symbols for the indicated modules
! get added to the RST.  If they are already there,  
! we do nothing (not even produce a warning 
! message).
!--

BEGIN
	LOCAL
		status,
		POINTER,		! Used to scan the arg list.
					
		MC_PTR : REF rst$entry;


	IF( .MODULE_TO_SET EQL 0 )
	THEN
		BEGIN
			IF( (POINTER = .DBG$GL_HEAD_LST) EQL 0 )
			THEN
				! Look at the whole MC chain.

				status = dbg$rst_setmod(0)
			ELSE
				DO
				BEGIN
					mc_ptr = .LIST_ELEM_EXP1(.POINTER);

				! Don't try to add one which is already there.

					IF( NOT .MC_PTR[rst$v_modset] )
					THEN

						DBG$rst_setmod(dbg$rst_char_addr(.mc_ptr),
							dbg$rst_name_count(.mc_ptr));
				END
				WHILE( (POINTER = .LIST_ELEM_FLINK(.POINTER)) NEQ 0 );
			END
		ELSE
			dbg$rst_setmod(dbg$rst_char_addr(.mc_ptr),
				dbg$rst_name_count(.mc_ptr));


END;

GLOBAL ROUTINE DBG$CANC_MODULE : NOVALUE =
!++
! Functional Description:
!
!	Delete the symbols for a given list of module
! names from the RST, or delete /ALL modules.
!
! Implicit Inputs:
!
!	If the command arg list pointer is 0, then
!	we assume that it is the /ALL function which 
!	was called for.
!
!	Otherwise the expression list contains MC_PTRs to the
!	indicated modules.
!
! Implicit Outputs:
!
!	none.
!
! Return Value:
!
!	NOVALUE - we SIGNAL our way out if something goes wrong.
!
! Side Effects:
!
!	The symbols for the indicated modules
! get deleted from the RST.  If they are not there,  
! we do nothing (not even produce a warning 
! message).
!--

BEGIN
	LOCAL

		status,
		POINTER,		! Used to scan the arg list.
		MC_PTR : REF rst$entry;

	IF( (POINTER = .DBG$GL_HEAD_LST) EQL 0 )
	THEN
		status = dbg$rst_canmod(0)
	ELSE
		DO
		BEGIN
		mc_ptr = .LIST_ELEM_EXP1(.POINTER);

		! Don't try to remove one which is not there.

		IF( .MC_PTR[rst$v_modset] )
		THEN

			DBG$rst_canmod(dbg$rst_char_addr(.mc_ptr),
				dbg$rst_name_count(.mc_ptr));
		END
		WHILE( (POINTER = .LIST_ELEM_FLINK(.POINTER)) NEQ 0 );

END;

END				!End of module
ELUDOM
