MODULE DBGNGO (IDENT = 'V03-000') =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:
!
!	DEBUG
!
! ABSTRACT:
!
!	This module contains the command parse and execution networks to support
!	the GO command. During parsing, a command execution tree is constructed
!	which is used as input to the execution network to actually perform
!	the semantic actions corresponding to the parsed command. The method
!	of parsing is that of ATN's.
!
! ENVIRONMENT:
!
!	VAX/VMS
!
! AUTHOR:
!
!	David Plummer
!
! CREATION DATE:
!
!	9-Jul-80
!
! VERSION:
!
!	V02.2-001
!
! MODIFIED BY:
!
!	Richard Title	16-Dec-1981	Added a flag saying whether GO has an
!					argument; used for deciding how to
!					continue from an EXCEPTION BREAK.
!--

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
	DBG$NPARSE_GO,		! Parse network for GO
	DBG$NEXECUTE_GO;	! Execution network for GO

!
! REQUIRE FILES:
!
REQUIRE 'SRC$:DBGPROLOG.REQ';

REQUIRE 'SRC$:DBGGEN.REQ';

!
! EQUATED SYMBOLS:
!

LITERAL

	! Legal verb composites
	!
	GO_NOADDR = 1,
	GO_ADDR = 2;

!
! EXTERNAL REFERNECES
!

EXTERNAL ROUTINE
	DBG$THREAD_RET,		! Address threaded breakpoints return to
	DBG$NMATCH,		! Matches input to counted strings
	DBG$NSYNTAX_ERROR,	! Formats a syntax error
	DBG$NNEXT_WORD,		! Obtains next word of input string
	DBG$NPARSE_ADDRESS,	! Interface to Address Expression Interpreter
	DBG$NGET_ADDRESS,	! Obtains an address value from an addr exp desc
	DBG$NLIST_FREEZ,	! Allocates listed dynamic storage
	DBG$IS_IT_ENTRY,	! Checks for address = entry point
	DBG$NMAKE_ARG_VECT;	! Constructs a message argument vector

EXTERNAL
    	DBG$GB_GO_ARG_FLAG: BYTE,		! Flag saying whether GO has
    						! an argument 
	DBG$GL_CONTEXT  : BITVECTOR,		! Context word
	DBG$GB_TAKE_CMD : BYTE,			! Tells monitor to take commands or not
	DBG$GL_RUNFRAME : REF BLOCK [,BYTE];	! Pointer to current runframe


GLOBAL ROUTINE DBG$NPARSE_GO (INPUT_DESC, VERB_NODE, MESSAGE_VECT) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine comprises the ATN parse network for the GO verb. During
!	processing of the input command, a command execution tree containing the
!	keywords and operands of the input command is constructed. On a non-
!	successful parse, a message argument vector is constructed and returned.
!
! FORMAL PARAMETERS:
!
!	INPUT_DESC	- A longword containing the address of a standard ascii
!			  string descriptor representing the user's input command
!
!	VERB_NODE	- A longword containing the address of the verb (head)
!			  node of the command execution tree.
!
!	MESSAGE_VECT	- The address of a longword to contain the address of a
!			  message argument vector for errors
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	On success, the command execution tree is constructed.
!
!	On failure, a message argument vector is constructed and returned.
!
! ROUTINE VALUE:
!
!	An unsigned integer longword completion code
!
! COMPLETION CODES:
!
!	STS$K_SUCCESS (1)	- Success. Input parsed and execution tree constructed.
!
!	STS$K_SEVERE  (4)	- Failure. Error detected. Message argument vector
!				  constructed and returned.
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    MAP
	VERB_NODE : REF dbg$verb_node;

    BIND
	DBG$CS_CR = UPLIT BYTE (1, dbg$k_car_return);

    LOCAL
	STATUS,					! Holds return status
	NOUN_NODE : REF dbg$noun_node;		! Will contain GO address, if any given


    ! The GO has already been accepted. Check for GO <CR>.
    !
    IF dbg$nmatch (.input_desc, dbg$cs_cr, 1)
    THEN
	BEGIN

	! Nothing left. Set the appropriate verb composite and return
	!
	verb_node [dbg$b_verb_composite] = go_noaddr;
	RETURN sts$k_success;
	END;


    ! There is some input left. Try to parse the address expression.
    !
    IF NOT dbg$nlist_freez (dbg$k_noun_node_size, noun_node, .message_vect)
    THEN
	RETURN sts$k_severe;

    verb_node [dbg$l_verb_object_ptr] = .noun_node;

    IF NOT (status = dbg$nparse_address (.input_desc, noun_node [dbg$l_noun_value],
					 dbg$k_default, .message_vect))
    THEN
	BEGIN

	! We are responsible for syntax error on sts$k_warning
	!
	IF .status EQL sts$k_warning
	THEN
	    .message_vect = dbg$nsyntax_error (dbg$nnext_word (.input_desc));
	RETURN sts$k_severe;
	END;


    ! Set the correct verb composite
    !
    verb_node [dbg$b_verb_composite] = go_addr;

    RETURN sts$k_success;

    END;			! End of dbg$nparse_go


GLOBAL ROUTINE DBG$NEXECUTE_GO (VERB_NODE, MESSAGE_VECT) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine accepts the command execution tree constructed by the parse
!	network and performs the semantic actions associated with the parsed
!	GO command as given by the user. The actual GO is not performed by this
!	routine. Rather the DEBUG monitor is informed to perform a GO.
!
! FORMAL PARAMETERS:
!
!	VERB_NODE	- A longword containing the address of the verb (head)
!			 node of the command execution tree
!
!	MESSAGE_VECT	- The address of a longword to contain the address of a
!			  standard message argument vector upon detection of
!			  errors.
!
! IMPLICIT INPUTS:
!
!	The entire command execution tree linked list pointed to by the verb node.
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	An unsigned integer longword completion code
!
! COMPLETION CODES:
!
!	STS$K_SUCCESS (1)	- Success. The GO command will be executed.
!
!	STS$K_SEVERE  (4)	- Failure. THE GO will not executed. Message argument
!				  vector constructed and returned.
!
! SIDE EFFECTS:
!
!	Semantic actions coresponding to the execution of the GO command are taken.
!
!--
    BEGIN

    MAP
	VERB_NODE 	: REF dbg$verb_node;

    BUILTIN
	PROBER;					! Probes read access

    LOCAL
	OLD_PC 		: REF VECTOR [, WORD],	! Used to access instruction
	NEW_PC,					! Starting PC value
	NOUN_NODE 	: REF dbg$noun_node,	! Noun node
	ADDRESS 	: VECTOR [2],		! Address contained by addr exp desc
	TYPE;					! Types of object described by addr exp desc

    ! Set up to do the go depending on whether there is a specified start address or not
    !
    IF .verb_node [dbg$b_verb_composite] EQL go_addr
    THEN
	BEGIN

	! Recover the noun node and obtain a PC value
	!
	noun_node = .verb_node [dbg$l_verb_object_ptr];
	IF NOT dbg$nget_address (.noun_node [dbg$l_noun_value], address [0],
				 type, .message_vect)
	THEN
	    RETURN sts$k_severe;


	! The GO address is now contained in address [0]. We don't care about the type.
	!
	new_pc = .address [0];


	! Check for an entry mask
	!
	IF dbg$is_it_entry (.new_pc)
	THEN
	    new_pc = .new_pc + 2;


	! Check for read access before we load the address into the user's PC
	!
	IF NOT PROBER (%REF (0), %REF (1), .new_pc)
	THEN
	    BEGIN
	    .message_vect = dbg$nmake_arg_vect (dbg$_badstartpc, 1, .new_pc);
	    RETURN sts$k_severe;
	    END;


	! For GO xxx, we load the new PC into the runframe and clear the FPD bit
	! in the PSL. We must also check for threaded code.
	!
	old_pc = .dbg$gl_runframe [dbg$l_user_pc];

	IF .dbg$gl_context [dbg$k_thread]	! Threaded code
	THEN
	    BEGIN
	    IF .old_pc [0] EQL %X'9B17'	! JMP @(R11)+
			OR
	       .old_pc EQL DBG$THREAD_RET ! Address of returns from threaded BPTs
	    THEN
		dbg$gl_runframe [dbg$l_user_r11] = .new_pc
	    ELSE
		BEGIN
		.message_vect = dbg$nmake_arg_vect (dbg$_notlinbnd);
		RETURN sts$k_severe;
		END;
	    END
	ELSE
	    dbg$gl_runframe [dbg$l_user_pc] = .new_pc;


	! Clear the FPD bit in the PSL
	!
	dbg$gl_runframe [dbg$l_user_psl] = .dbg$gl_runframe [dbg$l_user_psl]
						        AND
					   	    %X'F7FFFFFF';
	END;


    ! Check PC for read access
    !
    new_pc = .dbg$gl_runframe [dbg$l_user_pc];

    IF NOT PROBER (%REF (0), %REF (1), .new_pc)
    THEN
	BEGIN
	.message_vect = dbg$nmake_arg_vect (dbg$_badstartpc, 1, .new_pc);
	RETURN sts$k_severe;
	END;


    ! Inform the monitor to start the user program
    !
    dbg$gb_take_cmd = false;

    ! Set the global flag saying whether GO has an argument.
    !
    IF .verb_node[dbg$b_verb_composite] EQL go_addr
    THEN
    	dbg$gb_go_arg_flag = TRUE
    ELSE
    	dbg$gb_go_arg_flag = FALSE;

    RETURN sts$k_success;

    END;		! End of dbg$nexecute_go
    
END				!End of module
ELUDOM
