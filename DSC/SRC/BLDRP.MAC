	$BEGIN	BLDRP,0002,<BUILD RETRIEVAL POINTERS (ODS-2)>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; JOSEPH A. CARCHIDI  5-DEC-77  19:30
;
; **GR01---G.RITTENBURG 23-OCT-1978
; CONDITIONALIZED FOR 11M.
;
	.IF	NDF,R$$NVX
	.MCALL	FHDL2$
	FHDL2$				; DEFINE FILE HEADER OFFSETS
	.ENDC

;+
;
; *** - $BLDRP	BUILD RETRIEVAL POINTERS  (ODS-2 VERSION)
;
; THIS ROUTINE CONSTRUCTS ONE RETRIEVAL POINTER TO MAP THE
; SET OF BLOCKS INDICATED BY R0,R1 AND R2,R3.
; THE NEW POINTER IS APPENDED TO THE EXISTING MAP AREA OF THE HEADER.
;
;
; INPUTS:
;
;	R0,R1 = BLOCK COUNT
;	R2,R3 = STARTING LBN
;	R5 = FILE HEADER ADDRESS
;
; OUTPUTS:
;
;	RETRIEVAL POINTERS BUILT
;	CC - C	CLEAR IF ALL BLOCKS MAPPED
;		SET IF HEADER FULL
;	R0,R1,R2,R3 DESCRIBE UNMAPPED BLOCKS
;	R4,R5 PRESERVED
;-

$BLDRP::
	PUSH  R4			; SAVE CALLER'S R4.
	CLR	R4
	BISB	H.MPOF(R5),R4		; MAP AREA OFFSET TO R4.
	ASL	R4			; CONVERT IT TO A BYTE OFFSET.
	ADD	R5,R4			; ADD THE ADDRESS OF THE HEADER.
	CLR	-(SP)	
	BISB	H.USE(R5),(SP)		; NUMBER OF MAP AREA WORDS IN USE.
	ASL	(SP)			; CONVERT TO A BYTE OFFSET.
	ADD	(SP),R4			; POINT R4 AT NEXT AVAILABLE POSITION.
	CLR	-(SP)
	BISB	H.ACOF(R5),(SP)		; ACCESS LIST AREA OFFSET.
	ASL	(SP)			; CONVERT TO BYTE OFFSET.
	ADD	R5,(SP)			; ADD THE ADDRESS OF THE HEADER.
	SUB	R4,(SP)			; COMPUTE MAP AREA FREE SPACE IN BYTES.

	BEGIN	BLDPTR	
;
; USE 2 WORD POINTER IF POSSIBLE.
;
	IF  R0  EQ  #0  AND  R1  LOS  #256.  ; CAN THE COUNT BE REPRESENTED IN 8 BITS ?
	  IF  R2  LO  #64.		     ; IS STARTING LBN LT 2**22 ?
	    IF  (SP)  LO  #4  GOTO  FULL     ; WILL POINTER FIT ?
	    ADD  #4,2(SP)		; INCR MAP BYTES IN USE.
	    CLR  (R4)
	    BISB  R2,(R4)		; STORE THE HIGH ORDER LBN
	    SWAB  (R4)			; IN THE HIGH BYTE.
	    BISB  R1,(R4)		; STORE THE COUNT
	    DECB  (R4)			; REDUCED BY 1.
	    BIS  #40000,(R4)+		; INDICATE 2 WORD POINTER.
	    MOV  R3,(R4)		; STORE LOW ORDER LBN.
	    LEAVE  BLDPTR		; DONE, GET OUT.
	  END
	END
;
; 2 WORD POINTER WON'T SUFFICE, TRY 3 WORD POINTER.
;
	IF  R0  EQ  #0  AND  R1  LOS  #16384.  ; CAN THE COUNT BE REPRESENTED IN 14 BITS ?
	  IF  (SP)  LO  #6  GOTO  FULL	       ; WILL POINTER FIT ?
	  ADD  #6,2(SP)			; INCR MAP AREA BYTES IN USE.
	  CLR  (R4)
	  BIS  R1,(R4)			; STORE THE BLOCK COUNT.
	  DEC  (R4)			; REDUCED BY 1.
	  BIS  #100000,(R4)+		; INDICATE 3 WORD POINTER .
	  GOTO  MOVLBN
	END
;
; 3 WORD POINTER WON'T SUFFICE, TRY 4 WORD POINTER.
;
	IF  R0  LOS  #16384.		; IS BLOCK COUNT LOS (2**30) ?
	  IF  (SP)  LO  #8.  GOTO  FULL	; WILL POINTER FIT ?
	  ADD  #8.,2(SP)		; INCR MAP AREA BYTES IN USE.
	  MOV  R0,(R4)+			; STORE HIGH ORDER BLOCK COUNT.
	  MOV  R1,(R4)			; STORE LOW ORDER COUNT.
	  SUB  #1,(R4)+			; REDUCE COUNT BY 1.
	  SBC  -4(R4)
	  BIS  #140000,-4(R4)		; INDICATE 4 WORD POINTER.
MOVLBN:	  MOV  R3,(R4)+			; STORE LOW ORDER LBN.
	  MOV  R2,(R4)			; STORE HIGH ORDER LBN.
	  LEAVE  BLDPTR			; DONE, GET OUT.
	END
;
; IF WE GEÔ HERE, THE SET OF BLOCKS CAN'T BE MAPPED BY ONE
; RETRIEVAL POINTER. THIS IS HIGHLY UNLIKELY, AND IS TREATED
; AS AN ERROR.
;
	  .WORD	0			;TEMPORARY.....
	END	BLDPTR
;
; ADJUST R2,R3 TO THE LBN BEYOND THE LAST ONE MAPPED.
; CLEAR R0,R1 TO INDICATE NO UNMAPPED BLOCKS.
;
	ADD	R1,R3			; ADD CNT TO STARTING LBN
	ADC	R2
	ADD	R0,R2
	CLR	R0
	CLR	R1
	INC	(SP)+			; DISCARD FREE SPACE COUNT.
	ASR	(SP)			; CONVERT TO WORDS IN USE.
	MOVB	(SP)+,H.USE(R5)		; UPDATE MAP AREA WORDS IN USE.
	POP	R4			; RESTORE CALLER'S R4.
	RETURN  NOERROR
;
; TO HERE IF MAP AREA IS FULL
;
FULL:	CMP  (SP)+,(SP)+		; CLEAN STACK.
	POP  R4				; RESTORE CALLER'S R4,R5.
	RETURN  ERROR

	.END
