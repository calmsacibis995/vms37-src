	$BEGIN	WBOOT,0003,<WRITE BOOTSTRAP TO TAPE>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; ANDREW C. GOLDSTEIN  17-AUG-76  20:22

;+
;
; *** - $WBOOT	WRITE BOOTSTRAP TO OUTPUT TAPE
;
; THIS ROUTINE WRITES THE BOOTSTRAP RECORD TO THE OUTPUT TAPE
; BEING SET UP.
;
; INPUTS:
;
;	OUTLUN ASSIGNED TO OUTPUT TAPE
;
; OUTPUTS:
;
;	BOOT WRITTEN
;
;-

$WBOOT::
	$CALL $WRI1B <,,,,#$BOHD>	; WRITE THE BOOT RECORD
	RETURN				; THAT'S IT

B.SIZ	= 8.
TPS	= 177564			; TTY CSR
TPB	= 177566			; TTY DATA BUFFER
;
; THE BOOT BLOCK
;
$BOHD::	.BLKB	B.SIZ		; THE UBIQUITOUS BUFFER HEADER

BOOTBK:	NOP			; NOP IDENTIFIES BOOT BLOCK
	MOV	#1000,SP	; SET TEMP STACK
	MOV	PC,R0		; SET ADDRESS
	ADD	#BOTMSG-.,R0	; OF MESSAGE
10$:	MOVB	(R0)+,R1	; GET NEXT CHARACTER
	BEQ	20$		; END
	CALL	TYPIT		; NO, PRINT IT
	BR	10$		; LOOP FOR NEXT CHARACTER
20$:	RESET			;
	HALT			; HALT


TYPIT:	MOVB	R1,@#TPB	; PRINT CHARACTER
10$:	TSTB	@#TPS		; DONE?
	BPL	10$		; NO, WAIT
	RETURN			;


BOTMSG:	.BYTE	15,12,12
	.ASCII	/   ***THIS VOLUME DOES NOT CONTAIN A HARDWARE BOOTABLE SYSTEM ***/
	.BYTE	15,12,12,0
	.EVEN



	.END
