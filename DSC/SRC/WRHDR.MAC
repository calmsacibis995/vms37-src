	$BEGIN	WRHDR,0005,<WRITE FILE HEADER>
	$BEGIN	WRHDR,0004,<WRITE FILE HEADER>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; ANDREW C. GOLDSTEIN  16-AUG-76  3:31
;
; ***GR01---G.RITTENBURG 24-OCT-1978
; CONDITIONALIZED FOR 11M
;

	.IF	NDF,R$$NVX
	.MCALL	FHDL2$
	FHDL2$				; DEFINE FILE HEADER OFFSETS
	.ENDC

;+
;
; *** - $WRHDR	WRITE FILE HEADER
;
; THIS ROUTINE WRITES THE INDICATED FILE HEADER TO THE OUTPUT DISK.
;
; INPUTS:
;
;	R4 = BUFFER CONTROL BLOCK ADDRESS OF HEADER
;	OUTLUN ASSIGNED TO OUTPUT DEVICE
;
; OUTPUTS:
;
;	HEADER WRITE IN PROGRESS
;	R4 PRESERVED, OTHER REGISTERS CLOBBERED
;
;
; *** - $WRHDA	WRITE HEADER AT ALTERNATE ADDRESS
;
; AS ABOVE, BUT INPUTS:
;
;	R5 = HEADER ADDRESS
;
;
; *** - $WRHD1	WRITE DELETED FILE HEADER
;
; THIS ENTRY WRITES A DELETED FILE HEADER TO THE INDEX FILE.
;
; INPUTS:
;
;	R3 = FILE NUMBER
;	R4 = BCB ADDRESS
;	R5 = HEADER ADDRESS
;
; OUTPUTS:
;
;	AS ABOVE
;
;-

$WRHDR::
	LET R5 := R4 + #B.SIZ		; GET BUFFER ADDRESS

$WRHDA::
	$CALL $CKSUM <,,,,,R5>		; COMPUTE HEADER CHECKSUM
	LET R3 := H.FNUM(R5)		; GET FILE NUMBER

$WRHD1::
	LET R0 := $OUDEV		; GET OUTPUT DEVICE TABLE ENTRY
	LET $FNU := R3			; SAVE FOR ERROR MESSAGES
	LET R3 := R3 + V.IBSZ(R0)	; COMPUTE INDEX FILE VBN
	LET R2 := #0 + CARRY
;
; ACCOUNT FOR THE BOOT BLOCK, HOME BLOCK, BACKUP HOME BLOCK AND
; THE BACKUP COPY OF THE INDEX FILE HEADER.
;
; EACH OCCUPIES ONE CLUSTER OF VBNS AND LBNS. (CLUSTER FACTOR*4)+1
;
	MOV  V.CLF(R0),-(SP)		; CLUSTER FACTOR TO TEMP.
	ASL  (SP)			; MULT BY
	ASL  (SP)			;         FOUR.
	ADD  (SP)+,R3
	ADC  R2
	PUSH R5				; SAVE ADDRESS
	$CALL $MPVBN <,,R2,R3,,#$OXBF>	; MAP TO LBN
	ON.ERROR THEN ERROR ER.OFN	; NOT MAPPED
	POP R5
	$CALL $WRITA <,#512.,R2,R3,R4,R5> ; WRITE IT
	$CALL $WAITO <,,,,R4>		; WAIT FOR COMPLETION
	ON.ERROR THEN ERROR ER.WHD
	RETURN



	.END
