	$BEGIN	FILSZ,0003,<COMPUTE SIZE OF FILE (ODS-2)>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; JOSEPH A. CARCHIDI  8-DEC-77  20:30
; ANDREW C. GOLDSTEIN  11-MAY-78  17:00
;
; **GR01---G.RITTENBURG 23-OCT-1978
; CONDITIONALIZED FOR 11M
;

	.IF	NDF,R$$NVX
	.MCALL	FHDL2$
	FHDL2$				; DEFINE FILE HEADER LAYOUT
	.ENDC

;+
;
; *** - $FILSZ	COMPUTE FILE SIZE
;
; THIS ROUTINE COMPUTES THE SIZE IN LOGICAL BLOCKS OF THE FILE
; WHOSE HEADER IS IN THE INDICATED BUFFER.
;
; INPUTS:
;
;	R5 = HEADER ADDRESS
;
; OUTPUTS:
;
;	R0, R1 = HIGH AND LOW ORDER FILE SIZE
;	OTHER REGISTERS PRESERVED
;
;-

$FILSZ::
	PUSH	R2,R3,R4,R5		; SAVE CALLER'S REGS.
	CLR	-(SP)			; MAP WORDS IN USE COUNT TO
	BISB	H.USE(R5),(SP)		; LOOP COUNTER.
	CLR	R2
	BISB	H.MPOF(R5),R2		; MAP AREA WORD OFFSET TO R2.
	ASL	R2			; CONVERT TO A BYTE OFFSET.
	ADD	R2,R5			; POINT R5 AT MAP AREA.
	CLR	-(SP)			; INITIALIZE HIGH AND
	CLR	-(SP)			; LOW ORDER FILE SIZE.
	WHILE	4(SP)  GT  #0		; LOOP FOR ALL POINTERS.
	  CALL  $GT1RP			; GET 1 RETRIEVAL POINTER.
	  ADD  #1,R1			; ADJUST TO REAL BLOCK COUNT.
	  ADC  R0
	  ADD  R1,(SP)			; ACCUMULATE FILE SIZE.
	  ADC  2(SP)
	  ADD  R0,2(SP)
	  SUB  R4,4(SP)			; DECR BY POINTER SIZE.
	END	<WHILE LOOP>
	POP	R1,R0			; FILE SIZE TO R0,R1.
	INC	(SP)+			; DISCARD LOOP COUNTER.
	POP	R5,R4,R3,R2		;RESTORE CALLER'S REGS.
	RETURN



	.END

