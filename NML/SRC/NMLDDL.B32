!
!	%TITLE 'NMLDDL - NML Data Definitions'
!	IDENT = 'V03-000'
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:  DECnet-VAX Network Management Listener
!
! ABSTRACT:
!
!
! This module contains macro and symbol definitions used by all NML modules.
!
! ENVIRONMENT:  VAX/VMS Operating System
!
! AUTHOR:  Distributed Systems Software Engineering
!
! CREATION DATE:  30-DEC-1979
!
! MODIFIED BY:
!
!	V03-002	MKP0002		Kathy Perko	2-Nov-1981
!		Delete NML$GW_CMD_CHAN
!
!	V03-001	MKP0001		Kathy Perko	14-Sept-1981
!		Make P4 buffer size smaller so systems with SYSGEN parameter,
!		MAXBUF, don't get buffer quota exceeded for SHOW CIRCUIT
!		CHARACTERSITICS.
!
!--

!
! Miscellaneous symbols
!
LITERAL
    FALSE = 0,
    TRUE  = 1;
!
! The following symbols are internal parameter codes.  The values all have
! bit 15 set, indicating a counter value, to avoid conflicts with other
! network management parameter codes.
!
LITERAL
    NMA$C_PCNO_A$$ = 1 ^ 15 OR 0,	! Loop node address
    NMA$C_PCLI_LC$ = 1 ^ 15 OR 1,	! Line counters
    NMA$C_PCNO_EC$ = 1 ^ 15 OR 2,	! Executor node counters
    NMA$C_PCNO_NC$ = 1 ^ 15 OR 3,	! Node counters
    NMA$C_PCCI_CC$ = 1 ^ 15 OR 4,	! Circuit counters
    NMA$C_PCXP_PC$ = 1 ^ 15 OR 5;	! X-25 Protocol DTE counters.
!
! Structure declarations used for system defined structures to
! save typing.
!
STRUCTURE
    BBLOCK [O, P, S, E; N] =
	[N]
	(BBLOCK+O)<P,S,E>,

    BBLOCKVECTOR [I, O, P, S, E; N, BS] =
	[N*BS]
	((BBLOCKVECTOR+I*BS)+O)<P,S,E>;

!
! Macro to generate Network ACP Control QIO (NFB) P1 buffer contents.  The NFB
! describes SET, SHOW, CLEAR, and ZERO operations.
!
MACRO
	$NFB (FUNC, FLAGS, DATABASE, SRCH_KEY, STRT_KEY) =

	BYTE ( %IF %IDENTICAL (FUNC, 0)		! QIO function code.
		%THEN 0
		%ELSE %NAME ('NFB$C_FC_',FUNC)
		%FI),
	BYTE ( %IF %NULL (FLAGS)		! Update and known flags.
		%THEN 0
		%ELSE FLAGS
		%FI),
	BYTE ( %IF %IDENTICAL (DATABASE, 0)	! ACP database to update.
		%THEN 0
		%ELSE %NAME ('NFB$C_DB_',DATABASE)
		%FI),
	BYTE (0),				! Search equal
	LONG ($FIELD_ID (DATABASE, SRCH_KEY)),	! Search key ID
	LONG ( %IF %NULL (STRT_KEY)		! Start key ID
		%THEN NFB$C_WILDCARD
		%ELSE $FIELD_ID (DATABASE, STRT_KEY)
		%FI
		),
	LONG (0),				! Spare and variable cell size

	%IF NOT %NULL(%REMAINING)
	%THEN $FIELD_ID_LIST (DATABASE, %REMAINING)
		,LONG (NFB$C_ENDOFLIST)	! End delimiter for field ID list.
	%ELSE
		LONG (NFB$C_ENDOFLIST)	! End delimiter for field ID list.
	%FI

	%,

    !
    ! Generate a list of longwords containing the NETACP field IDs for
    ! the parameters.  This iterative macro will generate as many
    ! field IDs as are supplied.
    !
    $FIELD_ID_LIST (DATABASE) [FIELD_ID] =
	LONG ($FIELD_ID (DATABASE, FIELD_ID))
	%,

    $FIELD_ID (DATABASE, FIELD_ID) =
	%IF %IDENTICAL (FIELD_ID, NFB$C_WILDCARD) OR
	    %IDENTICAL (FIELD_ID, NFB$C_COLLATE)
	%THEN
		FIELD_ID
	%ELSE
		%IF %NULL (FIELD_ID)
		%THEN	0
		%ELSE %NAME ('NFB$C_',DATABASE,'_',FIELD_ID)
		%FI
	%FI
	%;

!
! Macros to generate Network Control I/O request descriptors.
!
MACRO
	!
	! Declare the NFB buffer (use the number of input parameters to figure
	! out how big to make it) and set up a descriptor for it.
	!
	$NFBDSC (NAM) =
		SWITCHES UNAMES;
		OWN
		    _NFB : VECTOR [$NFB_ALLOCATION (%REMAINING)]
					INITIAL ($NFB (%REMAINING));
		BIND
		    %NAME(NAM) = UPLIT (%ALLOCATION(_NFB), _NFB);
		UNDECLARE _NFB;
		SWITCHES NOUNAMES
		%,

	$NFB_ALLOCATION [] =
		5+(MAX(0,%LENGTH-4))
		%;
!
! Macro to extract the bit number from bit field references
!
MACRO
    $BITN (O, B, W, S) = B
    %;
!
! Macro to signal status message
!
MACRO
    $SIGNAL_MSG [] =
	SIGNAL (NML$K_SIG_CODE, %REMAINING)
    %;
!
! Macro to create constant string descriptor
!
MACRO
    $ASCID [] =
	(UPLIT (%CHARCOUNT(%STRING(%REMAINING)),
		UPLIT BYTE (%STRING(%REMAINING))))
    %;

MACRO
    $ASCIC [] =
	UPLIT BYTE (%ASCIC %STRING (%REMAINING))
    %;

MACRO
    DESCRIPTOR =
	 BBLOCK [8]
    %;
!
! I/O Status Block definition
!
FIELD
    IOSB_FIELDS =
	SET
	IOS$W_STATUS = [0, 0, 16, 0],	! Status field
	IOS$W_COUNT  = [2, 0, 16, 0],	! Byte count field
	IOS$L_INFO   = [4, 0, 32, 0]	! Device dependent information
	TES;

MACRO
    $IOSB =
	BBLOCK [8]  FIELD (IOSB_FIELDS)
    %;
!
! Macro to define Network Management version fields
!
FIELD
	NMV_FIELDS =
	SET
	NMV$B_VERSION = [0,0,8,0],
	NMV$B_DEC_ECO = [1,0,8,0],
	NMV$B_USER_ECO = [2,0,8,0]
	TES;

MACRO
	NMV = BBLOCK [3] FIELD (NMV_FIELDS)
    %;
!
! Macro to define external data used for service operations.
!
MACRO $NML_SRVDEF =

EXTERNAL
    NML$AB_SRVBUFFER	: BBLOCK [0],
    NML$AB_SRVIDBUFFER	: BBLOCK [0],
    NML$AB_MOPRCVBUFFER : BBLOCK [0],
    NML$AB_MOPSNDBUFFER : BBLOCK [0],
    NML$GB_SRVIDCODE	: BYTE,
    NML$GQ_SRVBFDSC	: DESCRIPTOR,
    NML$GQ_SRVDATDSC	: DESCRIPTOR,
    NML$GQ_SRVIDBFDSC	: DESCRIPTOR,
    NML$GQ_SRVIDDSC	: DESCRIPTOR,
    NML$GQ_MOPRCVBFDSC  : DESCRIPTOR,
    NML$GQ_MOPSNDBFDSC	: DESCRIPTOR;
    %;
!
! Macro to define external symbols common to most of the modules.
!
MACRO $NML_EXTDEF =
EXTERNAL
!
! Event data
!
    NML$GB_EVTSRCTYP : BYTE,		! Event source type
    NML$GQ_EVTSRCDSC : DESCRIPTOR,	! Event source descriptor
    NML$GW_EVTCLASS  : WORD,		! Event class
    NML$GB_EVTMSKTYP : BYTE,		! Mask type
    NML$GQ_EVTMSKDSC : DESCRIPTOR,	! Mask descriptor
    NML$GW_EVTSNKADR : WORD,		! Sink node address
!
    NML$GW_ACP_CHAN,
    NML$GL_LOGMASK	: BITVECTOR [32],
    NML$GQ_ENTSTRDSC	: DESCRIPTOR,
    NML$AB_QIOBUFFER	: BBLOCK [0],
    NML$GQ_QIOBFDSC	: DESCRIPTOR,
    NML$AB_EXEBUFFER	: VECTOR [0, BYTE],
    NML$GL_EXEDATPTR,
    NML$GQ_EXEDATDSC	: DESCRIPTOR,
    NML$GQ_EXEBFDSC	: DESCRIPTOR,
    NML$AB_RCVBUFFER	: VECTOR [NML$K_RCVBFLEN, BYTE],
    NML$GQ_RCVBFDSC	: DESCRIPTOR,
    NML$AB_SNDBUFFER	: VECTOR [NML$K_SNDBFLEN, BYTE],
    NML$GQ_SNDBFDSC	: DESCRIPTOR,
    NML$GL_RCVDATLEN,
    NML$AB_CPTABLE	: BBLOCKVECTOR [0, CPT$K_ENTRYLEN],
    NML$AB_MSGBLOCK	: BBLOCK [MSB$K_LENGTH],
    NML$AB_ENTID	: BBLOCK [16],
    NML$AB_ENTITYDATA	: BBLOCKVECTOR [, EIT$K_ENTRYLEN],
    NML$AB_NML_NMV	: NMV,
    NML$AB_PRMSEM	: BBLOCKVECTOR [0, PST$K_ENTRYLEN],
    NML$AB_RECBUF	: BBLOCK [0],
    NML$AL_ENTINFTAB	: VECTOR [0],
    NML$AL_PERMINFTAB	: VECTOR [0],
    NML$AW_PRM_DES	: BLOCKVECTOR [PDB$K_NUMBER, 4, WORD],
    NML$GB_CMD_VER	: BYTE,
    NML$GB_ENTCODE	: BYTE,
    NML$GB_ENTFRM	: BYTE,
    NML$GB_FUNC		: BYTE,
    NML$GB_INFO		: BYTE,
    NML$GB_OPTIONS	: BYTE,
    NML$GL_PRMCODE,
    NML$GL_PRS_FLGS	: BLOCK [1],
    NML$GQ_NETNAMDSC    : DESCRIPTOR,
    NML$GQ_RECBFDSC	: DESCRIPTOR,
    NML$GW_PRMDESCNT	: WORD;
    %;
!
! NPARSE argument block structure definitions
!
MACRO
    $NPA_ARGDEF =
	BUILTIN
	    AP;
	BIND
	    NPARSE_BLOCK = AP : REF $NPA_BLKDEF;
    %;
!
! NPARSE argument block definition macro
!
MACRO
    $NPA_BLKDEF =
	BBLOCK [NPA$K_LENGTH0]
    %;
!
! Buffer length parameters.
!
LITERAL
    NML$K_RCVBFLEN = 512,		! Receive buffer length
    NML$K_SNDBFLEN = 512,		! Send buffer length
    NML$K_QIOBFLEN = 730,		! QIO buffer length
    NML$K_P2BUFLEN = 80,		! Max length for P2 buffers.
    NML$K_RECBFLEN = 1024;		! Record buffer length
!
! Parameter descriptor block (PDB) definitions. 
! 
LITERAL	PDB$K_NUMBER	= 32;		! Number of parameter descriptor slots 

MACRO	PDB$W_INDEX	= 0,0,16,0%;	! Parameter change table (CPT) index
MACRO	PDB$W_COUNT	= 1,0,16,0%;	! Parameter byte count 
MACRO	PDB$A_POINTER	= 2,0,32,0%;	! Pointer to parameter value 

LITERAL PDB$K_SIZE	= 8;		! Size of parameter descriptor entry

