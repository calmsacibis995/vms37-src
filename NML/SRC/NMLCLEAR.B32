%TITLE 'NML CLEAR parameter module'
MODULE NML$CLEAR (
		LANGUAGE (BLISS32),
		ADDRESSING_MODE (NONEXTERNAL=LONG_RELATIVE),
		ADDRESSING_MODE (EXTERNAL=LONG_RELATIVE),
		IDENT = 'V03-000'
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:  DECnet-VAX Network Management Listener
!
! ABSTRACT:
!
!	This module contains routines to process the NCP CLEAR command.
!
! ENVIRONMENT:  VAX/VMS Operating System
!
! AUTHOR:  Distributed Systems Software Engineering
!
! CREATION DATE:  30-DEC-1979
!
! MODIFIED BY:
!
!	V03-002	MKP0002		Kathy Perko	31-Dec-1981
!		Fix CLEAR EXEC so that a wildcard search key is
!		used.
!
!	V03-001	MKP0001		Kathy Perko	21-July-1981
!		Add circuit entity, make changes for multidrop.
!--


%SBTTL 'Declarations'

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    NML$CLEARENTITY	: NOVALUE,
    NML$CLEAREXECUTOR	: NOVALUE,
    NML$CLEARKNONODES	: NOVALUE,
    NML_CLEARENTITY,
    NML$CLEARKNOLOG	: NOVALUE,
    NML$CLEARLOGGING	: NOVALUE,
    NML_CLEARLOGGING	: NOVALUE,
    NML_CLEARLOGALL	: NOVALUE,
    NML$CLEARKNOWN	: NOVALUE;

!
! INCLUDE FILES:
!

LIBRARY 'LIB$:NMLLIB.L32';
LIBRARY 'SHRLIB$:NMALIBRY.L32';
LIBRARY 'SHRLIB$:NET.L32';
LIBRARY 'SYS$LIBRARY:STARLET.L32';

!
! MACROS:
!

!
! EQUATED SYMBOLS:
!

LITERAL
    NML$K_ENTBUFLEN = 32;		! Length of entity buffer

!
! OWN STORAGE:
!

!
! Parameter buffer and descriptor for handling volatile data.
!
OWN
    NML$T_EVTBUFFER  : VECTOR [NML$K_RECBFLEN, BYTE],
    NML$T_P2BUFFER   : VECTOR [NML$K_P2BUFLEN,  BYTE],
    NML$T_NFBBUFFER  : VECTOR [100,  BYTE],
    NML$T_PRMBUFFER  : VECTOR [256, BYTE];
BIND
    NML$Q_EVTBFDSC  = UPLIT (NML$K_RECBFLEN, NML$T_EVTBUFFER)  : DESCRIPTOR,
    NML$Q_P2BFDSC   = UPLIT (NML$K_P2BUFLEN,  NML$T_P2BUFFER)   : DESCRIPTOR,
    NML$Q_NFBBFDSC  = UPLIT (100, NML$T_NFBBUFFER)   : DESCRIPTOR,
    NML$Q_PRMBFDSC  = UPLIT (256, NML$T_PRMBUFFER)  : DESCRIPTOR;
!
! Entity buffer and descriptor.
!
OWN
    NML$T_ENTBUFFER : BBLOCK [NML$K_ENTBUFLEN],
    NML$Q_ENTBFDSC  : DESCRIPTOR
		      INITIAL (0, NML$T_ENTBUFFER);

!
! EXTERNAL REFERENCES:
!

$NML_EXTDEF;

EXTERNAL ROUTINE
    NMA$INSERTFLD,
    NMA$SEARCHFLD,
    NML$ADDEVENTS,
    NML$ADDFILTERS,
    NML$BLD_REPLY,
    NML$BLDALLDES,
    NML$BLDP2,
    NML$BLDSETQBF,
    NML$ERROR_1,
    NML$GETEXEID,
    NML$GETINFTABS,
    NML$GETKNOWNLIST,
    NML$GETNXTSNK,
    NML$NETQIO,
    NML$REMSRC,
    NML$SAVEVENTS,
    NML$SEND;


%SBTTL 'NML$CLEARENTITY  Clear volatile entity parameters'
GLOBAL ROUTINE NML$CLEARENTITY (ENT, LEN, ADR) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine clears parameters for the specified entity type.
!	Its purpose is to allow the same code to be used for both
!	singular and plural entity operations.
!
! INPUTS:
!
!	ENT		Entity type code.
!	LEN		Byte count of entity id string.
!	ADR		Address of entity id string.
!
! OUTPUTS:
!
!	Specified parameters or entities cleared from database.
!--

    BEGIN

    LOCAL
	MSGSIZE;

    NML_CLEARENTITY (.ENT, .LEN, .ADR);

    NML$BLD_REPLY (NML$AB_MSGBLOCK, MSGSIZE); ! Build message
    NML$SEND (NML$AB_SNDBUFFER, .MSGSIZE);    ! Send message error

    END;				! End of NML$CLEARENTITY


%SBTTL 'NML$CLEAREXECUTOR  Clear volatile executor parameters'
GLOBAL ROUTINE NML$CLEAREXECUTOR (ENT, DUM1, DUM2) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine clears executor parameters.
!
! INPUTS:
!
!	ENT		Entity type code.
!	DUM1		Not used.
!	DUM2		Not used.
!
! OUTPUTS:
!
!	Executor parameters cleared from the database.
!--

    BEGIN

    LOCAL
	EXEADR,
	MSGSIZE;

    EXEADR = 0;				! Executor address is zero
!
! If the entire executor node entry is to be deleted then attempt
! to delete the executor parameters.  If this is successful then
! delete the executor remote node parameters.
!
    IF .NML$GL_PRS_FLGS [NML$V_PRS_ALL]
    THEN
	BEGIN

	IF NML_CLEARENTITY (NML$C_EXECUTOR, -1, EXEADR)
	THEN
	    NML_CLEARENTITY (NML$C_NODE, 2, EXEADR);

	END
    ELSE
	BEGIN
!
! If only certain executor parameters are to be deleted then check to see
! if the group of parameters is for the executor only or for the executor
! parameters which are common with other remote nodes.
!
	IF .NML$GL_PRS_FLGS [NML$V_PRS_EXEPG]
	THEN
	    NML_CLEARENTITY (NML$C_EXECUTOR, -1, EXEADR)
	ELSE
	    NML_CLEARENTITY (NML$C_NODE, 2, EXEADR);

	END;
!
! Build and send the status message.
!
    NML$BLD_REPLY (NML$AB_MSGBLOCK, MSGSIZE);
    NML$SEND (NML$AB_SNDBUFFER, .MSGSIZE);

    END;				! End of NML$CLEAREXECUTOR


%SBTTL 'NML$CLEARKNONODES  Clear volatile node parameters'
GLOBAL ROUTINE NML$CLEARKNONODES (ENT, DUM1, DUM2) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine clears parameters for all nodes.
!
! INPUTS:
!
!	ENT		Entity type code.
!	DUM1		Not used.
!	DUM2		Not used.
!
! OUTPUTS:
!
!	Specified node parameters or nodes cleared from database.
!--

BEGIN

IF NOT .NML$GL_PRS_FLGS [NML$V_PRS_LOOPG]
THEN
	BEGIN
!
! Clear executor node parameters.
!
	NML$CLEAREXECUTOR (NML$C_EXECUTOR, 0, 0);
!
! Clear remote node parameters.
!
	NML$CLEARKNOWN (NML$C_NODE, 0, 0);

	END
!
! If the parameter is specific to loop nodes then clear loop node parameters.
!
ELSE
	NML$CLEARKNOWN (NML$C_LOOPNODE, 0, 0);

END;				! End of NML$CLEARKNONODES


%SBTTL 'NML_CLEARENTITY  Clear volatile entity parameters'
ROUTINE NML_CLEARENTITY (ENT, LEN, ADR) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine clears parameters for the specified entity.
!
! INPUTS:
!
!	ENT		Entity code.
!	LEN		Length of entity id string in bytes.
!	ADR		Address of entity id string.
!
! OUTPUTS:
!
!	Modifies contents of the following:
!
!		NML$T_ENTBUFFER
!		NML$Q_ENTBFDSC [DSC$W_LENGTH]
!		NML$AB_MSGBLOCK
!		NML$T_P2BUFFER
!		NML$T_PRMBUFFER
!--

BEGIN

    LOCAL
	DB,
	SRCHKEY,
	FUNC,
	MSGSIZE,
	NODADDR,
	NFBDSC : DESCRIPTOR,
	P2DSC  : DESCRIPTOR,
	QBFDSC : DESCRIPTOR,
	STATUS,
	TMPSNK;

!
! Set up search key value for QIO.  In this case it's the entity ID for
! the entity being cleared or deleted (CLEAR ALL).
!
DB      = .NML$AB_ENTITYDATA [.ENT, EIT$B_DATABASE];
SRCHKEY = .NML$AB_ENTITYDATA [.ENT, EIT$L_SRCHKEY];

!
! Set appropriate function code
!
IF .NML$GL_PRS_FLGS [NML$V_PRS_ALL]
THEN
	FUNC = NFB$C_FC_DELETE
ELSE
	FUNC = NFB$C_FC_CLEAR;
!
! Clear parameters from the volatile data base entry.
!
NML$BLDSETQBF (.FUNC, .DB,
		.SRCHKEY, .LEN, .ADR,
		NML$Q_NFBBFDSC, NFBDSC,
		NML$Q_P2BFDSC,  P2DSC,
		NML$GQ_QIOBFDSC, QBFDSC);
STATUS = NML$NETQIO (NFBDSC, P2DSC, 0, QBFDSC);

IF .STATUS
THEN
	BEGIN
	NML$AB_MSGBLOCK [MSB$L_FLAGS] = 0;
	NML$AB_MSGBLOCK [MSB$B_CODE] = NMA$C_STS_SUC;
	END;

RETURN .STATUS
END;				! End of NML_CLEARENTITY


%SBTTL 'NML$CLEARKNOLOG  Clear parameters for known logging'
GLOBAL ROUTINE NML$CLEARKNOLOG (ENT, DUM1, DUM2) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Clear parameters for all volatile data base entries of the specified
!	type.
!
! INPUTS:
!
!	ENT		Entity type code.
!	DUM1		Not used.
!	DUM2		Not used.
!
! OUTPUTS:
!
!	All logging paramters are deleted from the database.
!--

    BEGIN
!
! Add parameters to all sinks.
!
    NML$CLEARLOGGING (.ENT, NMA$C_SNK_CON, 0); ! Console
    NML$CLEARLOGGING (.ENT, NMA$C_SNK_FIL, 0); ! File
    NML$CLEARLOGGING (.ENT, NMA$C_SNK_MON, 0); ! Monitor

    END;				! End of NML$CLEARKNOLOG

%SBTTL 'NML$CLEARLOGGING  Clear logging parameters'
GLOBAL ROUTINE NML$CLEARLOGGING (ENT, SNK, DUM2) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Removes parameters to the volatile data base entry for the specified
!	logging entity.
!
! INPUTS:
!
!	ENT		Entity type code.
!	SNK		Logging sink type.
!	DUM2		Not used.
!
! OUTPUTS:
!
!	Specified parameters or entities cleared from database.
!--

    BEGIN

    LOCAL
	MSG_SIZE;			! Message size

!
! See if parameter indicates all parameters.
!
    IF .NML$GL_PRS_FLGS [NML$V_PRS_ALL]
    THEN
	NML_CLEARLOGALL (.SNK)
    ELSE
!
! Decide if the parameter group is for filters (EFI) or sinks (ESI).
!
	IF .NML$GL_PRS_FLGS [NML$V_PRS_ESIPG]
	THEN
	    NML_CLEARENTITY (NML$C_SINK, 1, SNK)
	ELSE
	    NML_CLEARLOGGING (.SNK, .NML$GW_EVTSNKADR);
!
! Add entity id (sink type code) to entity buffer.
!
    NML$Q_ENTBFDSC [DSC$W_LENGTH] = 1;
    NML$Q_ENTBFDSC [DSC$A_POINTER] = NML$T_ENTBUFFER;
    CH$WCHAR (.SNK, NML$T_ENTBUFFER);
!
! Set up message information.
!
    NML$AB_MSGBLOCK [MSB$V_ENTD_FLD] = 1; ! Set entity descriptor flag
    NML$AB_MSGBLOCK [MSB$A_ENTITY] =
				NML$Q_ENTBFDSC; ! Add entity descriptor pointer
!
! Build and send the message.
!
    NML$BLD_REPLY (NML$AB_MSGBLOCK, MSG_SIZE);
    NML$SEND (NML$AB_SNDBUFFER, .MSG_SIZE);

    END;				! End of NML$CLEARLOGGING

%SBTTL 'NML_CLEARLOGGING  Clear entity parameters'
ROUTINE NML_CLEARLOGGING (SNK, SNKADR) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine performs clear functions on the logging volatile data 
!	base for both singular and plural requests.
!
! INPUTS:
!
!	SNK		Logging sink type.
!	SNKADR		Sink node address.
!
! OUTPUTS:
!
!	Specified parameters cleared from database.
!--

    BEGIN

    MAP
	SNKADR : WORD;

    LOCAL
	DB,				! Database ID
	SRCHKEY,			! Search key ID
	FUNC,				! Function to perform
	DUMDSC   : REF DESCRIPTOR,	! Dummy descriptor for table
	FLDSIZE,
	FLDADR,
	SHOW_NFBDSC	: REF DESCRIPTOR,
	CLEAR_NFBDSC	: DESCRIPTOR,
	P2DSC    : DESCRIPTOR,
	PTR,
	QBFDSC   : DESCRIPTOR,
	RECDSC   : DESCRIPTOR,
	STATUS,
	TABDES   : REF DESCRIPTOR,
	TMPSNK,				! Temporary sink address
	UPDFLG;				! Data base update flag

    NML$AB_MSGBLOCK [MSB$L_FLAGS] = 0;	! Initialize message flags
    RECDSC [DSC$W_LENGTH] = 2;		! Initial descriptor
    RECDSC [DSC$A_POINTER] = .NML$GQ_RECBFDSC [DSC$A_POINTER];
!
! Get the logging sink node information.
!
    NML$GETINFTABS (NML$C_LOGGING, NML$C_EVENTS, SHOW_NFBDSC, DUMDSC, 0);
    NML$BLDP2 (0, .SNKADR, -1, 0, NML$Q_P2BFDSC, P2DSC);
!
! Look for the sink node entry in the volatile data base.  If no entry is
! found then just return.  If an error is encountered then return it.
!
    STATUS = NML$NETQIO (.SHOW_NFBDSC, P2DSC, 0, NML$GQ_QIOBFDSC);

    IF .STATUS EQL NML$_STS_CMP
    THEN
	BEGIN
	NML$AB_MSGBLOCK [MSB$L_FLAGS] = 0;
	NML$AB_MSGBLOCK [MSB$B_CODE] = NMA$C_STS_SUC;
	END;

    IF NOT .STATUS
    THEN
	RETURN;

    PTR = .NML$GQ_QIOBFDSC [DSC$A_POINTER];
!
! If the length of the parameter is not zero then store the event information
! in a record that looks like the permanent data base.
!
    IF NOT NML$SAVEVENTS (NML$GQ_RECBFDSC,
		 	  .(.PTR)<0,16>,
			  .PTR + 2,
			  RECDSC)
    THEN
	RETURN;
!
! Add event to record.
!
    IF NOT NML$ADDEVENTS (FALSE, RECDSC, .SNK, .SNKADR, UPDFLG)
    THEN
	RETURN;
!
! If there are any filters left, replace them in the volatile data base.
! Otherwise, delete the entire sink node entry.
! 

    IF .UPDFLG
    THEN
	FUNC = NFB$C_FC_SET
    ELSE
	FUNC = NFB$C_FC_DELETE;

    DB = .NML$AB_ENTITYDATA [NML$C_LOGGING, EIT$B_DATABASE]; 
    SRCHKEY = .NML$AB_ENTITYDATA [NML$C_LOGGING, EIT$L_SRCHKEY]; 
    TABDES = .NML$AB_ENTITYDATA [NML$C_LOGGING, EIT$A_ALLTAB]; 

    NML$BLDALLDES (RECDSC, .TABDES);
    NML$BLDSETQBF (.FUNC, .DB,
    			.SRCHKEY, 0, SNKADR,
    			NML$Q_NFBBFDSC, CLEAR_NFBDSC,
			NML$Q_P2BFDSC,  P2DSC,
			NML$GQ_QIOBFDSC, QBFDSC);

    IF NML$NETQIO (CLEAR_NFBDSC, P2DSC, 0, QBFDSC)
    THEN
        BEGIN

        NML$AB_MSGBLOCK [MSB$L_FLAGS] = 0;
        NML$AB_MSGBLOCK [MSB$B_CODE] = NMA$C_STS_SUC;

        END;

    END;				! End of NML_CLEARLOGGING

%SBTTL 'NML_CLEARLOGALL  Clear all logging parameters'
ROUTINE NML_CLEARLOGALL (SNK) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine clears all parameters for the specified logging sink type
!	from the volatile data base.
!
! INPUTS:
!
!	SNK
!
! OUTPUTS:
!
!	All parameters for the sink type are deleted from the database.
!--

    BEGIN

    LOCAL
	BUFEND,
	DB,				! Database ID
	SRCHKEY,			! Search key
	DUMDSC : REF DESCRIPTOR,
	ENTADD,
	ENTLEN,
	LISDSC : DESCRIPTOR,		! List buffer descriptor
	MSGFLG : BYTE,			! Response message flag
	SHOW_NFBDSC	: REF DESCRIPTOR,	! NFB descriptor address
	CLEAR_NFBDSC	: DESCRIPTOR,
	P2DSC  : DESCRIPTOR,
	PRMDSC : DESCRIPTOR,
	PTR,				! Pointer into sink node list
	QBFDSC : DESCRIPTOR,
	RECDSC : DESCRIPTOR,		! Record descriptor
	SETDSC : DESCRIPTOR,		! Event parameter descriptor
	SNKADR : WORD,			! Address of sink node
	SRCPTR,				! Pointer to source block
	STATUS,
	STRTFLG,
	TMPSNK;				! Temporary sink address

!
! Get entity database ID, the search key value, and clear the
! parameter descriptor count.
!
    DB = .NML$AB_ENTITYDATA [NML$C_LOGGING, EIT$B_DATABASE];
    SRCHKEY = .NML$AB_ENTITYDATA [NML$C_LOGGING, EIT$L_SRCHKEY];
    NML$GW_PRMDESCNT = 0;
!
! Clear the sink parameters.  If error then don't do any more.
!
    STATUS = NML_CLEARENTITY (NML$C_SINK, 1, SNK);

    IF NOT .STATUS AND (.STATUS NEQ NML$_STS_CMP)
    THEN
	RETURN;
    !
    ! Purge the logging filter data.
    !
    STRTFLG = FALSE;

    WHILE (STATUS = NML$GETKNOWNLIST (NML$C_LOGGING,
				      .STRTFLG,
				      LISDSC)) DO
	BEGIN

	PTR = .LISDSC [DSC$A_POINTER];
	BUFEND = .LISDSC [DSC$A_POINTER] + .LISDSC [DSC$W_LENGTH];
	!
	! Set up success message as the default.
	!
	NML$AB_MSGBLOCK [MSB$L_FLAGS] = 0;
	NML$AB_MSGBLOCK [MSB$B_CODE]  = NMA$C_STS_SUC;

	WHILE .PTR LSSA .BUFEND DO
	    BEGIN

	    STRTFLG = TRUE;

	    SNKADR = .(.PTR)<0,16>;
	    PTR = .PTR + 4;
	    !
	    ! Get the data.
	    !
	    NML$GETINFTABS (NML$C_LOGGING, NML$C_EVENTS, SHOW_NFBDSC,
				DUMDSC, 0);
	    NML$BLDP2 (0, .SNKADR, -1, 0, NML$Q_P2BFDSC, P2DSC);

	    STATUS = NML$NETQIO (.SHOW_NFBDSC,
				 P2DSC,
				 0,
				 NML$GQ_QIOBFDSC);
	    !
	    ! If QIO fails then don't do any more.
	    !
	    IF NOT .STATUS
	    THEN
		EXITLOOP;

	    SETDSC [DSC$W_LENGTH]  =
		.(.NML$GQ_QIOBFDSC [DSC$A_POINTER])<0,16>;
	    SETDSC [DSC$A_POINTER] =
		.NML$GQ_QIOBFDSC [DSC$A_POINTER] + 2;

	    CH$MOVE (.SETDSC [DSC$W_LENGTH],
		     .SETDSC [DSC$A_POINTER],
		     .NML$Q_EVTBFDSC [DSC$A_POINTER]);

	    PRMDSC [DSC$W_LENGTH] = .SETDSC [DSC$W_LENGTH];
	    PRMDSC [DSC$A_POINTER] =
		.NML$Q_EVTBFDSC [DSC$A_POINTER];
	    !
	    ! Clear event filters.
	    !
	    SRCPTR = 0;
	    WHILE NML$GETNXTSNK (PRMDSC, .SNK, SRCPTR) DO
		BEGIN

		NML$REMSRC (PRMDSC, .SRCPTR);
		SRCPTR = 0;		    ! Start at the beginning again

		END;

	    !
	    ! If event parameter has nothing in it then clear the entire 
	    ! sink node entry from the volatile data base.
	    !
	    TMPSNK = .SNKADR;
	    IF .PRMDSC [DSC$W_LENGTH] EQLU 0
	    THEN
		NML_CLEARENTITY (NML$C_LOGGING, 0, TMPSNK)
	    ELSE
		BEGIN
		!
		! Set the event information up as a parameter in a 
		! permanent data base record for processing by the 
		! SET QIO routines.
		!
		RECDSC [DSC$W_LENGTH] = 2;
		RECDSC [DSC$A_POINTER] = .NML$GQ_RECBFDSC [DSC$A_POINTER];
	        IF NOT NML$SAVEVENTS (NML$GQ_RECBFDSC,
				      .PRMDSC [DSC$W_LENGTH],
				      .PRMDSC [DSC$A_POINTER],
				      RECDSC)
		THEN
		    RETURN;
		!
		! Build QIO buffer and add the parameters to the 
		! volatile database entry.
		!
		NML$BLDALLDES (RECDSC,
				.NML$AB_ENTITYDATA [NML$C_LOGGING,
						    EIT$A_ALLTAB]);
		NML$BLDSETQBF (NFB$C_FC_SET, .DB,
				.SRCHKEY, 0, TMPSNK,
				NML$Q_NFBBFDSC, CLEAR_NFBDSC,
				NML$Q_P2BFDSC,  P2DSC,
				NML$GQ_QIOBFDSC, QBFDSC);

        	STATUS = NML$NETQIO (CLEAR_NFBDSC, P2DSC, 0, QBFDSC);

		IF NOT .STATUS
		THEN
		    EXITLOOP;

		END;
	    END;

	IF NOT .STATUS
	THEN
	    EXITLOOP;

	END;

    IF .STATUS
    OR (.STATUS EQL NML$_STS_CMP)
    THEN
	BEGIN

	NML$AB_MSGBLOCK [MSB$L_FLAGS] = 0;
	NML$AB_MSGBLOCK [MSB$B_CODE] = NMA$C_STS_SUC;

	END;

    END;				! End of NML_CLEARLOGALL

%SBTTL 'NML$CLEARKNOWN  Clear volatile parameters'
GLOBAL ROUTINE NML$CLEARKNOWN (ENT, DUM1, DUM2) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine removes parameters from all entries of the specified
!	entity type in the volatile data base.
!
! INPUTS:
!
!	ENT		Entity type code.
!	DUM1		Not used.
!	DUM2		Not used.
!
! OUTPUTS:
!
!	Modifies contents of the following:
!
!		NML$T_ENTBUFFER
!		NML$Q_ENTBFDSC [DSC$W_LENGTH]
!		NML$AB_MSGBLOCK
!		NML$AB_SNDBUFFER
!		NML$T_PRMBUFFER
!--

BEGIN

LOCAL
	FUNC,
	BUFEND,
	DB,
	SRCHKEY,
	ENTADD,
	ENTLEN,
	ENTIDPTR,
	LISDSC    : DESCRIPTOR,
	MSGSIZE,
	NFBDSC    : DESCRIPTOR,
	P2DSC     : DESCRIPTOR,
	PTR,
	QBFDSC    : DESCRIPTOR,
	RESLEN    : WORD,
	STATUS,
	STRTFLG;
!
! Get volatile database info
!
DB = .NML$AB_ENTITYDATA [.ENT, EIT$B_DATABASE];
SRCHKEY = .NML$AB_ENTITYDATA [.ENT, EIT$L_SRCHKEY];

!
! Set function code
!
IF .NML$GL_PRS_FLGS [NML$V_PRS_ALL]
THEN
	FUNC = NFB$C_FC_DELETE
ELSE
	FUNC = NFB$C_FC_CLEAR;

STRTFLG = FALSE;

!
! Get a buffer of entity IDs for known entities.
!
WHILE NML$GETKNOWNLIST (.ENT, .STRTFLG, LISDSC) DO
	BEGIN

	STRTFLG = TRUE;

	BUFEND = .LISDSC [DSC$A_POINTER] + .LISDSC [DSC$W_LENGTH];
	PTR = .LISDSC [DSC$A_POINTER];

	WHILE .PTR LSSA .BUFEND DO
		BEGIN

		ENTIDPTR = NML$T_ENTBUFFER;
		NML$Q_ENTBFDSC [DSC$W_LENGTH] = NML$K_ENTBUFLEN;
		!
		! Set up entity id for clear QIO and id string for response
		! message.
		!
		SELECTONEU .ENT OF
			SET

			[NML$C_CIRCUIT,
			 NML$C_LINE,
			 NML$C_OBJECT]:
				BEGIN

				ENTLEN = .(.PTR)<0,16>;
				ENTADD = .PTR + 2;

				CH$WCHAR_A (.ENTLEN, ENTIDPTR);
				ENTIDPTR = CH$MOVE (.ENTLEN, .PTR+2, 
								.ENTIDPTR);
				PTR = .PTR + 2 + .ENTLEN;
				END;

			[NML$C_NODE]:
				BEGIN

				PTR = .PTR + 4;	! Skip loopnode flag.
				ENTADD = .PTR;	! Point to node address

				ENTIDPTR = CH$MOVE (2, .PTR, .ENTIDPTR);
				PTR = .PTR + 4;

				ENTLEN = .(.PTR)<0,8>; ! Get name length
				PTR = .PTR + 2;
				CH$WCHAR_A (.ENTLEN, ENTIDPTR);
				ENTIDPTR = CH$MOVE (.ENTLEN, .PTR, .ENTIDPTR);
				PTR = .PTR + .ENTLEN;
				ENTLEN = 2;	! Get length of node address

				END;

			[NML$C_LOOPNODE]:
				BEGIN

				CH$WCHAR_A (0, ENTIDPTR); ! Zero node address
				CH$WCHAR_A (0, ENTIDPTR);

				ENTLEN = .(.PTR)<0,8>; ! Get name length
				PTR = .PTR + 2;
				ENTADD = .PTR;

				CH$WCHAR_A (.ENTLEN, ENTIDPTR);
				ENTIDPTR = CH$MOVE (.ENTLEN, .PTR, .ENTIDPTR);

				PTR = .PTR + .ENTLEN;

				END;

			TES;

		NML$Q_ENTBFDSC [DSC$W_LENGTH] = .ENTIDPTR - NML$T_ENTBUFFER;
		!
		! Clear parameters from the volatile data base entry.
		!
		NML$BLDSETQBF (.FUNC, .DB,
				.SRCHKEY, .ENTLEN, .ENTADD,
				NML$Q_NFBBFDSC, NFBDSC,
				NML$Q_P2BFDSC,  P2DSC,
				NML$GQ_QIOBFDSC, QBFDSC);
		STATUS = NML$NETQIO (NFBDSC, P2DSC, 0, QBFDSC);

		IF .STATUS
		THEN
			BEGIN
		    	NML$AB_MSGBLOCK [MSB$L_FLAGS] = 0;
		    	NML$AB_MSGBLOCK [MSB$B_CODE] = NMA$C_STS_SUC;
		    	END;
		!
		! Add the entity id to the response message information.
		!
		NML$AB_MSGBLOCK [MSB$V_ENTD_FLD] = 1;
		NML$AB_MSGBLOCK [MSB$A_ENTITY] = NML$Q_ENTBFDSC;
		!
		! Build and send the response message.
		!
		NML$BLD_REPLY (NML$AB_MSGBLOCK, MSGSIZE);
		NML$SEND (NML$AB_SNDBUFFER, .MSGSIZE);

		END;
	END;

END;				! End of NML$CLEARKNOWN


END					! End of module

ELUDOM
