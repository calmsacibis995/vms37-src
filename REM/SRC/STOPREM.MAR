	.TITLE	STOPREM - Stop REMACP
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************

;++
;
; FACILITY:  REMACP - REMOTE I/O ACP
;
; ABSTRACT:
;
;	This program stops operation of REMACP.
;
; ENVIRONMENT:
;
;	VAX/VMS, INCLUDING PRIVILEGED SYSTEM SERVICES
;	AND INTERNAL EXEC ROUTINE.
;
; AUTHOR:  Scott G. Davis, CREATION DATE:  12-Oct-1979  15:31
;
; MODIFIED BY:
;
;	V02-002	LMK0001		Len Kawell		4-Jun-1981
;		Cleanup error handling to return correct error messages.
;--


	$AQBDEF
.PAGE
;++
;
; FUNCTIONAL DESCRIPTION:
;
;	This routine looks up the AQB for REMACP and clears the mount count.
;
; CALLING SEQUENCE:
;
; INPUT PARAMETERS:
;
; SIDE EFFECTS:
;
	.PSECT	INSTR,NOWRT

;--

STOP_REMACP::
	.WORD	0
	$CMKRNL_S	B^STARTUP	; Do in REMACP
	RET				; Return with status


STARTUP:	.WORD	0		; ENTRY POINT
	MOVL	G^SCH$GL_CURPCB,R4	; GET OWN PCB ADDRESS
	JSB	G^SCH$IOLOCKW		; Lock the database
	MOVAB	G^IOC$GL_AQBLIST,R1	; GET THE LIST ADDRESS
	MOVL	(R1),R6			; GET FIRST AQB POINTER
70$:
	CMPB	AQB$B_ACPTYPE(R6),#AQB$K_REM ; IS THIS IT?
	BEQL	80$			; IF EQL YES
	MOVL	AQB$L_LINK(R6),R6	; GET LINK
	BNEQ	70$			; If NEQ keep looking
	JSB	G^SCH$IOUNLOCK		; Unlock I/O database
	SETIPL	#0			; Restore IPL
	MOVZWL	#SS$_NONEXPR,R0		; Couldn't find REMACP
	RET				; Go back
80$:
	CLRB	AQB$B_MNTCNT(R6)	; Mark as finished
	JSB	G^SCH$IOUNLOCK		; Unlock I/O database
	SETIPL	#0			; Bring down the IPL
	MOVAB	AQB$L_ACPPID(R6),R6	; Get the PIDADR
	$WAKE_S	PIDADR=(R6)		; Wake up REMACP
	RET				; Go back

	.END	STOP_REMACP
