	.TITLE	CRF_MACROS 
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:  CROSS REFERENCE PROGRAM
;
; ABSTRACT:  ACCEPTS CROSS REFERENCE DATA AND PRODUCES A SUMMARY
;
;
; ENVIRONMENT:  LINKABLE IMAGE TO BE USED WITH THE NATIVE MODE LINKER AND COMPILERS
;
; AUTHOR:  K.D. MORSE	, CREATION DATE:  12-APR-77
;
; MODIFIED BY:
;
;	01.01	B.L. SCHREIBER		25-Oct-1979
;	Change macro name and arguments to conform with bliss names
;
;	01.02	B.L. SCHREIBER		12-Dec-1979
;	Modify for new Cref.
;--

	.SBTTL	DECLARATIONS

;
; MACROS:
;

;
; MACRO TO INITIALIZE THE OUTPUT LINE DESCRIPTION TABLE
;
; $CRFCTLTABLE		KEYTYPE, ERROR, OUTPUT, MEMEXP, KEY1TABLE, KEY2TABLE,-
;			VAL1TABLE,VAL2TABLE,REF1TABLE,REF2TABLE
;
; WHERE
;
;	KEYTYPE = TYPE OF KEY, 0=ASCIC, 1=BINARY LONGWORD
;	ERROR = ADDRESS OF USER'S ERROR ROUTINE
;	OUTPUT = ADDRESS OF USER'S OUTPUT ROUTINE
;	MEMEXP = NUMBER OF PAGES TO EXPREG BY WHEN NEEDED
;	KEY1TABLE = ADDRESS OF KEY1 FIELD DESCRIPTION TABLE
;	KEY2TABLE = ADDRESS OF KEY2 FIELD DESCRIPTION TABLE
;	VAL1TABLE = ADDRESS OF VAL1 FIELD DESCRIPTION TABLE
;	VAL2TABLE = ADDRESS OF VAL2 FIELD DESCRIPTION TABLE
;	REF1TABLE = ADDRESS OF REF1 FIELD DESCRIPTION TABLE
;	REF2TABLE = ADDRESS OF REF2 FIELD DESCRIPTION TABLE
;
	.MACRO	$CRFCTLTABLE	KEYTYPE=ASCIC,OUTPUT,ERROR=0,MEMEXP=50,KEY1TABLE=0,-
				KEY2TABLE=0,VAL1TABLE=0,VAL2TABLE=0,REF1TABLE=0,-
				REF2TABLE=0

	.LONG	0				; ADDRESS OF HASH TABLE
	.LONG	0				; ADDRESS OF LAST KEY LOOKED AT
	.BYTE	CRF$K_'KEYTYPE			; TYPE OF KEY
	.BYTE	0				; CRF FLAGS
	.WORD	0				; SECONDARY ERROR STATUS CODE
	.LONG	0				; NUMBER OF ENTRIES IN CROSS REFERENCE
	.LONG	0,0				; LIST HEAD FOR VIRTUAL MEMORY ALLOCATED
	.LONG	MEMEXP*512			; NUMBER OF BYTES TO EXPAND REGION BY
	.LONG	MEMEXP				; NUMBER OF PAGES TO EXPAND REGION BY
	.LONG	0,0,0,0,0,0,0,0			; RESERVED FOR EXPANSION
	.ADDRESS ERROR				; ADDR OF ERROR ROUTINE
	.ADDRESS OUTPUT				; ADDR OF OUTPUT ROUTINE
						; ADDRESSES OF FIELD DESCRIPTIONS
	.ADDRESS KEY1TABLE			; KEY1 FIELD DESCRIPTIONS
	.ADDRESS KEY2TABLE			; KEY2 FIELD DESCRIPTIONS
	.ADDRESS VAL1TABLE			; VAL1 FIELD DESCRIPTIONS
	.ADDRESS VAL2TABLE			; VAL2 FIELD DESCRIPTIONS
	.ADDRESS REF1TABLE			; REF1 FIELD DESCRIPTIONS
	.ADDRESS REF2TABLE			; REF2 FIELD DESCRIPTION
	.ENDM	$CRFCTLTABLE			; END OF INIT MACRO


;
; MACRO TO DEFINE ONE FIELD OF THE OUTPUT LINE
;
; $CRFFIELD	BIT_MASK, FAO_STRING, SET_CLEAR, FIELD_WIDTH
;
; WHERE:
;
;	BIT_MASK=MASK OF BITS INDICATING THIS OUTPUT DESCRIPTION HOLDS
;	FAO_STRING=FAO CONTROL STRING FOR THIS FIELD
;	FIELD_WIDTH=MAXIMUM SIZE OF THIS OUTPUT LINE FIELD
;	SET_CLEAR=INDICATOR HOW TO TEST MASK BITS, I.E. ONE OF THE FOLLOWING:
;		SET=MASK BITS ARE TO BE TESTED AS SET
;		CLEAR=MASK BITS ARE TO BE TESTED AS CLEAR
;

	.MACRO	$CRFFIELD	BIT_MASK=0,FAO_STRING=0,FIELD_WIDTH=0,SET_CLEAR=0

	.SAVE					; REMEMBER USER'S PSECT
	.PSECT	$$CRFSTRNG,RD,WRT,REL,NOEXE,NOSHR
$$$LNG1=.					; ADDRESS OF FAO CONTROL STRING
	.ASCII	\FAO_STRING\			; OUTPUT FIELD CONTROL STRING
$$$LNG2=.					; INDICATOR FOR LENGTH

	.RESTORE				; RESTORE USER'S PSECT
	.WORD	BIT_MASK!^X8000			; RECORD MASK BITS

	.IF	IDN	<SET_CLEAR>,<CLEAR>	; DETERMINE WHETHER
	.BYTE	0				; MASK IS TO BE CLEAR
	.IFF					; OR IF
	.BYTE	1				; MASK IS TO BE SET
	.ENDC					;

	.BYTE	FIELD_WIDTH			; MAXIMUM SIZE OF FIELD

	.LONG	$$$LNG2-$$$LNG1			; STRING DESCRIPTOR
	.ADDRESS $$$LNG1			; FOR FAO CONTROL STRING

	.ENDM	$CRFFIELD			; END OF FIELD DESCRIPTION MACRO

;
; MACRO TO INDICATE END OF OUTPUT LINE DESCRIPTION TABLE
;
; $CRFFIELDEND
;
; WHERE:
;
;	THERE ARE NO FORMAL PARAMETERS.
;

	.MACRO	$CRFFIELDEND			;

	.WORD	0				; END FIELD DESCRIPTIONS

	.ENDM	$CRFFIELDEND			; END OF MACRO
