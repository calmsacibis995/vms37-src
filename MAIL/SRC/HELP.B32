MODULE HELP( %TITLE 'DISPLAY HELP TEXT'
		IDENT = 'V03-000') =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	VAX/VMS MAIL UTILITY
!
! ABSTRACT:
!		Open help file and display requested help.
!
! ENVIRONMENT:	NATIVE/USER MODE
!
! AUTHOR: Len Kawell, CREATION DATE: 21-APR-1978
!
! MODIFIED BY:
!
!--

!
! TABLE OF CONTENTS:
!

!
! INCLUDE FILES:
!
	LIBRARY 'SYS$LIBRARY:STARLET.L32';
!
! MACROS:
!

!
! EQUATED SYMBOLS:
!

!
! OWN STORAGE:
!
OWN
	INDEX;					! Librarian control table index
!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
	LBR$INI_CONTROL: ADDRESSING_MODE(GENERAL),	! Librarian init control table
	LBR$OPEN: ADDRESSING_MODE(GENERAL),		! Librarian open library file
	LBR$GET_HELP: ADDRESSING_MODE(GENERAL),		! Librarian get help
	SCR$ERASE: ADDRESSING_MODE(GENERAL);		! Screen package erase screen

GLOBAL ROUTINE MAIL$HELP(TOPIC,SUBTOPIC) :NOVALUE =	!

!++
! FUNCTIONAL DESCRIPTION:
! 	This routine opens the help file and displays the requested help.
!
! FORMAL PARAMETERS:
!	TOPIC = Address of desired help topic descriptor.
!
! IMPLICIT INPUTS:
!	If help file is open, INDEX is non-zero.
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!	NONE
!
! SIDE EFFECTS:
!	Specified topic found and displayed on sys$output.
!
!--
BEGIN
MAP
	TOPIC : REF BLOCK[4,BYTE];
MACRO
	CHECK_STATUS[] = 			! Check routine status
		BEGIN
		 LOCAL STATUS;
		 STATUS = (%REMAINING);
		 IF NOT .STATUS THEN BEGIN SIGNAL(.STATUS); RETURN END
		END%;

IF .INDEX EQL 0					! If file not open
THEN						! Then open it
	BEGIN
	CHECK_STATUS(LBR$INI_CONTROL(	INDEX
					,UPLIT(LBR$C_READ)
					,UPLIT(LBR$C_TYP_HLP)));
	CHECK_STATUS(LBR$OPEN(	INDEX
				,$DESCRIPTOR('MAILHELP')
				,0
				,$DESCRIPTOR('SYS$HELP:.HLB')));
	END;
SCR$ERASE(1,1);					! Erase the screen
CHECK_STATUS(LBR$GET_HELP(	INDEX		! Display the help text
				,0,0,0
				,.TOPIC
				,.SUBTOPIC))
END;

END						!End of module
ELUDOM
