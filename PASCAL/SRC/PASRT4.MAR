;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;*                                                                         *
;*                            PAS$RT_FUNC                                  *
;*           RUNTIME SUPPORT MODULE FOR PASCAL -- SECTION 4                *
;*                                                                         *
;*                        VERSION V1.0-1 -- OCTOBER 1979                   *
;*                                                                         *
;*   This module defines the following routines:                           *
;*                                                                         *
;*      pas$<math>:  a number of routines to convert call by value         *
;*                   to call by name for mathematical library functions    *
;*                                                                         *
;*   Written by: Hellmut Golde 15-Feb-79                                   *
;*                                                                         *
;*   Revision history:							   *
;*									   *
;*	1-001	5/14/80: Paul Hohensee: add a .psect directive for the code section   *
;*									   *
;	1-002	11-Aug-81	Paul Hohensee. Change to general addressing of external routines
;
;***************************************************************************
	.title	pas$rt_func
	.ident	'V03-000'

	.psect	_pas$code,pic,shr,exe,nowrt
;
;   ROUTINES TO CONVERT CALL BY VALUE (IMMEDIATE) TO CALL BY NAME
;   FOR MATHEMATICAL LIBRARY FUNCTIONS
;
	.macro	libsngl p1		; macro to make definitions
	.entry	pas$'p1,^m<>
	pushl	4(ap)			; move value to stack
	pushal	(sp)			; and address on stack
	calls	#1,G^mth$'p1		; call library routine
	ret
	.endm	libsngl
;
;   Macro calls to create entry points for math library functions
;
	libsngl	SIN
	libsngl	COS
	libsngl	EXP
	libsngl SQRT
	libsngl ALOG
	libsngl ATAN
;
;    Entry points for various Pascal standard functions--used only when
;    functions are passed as parameters
;
	.entry	pas$immABSint,^m<>
	movl	4(ap),r0
	bbc	#31,r0,10$
	mnegl	r0,r0
10$:	ret
;
	.entry  pas$immABSreal,^m<>
	movf	4(ap),r0
	bbc	#15,r0,10$
	mnegf	r0,r0
10$:	ret
;
	.entry	pas$ABSdoub,^m<>
	movd	@4(ap),r0
	bbc	#15,r0,10$
	mnegd	r0,r0
10$:	ret
;
	.entry	pas$immSQRint,^m<>
	mull3	4(ap),4(ap),r0
	ret
;
	.entry	pas$immSQRreal,^m<>
	mulf3	4(ap),4(ap),r0
	ret
;
	.entry	pas$SQRdoub,^m<>
	muld3	@4(ap),@4(ap),r0
	ret
;
	.entry	pas$immORD,^m<>
	movl	4(ap),r0
	ret
;
	.entry	pas$immCHR,^m<>
	movl	4(ap),r0
	ret
;
	.entry	pas$immPRED,^m<>
	subl3	#1,4(ap),r0
	ret
;
	.entry	pas$immSUCC,^m<>
	addl3	#1,4(ap),r0
	ret
;
	.entry	pas$immODD,^m<>
	extzv	#0,#1,4(ap),r0
	ret
;
	.entry	pas$SNGL,^m<>
	cvtdf	@4(ap),r0
	ret
;
	.entry	pas$refABSint,^m<>
	movl	@4(ap),r0
	bbc	#31,r0,10$
	mnegl	r0,r0
10$:	ret
;
	.entry  pas$refABSreal,^m<>
	movf	@4(ap),r0
	bbc	#15,r0,10$
	mnegf	r0,r0
10$:	ret
;
	.entry	pas$refSQRint,^m<>
	mull3	@4(ap),@4(ap),r0
	ret
;
	.entry	pas$refSQRreal,^m<>
	mulf3	@4(ap),@4(ap),r0
	ret
;
	.entry	pas$refORD,^m<>
	movl	@4(ap),r0
	ret
;
	.entry	pas$refCHR,^m<>
	movl	@4(ap),r0
	ret
;
	.entry	pas$refPRED,^m<>
	subl3	#1,@4(ap),r0
	ret
;
	.entry	pas$refSUCC,^m<>
	addl3	#1,@4(ap),r0
	ret
;
	.entry	pas$refODD,^m<>
	extzv	#0,#1,@4(ap),r0
	ret
	.end
