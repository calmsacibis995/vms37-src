MODULE CHKHM1 (
		LANGUAGE (BLISS32),
		IDENT = 'V03-001'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  MOUNT Utility Structure Level 1
!
! ABSTRACT:
!
!	This routine verifies whether a given block is Files-11 Structure
!	Level 1 home block.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  21-Nov-1977  19:23
!
! MODIFIED BY:
!
!	V03-001	LMP0021		L. Mark Pilant,		 5-Apr-1982  15:10
!		Add support for ODS-1 structure version 2.
!
!	V02-000	ACG0167		Andrew C. Goldstein,	18-Apr-1980  13:37
!		Previous revision history moved to MOUNT.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:MOUDEF.B32';
REQUIRE 'LIBD$:[VMSLIB.OBJ]MOUNTMSG.B32';

GLOBAL ROUTINE CHECK_HOMEBLK1 (HOME_BLOCK, LBN, VOLUME_LABEL) =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine verifies whether the given block is a Files-11 Structure
!	Level 1 home block.
!
!
! CALLING SEQUENCE:
!	CHECK_HOMEBLK1 (ARG1, ARG2, ARG3)
!
! INPUT PARAMETERS:
!	ARG1: address of buffer containing block
!	ARG2: LBN of block
!	ARG3: string descriptor of user specified volume label
!
! IMPLICIT INPUTS:
!	NONE
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	1 if valid and correct
!	0 if not
!	SS$_INCVOLLABEL if valid but volume label is wrong
!
! SIDE EFFECTS:
!	NONE
!
!--

BEGIN

MAP
	HOME_BLOCK	: REF BBLOCK,	! home block buffer
	VOLUME_LABEL	: REF VECTOR;	! volume label descriptor

EXTERNAL ROUTINE
	CHECKSUM2;			! compute home block checksums


! Check the required non-zero fields and compute the checksums.
!

IF NOT (
	.HOME_BLOCK[HM1$W_IBMAPSIZE] NEQ 0
    AND .HOME_BLOCK[HM1$L_IBMAPLBN] NEQ 0
    AND .HOME_BLOCK[HM1$W_MAXFILES] NEQ 0
    AND .HOME_BLOCK[HM1$W_CLUSTER] NEQ 0
    AND CHECKSUM2 (.HOME_BLOCK, $BYTEOFFSET (HM1$W_CHECKSUM1))
    AND CHECKSUM2 (.HOME_BLOCK, $BYTEOFFSET (HM1$W_CHECKSUM2))
	)
THEN RETURN 0;

IF .HOME_BLOCK[HM1$W_STRUCLEV] NEQ HM1$C_LEVEL1
AND .HOME_BLOCK[HM1$W_STRUCLEV] NEQ HM1$C_LEVEL2
THEN ERR_EXIT (SS$_FILESTRUCT);

IF CH$NEQ (.VOLUME_LABEL[0], .VOLUME_LABEL[1],
	   HM1$S_VOLNAME, HOME_BLOCK[HM1$T_VOLNAME], 0)
THEN RETURN (SS$_INCVOLLABEL);

RETURN 1;

END;					! end of routine CHECK_HOMEBLK1

END
ELUDOM
