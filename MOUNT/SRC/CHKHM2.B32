MODULE CHKHM2 (
		LANGUAGE (BLISS32),
		IDENT = 'V03-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  MOUNT Utility Structure Level 2
!
! ABSTRACT:
!
!	This routine verifies whether a given block is Files-11 Structure
!	Level 2 home block.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  21-Nov-1977  19:23
!
! MODIFIED BY:
!
!	V02-000	ACG0167		Andrew C. Goldstein,	18-Apr-1980  13:38
!		Previous revision history moved to MOUNT.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:MOUDEF.B32';
REQUIRE 'LIBD$:[VMSLIB.OBJ]MOUNTMSG.B32';

GLOBAL ROUTINE CHECK_HOMEBLK2 (HOME_BLOCK, LBN, VOLUME_LABEL) =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine verifies whether the given block is a Files-11 Structure
!	Level 2 home block.
!
!
! CALLING SEQUENCE:
!	CHECK_HOMEBLK2 (ARG1, ARG2, ARG3)
!
! INPUT PARAMETERS:
!	ARG1: address of buffer containing block
!	ARG2: LBN of block
!	ARG3: string descriptor of user specified volume label
!
! IMPLICIT INPUTS:
!	NONE
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	MOUNT_OPTIONS: structure level 2 bit set if so
!
! ROUTINE VALUE:
!	1 if valid and correct
!	0 if not
!	SS$_INCVOLLABEL if valid but volume label is wrong
!
! SIDE EFFECTS:
!	NONE
!
!--

BEGIN

MAP
	HOME_BLOCK	: REF BBLOCK,	! home block buffer
	VOLUME_LABEL	: REF VECTOR;	! volume label descriptor

EXTERNAL
	MOUNT_OPTIONS	: BITVECTOR;	! command options

EXTERNAL ROUTINE
	CHECKSUM2;			! compute home block checksums


! Check the required non-zero fields and compute the checksums.
!

IF NOT (
	.HOME_BLOCK[HM2$L_HOMELBN] EQL .LBN
    AND .HOME_BLOCK[HM2$L_ALTIDXLBN] NEQ 0
    AND .HOME_BLOCK[HM2$W_CLUSTER] NEQ 0
    AND .HOME_BLOCK[HM2$W_HOMEVBN] NEQ 0
    AND .HOME_BLOCK[HM2$W_ALHOMEVBN] NEQ 0
    AND .HOME_BLOCK[HM2$W_ALTIDXVBN] NEQ 0
    AND .HOME_BLOCK[HM2$W_IBMAPVBN] NEQ 0
    AND .HOME_BLOCK[HM2$L_IBMAPLBN] NEQ 0
    AND .HOME_BLOCK[HM2$L_MAXFILES] NEQ 0
    AND .HOME_BLOCK[HM2$W_IBMAPSIZE] NEQ 0
    AND .HOME_BLOCK[HM2$W_RESFILES] NEQ 0
    AND CHECKSUM2 (.HOME_BLOCK, $BYTEOFFSET (HM2$W_CHECKSUM1))
    AND CHECKSUM2 (.HOME_BLOCK, $BYTEOFFSET (HM2$W_CHECKSUM2))
	)
THEN RETURN 0;

MOUNT_OPTIONS[OPT_IS_FILES11B] = 1;	! set structure level 2 flag

IF CH$NEQ (.VOLUME_LABEL[0], .VOLUME_LABEL[1],
	   HM2$S_VOLNAME, HOME_BLOCK[HM2$T_VOLNAME], ' ')
THEN RETURN (SS$_INCVOLLABEL);

RETURN 1;

END;					! end of routine CHECK_HOMEBLK2

END
ELUDOM
