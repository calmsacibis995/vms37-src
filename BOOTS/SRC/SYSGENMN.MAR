	.TITLE	SYSGENMN - SYSGEN UTILITY MAIN ROUTINE
	.IDENT	'V03-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SYSGEN
;
; ABSTRACT:
;	This module contains the main routine and a subroutine for
;	the SYSGEN utility.  These routines are in a separate module
;	so that a parallel set may be used in a standalone SYSGEN.
;
; ENVIRONMENT:	USER, EXEC, AND KERNEL MODES
;
; AUTHOR:  STEVE BECKHARDT,	CREATION DATE:  19-SEP-1979
;	   (ORIGINAL AUTHOR - LEN KAWELL)
;
; MODIFIED BY:
;
;	V02-004	JLV0087		Jake VanNoy		22-Sep-1981
;		Changed broken word offsets to L^.
;
;	V02-003	JLV0002		Jake VanNoy		9-Feb-1981
;		Changed psects to allow paged and non-paged segments
;		is SYSGEN image. Add SHOW/CONFIGURATION and 
;		CONFIGURE.
;
;	02-002	SRB0001		Steve Beckhardt		4-Jun-1980
;		Modify SYSGEN to come up with the ACTIVE parameters
;		in the work area instead of the DEFAULT parameters.
;
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

;
; MACROS:
;

	$CLIDEF				; DEFINE CLI CODES AND VALUES

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;


	.PSECT	PAGED_CODE	rd,nowrt,exe,long

	.SBTTL	Main routine
;++
; Functional Description:
;	SYSGEN is the control module for the sysgen utility program which
;	provides functional commands for the creation, examination and
;	editing of parameter files, the creation of I/O data base and the loading
;	of device drivers.
;
; Calling Sequence:
;	CALLG ARGLIST,BOO$SYSGEN	called by the image activator
;
; Input Parameters:
;
; Output Parameters:
;
;--
BOO$SYSGEN::	.WORD	0
	$LKWSET_S	INADR=BOO$GQ_LIMITS,-	;
			RETADR=BOO$GQ_RETADR	; Lock entire image in working set
	BLBC	R0,10$			; EXIT IF ERROR LOCKING PAGES, FATAL
	CLRQ	-(SP)			; Null call back arguments
	PUSHAB	L^BOO$AL_CLIBLK		; Address of request block
	CALLS	#3,@CLI$A_UTILSERV(AP)	; Call utility service routine
	CMPB	#CLI$K_VERB_FORE,L^BOO$AL_CLIBLK+CLI$B_RQSTAT ; Foreign command?
	BEQL	5$			; Branch if yes
	CLRQ	L^BOO$GQ_CMDESC		; Clear command string descriptor
5$:	EXTZV	#2,#1,24(AP),-(SP)	; GET VERIFY FLAG
	CALLS	#0,BOO$USEACT		; Use ACTIVE parameters
	CALLS	#1,L^BOO$GETPARAM	; READ AND PROCESS COMMANDS
	CMPL	#RMS$_EOF,R0		; CHECK FOR END OF FILE
	BNEQ	10$			; NO, RETURN STATUS
	MOVL	#1,R0			; SET NORMAL STATUS
10$:	RET				;

	.SBTTL	PUTERROR - Routine to print system message for error code
;+
;  PUTERROR is called to output the text for the status code in R0.
;  This text is output to the logical name SYS$OUTPUT using SYS$PUTMSG.
;
; Input:
;	R0 - Error code
;
;-
PUTERROR::				;
	PUSHL	#0			; Number of FAO arguments
	PUSHL	R0			; Status code value
	PUSHL	#2			; count of message arguments
	MOVL	SP,R1			; Save current place
	PUSHAB	L^FACNAMED		; Pointer to facility name descriptor
	PUSHL	#0			; Null action routine address
	PUSHL	R1			; Address of message vector
	CALLS	#6,SYS$PUTMSG		; Output error message text
	RSB				;



	.END	BOO$SYSGEN
