	.TITLE	SYSGEN - SYSGEN UTILITY AND PARAMETER FILE EDITOR
	.IDENT	'V03-000'
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************

;++
;
; Facility:  System generation and initialization
;
; Abstract: SYSGEN is the main routine to provide all SYSBOOT parameter
;	alteration commands in an online environment.
;
; Environment:
;
; Author:  RICHARD I. HUSTVEDT, Creation date:  4-MAY-1978
;
; Modification History:
;
;	V02-029	JLV0182		Jake VanNoy		12-FEB-1982
;		Change BOO$GL_SYSID to BOO$GQ_SYSID. Add logic to
;		use SYSID instead of CSR in CONNECT.
;
;	V02-028	JLV0149		Jake VanNoy		4-Jan-1981
;		Add global symbols formerly in SYSBOOCMD so that
;		the symbols will be included in STASYSGEN.
;
;	V02-027	JLV0138		Jake VanNoy		31-Dec-1981
;		Add use of PRM$M_DYNFLAGS to write active.
;		Add one page of non-paged patch area.
;		Removed calls to BOO$MSGOUT.
;
;	V02-026	JLV0120		Jake VanNoy		29-Oct-1981
;		Add version checking to WRITE CURRENT.
;
;	V02-025	JLV0085		Jake VanNoy		22-Sep-1981
;		Change all W^'s in psect paged_code to L^.
;
;	V02-024 CAS0034		C. A. Samuelson		14-Sep-1981
;		Add support for 2nd TU58 on 11/730 console.
;
;	V02-023	MIR0013		Michael I. Rosenblum	17-Aug-1981
;		Fixed referance to PRM$W changed it to long word
;		Implimented noautoconfigure sysgen parameter
;
;	V02-022	JLV0036		Jake VanNoy		6-Jul-1981
;		Changed help call to new LBR$OUTPUT_HELP.
;
;	V02-021	BLS0057		Benn Schreiber		13-Jun-1981
;		Correct references to LBR$XXX to use general mode.
;
;	V02-020	SPF0002		Steve Forgey		11-Jun-1981
;		Log WRITE CURRENT and WRITE ACTIVE to operator.
;
;	V02-019	JLV0021		Jake VanNoy		10-Jun-1981
;		Added SGN$GET_DEVICE - a general routine to fill
;		in global symbols if device in ACF block exists.
;		Added code to CONNECT so it doesn't require a /ADAPTER
;		qualifier if another unit on the same controller has
;		already been connected. Also changed CONNECT to
;		use the DDB for driver name if a DDB exists.
;
;	V02-018	JLV0016		Jake VanNoy		7-May-1981
;		Added /LOG to AUTO ALL. Corrected error reporting 
;		if no privilege during an AUTOCONFIGURE.
;
;	V02-017	JLV0003		Jake VanNoy		27-Feb-1981
;		Added code for /EXCLUDE on AUTOCONFIGURE ALL and /USER 
;		qualifier.
;
;	V02-016	JLV0002		Jake VanNoy		9-Feb-1981
;		Changed psects to allow paged and non-paged segments
;		is SYSGEN image. Requires changing offset on 
;		BOO$AB_PRMBUF to L^. Add SHOW/CONFIGURATION and 
;		CONFIGURE. Also fixed bug by saving "dot" through
;		a USE <filespec>.
;
;	V02-015	SPF0001		Steve Forgey		01-Oct-1980
;		Accept old parameter files that contain obsolete
;		parameters.  If the file is not a parameter file,
;		then issue only one error message.
;
;	V02-014	SRB0004		Steve Beckhardt		4-Sep-1980
;		Made WRITE ACTIVE work for ACP_MULTIPLE and BUGCHECKFATAL.
;
;--

;
; Include files:
;
	$ACFDEF				; Define autoconfiguration block
	$ADPDEF				; Define adapter control block
	$CLIDEF				; Define CLI codes and values
	$CRBDEF				; Define CRB offsets
	$DDBDEF				; Define DDB offsets
	$HLPDEF				; Define HELP symbols
	$IDBDEF				; Define IDB offsets
	$IHDDEF				; Image header offsets
	$IPLDEF				; Define IPLs
	$JPIDEF				; $GETJPI definitions
	$LBRDEF				; Librarian symbols
	$OPCDEF				; Operator message definitions
	$PRDEF				; Define processor registers
	$PRMDEF				; Parameter descriptor definitions
	$TPADEF				; TPARSE definitions
	$SHRDEF				; Error codes
	$SYSGMSGDEF			; Sysgen messages
	$UCBDEF				; Define UCB offsets
	$VECDEF				; Define VEC offsets
	
;
; Equated Symbols:
;
	CR=13				; Character value for carriage return
	FF=12				; Character value for form feed
	LF=10				; Character value for line feed
	UBA_IOBASE=8*512		; Offset from UBA configuration register
					; to base of I/O page
;
; Own Storage
;
	.PSECT	$$$$000,NOEXE,NOWRT	; PSECT to mark lower address
BOO$LOLIM::				; Marker definition
	.PSECT	____ZZZ,WRT,PAGE	; PSECT to mark upper address limit
BOO$HILIM::				;
	.PSECT	NONPAGED_DATA	rd,wrt,noexe,quad

BOO$AB_PATCH::				; Non-paged Patch area
	.BLKB	512			; One page
BOO$AB_PRMBUF::				; Parameter buffer
	.BLKB	512*16			; A generous buffer
BOO$GL_CONADP::				; Adapter TR number
	.LONG	-1			; Null value
BOO$GL_CONCREG::			; Control register
	.LONG	-1			; Null value
BOO$GL_CONCUNIT::			; Controller unit
	.LONG	-1			; Null value
BOO$GL_CONNUMU::			; Number of Units to configure
	.LONG	1			; Default value is 1 unit
BOO$GL_CONVECT::			; Vector offset
	.LONG	-1			; Null value
BOO$GL_CONNUMV::			; Number of vectors
	.LONG	-1			; Null value
BOO$GL_CONAUNIT::			; Adapter unit
	.LONG	-1			; Null value
BOO$GL_CONDEV::				; Device name string address
	.LONG	-1			; Null value
BOO$GL_CONDRV::				; Driver name string address
	.LONG	-1			; Null value
BOO$GL_CONUNITS::			; Maximum units
	.LONG	0			;
BOO$GQ_CONSYSID::			; System ID
	.LONG	0			; quadword
	.LONG	0			;
BOO$GL_CONCRB::				; CRB address
	.LONG	0			;
BOO$GL_CONFLAGS::			; Flags
	.LONG	0			;
BOO$GL_NEXTSTR::			; Next string location
	.LONG	0			;
BOO$GL_SELECT::				; Address of select list
	.LONG	0			;
BOO$AL_CLIBLK::				; CLI call back block
	$CLIREQDESC	-		; Get command call back block
		RQTYPE=CLI$K_GETCMD	;
BOO$GQ_CMDESC==BOO$AL_CLIBLK+CLI$W_RQSIZE	; Command descriptor address
BOO$GT_PROMPT::				; Prompt string
	.ASCIZ	<CR><LF>%SYSGEN>  %	;
BOO$AL_ACF::				; Auto-configuration block
	.BLKB	ACF$C_LENGTH		; Allocate space for it
BOO$GQ_LIMITS::				; High and low address limits for lockdown
	.LONG	BOO$LOLIM		; Lower address bound
	.LONG	BOO$HILIM-1		;
BOO$GQ_RETADR::				; Return address receiver
	.LONG	0,0			;
BOO$GL_RETSAVE::			; Saved co-routine return address
	.LONG	0			;
FACNAMED::				; Facility name descriptor
	.LONG	FACNAMSZ,FACNAME	;
FACNAME:.ASCII	/SYSGEN/		;
FACNAMSZ=.-FACNAME			; Length of facility name
CONSNAME:				; Console block storage 
	.ASCIC	/CSA/			; device name
BOO$GT_DXNAME::				; Name of floppy driver
	.ASCIC	/DXDRIVER/
BOO$GT_DDNAME::				; Name of TU58 driver
	.ASCIC	/DDDRIVER/

BOO$GL_FILEADDR::			; File spec address
	.LONG	0			;
BOO$GB_FILELEN::			; File spec length
	.BYTE	0			;

BOO$GL_PARINUSE::	.LONG	0
BOO$GT_CURRENT::	.ASCIC	/Current/
BOO$GT_ACTIVE::		.ASCIC	/Active/
BOO$GT_DEFAULT::	.ASCIC	/Default/
BOO$GT_FILE::		.BLKB	64

HELP_FILE:				; Help library file name
	.ASCID	/SYS$HELP:SYSGEN.HLB/
HELP_FLAG:	.long		hlp$m_prompt
HELP_DESC:	.ascid	//		; Filled in as pointer

VALID_PAR_FILE:				; Valid parameter file flag
	.LONG	0
SAVE_DOT:				; Save dot through USE filespec
	.LONG	0			

; SGN$GET_DEVICE storage

SGN$GL_DDB::		.LONG	0
SGN$GL_UCB::		.LONG	0
SGN$GL_IDB::		.LONG	0
SGN$GL_CRB::		.LONG	0
SGN$GL_LASTDDB::	.LONG	0

; AUTO ALL /LOG storage

CTRSTR_AUTOLOG:		.ascid	/ !AC!UB/
CTRSTR_AUTOLOG_UNIT:	.ascid	/,!UB/
Outlen_unit:		.long	0
Outlen:			.long	0
Boo$gt_save_devname:	.blkb	20
outbuf:			.ascid	//
outbuf_str:		.blkb	100

; Send operator message data

OPERGETJPI:					; $GETJPI item list
	.WORD		4				; Buffer length
	.WORD		JPI$_PID			; Process ID code
	.ADDRESS	OPERMSGPID			; Buffer address
	.LONG		0				; Don't return length
	.LONG		0				; List terminator

OPERMSGVEC:					; $PUTMSG message vector
	.WORD	3					; Argument count
	.WORD	^B1111					; Default message flags
OPERMSGID:
	.LONG	0					; Message ID
OPERMSGFAO:
	.WORD	1					; FAO argument count
	.WORD	0					; No new message flags
OPERMSGPID:						; PID of this process
	.LONG	0
OPERMSGNAM:						; File specification
	.ADDRESS	OPERNAMDESC

OPERNAMDESC:
	.LONG	0,0

OPERMSG:					; Message descriptor
	.LONG		0
	.ADDRESS	OPERMSGBUF

OPERMSGBUF:					; Message buffer
	.LONG	OPC$_RQ_RQST!<OPC$M_NM_CENTRL@8>	; Message type and target
	.LONG	0					; No reply message
OPERMSGTXT:						; Message text
	.BLKB	256	


	.PAGE
	.SBTTL	BOO$USEFILE - Use parameter file
;++
; Functional description:
;	BOO$USEFILE reads the specified file in response to the USE
;	command and merges all of the values specified in that file into
;	the working copy of the parameter values.  This is accomplished
;	by looking up each value specified and merging the associated 
;	value.
;
; Calling sequence:
;	CALLG	arglist,BOO$USEFILE
;
; Input Parameters:
;	TPA$L_TOKENCNT(AP) - Length of file name string
;	TPA$L_TOKENPTR(AP) - Address fo file name string
; Output Parameters:
;	R0 - Completion status code
;
;--

.PSECT	PAGED_CODE	rd,nowrt,exe,long

.Entry	BOO$USEFILE, ^M<R2,R3,R4,R5,R6,R7,R8,R9>	; Entry mask

	MOVL	BOO$GL_DOT,L^SAVE_DOT	; Save dot
	MOVAB	TPA$L_TOKENCNT(AP),R7	; Set address of file name descriptor
	BSBW	BOO$FILOPEN		; Open specified file
	BLBS	R0,20$			; Continue if success
10$:	MOVZWL	#1,R0			; Force success
	RET				;
20$:	MOVAB	BOO$AB_PRMBUF,R6	; Set address of parameter buffer
	MOVL	#16,R9			; Set size of buffer
	BSBW	BOO$READFILE		; Read file content into parameter buffer
	BLBC	R0,10$			; Exit if error
	MOVAB	BOO$AB_PRMBUF,R8	; Init pointer to parameter buffer
	MOVC3	#32,(R8),EXE$GT_STARTUP	; Set startup command file name
	ADDL	#32,R8			;  and advance buffer pointer
	CLRL	VALID_PAR_FILE		; Initialize valid parameter file flag
30$:	TSTL	(R8)			; Check for end of list
	BEQL	DONE			; Branch if yes
	MOVZBL	(R8),TPA$L_TOKENCNT(AP)	; Set token count for search
	MOVAB	1(R8),TPA$L_TOKENPTR(AP); And address of string
	ADDL	#16,R8			; Advance to value
	MOVL	(R8)+,TPA$L_NUMBER(AP)	; Set number 
	CALLG	(AP),L^BOO$SEARCH	; Search for parameter
	BLBC	R0,30$			; Next parameter if not found
	MOVL	#1,VALID_PAR_FILE	; Indicate valid parameter file
	CALLG	(AP),L^BOO$SETVALUE	; Set value of parameter
	BRW	30$			; Continue with next parameter
DONE:	BLBS	VALID_PAR_FILE,10$	; If LBS, valid parameter file
	MOVL	#SYSG$_NOTPARAM,R0	; Set error
	BRB	20$			; Branch
10$:
;
; Set file name in BOO$GL_PARINUSE
;
	MOVAL	BOO$GT_FILE,R8		; Set address of String
	MOVL	R8,BOO$GL_PARINUSE	; Set address
	MOVZBL	BOO$GB_FILELEN,(R8)	; Set count
	MOVC3	(R8),@BOO$GL_FILEADDR,-
		1(R8)			; Move string

	MOVZWL	#SS$_NORMAL,R0		; Return success
20$:	MOVL	L^SAVE_DOT,BOO$GL_DOT	; Restore dot
	RET				;
	.PAGE
	.SBTTL	BOO$USEACT - Use active parameters
;++
; Functional description:
;	This routine copies the parameter values from the running
;	system to the working copy of the parameter values.
; Calling sequence:
;
;	CALLS	#0,BOO$USEACT
;
; Input parameters:
;	None
; Output Parameters:
;	R0 - Completion status code
;--

.Entry	BOO$USEACT,^M<R2,R3,R4,R5>

	MOVC3	#EXE$C_SYSPARSZ,-	; Move parameters
		MMG$A_SYSPARAM,EXE$A_SYSPARAM
	MOVAL	BOO$GT_ACTIVE,-
		BOO$GL_PARINUSE		; Set parameter in use
	MOVL	#1,R0			; Return success
	RET

	.SBTTL	BOO$WRTACT - Write parameters to system
;++
; Functional Description:
;	This routine writes the parameters in the working parameter
;	buffer to the system's parameter area.  Only dynamic
;	parameters are copied.
;
; Calling Sequence:
;	CALLS	#0,BOO$WRTACT
;
; Input Parameters:
;	None
;
; Output Parameters:
;	R0 - Completion status code
;--

.PSECT	NONPAGED_CODE	rd,nowrt,exe,long

.Entry 	BOO$WRTACT, ^M<>

	$CMKRNL_S	B^10$,(AP)	; Do it in kernel mode
	BLBC	R0,1$			; If LBC, error
	BSBW	BOO$SENDOPER		; Notify operator of WRITE ACTIVE
		.LONG	SYSG$_WRITEACT
	BLBS	R0,5$			; If LBS, success
1$:	BSBW	PUTERROR		; Report error
	MOVL	#1,R0			; Force success
5$:	RET

10$:	.WORD	^M<R2,R3,R4,R5>
	MOVAB	L^BOO$A_PRMBLK,R5	; Get base of parameter blocks
	DSBINT	#IPL$_SCHED		; Raise IPL to prevent being unscheduled
					; (Assumes pages are locked in W.S.)

	ASSUME	PRM$L_ADDR  EQ  0

20$:	MOVL	PRM$L_ADDR(R5),R3	; Get address of parameter
	BEQL	40$			; Reached the end
	BBC	#PRM$V_DYNAMIC,-	; Branch if this is not a 
		PRM$L_FLAGS(R5),30$	; dynamic parameter
	MOVZBL	PRM$B_POS(R5),R1	; Get position of parameter
	EXTZV	R1,PRM$B_SIZE(R5),(R3),R2  ; Extract parameter value
	MOVAB	L^EXE$A_SYSPARAM,R0	; Get address of working buffer
	SUBL	R0,R3			; Get parameter offset
	INSV	R2,R1,PRM$B_SIZE(R5),-	; Store in system
		L^MMG$A_SYSPARAM(R3)

30$:	ADDL	#PRM$C_LENGTH,R5	; Point to next paramter block
	BRB	20$			; Repeat

; Copy dynamic flags from default flags to R0

40$:	BICL3	#^C<PRM$M_DYNFLAGS>,-
		MMG$A_SYSPARAM+<EXE$GL_DEFFLAGS-EXE$A_SYSPARAM>,R0
	BICL	#PRM$M_DYNFLAGS,-	; Clear dynamic flags in real flags
		EXE$GL_FLAGS
	BISL	R0,EXE$GL_FLAGS		; Set dynamic flags in real flags

	ENBINT				; Lower IPL
	MOVL	#1,R0			; Set success
	RET

	.SBTTL	BOO$WRTCUR - Write Current Parameters
;++
; Functional Description:
;	This routine writes the parameters from the working parameter
;	buffer to the system image on disk.  They will take effect the
;	next time the system is booted.  Note that is is necessary to
;	first read the parameters from the disk image, copy the
;	new parameters over the old, and then write the parameters
;	back.  The reason for this is to prevent destroying the data
;	in the remainder of the disk block following the last parameter.
;
; Calling Sequence:
;	CALLS	#0,BOO$WRTCUR
;
; Input parameters:
;	None
;
; Output Parameters:
;	R0 - Completion status code
;--

.PSECT	PAGED_CODE	rd,nowrt,exe,long

.Entry	BOO$WRTCUR, ^M<R2,R3,R4,R5,R6,R7,R8,R9>

	MOVAB	BOO$GT_SYSNAME,R0	; Get address of system file name
	MOVAB	BOO$GQ_FILDESC,R7	; Address of file name descriptor
	MOVZBL	(R0)+,(R7)		; Store length in descriptor
	MOVL	R0,4(R7)		; Store address in descriptor
	BSBW	BOO$FILOPENW		; Open file for read/write access
	BLBC	R0,30$			; Error
;
; Check Version number of SYS.EXE against this image
;
	MOVZBL	#1,R9			; Set size of read
	PUSHL	R8			; Save VBN
	MOVL	#1,R8 			; Set VBN
	MOVAB	BOO$AB_PRMBUF,R6	; Get address of buffer
	BSBW	BOO$READFILE		; Read current parameters
	BLBC	R0,30$			; Error
	MOVAB	BOO$AB_PRMBUF,R6	; Get address of buffer
	CMPL	#SYS$K_VERSION,-
		IHD$L_SYSVER(R6)	; Compare versions
	BEQL	10$
	PUSHL	#SYSG$_SYSVERCHK	; Set error
	CALLS	#1,G^LIB$SIGNAL		; Signal error
	BRB	30$
;
; Read current 
;
10$:	POPL	R8			; Restore VBN
	MOVZBL	#<<EXE$C_SYSPARSZ+511>@-9>,R9	; Set size of read
	ADDL	#<<MMG$A_SYSPARAM-^X80000000+512>@-9>,R8  ; Set VBN
	MOVAB	BOO$AB_PRMBUF,R6	; Get address of buffer
	PUSHR	#^M<R6,R8,R9>		; Save address, VBN, size
	BSBW	BOO$READFILE		; Read current parameters
	BLBC	R0,30$			; Error
	POPR	#^M<R6,R8,R9>		; Restore address, VBN, size
	MOVC3	#EXE$C_SYSPARSZ,-	; Copy new parameters over old
		L^EXE$A_SYSPARAM,(R6)
	BSBW	BOO$WRITEFILE		; Write parameters back to disk
	BSBB	BOO$SENDOPER		; Notify operator of WRITE ACTIVE
		.LONG	SYSG$_WRITECUR
	BLBS	R0,30$			; If LBS, success
	BSBW	PUTERROR		; Report error
30$:	MOVL	#1,R0			; Return success
	RET


	.SBTTL	BOO$SENDOPER - Output facility error message to operator
;
; Functional Description:
;	BOO$SENDOPER outputs an error message to the operator.
;
; Calling Sequence:
;	BSBW	BOO$SENDOPER
;		.LONG	<msg-id>
;
BOO$SENDOPER::
	MOVL	@(SP),OPERMSGID			; Put message ID in vector
	ADDL2	#4,(SP)				; Advance return address
	$GETJPI_S	ITMLST=OPERGETJPI	; Get process ID
	BLBC	R0,10$				; If LBC, error
	MOVL	#3,OPERMSGVEC			; Assume WRITE ACTIVE
	MOVL	#1,OPERMSGFAO
	CLRL	OPERMSGNAM
	CMPL	#SYSG$_WRITECUR,OPERMSGID	; WRITE CURRENT ?
	BNEQ	5$				; If NEQ, no
	INCL	OPERMSGVEC			; Set up WRITE CURRENT
	INCL	OPERMSGFAO
	MOVAB	OPERNAMDESC,OPERMSGNAM
	MOVZBL	RIO_INPNAM+NAM$B_RSL,OPERNAMDESC; Build descriptor
	MOVL	RIO_INPNAM+NAM$L_RSA,OPERNAMDESC+4
5$:	$PUTMSG_S -				; Get and format message
			MSGVEC=OPERMSGVEC, -
			ACTRTN=666$
	BLBC	R0,10$				; If LBC, error
	$SNDOPR_S	MSGBUF=OPERMSG
	BLBS	R0,20$				; If LBS, success
10$:	BSBW	PUTERROR			; Report error
	MOVL	#1,R0				; Force success
20$:
	RSB
666$:
	.WORD	^M<R2,R3,R4,R5>
	MOVQ	@4(AP),R0		; Get string descriptor
	ADDL3	#OPC$L_MS_TEXT,R0,OPERMSG; Store total operator message size
	MOVC3	R0,(R1),OPERMSGTXT	; Copy text to operator message buffer
	CLRL	R0			; Prevent message output to SYS$OUTPUT
	RET

	.PAGE
	.SBTTL	BOO$CONFIGALL - Auto-configure all adapters
;++
; Functional Description:
;	BOO$CONFIGALL is called to implement the "AUTOCONFIGURE ALL"
;	command. All standard devices supported by VAX/VMS will be 
;	located and connected for use with any necessary drivers being
;	loaded.
;
; Calling Sequence:
;	CALLG	ARGLIST,BOO$CONFIGALL
;
; Output parameters:
;	R0 - Completion status code
;--

.PSECT	NONPAGED_CODE	rd,nowrt,exe,long

.Entry	BOO$CONFIGALL, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>	; Entry mask
	BBC	#EXE$V_NOAUTOCNF,EXE$GL_DEFFLAGS,5$; do we allow auto configure
	MOVL	#SYSG$_NOAUTOCNF,R0	;Give them a no autoconfigure error
	RET				; and return

5$:	BSBW	IOC$AUTORESET		; Reset controller characters for device
					; names
	CLRL	R11			; Indicate no ADP address yet
10$:	PUSHL	R11			; Set as argument
	CALLS	#1,NEXTADP		; Get next ADP address
	BLBC	R0,30$			; Branch if error (NOPRIV)
	MOVL	R1,R11			; Check return status
	BGEQ	20$			; Branch if done
	PUSHL	R11			; Set as ADP argument
	CALLS	#1,W^CONFIGADP		; Configure the entire adapter
	BLBS	R0,10$			; Continue if no error
	BSBW	PUTERROR		; Report error
20$:	MOVL	#1,R0			; Set success

	BBC	#BOO$V_AUTOLOG,L^BOO$GL_CMDOPT,30$ ; Branch if not /LOG
	CLRL	BOO$GT_SAVE_DEVNAME	; Clear name
	BSBW	AUTOLOG			; Output last line if there is one
30$:	RET				;

.Entry	BOO$CONFIGONE, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>	; Entry mask

	BSBW	IOC$AUTORESET		; Reset controller characters for device
					; device names
	PUSHL	TPA$L_NUMBER(AP)	; Set TR number of adapter
	CALLS	#1,B^LOCADP		; Locate adapter control block
	BLBC	R0,20$			; Branch if error (NOPRIV)
	PUSHL	R1			; Set as argument to CONFIGADP
	BEQL	10$			; Done if no adapter
	CALLS	#1,B^CONFIGADP		; Configure adapter
	BLBS	R0,10$			; Continue if no error
	BSBW	PUTERROR		; Give error status
10$:	MOVL	#1,R0			; Set success for parse
	BBC	#BOO$V_AUTOLOG,L^BOO$GL_CMDOPT,20$ ; Branch if not /LOG
	CLRL	BOO$GT_SAVE_DEVNAME	; Clear name
	BSBW	AUTOLOG			; Output last line if there is one
20$:	RET				; 

NEXTADP:				; Return next ADP address in R0
	.WORD	0			; Null entry mask
	$CMEXEC_S	B^10$,(AP)	; Call real routine in exec mode
	RET				;

10$:	.WORD	0			; Null entry mask
	MOVL	4(AP),R1		; Get current address
	BEQL	20$			; 0 => start of list
	MOVL	ADP$L_LINK(R1),R1	; Flink onward
	BRB	30$
20$:	MOVL	IOC$GL_ADPLIST,R1	; Return head of list
30$:	MOVL	#1,R0
	RET				;

LOCADP:					; Return address of ADP for TR number
	.WORD	0			;
	$CMEXEC_S	B^5$,(AP)	; Call routine in exec mode
	RET				;

5$:	.WORD	0			; Null entry mask
	MOVAB	IOC$GL_ADPLIST-ADP$L_LINK,R1 ; Set starting address
10$:	MOVL	ADP$L_LINK(R1),R1	; Flink onward
	BEQL	20$			; Done if at end
	CMPW	4(AP),ADP$W_TR(R1)	; Is this the specified TR?
	BNEQ	10$			; No, try another
20$:	MOVL	#1,R0
	RET				;

.Entry	CONFIGADP, ^M<R2,R3,R4,R5,R6,R7>; Entry mask 

	CLRL	BOO$GL_RETSAVE		; Zap return address for initial call
	BBC	#BOO$V_SELECT,L^BOO$GL_CMDOPT,10$ ; Mutually exclusive - test
	BBC	#BOO$V_EXCLUDE,L^BOO$GL_CMDOPT,10$ ; to make sure one bit clear
	MOVL	#SYSG$_CONFQUAL,R0 	; Conflicting qualifiers
	RET

10$:	CALLG	(AP),W^50$		; Call configure one device
	BLBS	R0,20$			; Branch if not done with this adapter
	CMPW	#SS$_NOPRIV,R0		; Was there a privilege error
	BEQL	15$			; Yes, branch
	MOVL	#1,R0			; Set success
15$:	RET				; and return

20$:	MOVAB	BOO$AL_ACF,R5		; Set address of arguments describing device

	CLRW	ACF$W_MAXUNITS(R5)	; Always use driver specified max units
	MOVL	L^BOO$GL_SELECT,R6	; Get pointer to select list
	BEQL	35$			; Branch if null
	BSBW	SELECT			; Check select/exclude string
	BLBC	R0,10$			; Branch if device is not to be configured

35$:	BBC	#BOO$V_AUTOLOG,L^BOO$GL_CMDOPT,37$   ; Check /LOG, and
	BBS	#ACF$V_NOLOAD_DB,ACF$B_AFLAG(R5),37$ ; for database loading

	BSBW	AUTOLOG			; Branch to output log
	BLBS	R0,37$			; Branch if no error
36$:	BSBW	PUTERROR		; Give error message

37$:	CALLG	(R5),W^IOGEN$LOADER	; Load database and driver if necessary
	BLBS	R0,10$			; Branch if no error
	BSBW	PUTERROR		; Give error message
	BRW	10$			; continue loop

50$:	.WORD	0			; 
	$CMKRNL_S	B^55$,(AP)	; Call auto configure in kernel mode
	RET				;

55$:	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>	;
	MOVL	BOO$GL_RETSAVE,R0	; Get saved return address
	BNEQ	60$			; Branch if one present
	MOVAB	IOC$AUTOCONFIG,R0	; Else use main entry point
60$:	PUSHL	R0			; Stack call back address
	MOVL	4(AP),R8		; Get address of ADP
	MOVL	ADP$L_CSR(R8),R6	; Get Configuration register address
	MOVAB	BOO$AL_ACF,R7		; Address of configuration control block
	SETIPL	#31			; Disable interrupts
	JSB	@(SP)+			; Call Auto configurtation code
	SETIPL	#0			; Enable interrupts
	MOVL	(SP)+,BOO$GL_RETSAVE	; Save return
	BLBS	R0,70$			; Continue if another device
	CLRL	BOO$GL_RETSAVE		; Else clear return 
70$:	RET				; And return
90$:	MOVZWL	#1,R0			; Set success status
	RET				; and return
;
; SELECT - decide whether current device name is one of those either
; 	   specified in /SELECT or /EXCLUDE
;
; Returns: R0 = 1   ==> configure device
;	   R0 = 0   ==> don't configure device
;

SELECT:
10$:	MOVL	ACF$L_DEVNAME(R5),R7	; Get pointer to device name
	MOVZBL	(R6)+,R4		; Get length of select entry
	BEQL	20$			; End of list, no match
	CMPB	(R7)+,R4		; Compare with device entry
	BLSS	15$			; Branch if select longer than device
	CMPC3	R4,(R6),(R7)		; Do we have a match?
	BEQL	40$			; Yes, check SELECT or EXCLUDE
15$:	ADDL	R4,R6			; Advance to next entry in select list
	BRB	10$			; And try again

20$:	CLRL	R0			; Assume don't configure
	BBC	#BOO$V_EXCLUDE,BOO$GL_CMDOPT,30$ ; Branch if SELECT
	MOVL	#1,R0			; EXCLUDE - configure device
30$:	RSB

40$:	CLRL	R0			; Assume don't configure
	BBS	#BOO$V_EXCLUDE,BOO$GL_CMDOPT,50$ ; Branch if EXCLUDE
	MOVL	#1,R0			; SELECT - configure device
50$:	RSB
.PAGE
.SBTTL	AUTOLOG - AUTO ALL /LOG formating

AUTOLOG::
	MOVAB	BOO$AL_ACF,R5		; Address of configuration control block
	MOVL	ACF$L_DEVNAME(R5),R6	; Get address of current device
	MOVZBL	(R6)+,R7		; Get count and addr.
	CMPC3	R7,(R6),BOO$GT_SAVE_DEVNAME ; Compare to previous string
	BNEQ	50$			; Branch if new device

	$FAO_S	CTRSTR=CTRSTR_AUTOLOG_UNIT ,- ; Format Unit Number
		OUTBUF=OUTBUF,-
		OUTLEN=OUTLEN_UNIT ,-
		P1=ACF$B_CUNIT(R5)
	BLBS	R0,40$			; Branch if OK
	BRW	100$			; Branch if error

40$:	ADDL2	W^OUTLEN_UNIT,W^OUTLEN	; Add to total length
	ADDL2	W^OUTLEN_UNIT,W^OUTBUF+4; Add to descriptor
	SUBW2	W^OUTLEN_UNIT,W^OUTBUF	; Subtract from length
	BRB	100$			; Return with success

50$:	TSTL	W^OUTLEN		; Is this a first call to this routine?
	BEQL	70$			; Branch if yes

	MOVAL	W^OUTBUF_STR,W^OUTBUF+4	; reset descriptor
	MOVW	W^OUTLEN,W^RIO$GW_OUTLEN ; Length of string
	MOVC3	W^RIO$GW_OUTLEN,-
		W^OUTBUF_STR,-
		W^RIO$AB_BUFFER		; Move text into global buffer

	BSBW	RIO$OUTPUT_LINE
	BLBC	R0,100$			; Branch on error
	
70$:	MOVW	#100,W^OUTBUF		; Set full buffer length
	MOVC3	R7,(R6),BOO$GT_SAVE_DEVNAME ; Save new devname
	MOVAB	BOO$AL_ACF,R5		; Reset R5
	$FAO_S	CTRSTR=CTRSTR_AUTOLOG,-	; Format device name
		OUTBUF=OUTBUF,-
		OUTLEN=OUTLEN,-
		P1=ACF$L_DEVNAME(R5),-
		P2=ACF$B_CUNIT(R5)
	ADDL2	W^OUTLEN,W^OUTBUF+4	; Add to descriptor
	SUBW2	W^OUTLEN,W^OUTBUF	; Subtract from length
					; Return with FAO status
100$:	RSB

.PAGE
.SBTTL	SGN$GET_DEVICE - Locate device database

;
; Inputs: 
;	4(SP) - Address of Device name in ascic format
;       8(SP) - Unit number
;
; Outputs:
;	(Any of these are 0 if the data block doesn't exist)
;	SGN$GL_DDB - Address of DDB
;	SGN$GL_UCB - Address of UCB
;	SGN$GL_IDB - Address of IDB
;	SGN$GL_CRB - Address of CRB
;	SGN$GL_LASTDDB - If SGN$GL_DDB is non-zero, then equal to that,
;		     otherwise, last DDB in DEVLIST

;
; Can be called at IPL$_SYNCH, must be called from EXEC or KERNEL mode
;

SGN$GET_DEVICE::

	PUSHR	#^M<R2,R3,R4,R5,R6>	; ADDS 20 to offset to input

	CLRL	W^SGN$GL_DDB		;INIT TO ZERO
	CLRL	W^SGN$GL_UCB		;INIT TO ZERO
	CLRL	W^SGN$GL_IDB		;INIT TO ZERO
	CLRL	W^SGN$GL_CRB		;INIT TO ZERO

	MOVL	24(SP),R5		;GET ADDR OF DEVICE NAME
	MOVZBL	(R5)+,R4		;GET SIZE OF DEVICE NAME
	MOVAL	G^IOC$GL_DEVLIST,R6	;GET ADDR OF DEVICE LISTHEAD

20$:	TSTL	DDB$L_LINK(R6)		;IS THERE A NEXT DDB?
	BEQL	60$			;BR IF NOT - EXIT WITH ALL ZEROES
	MOVL	DDB$L_LINK(R6),R6	;GET ADDR OF NEXT DDB
	MOVAB	DDB$T_NAME(R6),R1	;GET ADDR OF DEVICE NAME
	MOVZBL	(R1)+,R0		;GET SIZE OF DEVICE NAME
	CMPC5	R0,(R1),#0,R4,(R5)	;COMPARE DEVICE NAMES
	BNEQ	20$			;BR IF NOT EQUAL

	MOVL	R6,L^SGN$GL_DDB		;ADDRESS OF DDB
	MOVL	DDB$L_UCB(R6),R0	;GET ADDR OF FIRST UCB
	MOVL	UCB$L_CRB(R0),R1	;GET ADDR OF CRB
	MOVL	R1,L^SGN$GL_CRB 	;SAVE
	MOVL	CRB$L_INTD+VEC$L_IDB(R1),W^SGN$GL_IDB ;GET ADDR OF IDB

30$:	CMPB	28(SP),UCB$W_UNIT(R0)	;IS UCB ALREADY LOADED?
	BEQL	50$			;BRANCH IF IT IS
40$:	MOVL	UCB$L_LINK(R0),R0	;GET ADDR OF NEXT UCB
	BNEQ	30$			;BR IF THERE IS ONE
	BRB	60$			;EXIT WITH UCB = 0

50$:	MOVL	R0,W^SGN$GL_UCB
60$:	MOVL	R6,SGN$GL_LASTDDB	;LAST DDB IN LIST AS SEARCHED
	POPR	#^M<R2,R3,R4,R5,R6>
	MOVL	#1,R0
	RSB

	.PAGE
	.SBTTL	Reset routines BOO$RESETLIST and BOO$CONRESET
;
; BOO$CONRESET - Reset values for connect command
;

.PSECT	PAGED_CODE	rd,nowrt,exe,long

.Entry	BOO$CONRESET, ^M<>			; Null entry mask

	MOVAB	L^BOO$AB_PRMBUF,BOO$GL_NEXTSTR	; Reset for string allocation
	MNEGL	#1,BOO$GL_CONCREG	; Null control register
	MNEGL	#1,BOO$GL_CONAUNIT	; Null adapter unit
	MNEGL	#1,BOO$GL_CONVECT	; Null vector
	MOVL	#1,BOO$GL_CONNUMV	; Default number of vectors
	MNEGL	#1,BOO$GL_CONADP	; Invalidate adapter TR value
	CLRL	BOO$GL_CONDEV		; Clear device name pointer
	CLRL	BOO$GL_CONDRV		; and driver name pointer
	CLRL	BOO$GL_CONUNITS		; and maximum units
	CLRQ	BOO$GQ_CONSYSID		; and system id
	CLRL	BOO$GL_CONFLAGS		; and flags
	MOVL	#1,L^BOO$GL_CONNUMU	; Set number of units to 1
	RET				; Return
;
;	BOO$RESETLIST - Reset select list values
;
.Entry	BOO$RESETLIST, ^M<>		; Null entry mask

	CLRL	BOO$GL_SELECT		; Zap select list pointer
	MOVAB	BOO$AB_PRMBUF,BOO$GL_NEXTSTR ; Set next string address
	CLRL	BOO$GT_SAVE_DEVNAME	; Clear autolog string
	CLRL	OUTLEN			; Clear autolog output size
	MOVAL	OUTBUF_STR,OUTBUF+4	; Set address in descriptor of block
	CLRB	BOO$AL_ACF+ACF$B_AFLAG	; Clear ACF flags
	MOVL	#1,L^BOO$GL_CONNUMU	; Set number of units to 1
	RET				; and return

;
; BOO$MAKLIST - Make a select list entry
;
.Entry	BOO$MAKLIST, ^M<R2,R3,R4,R5,R6>	; Entry mask

	MOVL	L^BOO$GL_NEXTSTR,R6	; Get pointer to next available string space
	TSTL	L^BOO$GL_SELECT		; Is selection pointer already set
	BNEQ	10$			; Yes, continue to add entry
	MOVL	R6,L^BOO$GL_SELECT	; Else set pointer to first select entry
10$:	MOVL	TPA$L_TOKENCNT(AP),R0	; Get string length
	MOVB	R0,(R6)+		; Set count for string
	MOVC3	R0,@TPA$L_TOKENPTR(AP),(R6) ; Copy string body
	CLRB	(R3)			; Mark end of list
	MOVL	R3,L^BOO$GL_NEXTSTR	; Save next string address
	MOVL	#1,R0			; Set success status
	RET				;
	.PAGE
	.SBTTL	BOO$CONADP - Set connect adapter number

.Entry	BOO$CONADP, ^M<>		;
	MOVL	TPA$L_NUMBER(AP),L^BOO$GL_CONADP ; Set adapter number
	RET				; and return

.Entry	BOO$CONNLADP ^M<>		; Connect with null adapter
	CLRL	L^BOO$GL_CONADP		; Clear adapter number
	RET				; and return

.Entry	BOO$CONCREG, ^M<>		; Control register address
	EXTZV	#0,#13,TPA$L_NUMBER(AP),L^BOO$GL_CONCREG; Set control register 
	RET				; and return

.Entry	BOO$CONCVEC, ^M<>		; Set controller vector 
	BICL3	#^XFFFFFE03,TPA$L_NUMBER(AP),L^BOO$GL_CONVECT ; Set vector offset
	RET				; and return

.Entry	BOO$CONCNUM, ^M<>		; Number of vectors
	MOVL	TPA$L_NUMBER(AP),L^BOO$GL_CONNUMV ; Set number of vectors
	RET				; and return

.Entry	BOO$CONAUNIT, ^M<>		; Adapter unit number
	MOVL	TPA$L_NUMBER(AP),L^BOO$GL_CONAUNIT; Set adapter unit number
	RET				; and return

.Entry	BOO$CONDRVNAM, ^M<R2,R3,R4,R5,R6> ; Entry mask (R2-R6)

	MOVL	L^BOO$GL_NEXTSTR,R6	; Address of next string storage
	MOVL	R6,BOO$GL_CONDRV	; Save pointer to driver name
	MOVB	TPA$L_TOKENCNT(AP),(R6)+	; Set count for string
	ADDL3	TPA$L_TOKENCNT(AP),R6,BOO$GL_NEXTSTR ; Mark string allocated
	MOVC3	TPA$L_TOKENCNT(AP),@TPA$L_TOKENPTR(AP),(R6)	; Copy string
	MOVL	#1,R0			; and return success
	RET				;

.Entry	BOO$DEVNAME, ^M<R2,R3,R4,R5,R6>	; Device name/unit

	CLRL	R2			; Initialize unit accumulator
	MOVL	BOO$GL_NEXTSTR,R6	; Get pointer to next available string
	MOVAB	(R6)+,R5		; Save pointer to count byte
	CLRB	(R5)			; Initialize count to zero
	MOVL	TPA$L_TOKENPTR(AP),R4	; Get pointer to string
	MOVL	TPA$L_TOKENCNT(AP),R3	; And number of characters
10$:	MOVZBL	(R4)+,R0		; Get a character from device name
	CMPB	R0,#^A/0/		; And check for a digit
	BLSSU	20$			; Branch if not
	CMPB	R0,#^A/9/		; Final check for digit
	BLEQU	40$			; Yes it is
20$:	MOVB	R0,(R6)+		; Part of device name
	INCB	(R5)			; Increase count
	SOBGTR	R3,10$			; Continue
	BRB	50$			;
30$:	MOVZBL	(R4)+,R0		; Get another digit
40$:	SUBL	#^A/0/,R0		; Get value
	MULL	#10,R2			; Scale accumulator before adding digit
	BLSS	60$			; Error
	CMPL	#9,R0			; Check for numeric
	BLSS	60$			; Error if not
	ADDL	R0,R2			; And add new digit
	SOBGTR	R3,30$			; Continue for entire unit number
50$:	MOVL	R6,BOO$GL_NEXTSTR	; Save updated string pointer
	MOVL	R2,BOO$GL_CONCUNIT	; Set unit number
	MOVL	R2,BOO$GL_CONAUNIT	; Assume same for adapter unit
	MOVL	R5,BOO$GL_CONDEV	; Save device name pointer
	TSTB	(R5)			; Must not be null device name
	BEQL	60$			; Error if so
	MOVL	#1,R0			; Return success
	RET				; and return
60$:	CLRL	R0			; Return error status
	RET				;

.Entry	BOO$CONUNITS, ^M<>		; Maximun units to be connected
	MOVL	TPA$L_NUMBER(AP),L^BOO$GL_CONUNITS ; Set maximum units
	RET				; and return

.Entry	BOO$CONSYSID_LOW, ^M<>		; System ID
	MOVL	TPA$L_NUMBER(AP), -
		L^BOO$GQ_CONSYSID 	; Set System ID (low longword)
	RET				; and return

.Entry	BOO$CONSYSID_HIGH, ^M<>		; System ID
	MOVL	TPA$L_NUMBER(AP), -
		L^BOO$GQ_CONSYSID+4 	; Set System ID (high longword)
	RET				; and return

.Entry	BOO$CONSOLE, ^M<>		; Connect console block stor. device

	CLRL	L^BOO$GL_CONADP		; No adapter
	MOVL	#1,L^BOO$GL_CONAUNIT	; Set adapter unit = 1 (not used)
	MOVL	#1,L^BOO$GL_CONCUNIT	; Set unit = 1
	MOVAB	L^CONSNAME,L^BOO$GL_CONDEV  ; Set device name pointer
	MOVL	#2,L^BOO$GL_CONNUMV	; Set 2 vectors
	CLRL	L^BOO$GL_CONCREG	; No control register
	MOVL	#1,L^BOO$GL_CONNUMU	; Set number of units to 1
	MOVL	#2,L^BOO$GL_CONUNITS	; Set max units to 2 (OPA0 is 1st unit)
	BSBW	IOGEN$CONSOLE		; Do cpu dependent stuff
	MOVL	#1,R0
	RET
	.PAGE
	.SBTTL	BOO$CONNECT - Connnect specified device and load driver
;
; BOO$CONNECT - Allows a single device to be introduced, appropriate data
;		structures allocated and initialized, the driver loaded if 
;		required and the controller and device initialized.
;
.Entry	BOO$CONNECT, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>	;

5$:	TSTL	L^BOO$GL_CONADP		; Has an adapter been specified?
	BGTR	20$			; If so, branch
	BEQL	10$			; If EQL then null adapter

	$CMEXEC_S W^CONN_ADAP		; Get adapter number from I/O database
	BLBC	R0,60$			; Exit with error
	BRB	5$			; Dispatch now on adapter type

10$:	$CMEXEC_S	W^CONNLADP	; Change mode to see data base
	BRB	30$			; Continue

20$:	$CMEXEC_S	W^CONNECT	; Change mode to see data base
30$:	BLBC	R0,40$			; Error occured
	CALLG	L^BOO$AL_ACF,IOGEN$LOADER ; Load database and driver
	BLBS	R0,50$			; Branch if success
40$:	BSBW	PUTERROR		; Give error message
50$:	MOVL	#1,R0			; Set success for parser
60$:	RET				;

.Entry	CONN_ADAP, ^M<>	; Local routine to get adapter number from I/O database

	MOVZBL	L^BOO$GL_CONUNITS,-(SP)	; Unit number
	PUSHL	L^BOO$GL_CONDEV		; Device name
	BSBW	SGN$GET_DEVICE		; Get device data base addresses
	ADDL2	#8,SP			; Pop off input parameters

	MOVL	L^SGN$GL_IDB,R0		; Address of IDB
	BNEQ	5$			; Error if zero
	MOVL	#SYSG$_NOADAPTER,R0	; Set no adapter specified error
	BRB	20$			; Branch to exit
	
5$:	CLRL	L^BOO$GL_CONADP 	; Assume null adapter
	MOVL	IDB$L_ADP(R0),R0	; Address of ADP block
	BEQL	10$			; Null adapter if zero
	MOVZWL	ADP$W_TR(R0),L^BOO$GL_CONADP ;Set adapter number

10$:	MOVL	#1,R0			; Set success
20$:	RET				; Return

.PAGE
.ENABL	LSB
; Connect with null adapter

.Entry	CONNLADP, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>

	MOVAB	L^BOO$AL_ACF,R10	; Address ACF
	CLRL	ACF$L_ADAPTER(R10)	; Set no adapter
	CLRL	ACF$L_CONFIGREG(R10)	; Set address of config reg
	CLRW	ACF$W_AVECTOR(R10)	; Set SCB offset for adapter
	BBC	#ACF$V_CRBBLT,-		; Br. if CRB built flag is clear
		BOO$GL_CONFLAGS,17$
	MOVL	BOO$GL_CONCRB,ACF$L_ADAPTER(R10)  ; Store CRB address
	BRB	17$			; Continue in common

.Entry	CONNECT, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>	;

	MOVAB	G^IOC$GL_ADPLIST-ADP$L_LINK,R11	; Get address of adapter list
10$:	MOVL	ADP$L_LINK(R11),R11	; Flink onward through adapter list
	BNEQ	15$			; Continue if another adapter
	MOVL	#SYSG$_INVADAP,R0	; Set invalid adapter error
	RET				; Return

15$:	CMPW	ADP$W_TR(R11),L^BOO$GL_CONADP	; Is this the specified TR?
	BNEQ	10$			; No, try another
	MOVAB	L^BOO$AL_ACF,R10	; Get address of ACF
	MOVL	R11,ACF$L_ADAPTER(R10)	; Set address of ADP
	MOVL	ADP$L_CSR(R11),ACF$L_CONFIGREG(R10)	; Set address of config reg
	ASHL	#2,L^BOO$GL_CONADP,R0	; Get TR number*4
	ADDW3	#^X100,R0,ACF$W_AVECTOR(R10)	; Set SCB offset for adapter

17$:
	MOVL	BOO$GL_CONDEV,ACF$L_DEVNAME(R10); Set pointer to device name
	MOVL	BOO$GL_CONDRV,ACF$L_DRVNAME(R10); And driver name
	BGTR	20$				; Branch if driver specified

; Now try to get driver name from DDB if it exists.

	MOVZBL	L^BOO$GL_CONUNITS,-(SP)	; Unit number
	PUSHL	L^BOO$GL_CONDEV		; Device name
	BSBW	SGN$GET_DEVICE		; Get device data base addresses
	ADDL2	#8,SP			; Pop off input parameters
	
	MOVL	SGN$GL_DDB,R1		; DDB address
	BEQL	18$			; Branch if none
	MOVAL	DDB$T_DRVNAME(R1),ACF$L_DRVNAME(R10) ; Address from DDB
	BRB	20$			; Branch around name hackery

18$:	MOVL	L^BOO$GL_NEXTSTR,R6	; Get address of next free space
	MOVL	R6,ACF$L_DRVNAME(R10)	; Set as driver name address
	MOVB	#8,(R6)+		; Set count for string
	MOVQ	#^A/  DRIVER/,(R6)	; Set driver suffix
	MOVL	ACF$L_DEVNAME(R10),R1	; Pointer to device name
	MOVW	1(R1),(R6)		; Form default driver name
20$:	MOVB	BOO$GL_CONAUNIT,ACF$B_AUNIT(R10); Set adapter unit
	MOVB	L^BOO$GL_CONNUMU,ACF$B_NUMUNIT(R10)
					; Store number of units to configure
	MOVB	BOO$GL_CONFLAGS,ACF$B_AFLAG(R10)  ; Store flags
	MOVW	BOO$GL_CONVECT,ACF$W_CVECTOR(R10); Set vector address
;
; Set up ACF$L_CONTRLREG - can either be UNIBUS CSR or address of CI
; 			   System id.
;
	TSTL	BOO$GQ_CONSYSID		; See if SYSIDLOW was specified
	BEQL	40$			; Branch if not
	MOVAB	BOO$GQ_CONSYSID, -
		ACF$L_CONTRLREG(R10)	; Set system id address
	BRB	50$			; Branch 
;
; Calculate system virtual address of UNIBUS CSR
;
40$:	ADDL3	#UBA_IOBASE, -
		BOO$GL_CONCREG, -
		ACF$L_CONTRLREG(R10)	; control register address
	ADDL	ACF$L_CONFIGREG(R10), -
		ACF$L_CONTRLREG(R10) 	; Add adapter va base

50$:	MOVB	BOO$GL_CONCUNIT, -
		ACF$B_CUNIT(R10) 	; Set controller unit number
	MOVW	BOO$GL_CONUNITS, -
		ACF$W_MAXUNITS(R10) 	; Set maximum units
	MOVB	BOO$GL_CONNUMV, -
		ACF$B_CNUMVEC(R10)	; Set count of vectors
	MOVL	#1,R0			; Set success
	RET				;

		.DSABL LSB

	.PAGE
	.SBTTL	BOO$LOAD - Load a specified driver if not already loaded
;
; BOO$LOAD - 	Loads the specified driver if not already loaded.
;
.Entry	BOO$LOAD, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>

	CLRL	R2			; Clear reload flag
	BRB	LOADRV			; And merge with common code
	.PAGE
	.SBTTL	BOO$RELOAD - Reload a specified driver
;
; BOO$RELOAD -	Reloads the specified driver replacing any existing copy
;		unless there are busy units requiring the driver that would
;		be replaced.
;
.Entry	BOO$RELOAD, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>	;

	MOVB	#ACF$M_RELOAD,R2	; Set flag to force reload
LOADRV:	MOVAB	L^BOO$AL_ACF,R10	; Get base address for ACF block
	MOVL	BOO$GL_CONDRV,ACF$L_DRVNAME(R10)	;
	MOVB	R2,ACF$B_AFLAG(R10)	; Set flags for load or reload
	CLRL	ACF$L_DEVNAME(R10)	; No device name
	CALLG	(R10),L^IOGEN$LOADER	; Load requested driver
	BLBS	R0,10$			; Continue if no error
	BSBW	PUTERROR		; Give error message
10$:	RET				;
	.PAGE
	.SBTTL	BOO$GIVEHELP - Print Help information
;
; 	Print Help Information
;
.Entry	BOO$GIVEHELP, ^M<R2,R3,R4,R5>	;

	PUSHAB	G^LIB$GET_INPUT		; Input routine
	PUSHAB	HELP_FLAG		; Flags
	PUSHAB	L^HELP_FILE		; Library
	MOVW	TPA$L_STRINGCNT(AP),-
		HELP_DESC		; Set length
	MOVL	TPA$L_STRINGPTR(AP),-
		HELP_DESC+4		; Set address
	PUSHAB	HELP_DESC		; Input string
	CLRL	-(SP)			; Width
	PUSHAB	G^LIB$PUT_OUTPUT	; Output routine
	CALLS	#6,G^LBR$OUTPUT_HELP	; Call help routine

	RET				; Return with status

	.END
