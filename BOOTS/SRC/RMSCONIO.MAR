	.TITLE	RMSCONIO - RMS INPUT/OUTPUT MODULE
	.IDENT	'V03-003'
	.DEFAULT DISPLACEMENT,LONG
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SYSGEN UTILITY
;
; ABSTRACT:	RMSCONIO PROVIDES ROUTINES TO PERFORM CONSOLE, COMMAND
;		AND FILE I/O FOR THE SYSGEN UTILITY
;
; ENVIRONMENT:	NATIVE/USER MODE CODE
;
; AUTHOR:	R.I. HUSTVEDT, CREATION DATE: 7-SEP-1977
;
; MODIFIED BY:
;
;	V03-003	JLV0204		Jake VanNoy		29-MAR-1982
;		Set UFO bit in SWAPFHCFAB in INSTALL.
;
;	V03-002	JLV0198		Jake VanNoy		17-MAR-1982
;		New create logic to extend existing files if /CONTIGUOUS
;		isn't specified.
;
;	V03-001	LJK0148		Lawrence J. Kenah	16-Mar-1982
;		Remove ACP call for partially mapped file as that situation
;		can never happen.
;
;	V02-027	TMH0027		Tim Halvorsen		10-Feb-1982
;		Use longword relative addressing to fix truncation errors.
;
;	V02-026	JLV0161		Jake VanNoy		19-Jan-1981
;		Make changes to BOO$WRTFILE to make sure correct
;		zero-extended values are written to the file.
;
;	V02-025	JLV0151		Jake VanNoy		4-Jan-1981
;		Change some W^'s to L^'s.
;
;	V02-024	JLV0140		Jake VanNoy		2-Jan-1981
;		Signal error in RIO$OUTPUT_LINE.
;
;	V02-023	JLV0135		Jake VanNoy		31-Dec-1981
;		ADD CR to OUTFAB RAT. Add RIO$OUTPUT_LINE routine
;		and use of RIO$AB_OUTBUF and RIO$GW_OUTLEN
;		thoughout SYSGEN. Remove calls to BOO$FACMSG
;		by inserting SYSG$_xxx messages for errors. Add
;		BOO$SET_OUTPUT routine.
;
;	V02-022	LJK0104		Lawrence J. Kenah	17-Dec-1981
;		If all mapping pointers for page or swap file are not
;		permanently resident, call ACP to make them so.
;
;	V02-021	JLV0117		Jake VanNoy		12-Nov-1981
;		Use BOO$V_INPUT and BOO$V_OUTPUT to control  use of
;		/INPUT and /OUTPUT.
;
;	V02-020	LJK0065		Lawrence J. Kenah	11-Sep-1981
;		Correct two bugs in LJK0044.
;
;	V02-019	LJK0044		Lawrence J. Kenah	10-Aug-1981
;		Make all page file and swap file initialization code use
;		the same initialization routines.
;
;	V02-018	JLV0039		Jake VanNoy		13-Jul-1981
;		Added G^ to LIB$ calls.
;
;	V02-017	SPF0001		Steve Forgey		12-Jun-1981
;		Add $NAM block to paramater file input FAB.
;
;	V02-016	JLV0014		Jake VanNoy		7-May-1981
;		Added .LIS default to /OUTPUT= qualifier.
;
;	V02-015 LJK0010		Lawrence J. Kenah	3-Mar-1981
;		Moved page file control block initialization to a separate
;		module called INITPGFIL.
;
; 	V02-014	JLV0003		Jake VanNoy		27-Feb-1981
;		Made changes to BOO$RESET_IO and added BOO$RESET_SAVE 
;		to allow SAVE command. Change some bit defintions and
;		remove BOO$GL_FLAGS, replace with BOO$GL_CMDOPT.
;
;	V02-013	JLV0002		Jake VanNoy		9-Feb-1981
;		Changed psects to allow paged and non-paged segments
;		is SYSGEN image. Required changing offset on BOO$AB_PRMBUF
;		to L^. Add SHOW/CONFIGURATION and CONFIGURE. 
;
;	V02-012 GRR0001			Greg Robert		14-OCT-1980
;		Replaced SFT$B_SLTCNT references with SFT$W_SLTCNT
;
;	V02-011	RIH32713		Richard I. Hustvedt	15-Sep-1980
;		Change swapfile initialization to provide for up to 32767
;		slots per swapfile.
;
;--
	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$CCBDEF				; DEFINE CHANNEL CONTROL BLOCK
	$DYNDEF				; DEFINE STRUCTURE TYPE CODES
	$FABDEF				; RMS FAB DEFINITIONS
	$FIBDEF				; FILE INFORMATION BLOCK
	$PRMDEF				; DEFINE PARAMETER BLOCK
	$PTEDEF				; DEFINE PTE FIELDS AND CONSTANTS
	$RABDEF				; RMS RAB DEFINITIONS
	$SHRDEF				; SHARED MESSAGE DEFINITIONS
	$STSDEF				; STATUS MESSAGE DEFINITIONS
	$SYSGMSGDEF			; SYSGEN MESSAGE DEFINITIONS
	$TPADEF				; DEFINE TPARSE SYMBOLS

;
; EQUATED SYMBOLS:
;
	BUFFER_SIZE=256			; Buffer size for /OUTPUT= & /INPUT=
	FILNAMSIZ=128

;
; OWN STORAGE:
;

	.Psect PAGED_DATA	rd,wrt,noexe,quad
 
DEFNAM:	.ASCII	/.LIS/			; INITIAL DEFAULT IS LISTING
	.BLKB	48-4			; DEFAULT NAME GOES HERE
EXPFNM:	.BLKB	FILNAMSIZ		; EXPANDED NAME HERE
RSLFNM:	.BLKB	FILNAMSIZ		; RESULTANT NAME HERE
OUTNAM:	.BLKB	FILNAMSIZ		; OUTPUT FILE NAME HERE
RHBUF:	.BLKB	20			; RECORD HEADER BUFFER
 
INPNAM:	.ASCII	/SYS$INPUT/		; INPUT NAME STRING
INPNAMSZ=.-INPNAM			; SIZE OF INPUT STRING

OUTPNAM:.ASCII	/SYS$OUTPUT/		; OUTPUT NAME STRING
OUTPNAMSZ=.-OUTPNAM			; SIZE OF OUTPUT STRING

PRMDEF:	.ASCII	/DEFAULT.PAR/		; DEFAULT NAME STRING
PRMDEFSIZ=.-PRMDEF			; STRING LENGTH
 

INP_OPEN: .BYTE	0			; INPUT OPEN FLAG
OUTP_OPEN:.BYTE	0			; OUTPUT OPEN FLAG

 
ZEROES:	.BLKL	8			;
;
; RMS 32 DATA STRUCTURES
;
 
	.ALIGN	QUAD			; ALIGN FOR PERFORMANCE
RIO_OUTFAB::				; EXTERNAL NAME
	$FAB	FAC=PUT,-		; OUTPUT FAB, ACCESS IS PUT
		FNA=OUTPNAM,-		; OUTPUT NAME STRING
		FNS=OUTPNAMSZ,-		; AND LENGTH
		ORG=SEQ,-		; SEQUENTIAL FILE
		NAM=RIO_OUTNAM,-	; ASSOCIATED NAME BLOCK
		FOP=SUP,-		; SUPERSEED IF NECESSARY
		DNA=DEFNAM,-		; DEFAULT NAME ADDRESS
		RAT=CR			; DO CR/LF PER LINE
 
RIO_OUTNAM::				; OUTPUT NAME BLOCK
	$NAM	RSA=OUTNAM,-		;
		RSS=FILNAMSIZ,-		;
		ESA=OUTNAM,-		; EXPANDED AND RESULT IN SAME PLACE
		ESS=FILNAMSIZ,-		;
 
RIO_OUTRAB::				;
	$RAB	RAC=SEQ,-		; RECORD ACCESS IS SEQENTIAL
		RHB=RHBUF,-		; RECORD HEADER BUFFER
		FAB=RIO_OUTFAB		; ASSOCIATED FAB
 
RIO_INPFAB::				;
	$FAB	FAC=GET,-		; FILE ACCESS IS GET(READ)
		FNA=INPNAM,-		; INPUT FILE NAME STRING ADDRESS
		FNS=INPNAMSZ,-		; INPUT STRING SIZE
		NAM=RIO_INPNAM,-	; ADDRESS OF ASSOCIATED NAME BLOCK
		DNA=DEFNAM,-		; ADDRESS OF DEFAULT NAME BUFFER
		DNS=4			; DEFAULT SIZE STARTS AT 4
 
RIO_INPNAM::				;
	$NAM	ESA=EXPFNM,-		; EXPANDED NAME BUFFER ADDRESS
		ESS=FILNAMSIZ,-		; MAXIMUM SIZE FOR EXPANDED NAME
		RSA=RSLFNM,-		; RESULTANT NAME
		RSS=FILNAMSIZ,-		; AND SIZE IS SAME
 
RIO_INPRAB::				;
	$RAB	RAC=SEQ,-		; SEQUENTIAL ACCESS TO FILE
		ROP=<PMT,CVT>,-		; PROMPT READ AND CONVERT TO UPPER CASE
		RHB=RHBUF,-		; RECORD HEADER BUFFER
		FAB=RIO_INPFAB		; ASSOCIATED FILE ACCESS BLOCK
 
;
;	PARAMETER FILE INPUT/OUTPUT FAB/RAB
;
PRM_INFAB:			;
	$FAB	FAC=<GET,BIO>,-		; READ ACCESS
		DNA=PRMDEF,-		; DEFAULT NAME
		DNS=PRMDEFSIZ,-		; DEFAULT NAME SIZE
		NAM=RIO_INPNAM		; NAME BLOCK
PRM_INRAB:			;
	$RAB	FAB=PRM_INFAB	; ASSOCIATED FAB
;
;	PARAMETER FILE OUTPUT RAB/RAB
;
PRM_OUTFAB:			;
	$FAB	FAC=PUT,-	;
		DNA=PRMDEF,-	; DEFAULT NAME
		DNS=PRMDEFSIZ,-	; DEFAULT NAME LENGTH
		RFM=FIX,-	; FIXED RECORD FORMAT
		MRS=512,-	; 
		FOP=CTG		; CONTIGUOUS
PRM_OUTRAB:			; OUTPUT RAB
	$RAB	FAB=PRM_OUTFAB,-	; ASSOCIATED FAB
		RSZ=512,-		; RECORD SIZE
		RBF=BOO$AB_PRMBUF	; BUFFER ADDRESS

;
;	SWAPFILE FAB  USED TO OBTAIN STARTING LBN OF SWAPFILE
;
SWAPFHCFAB:	$FAB	FAC=<GET,PUT,BIO>,- ; FILE ACCESS IS GET & PUT (READ & WRITE)
		DNA=SWPDEFNAM,-	; DEFAULT FILE NAME STRING ADDRESS
		DNS=SWPDEFNAMSZ,-	; DEFAULT FILE NAME STRING SIZE
		RFM=FIX,-	; FIXED RECORD FORMAT
		MRS=512,-	; MAXIMUM RECORD SIZE OF ONE PAGE
		NAM=RIO_INPNAM,-; NAME BLOCK ADDRESS
		RTV=255,-	; LET ACP COMPUTE LARGEST RETRIVAL WINDOW
		XAB=SWAPFHCXAB	; EXTENDED ATTRIBUTE BLOCK
SWAPFHCXAB:	$XABFHC		; EXTENDED ATTRIBUTE BLOCK FOR FILE HEADER
SWAPALLXAB:	$XABALL -	; EXTENDED ATTRIBUTE BLOCK FOR ALLOCATION
		VOL=1,-		; ALWAYS ALLOCATE ON RELATIVE VOLUME #1
		AOP=<CTG,HRD>,-	; CONTIGUOUS AND FORCE VOLUME PLACEMENT
		ALN=LBN		; (MUST SPECIFY SO VOL IS USED)
SWAPALLXABNC:	$XABALL	-	; EXTENDED ATTRIBUTE BLOCK FOR NON-CONTIGUOUS ALLOCATION
		AOP=<CBT>	; CONTIGUOUS BEST TRY

SWAP_RAB:			; OUTPUT RAB
	$RAB	FAB=SWAPFHCFAB,-	; ASSOCIATED FAB
		RAC=KEY,-
		RSZ=512,-		; RECORD SIZE
		RBF=ZEROES		; BUFFER ADDRESS

SWPDEFNAM:	.ASCII	/.SYS/		; DEFAULT NAME STRING
SWPDEFNAMSZ=.-SWPDEFNAM		; SIZE OF DEFAULT NAME STRING

;
;	PARSED PARAMETERS FOR SWAP FILE CREATION AND INSTALLATION
;
BOO$GL_SIZE:			; FILE SIZE
	.LONG	0		;

;
; OWN STORAGE:
;
 
 
OUTFNM:	.ASCII	\OUTPUT:\
 
	OUTSIZ=.-OUTFNM
 
	.PAGE
	.SBTTL	 RIO$INPUT - RMS INPUT INTERFACE
;++
; FUNCTIONAL DESCRIPTION:
;	RIO$INPUT READS A RECORD FROM THE DEVICE OR FILE DESCRIBED BY THE
;	LOGICAL NAME 'INPUT' INTO THE BUFFER DESCRIBED BY THE ARGUMENTS BUF
;	AND SIZE.  THE ACTUAL SIZE READ IS RETURNED IN R1.
;
; CALLING SEQUENCE:
;	CALLG	ARGLIST,RIO$INPUT
;
; INPUT PARAMETERS:
;	BUF(AP)	-	BUFFER ADDRESS
;	SIZE(AP)-	MAXIMUM SIZE OF READ
;
; OUTPUT PARAMETERS:
;	R0	- 	COMPLETION STATUS
;	R1	-	ACTUAL SIZE OF RECORD IN BYTES
;
; COMPLETION STATUS CODES:
;	STANDARD RMS COMPLETION STATUS CODES
;
;--
SIZE=8					; OFFSET TO SIZE ARGUMENT

	.Psect PAGED_CODE	rd,nowrt,exe,long

RIO$INPUT::				; INPUT INTERFACE ROUTINE
	.WORD	^M<R2,R3,R4,R5,R6,R7,R8>; REGISTER SAVE MASK
	MOVAB	RIO_INPRAB,R2		; GET INPUT RAB ADDRESS
	BLBS	INP_OPEN,15$		; BR IF INPUT IS ALREADY OPEN
	PUSHL	#0			; OUTPUT OPEN FAILURE AST
	PUSHAB	RIO_INPFAB		; THE FAB
	CALLS	#2,SYS$OPEN		; OPEN THE FILE
	BLBS	R0,10$			; BR IF OPEN OK
	RET				; RETURN ERROR STATUS
10$:					;
	$CONNECT (R2)			; CONNECT TO INPUT STREAM
	BLBC	R0,30$			; BR IF ERROR
	MOVB	#1,INP_OPEN		; MARK INPUT OPEN AND CONNECTED

15$:	MOVW	SIZE(AP),RAB$W_USZ(R2)	; SET SIZE FOR INPUT
	MOVL	BUF(AP),RAB$L_UBF(R2)	; AND BUFFER ADDRESS
	$GET	(R2)			; GET A RECORD	
	BLBS	R0,20$			; SUCCESS
	RET				; RETURN ERROR STATUS
20$:	MOVZWL	RAB$W_RSZ(R2),R1	; GET SIZE OF RECORD
30$:	RET				; RETURN

	.PAGE
	.SBTTL	RIO$OUTPUT - RMS OUTPUT INTERFACE ROUTINE
;++
; FUNCTIONAL DESCRIPTION:
;	RIO$OUTPUT WRITES THE BUFFER DESCRIBED BY THE BUF AND SIZE
;	ARGUMENTS TO THE DEVICE OR FILE SPECIFIED BY THE LOGICAL NAME
;	OUTPUT.
;
;	RIO$OUTPUTC WRITES THE COUNTED ASCII STRING SPECIFIED BY THE
;	BUF ARGUMENT TO THE DEVICE OR FILE SPECIFIED BY THE LOGICAL NAME
;	OUTPUT.
; 
; CALLING SEQUENCE:
;	CALLG	ARGLIST,RIO$OUTPUT/RIO$OUTPUTC
; 
; INPUT PARAMETERS:
;	BUF(AP)	-	ADDRESS OF BUFFER OR ADDRESS OF COUNTED STRING IF
;			RIO$OUTPUTC.
;	SIZE(AP)-	NUMBER OF BYTES TO BE OUTPUT (RIO$OUTPUT ONLY)
;
; 
; IMPLICIT INPUTS:
;	INPUT RAB AND FAB
; 
; OUTPUT PARAMETERS:
;	R0	-	COMPLETION STATUS CODE FROM RMS
; 
; COMPLETION CODES:
;	STANDARD RMS COMPLETION STATUS CODES
; 
;--

BUF=4					; OFFSET TO BUFFER ADDRESS ARGUMENT

RIO$OUTPUTC::				; OUTPUT COUNTED STRING
	.WORD	0			; ENTRY MASK
	MOVL	BUF(AP),R1		; GET BUFFER ADDRESS
	MOVZBL	(R1)+,-(SP)		; PUSH SIZE OF WRITE
	PUSHL	R1			; AND BUFFER ADDRESS
	CALLS	#2,B^RIO$OUTPUT		; CALL OUTPUT
	RET				; AND RETURN

RIO$OUTPUT::				; RMS OUTPUT INTERFACE ROUTINE
	.WORD	^M<R2,R3,R4,R5,R6,R7>	; REGISTER SAVE MASK
	MOVAB	RIO_OUTRAB,R2		; GET ADDRESS OF OUTPUT RAB
	BLBS	OUTP_OPEN,10$		; BR IF FILE ALREADY OPEN
	CLRB	NAM$B_ESL+RIO_OUTNAM	; ZERO EXPANDED NAME LENGTH
	$CREATE	RIO_OUTFAB		; CREATE TYPING FILE
	BLBC	R0,20$			; BR IF OPEN FAILED

	BICL	#BOO$M_TERMINAL,-
		BOO$GL_CMDOPT		; ASSUME NOT A TERMINAL DEVICE
	BBC	#DEV$V_TRM,-
		RIO_OUTFAB+FAB$L_DEV,-
		5$			; BRANCH IF NOT
	BISL	#BOO$M_TERMINAL,-
		BOO$GL_CMDOPT		; SET AS A TERMINAL DEVICE

5$:	$CONNECT  (R2)		 	; CONNECT TO OUTPUT STREAM
	MOVB	#1,OUTP_OPEN		; MARK OUTPUT STREAM OPEN
10$:	MOVL	BUF(AP),RAB$L_RBF(R2)	; SET OUTPUT BUFFER
	MOVW	SIZE(AP),RAB$W_RSZ(R2)	; AND SIZE
	$PUT	(R2)			; PRINT LINE
20$:	RET				; RETURN


	.PAGE
	.SBTTL	BOO$READPROMPT - Prompt and read input string
;++
; Functional Description:
;	BOO$READPROMPT outputs the specified ASCIZ prompt string on the
;	console terminal then checks the count of characters to be read.
;	If zero it exits, otherwise it reads the console terminal until
;	either a carriage return is encountered or the character count
;	is satisfied.  The specified buffer is filled with an ASCIC
;	string containing the characters read but not including the
;	terminating carriage return.
;
; Calling Sequence:
;	CALLG	ARGLIST,BOO$READPROMPT
;
; Input Parameters:
;	PROMPT(AP)  -  Address of ASCIZ prompt string
	PROMPT	=  4
;
;	SIZE(AP)    -  Maximum length of input string
	SIZE	=  8
;			Note: if size is zero, then nothing is read
;			      and only the prompt string is written.
;
;	BUF(AP)     -  Address of input buffer
	BUF	=  12
;
; Output Parameters:
;	R0 - Completion status code (always SS$_NORMAL)
;
;	Buffer located by BUF(AP) will be filled with the string
;	read as an ASCIC string.
;
;--
BOO$READPROMPT::			;
	.WORD	^M<R2,R3,R4,R5,R6,R7>	;
	LOCC	#0,#64000,@PROMPT(AP)	; FIND END OF PROMPT STRING
	SUBL3	PROMPT(AP),R1,R7	; COMPUTE CHARACTER COUNT
	MOVAB	RIO_INPRAB,R2		; GET INPUT RAB ADDRESS
	BLBS	INP_OPEN,15$		; BR IF INPUT IS ALREADY OPEN
	PUSHL	#0			; OUTPUT OPEN FAILURE AST
	PUSHAB	RIO_INPFAB		; THE FAB
	CALLS	#2,SYS$OPEN		; OPEN THE FILE
	BLBS	R0,10$			; BR IF OPEN OK
	RET				; RETURN ERROR STATUS
10$:					;
	$CONNECT (R2)			; CONNECT TO INPUT STREAM
	BLBC	R0,30$			; BR IF ERROR
	MOVB	#1,INP_OPEN		; MARK INPUT OPEN AND CONNECTED
15$:	MOVW	SIZE(AP),RAB$W_USZ(R2)	; SET SIZE FOR INPUT
	BEQL	NOREAD			; IF NO BUFFER THEN NO READ
	MOVB	R7,RAB$B_PSZ(R2)	; SET SIZE FOR PROMPT
	MOVL	PROMPT(AP),RAB$L_PBF(R2); AND PROMPT ADDRESS
	ADDL3	#1,BUF(AP),RAB$L_UBF(R2); AND BUFFER ADDRESS
	CLRB	@BUF(AP)		; INITIALIZE COUNT FOR READ STRING
	MOVQ	BOO$GQ_CMDESC,R0	; GET COMMAND STRING DESCRIPTOR
	TSTL	R0			; IS THERE ANY COMMAND STRING
	BLSS	50$			; NO, ALREADY USED IT
	BGTR	40$			; YES, USE IT AS RECORD
	$GET	(R2)			; GET A RECORD	
	BLBS	R0,20$			; SUCCESS
	RET				; RETURN ERROR STATUS
20$:	MOVZWL	RAB$W_RSZ(R2),R1	; GET SIZE OF RECORD
	CVTWB	R1,@BUF(AP)		; RETURN SIZE OF RECORD
30$:	RET				; RETURN
40$:	MOVL	BUF(AP),R3		; GET BUFFER ADDRESS
	CVTWB	R0,(R3)+		; SET COUNT FOR BUFFER
	MOVC3	R0,(R1),(R3)		; COPY COMMAND STRING
	MOVL	#1,R0			; SET SUCCESS STATUS
	MNEGL	#1,BOO$GQ_CMDESC	; MARK AS USED
	RET				;
50$:	MOVL	#RMS$_EOF,R0		; GIVE END OF FILE STATUS
	RET				; AND RETURN
NOREAD:					;
	MOVAB	RIO_OUTRAB,R2		; GET ADDRESS OF OUTPUT RAB
	BLBS	OUTP_OPEN,10$		; BR IF FILE ALREADY OPEN
	CLRB	NAM$B_ESL+RIO_OUTNAM	; ZERO EXPANDED NAME LENGTH
	$CREATE	RIO_OUTFAB		; CREATE TYPING FILE
	BLBC	R0,20$			; BR IF OPEN FAILED

	BICL	#BOO$M_TERMINAL,-
		BOO$GL_CMDOPT		; ASSUME NOT A TERMINAL DEVICE
	BBC	#DEV$V_TRM,-
		RIO_OUTFAB+FAB$L_DEV,-
		5$			; BRANCH IF NOT
	BISL	#BOO$M_TERMINAL,-
		BOO$GL_CMDOPT		; SET AS A TERMINAL DEVICE

5$:	$CONNECT  (R2)		 	; CONNECT TO OUTPUT STREAM
	MOVB	#1,OUTP_OPEN		; MARK OUTPUT STREAM OPEN
10$:	MOVL	PROMPT(AP),RAB$L_RBF(R2); SET OUTPUT BUFFER
	MOVW	R7,RAB$W_RSZ(R2)	; AND SIZE
	$PUT	(R2)			; PRINT LINE
20$:	RET				; RETURN
	.PAGE
	.SBTTL	BOO$FILOPEN - Routine to open input file
;
; Input Parameters:
;	R7 - Address of file name descriptor
; Output Parameters:
;	R0 - Completion status
;	R8 - RAB address
;

BOO$FILOPENW::				; Open file for reads and writes
	MOVAB	PRM_INFAB,R8		; Get address of FAB
	BISB	#FAB$M_PUT,FAB$B_FAC(R8); Set PUT bit 
	BRB	FILOPEN

BOO$FILOPEN::				; Open file for read only
	MOVAB	PRM_INFAB,R8		; Get address of FAB
	BICB	#FAB$M_PUT,FAB$B_FAC(R8); Clear PUT bit

FILOPEN:
	MOVB	(R7),FAB$B_FNS(R8)	; Set size of file name
	MOVL	4(R7),FAB$L_FNA(R8)	; and address of name string
	$CLOSE	FAB=(R8)		; Close if open
	$OPEN	FAB=(R8)		; Open file for read
	BLBS	R0,10$			; Continue if successful
	BSBW	PUTERROR		; Output error message
	MNEGL	#2,R0			; Return failure
	RSB				;
10$:	MOVAL	RSLFNM,BOO$GL_FILEADDR	; Set expanded file address
	MOVB	RIO_INPNAM+NAM$B_RSL,-	;
		BOO$GB_FILELEN		; Set expanded file length
	MOVAB	PRM_INRAB,R8		; Get RAB address
	$CONNECT RAB=(R8)		; Connect record stream
	MOVL	#1,R8			; Return starting virtual block
	RSB				;

	.PAGE
	.SBTTL	BOO$READFILE - Read parameter file
;
; Input Parameters:
;	R6 - Buffer address
;	R8 - VBN
;	R9 - Count of pages to read
;
; Output Parameters:
;	R0 - Completion status
;
BOO$READFILE::				;
	MOVAB	PRM_INRAB,R1		; Get address of input RAB
	MOVW	#512,RAB$W_USZ(R1)	; Set record size
	MOVL	R6,RAB$L_UBF(R1)	; Set buffer address
	MOVL	R8,RAB$L_BKT(R1)	; Set VBN to read
	$READ	RAB=(R1)		;
	BLBS	R0,10$			;
	CMPL	R0,#RMS$_EOF		; Is it end of file?
	BEQL	20$			; Branch if yes
	MOVL	#SYSG$_INVPARFIL,R0	; Invalid parameter file
	RSB				;  and return
10$:	MOVAB	512(R6),R6		; Advance buffer pointer
	INCL	R8			; and block number
	SOBGTR	R9,BOO$READFILE		; Read all required blocks
20$:	MOVZWL	#1,R0			; Return success
	RSB				;
	.PAGE
	.SBTTL	BOO$WRITEFILE - Write blocks to file
;
; Input Parameters:
;	R6 - Buffer address
;	R8 - VBN
;	R9 - Count of pages to write
;
; Output Parameters:
;	R0 - Completion status
;
BOO$WRITEFILE::
	MOVAB	PRM_INRAB,R1		; Use input RAB for output too
	MOVW	#512,RAB$W_RSZ(R1)	; Set block size
	MOVL	R6,RAB$L_RBF(R1)	; Set buffer address
	MOVL	R8,RAB$L_BKT(R1)	; Set VBN to write
	$WRITE	RAB=(R1)		; Do it!
	BLBC	R0,20$			; Error
	MOVAB	512(R6),R6		; Advance buffer pointer
	INCL	R8			; and block number
	SOBGTR	R9,BOO$WRITEFILE	; Repeat

	MOVAB	PRM_INFAB,R1		; Get address of FAB
	$CLOSE	FAB = (R1)		; Close file
	BLBC	R0,20$			; Error
	MOVZWL	#1,R0			; Return success
	RSB

20$:	BSBW	PUTERROR		; Output error message
	MNEGL	#2,R0			; Set error status
	RSB

	.SBTTL	BOO$WRTFILE - Routine to write parameter file
;
; Input parameters:
;	TPA$L_TOKENCNT(AP) - SIZE OF FILE NAME
;	TPA$L_TOKENPTR(AP) - ADDRESS OF FILE NAME
;
; Output Parameters:
;	R0 - Completion status code
;
BOO$WRTFILE::				;
	.WORD	^M<R2,R3,R4,R5,R6,R7,R8,R9>	;
	MOVAB	PRM_OUTFAB,R7		; Get FAB address
	MOVL	TPA$L_TOKENPTR(AP),FAB$L_FNA(R7); Set into FAB
	MOVB	TPA$L_TOKENCNT(AP),FAB$B_FNS(R7); and set size of name
	MOVAB	BOO$A_PRMBLK,R9		; Get base of parameter blocks
	MOVAB	BOO$AB_PRMBUF,R8	; And set base of buffer 
;
; Set startup filename
;
	MOVC3	#32,EXE$GT_STARTUP,(R8)	; Copy name of startup file
	MOVL	R8,R0			; Save base of buffer
	ADDL	#32,R8			; Advance buffer pointer

;
; Loop through all parameters
;

5$:	TSTL	(R9)			; Check for end of buffer
	BEQL	8$			; Yes, write file
	MOVQ	PRM$T_NAME(R9),(R8)+	; Copy name
	MOVQ	PRM$T_NAME+8(R9),(R8)+	; string to buffer
	MOVZBL	PRM$B_POS(R9),R1	; Get position
	EXTV	R1,PRM$B_SIZE(R9),-
		@PRM$L_ADDR(R9),R2	; Extract value
	BBC	#PRM$V_NEG,-
		PRM$L_FLAGS(R9),7$	; Branch if no negation needed
	MNEGL	R2,R2			; Convert to negative
7$:	EXTZV	#0,PRM$B_SIZE(R9),R2,R2	; Convert value to unsigned form
	MOVL	R2,(R8)+		; Put value in buffer
	ADDL	#PRM$C_LENGTH,R9	; Next parameter
	BRB	5$			;

8$:	CLRL	(R8)+			; Mark end of buffer
	SUBL	R0,R8			; Size of buffer
	MOVAB	511(R8),R8		; Round to page bound
	ASHL	#-9,R8,R8		; Compute file size in pages
	MOVL	R8,FAB$L_ALQ(R7)	; Set size of file to create
	$CREATE	FAB=(R7)		; Create and open file
	BLBS	R0,10$			; Continue if success
	MOVL	#SYSG$_CREPARFIL,R0	; Set message
	RET				;
10$:	$CONNECT RAB=PRM_OUTRAB		; Connect output RAB
	MOVAB	PRM_OUTRAB,R5		; Get base address of output RAB
	MOVAB	BOO$AB_PRMBUF,RAB$L_RBF(R5)	; Set base of buffer 
20$:	$PUT	RAB=(R5)		; Write one block
	ADDL	#512,RAB$L_RBF(R5)	; Advance buffer pointer
	SOBGTR	R8,20$			; Do them all
	$CLOSE	FAB=(R7)		; 
	RET				;
	.PAGE
	.SBTTL	BOO$SWPCREATE - CREATE SWAP OR PAGING FILE
;++
; FUNCTIONAL DESCRIPTION:
;	BOO$CREATE IS CALLED TO IMPLEMENT THE CREATE COMMAND IN THE SYSGEN
;	UTILITY.  THIS COMMAND IS USED TO CREATE DUMP,SWAP AND PAGING FILES.
;
; INPUT PARAMETERS:
;	BOO$GL_SIZE - SIZE OF FILE TO CREATE IN BLOCKS
;
; OUTPUT PARAMETERS:
;	R0 - COMPLETION STATUS CODE
;--
.ENTRY	BOO$CREATE	^M<R2,R3,R4,R5,R6,R7>	; SAVE R2-R7

	MOVAL	SWAPFHCFAB,R7		; GET FAB ADDRESS
	TSTB	FAB$B_FNS(R7) 		; SEE IF FILE WAS SPECIFIED
	BNEQ	10$			; BRANCH IF IT WAS
	MOVL	#<<RMS$_FNM> & ^C<STS$M_SEVERITY>>,R0 ; SET WARNING
	RET				; RETURN

10$:	MOVL	BOO$GL_SIZE,R5		; SET DESIRED SIZE
	MOVAL	SWAPALLXABNC,R6		; GET ADDRESS OF NONCONTIG ALLOCATION XAB
	BBC	#BOO$V_CONTIG, -
		BOO$GL_CMDOPT,20$ 	; BR IF THAT'S WHAT'S WANTED
	MOVAL	SWAPALLXAB,R6		; GET ADDRESS OF CONTIG ALLOCATION XAB
20$:	MOVL	R6,SWAPFHCXAB+XAB$L_NXT ; SET ADDRESS OF ALLOCATION XAB
	BBS	#BOO$V_CONTIG, -
		BOO$GL_CMDOPT,CREATE 	; BR IF CONTIGUOUS SPECIFIED
;
; Try to open file to see if it exists
;
	BICL	#FAB$M_UFO,FAB$L_FOP(R7); CLEAR USER FILE OPEN
	$OPEN	FAB=(R7)		; OPEN
	BLBC	R0,CREATE		; IF ERROR, CREATE FILE
;
; Extend file
;
	SUBL3	XAB$L_ALQ(R6),R5,R1	; DIFF
	BEQL	30$			; IF THE SAME, THEN NOTHING TO DO
	BLSS	50$			; IF LESS THEN NEW FILE
	MOVL	R1,XAB$L_ALQ(R6)
	$EXTEND FAB=(R7)
	BLBC	R0,40$

	MOVAB	SWAP_RAB,R4		; RAB ADDRESS
	$CONNECT RAB=(R4)
	BLBC	R0,40$
	MOVL	R5,RAB$L_BKT(R4)	; SET LENGTH
	$WRITE	RAB=(R4)		; FORCE EOF TO SIZE SPECIFIED
	BLBC	R0,40$			; BRANCH ON ERROR
30$:	$CLOSE	FAB=(R7)		; CLOSE FILE
	BLBC	R0,40$			; EXIT IF ERROR
	MOVL	#SYSG$_EXTENDED,R0	; SET STATUS
	BRB	CREATE_SIGNAL		; OK TO SIGNAL
40$:	BRW	CREATE_ERROR		; EXIT

50$:	$CLOSE	FAB=(R7)

CREATE:	MOVL	R5,XAB$L_ALQ(R6) 	; SET ALLOCATION REQUEST SIZE
	BISL	#FAB$M_UFO,FAB$L_FOP(R7); SET USER FILE OPEN, SO STV WILL BE CHANNEL
	$CREATE	FAB=(R7)		; OPEN AND CREATE FILE
	BLBS	R0,10$			; CONTINUE IF NO ERROR
	PUSHL	FAB$L_STV(R7)		; SET FAILURE STATUS VALUE
	BICL3	#STS$M_SEVERITY,R0,-(SP); SET FAILURE STATUS, CONVERTING TO WARNING
	CALLS	#2,G^LIB$SIGNAL		; SIGNAL THE FAILURE
	BRB	CREATE_EXIT		;

10$:	$QIOW_S	CHAN=FAB$L_STV(R7),-	; ZERO FIRST LONGWORDS IN FILE
		FUNC=#IO$_WRITEVBLK,-	; WRITE VIRUAL BLOCK
		P1=ZEROES,-		; ADDRESS OF ZEROES
		P2=#512,-		; ONE PAGE
		P3=#1			; FIRST BLOCK OF FILE
	$DASSGN_S CHAN=FAB$L_STV(R7)	; DEASSIGN CHANNEL
	BLBC	R0,CREATE_ERROR
	MOVL	#SHR$_CREATED,R0	; RETURN SUCCESS

CREATE_SIGNAL:

	PUSHAB	RSLFNM			; SET FILE NAME ADDRESS
	MOVZBL	RIO_INPNAM+NAM$B_RSL,-(SP) ; SET FILE NAME SIZE
	MOVL	SP,R1			; GET ADDRESS OF DESCRIPTOR
	PUSHL	R1			; SET FILE NAME DESCRIPTOR ADDRESS
	PUSHL	#1			; SET FAO COUNT
	BISL3	#<SYSG$_FACILITY@16!STS$K_INFO> -
		,R0,-(SP) 		; MAKE SYSGEN MESSAGE
	CALLS	#3,G^LIB$SIGNAL		; SIGNAL THE ACTION JUST TAKEN

CREATE_EXIT:
	MOVL	#1,R0			; Set sucess

CREATE_ERROR:
	CLRB	FAB$B_FNS(R7) 		; CLEAR FILE NAME LENGTH
	CLRL	FAB$L_FNA(R7) 		; AND FILE NAME ADDRESS
	RET

	.PAGE
	.SBTTL	INSTALL PAGE OR SWAP FILE
;++
;
; Functional Description:
;
;	BOO$INSTALL is called to install a second page or swap file. The
;	specified file is opened, the window pointer is removed from the
;	associated channel control block and moved to the page file control
;	block. The PFL address is loaded into the page file control block
;	vector. 
;
; Input Parameters:
;
;	SWAPFHCFAB - Specified file name fields filled in.
;	SWAPFHCXAB - File size is stored in XAB
;	BOO$GL_CMDOPT <BOO$V_PAGEFILE> - Set if page file installation
;
; Output Parameters:
;
;	None
;
; Implicit Output:
;
;	A page file control block describing the designated file is created
;	and loaded into the page file control block vector. (The details
;	can be found in the module header in INITPGFIL.)
;
; Completion Status:
;
;	R0 - Completion status 
;
;		All status returns (success and failure codes) are returned
;		from kernel mode by INITPAGSWPFIL (which gets its status
;		from either BOO$INITPAGFIL or BOO$INITSWPFIL).
;
;--
BOO$INSTALL::	
	.WORD	^M<R2,R3,R4,R5,R6,R7>	; Entry mask
	MOVAL	SWAPFHCFAB,R7		; Get base of FAB
	BISL	#FAB$M_UFO,FAB$L_FOP(R7); SET USER FILE OPEN, SO STV WILL BE CHANNEL

10$:	$OPEN	FAB=(R7)		; Open file for install
	BLBS	R0,20$			; Continue if no error
	PUSHAB	EXPFNM			; Set file name address
	MOVZBL	RIO_INPNAM+NAM$B_ESL,-(SP) ; Set file name size
	MOVL	SP,R1			; Get address of descriptor
	PUSHL	FAB$L_STV(R7)		; Set status value
	PUSHL	R0			; Set status
	PUSHL	R1			; Set file name descriptor address
	PUSHL	#1			; Set FAO count
	PUSHL	#SHR$_OPENIN!<SYSG$_FACILITY@16> ; Set message status
	CALLS	#5,G^LIB$SIGNAL		; Signal the failure
	BRB	30$			; Return successfully because
					;  error already signalled

20$:	$CMKRNL_S INITPAGSWPFIL
	BLBS	R0,40$			; Branch if success
	PUSHL	R0			; Set failure status
	CALLS	#1,G^LIB$SIGNAL		; Signal the failure
30$:	MOVZWL	#SS$_NORMAL,R0		; Error already signalled
40$:	RET

;
; Install a second page or swap file 
;

INITPAGSWPFIL:
	.WORD	^M<R2>			; Entry mask
	CLRQ	-(SP)			; Ignore returned PFL index. 
					; Default MAXVBN parameter to default
	MOVAL	SWAPFHCFAB,R2		; Get base of FAB
	MNEGL	FAB$L_STV(R2),R2	; Get channel number and invert
	ADDL	@#CTL$GL_CCBBASE,R2	; Compute base of channel
	PUSHL	CCB$L_WIND(R2)		; Get window pointer address
	SUBL3	#1,SWAPFHCXAB+XAB$L_EBK,-(SP) ; Get size of file
	BBS	#BOO$V_PAGEFILE,BOO$GL_CMDOPT,10$ ; Br if installing pagefile
	CALLS	#4,G^BOO$INITSWPFIL	; Call external procedure
	BRB	20$			; Join common completion code
10$:	CALLS	#4,G^BOO$INITPAGFIL	; Call external procedure
20$:	BLBC	R0,30$			; Skip next if error
	CLRL	CCB$L_WIND(R2)		; Break link from channel to window
30$:	RET

	.PAGE
	.SBTTL	INSTALL/CREATE ACTION ROUTINES

BOO$SETPGFL::				; SET PAGEFILE OPTION
	.WORD	0			;
	BBSS	#BOO$V_PAGEFILE,BOO$GL_CMDOPT,10$
10$:	RET				; 

BOO$CRENCONTIG::			; CREATE NON-CONTIGUOUS FILE
	.WORD	0
	BISL	#BOO$M_NONCONTIG,BOO$GL_CMDOPT  ; SET OPTION BIT
	BICL	#BOO$M_CONTIG,BOO$GL_CMDOPT 	; CLEAR OPTION BIT
	RET				;

BOO$CRECONTIG::				; CREATE CONTIGUOUS FILE
	.WORD	0
	BICL	#BOO$M_NONCONTIG,BOO$GL_CMDOPT 	; CLEAR OPTION BIT  
	BISL	#BOO$M_CONTIG,BOO$GL_CMDOPT	; SET OPTION BIT
	RET				;

BOO$FILESIZE::				; SET FILE SIZE
	.WORD	0			;
	MOVL	TPA$L_NUMBER(AP),BOO$GL_SIZE	;
	RET				;

BOO$SETFILNAM::				;
	.WORD	0			;
	MOVAB	SWAPFHCFAB,R1		;
	MOVB	TPA$L_TOKENCNT(AP),FAB$B_FNS(R1) ; SET FILE NAME LENGTH
	MOVL	TPA$L_TOKENPTR(AP),FAB$L_FNA(R1) ; AND FILE NAME ADDRESS
	RET				;

.PAGE  
.SBTTL RMS DEFINITIONS FOR /OUTPUT= QUALIFIER

.Psect	PAGED_DATA	rd,wrt,noexe,quad
.ALIGN	LONG 

;	Output File Data

RIO$GW_OUTLEN::		.WORD	0		;LENGTH OF STRING 
RIO$AB_OUTBUF::		.LONG	BUFFER_SIZE 	;OUTPUT BUFFER 
			.LONG	RIO$AB_BUFFER 
RIO$AB_BUFFER:: 	.BLKB	BUFFER_SIZE 
;  
;	Output Device Data 
; 
.ALIGN LONG
RIO_OUTFAB2::	$FAB	RAT=CR 
RIO_OUTRAB2::	$RAB	FAB=RIO_OUTFAB2 
  
;	 Input File Data
  
RIO$GL_INBUF_SZ:: 	.LONG	BUFFER_SIZE 
RIO$AB_INBUFFER:: 	.BLKB	BUFFER_SIZE 
  
; Input Device Data 

	.ALIGN 	LONG 
RIO_INFAB2::	$FAB	FNM=<INFILE:> 
RIO_INRAB2::	$RAB	FAB=RIO_INFAB2,- 
				UBF=RIO$AB_INBUFFER,- 
				USZ=RIO$GL_INBUF_SZ,PBF=PROMPT_IN, -
				ROP=<PMT,CVT>,PSZ=8
PROMPT_IN:	.ASCII	/DEVICE> /

INNAM_SIZE: 	.BYTE 	INPNAMSZ
INNAM_ADDR: 	.LONG 	INPNAM

COM:	.ASCII	/.COM/
LIS:	.ASCII	/.LIS/
.PAGE
.Psect	PAGED_CODE	rd,nowrt,exe,long

;
; The following are called as TPARSE action routines from SYSBOOCMD
;

.ENTRY	BOO$INPUT_FILE, ^M<>

	MOVL	TPA$L_TOKENPTR(AP),-
		INNAM_ADDR 		; Save pointer to input file
	CVTLB	TPA$L_TOKENCNT(AP),-
		INNAM_SIZE 		; Save length of file name
	BISL	#BOO$M_INPUT,-
		BOO$GL_CMDOPT	    	; Set flag for /INPUT
	RET				; Return

.ENTRY	BOO$OUTPUT_FILE, ^M<>

	MOVL	TPA$L_TOKENPTR(AP),-
		BOO$GL_FILEADDR 	; Save pointer to output file
	CVTLB	TPA$L_TOKENCNT(AP),-
		BOO$GB_FILELEN		; Save length of file name
	BISL	#BOO$M_OUTPUT,-
		BOO$GL_CMDOPT 	     	; Set flag for /INPUT
	RET				; Return

.ENTRY	BOO$RESET_IO, ^M<>

	MOVAL	LIS,RIO_OUTFAB2+FAB$L_DNA	; Set .lis default
	MOVB	#4,RIO_OUTFAB2+FAB$B_DNS	; Set size of .lis

	MOVB	#OUTPNAMSZ,BOO$GB_FILELEN 	; Set default length
	MOVAL	OUTPNAM,BOO$GL_FILEADDR 	; Default output is SYS$OUTPUT
	MOVB	#INPNAMSZ,INNAM_SIZE 		; Set default length
	MOVAL	INPNAM,INNAM_ADDR 		; Default input is SYS$INPUT
	RET					; Return

.ENTRY	BOO$RESET_COMMAND, ^M<>

; Command SHOW/CONFIG/COMMAND_FILE was originally SAVE <file-spec>

	MOVAL	COM,RIO_OUTFAB2+FAB$L_DNA	; Set up RMS default extension
	MOVB	#4,RIO_OUTFAB2+FAB$B_DNS 	; Set up extension size
	BBSS	#BOO$V_SAVE,BOO$GL_CMDOPT,10$	; Set SAVE command flag
10$:	RET

.PAGE
;
; These routines are called via a BSBW from TPARSE action routines
;
 
BOO$OPEN_OUTPUT_2::			; Open option output file

	BBC	#BOO$V_OUTPUT,BOO$GL_CMDOPT,10$	  ; Branch if no /OUTPUT
	BBC	#BOO$V_SAVE,BOO$GL_CMDOPT,5$      ; Branch if not ".com"
	MOVAL	COM,RIO_OUTFAB2+FAB$L_DNA	  ; Set .com default
	MOVB	#4,RIO_OUTFAB2+FAB$B_DNS	  ; Set size of .com

5$:	MOVB	BOO$GB_FILELEN,-
		RIO_OUTFAB2+FAB$B_FNS	; Set up RMS data of size
	MOVL	BOO$GL_FILEADDR,-
		RIO_OUTFAB2+FAB$L_FNA 	;  and addr. of file spec.
	$CREATE	FAB = RIO_OUTFAB2	; Create file
	BLBC	R0,10$			; Branch on error

	BICL	#BOO$M_TERMINAL,-
		BOO$GL_CMDOPT		; ASSUME NOT A TERMINAL DEVICE
	BBC	#DEV$V_TRM,-
		RIO_OUTFAB2+FAB$L_DEV,-
		7$			; BRANCH IF NOT
	BISL	#BOO$M_TERMINAL,-
		BOO$GL_CMDOPT		; SET AS A TERMINAL DEVICE

7$:
	$CONNECT RAB = RIO_OUTRAB2	;  and open file
10$:	RSB

BOO$OPEN_INPUT_2::
	BBC	#BOO$V_INPUT,-
		BOO$GL_CMDOPT,10$	; Branch if no /INPUT
	MOVB	INNAM_SIZE,-
		RIO_INFAB2+FAB$B_FNS	; Set up RMS data of size
 	MOVL	INNAM_ADDR,-
		RIO_INFAB2+FAB$L_FNA	; and addr. of file spec.
 	$OPEN	FAB=RIO_INFAB2		; and open file
 	BLBC	R0,10$			; branch if error
 
	$CONNECT RAB=RIO_INRAB2 	; Establish record stream
10$:	RSB

.PAGE
.SBTTL	BOO$SET_OUTPUT - Open file for SET/OUTPUT

.Entry	BOO$SET_OUTPUT,^M<>		; Open option output file

	BBC	#BOO$V_SETOUTPUT,-
		BOO$GL_CMDOPT,10$	; Has this file been open?
	$CLOSE	FAB = RIO_OUTFAB2	; Close it first then
	BLBC	R0,30$			; Branch if failure

10$:	MOVAL	LIS,-
		RIO_OUTFAB2+FAB$L_DNA	; Set .lis default
	MOVB	#4,-
		RIO_OUTFAB2+FAB$B_DNS	; Set size of .lis

	MOVB	BOO$GB_FILELEN,-
		RIO_OUTFAB2+FAB$B_FNS	; Set up RMS data of size
	MOVL	BOO$GL_FILEADDR,-
		RIO_OUTFAB2+FAB$L_FNA	;  and addr. of file spec.

	$CREATE	FAB = RIO_OUTFAB2	; Create file
	BLBC	R0,30$			; Branch on error

	BICL	#BOO$M_TERMINAL,-
		BOO$GL_CMDOPT		; ASSUME NOT A TERMINAL DEVICE
	BBC	#DEV$V_TRM,-
		RIO_OUTFAB2+FAB$L_DEV,-
		20$			; BRANCH IF NOT
	BISL	#BOO$M_TERMINAL,-
		BOO$GL_CMDOPT		; SET AS A TERMINAL DEVICE

20$:	$CONNECT RAB = RIO_OUTRAB2	;  and open file
	BLBC	R0,30$			; Branch on error
	
	BISL	#BOO$M_SETOUTPUT,-
		BOO$GL_CMDOPT		; Set output open
30$:	RET



RIO$Output_line::

; Output Routine: Use special FAB if SET/OUTPUT or /OUTPUT was specified,
; RIO$OUTPUT if not.

	BITL	#<BOO$M_OUTPUT!BOO$M_SETOUTPUT>,-
		BOO$GL_CMDOPT		; test for SET OUTPUT or /OUTPUT=
	BEQL	10$			; Branch if neither

5$:	MOVW	RIO$GW_OUTLEN,RIO_OUTRAB2+RAB$W_RSZ 
	MOVAL	RIO$AB_BUFFER,RIO_OUTRAB2+RAB$L_RBF
	$PUT	RAB=RIO_OUTRAB2
	BRB	20$
10$:
	MOVZWL	RIO$GW_OUTLEN,-(SP)	; Length 
	PUSHAL	RIO$AB_BUFFER		; Address
	CALLS	#2,RIO$OUTPUT		; Output to SYS$OUTPUT
20$:	BLBS	R0,30$			; Branch if OK
	PUSHL	R0
	CALLS	#1,G^LIB$SIGNAL		; Signal

30$:	RSB				; Return


	.END				
