	.TITLE	MPINTEXC - SECONDARY PROCESSOR INTERRUPT AND EXCEPTIONS
	.IDENT	'V03-001'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
;
; Facility:  Executive , Hardware fault handling
;
; Abstract:  Unhandled exception and interrupt routines
;
; Environment: MODE=Kernel 
;
; Author:  RICHARD I. HUSTVEDT, Creation date:  15-May-1979
;
; Modified by:
;
;	V03-001	KDM0080		Kathleen D. Morse	31-Mar-1982
;		Assembly switch the debug and performance measurement
;		code.  Longword align some interrupt vectors.
;
;	V02-011	KDM0031		Kathleen D. Morse	25-Aug-1981
;		Add compatibility mode fault handler for secondary.
;
;	V02-010	KDM0030		Kathleen D. Morse	20-Aug-1981
;		Add AST exit system service routine for secondary.
;
;	V02-009	KDM0027		Kathleen D. Morse	23-Jul-1981
;		Add error logging and bugchecking for secondary unexpected
;		interrupts.
;
;	V02-008	KDM0024		Kathleen D. Morse	22-Jul-1981
;		Add secondary bugcheck for kernel stack not valid.
;
;	V02-007	KDM0016		Kathleen D. Morse	08-Jul-1981
;		Remove MPS$INTxx and MPS$MCHK symbols as they have
;		been introduced in the module MPMCHECK.
;
;	V02-006	KDM0014		Kathleen D. Morse	10-Jun-1981
;		Correct PSL stored on stack to work during powerfail.
;
;	V02-005	KDM0012		Kathleen D. Morse	08-Jun-1981
;		Remove MPS$POWERFAIL symbol as real power fail logic
;		is added.
;
;	V02-004	KDM0003		Kathleen D. Morse	15-Sep-1980
;		Separate AST delivery from unexpected interrupts.
;
;	V02-003	KDM0002		Kathleen D. Morse	09-Sep-1980
;		Make source fixes for patches.
;
;	V02-002	KDM0001		Kathleen D. Morse	04-Sep-1980
;		Replace copyright.
;
; 01	- 
;--
	.PAGE
;
; INCLUDE FILES:
;
 
;
; MACROS:
;


;
; Macro to define an interrupt service routine label
; for unexpected interrupts
;
	.MACRO	ISRDEF,VNUM
	.ALIGN	LONG			; Make all vectors long word alligned
ERL$VEC'VNUM::				; Unexpected interrupt service rtn label
	.ENDM	ISRDEF
;
; Macro to define the interrupt service routine labels
; for an adapter
;
	.MACRO	ADPISR,SLOT
VECTOR = SLOT * 4 + 256
	.REPT	4
	ISRDEF	\VECTOR
VECTOR = VECTOR + <16 * 4>
	.ENDR
	BSBB	ADP_HANDLER		; Call interrupt service routine
	.BYTE	SLOT			; TR index for this int srv rtn
	.ENDM	ADPISR

;
; EQUATED SYMBOLS:
;
 
	$ACBDEF				; AST control block offsets
	$EMBDEF	<UI>			; Error log message buffer offsets
	$IPLDEF				; Interrupt priority levels
	$PCBDEF				; Process control block offsets
	$PHDDEF				; Process header block offsets
	$PRDEF				; Define processor register numbers
	$PSLDEF				; Define PSL fields

					;***
;DEBUG=1				;***If defined, enable unexpected
					;*** interrupt identifies vector #

;
; OWN STORAGE:
;
	.PAGE
	.SBTTL	MPS$UNEXPINT - Unexpected interrupt routine

;+
; ERL$VEC'VNUM - INTERRUPT SERVICE FOR SCB VECTOR VNUM.
; MPS$UNEXPINT - General unexpected interrupt service routine
;
; These interrupt service routines are executed for unused SCB vectors.
; If DEBUG is defined, each interrupt service calls ERL$UNEXP with
; the <vector offset>/2 into the SCB as a 1 byte argument.  If
; DEBUG is not defined, all interrupt service routines collapse to
; global labels equal to ERL$UNEXP.  There are enought interrupt
; service routines for the architectural page of the SCB, i.e.,
; 128 routines.
;
; INPUTS:
;
;	(SP) = PC at interrupt
;	4(SP) = PSL at interrupt
;
; OUTPUTS:
;
;
;-

	.PSECT	$AEXNONPAGED,LONG

	.ALIGN	LONG

;
; All unused vectors in the SCB point to locations in the following table.
; There is one longword for each longword in the SCB.  The unused vectors
; may sometimes receive interrupts which must be handled in some way.  For
; all cpu-type interrupts, the routine MPS$UNEXPINT is executed.  For all
; adapter interrupts, the routine ADP_HANDLER is executed.  The former
; passes the process currently executing back to the primary or bugchecks,
; depending upon whether or not it is on the interrupt stack.  The latter
; routine creates and error log entry for the unexpected interrupt and REIs.
;
	.ALIGN	LONG
CPU_UNEXP:
	VNUM=000			; First vector = 0
	.REPT	64			; ISRs for cpu interrupts only
	ISRDEF	\VNUM			; Define ERL$VEC'VNUM label and ISR
	BSBW	MPS$UNEXPINT		; Call unexpected interrupt service rtn
	.BYTE	<VNUM>/2		; Identify vector offset into SCB
	VNUM=VNUM+4			; Next vector
	.ENDR
ADP_UNEXP:
	NEXUS = 0			; First adapter = 0
	.REPT	16			; ISR's for 16 adapters only
	ADPISR	\NEXUS			; Define ERL$VEC'VNUM labels and ISRs
	NEXUS = NEXUS + 1		; Next adapter
	.ENDR
;
; Unexpected adapter interrupt handler:
;
; This routine is entered whenever an adapter on the secondary interrupts.
; It logs the unexpected interrupt by creating an error log entry and then
; attempts to clear the interrupt.
;

	.ALIGN	LONG
ADP_HANDLER:
	SUBL	#ADP_UNEXP+2,(SP)	; Compute adapter offset
	DIVL	#2,(SP)			; Compute adapter slot/TR number
	PUSHR	#^M<R0,R1,R2,R3,R4>	; Save registers
	MOVL	5*4(SP),R3		; Retrieve slot number
	MOVL	@MMG$GL_SBICONF[R3],R4	; Get address of adapter registers
	CLRL	4(R4)			; Disable adapter interrupts
	MOVL	(R4),R4			; Get the adapter's configuration reg
10$:
	MOVL	#EMB$C_UI_LENGTH,R1	; Set size of message to allocate
	BSBW	MPS$ALLOCEMB		; Allocate an error log buffer
	BLBC	R0,20$			; Branch if none available
	MOVW	#EMB$C_UI,EMB$W_UI_ENTRY(R2) ; Set message type
	MOVL	R3,EMB$L_UI_TR(R2)	; Set slot/TR number
	MOVL	R4,EMB$L_UI_CSR(R2)	; Set configuration register value
	BSBW	MPS$RELEASEMB		; Release buffer
20$:	POPR	#^M<R0,R1,R2,R3,R4>	; Restore registers
	ADDL	#4,SP			; Remove slot number
	REI				;
;
; The following symbols are defined so that XDELTA can be linked
; into MP.EXE for debugging the secondary processor.
;
EXE$ROPRAND==ERL$VEC24
EXE$ACVIOLAT==ERL$VEC32
MMG$PAGEFAULT==ERL$VEC36
EXE$TBIT==ERL$VEC40
EXE$BREAK==ERL$VEC44

	.PAGE
;+
; FUNCTIONAL DESCRIPTION:
;
; This routine is the unexpected interrupt handler.  It will halt
; if vector identification (DEBUG) is enabled.  On the top of the
; stack will be the <vector offset>/2 into the SCB.
;
; At the time the routine is entered, the stack looks like the following:
;
;		+--------------------------+
;		!    PC of a BSBW in SCB   !
;		+--------------------------+
;		!                          !
;		\         optional         \
;		\        parameters        \
;		!                          !
;		+--------------------------+
;		! PC at time of exception  !
;		+--------------------------+
;		! PSL at time of exception !
;		+--------------------------+
;
; The secondary processor computes the address of the vector for
; this particular interrupt in the primary's SCB.  It places this
; PC and the current PSL on the stack and returns the process to
; the primary processor.  A SVPCTX is done by the secondary, which
; takes the PC-PSL pair and places them in the hardware PCB.  The
; primary will immediately handle the interrupt when it starts
; executing this process.
;
; This code is executed for any exception that happens on the secondary,
; e.g., access violation, pagefault, change mode to kernel, etc.
;
; ENVIRONMENT:
;
;	Executes on the secondary processor.
;
;-

	.ALIGN	LONG
MPS$UNEXPINT::				;
	MOVZBL	@(SP),(SP)		;***Overlay return with vector offset
	MULL	#2,(SP)			;***Convert arg to vector offset
	CMPL	(SP),#^X40		; Is this a CHMK request?
	BNEQ	5$			; Br on no, continue w/unexpected int
	TSTL	4(SP)			; Is this AST exit system service?
	BEQL	AST_EXIT_SSRV		; Br on yes, this is AST exit sys srv
5$:

	.IF	DF,MPPFMSWT
	BSBW	MPS$PFM_UNEXP		; Gather performance statistics
	.ENDC

	ADDL3	G^EXE$GL_SCB,(SP),-(SP)	; Compute proper PC for continue
					;  in primary processor
	BICL3	#3,@(SP),(SP)		; Continuation PC is the SCB vector
					; Location in the primary less int stk bit
	MOVPSL	4(SP)			; Save current PSL
	BBS	#PSL$V_IS,4(SP),10$	; Br if on interrupt stack
	BICL	#PSL$M_IPL,4(SP)	; Force IPL to zero
;
; Now drop the current process and ask the primary processor for another.
;
	BRW	MPS$MPSCHED2		; And get another

10$:	SECBUG_CHECK MPUNEXPINT,FATAL	; Unexpected interrupt or exception
	.PAGE
	.SBTTL AST EXIT SYSTEM SERVICE FOR SECONDARY PROCESSOR
;+
; FUNCTIONAL DESCRIPTION:
;
; This is the AST exit system service routine for the secondary processor
; only.  It clears the AST active bit for the appropriate mode, in the
; process' PCB and then sets a new AST level (both in the PHD and the
; secondary's processor register).  Because an AST may be delivered by
; the primary while the secondary is executing this code, the routine
; is repeated until the new AST level is consistent.
;
;
; INPUTS:
;
;	  (SP) - offset into SCB
;	 4(SP) - CHMK parameter (0 for AST exit system service)
;	 8(SP) - PC at time of interrupt
;	12(SP) - PSL at time of interrupt
;
; ENVIRONMENT:
;
;	Executes on the secondary processor.
;
;-

AST_EXIT_SSRV:
	ADDL2	#8,SP			; Clean offset and param off stack
	EXTZV	#PSL$V_CURMOD,#PSL$S_CURMOD,4(SP),R0 ; Get previous mode
	PUSHL	R4			; Save register
	PUSHL	R3			; Save register (This is faster)
	PUSHL	R2			; Save register (than a PUSHR.)
	MOVL	W^MPS$GL_CURPCB,R4	; Get address of current process' PCB
	MOVZBL	#-1,R3			; Initialize to an invalid AST level
	SETIPL	#IPL$_SYNCH		; Disable system events
	BBCCI	R0,PCB$B_ASTACT(R4),10$	; Clear AST active bit for this mode
10$:	MOVAL	PCB$L_ASTQFL(R4),R0	; Get address of AST queue
	CLRL	R2			; Assume kernel mode
	MOVL	(R0),R1			; Get flink
	CMPL	R0,R1			; Is the queue empty?
	BEQL	30$			; Br on yes, queue is empty
	ASSUME	ACB$V_KAST EQ 7
	TSTB	ACB$B_RMOD(R1)		; Check for kernel AST
	BLSS	20$			; Br if not kernel AST
	BICB3	#^C<3>,ACB$B_RMOD(R1),R2 ; Get request mode
20$:	MOVL	PCB$L_PHD(R4),R0	; Get address of PHD
	MTPR	R2,#PR$_ASTLVL		; Set ASTLVL register
	MOVB	R2,PHD$B_ASTLVL(R0)	; Set ASTLVL in PHD
	CMPB	R2,R3			; Was new AST level consistent?
	BEQL	25$			; Br if was consistent, all done
	MOVB	R2,R3			; Remember new AST level set
	BRB	10$			; Try again
25$:	MOVQ	(SP)+,R2		; Restore register
	POPL	R4			; Restore register
	REI				; Return from interrupt

30$:	MOVL	#4,R2			; Set null AST level
	BRB	20$			; Continue with common code
	.PAGE
	.SBTTL AST DELIVERY INTERRUPT SERVICE
;+
; FUNCTIONAL DESCRIPTION:
;
; This routine is entered from the AST delivery interrupt.  If it
; interrupts some kernel mode execution, then the AST delivery cannot
; be handled now and it merely sets the hardware register ASTLVL so
; that the interrupt is canceled.  If any other mode is interrupted, then 
; the process is folded up and returned to the primary processor.  When
; the primary processor does a LDPCTX and an REI to start executing this
; process, the REI finds that the hardware register ASTLVL differs from
; that in the process header and the AST is delivered.
;
;
; INPUTS:
;
;	 (SP) - PC at time of interrupt
;	4(SP) - PSL at time of interrupt
;
; ENVIRONMENT:
;
;	Executes on the secondary processor.
;
;-

	.ALIGN	LONG			; Longword align interrupt routines
MPS$ASTDEL::
	BITB	#<PSL$M_CURMOD -	; Check if a kernel mode routine
		@<-PSL$V_CURMOD>>, -	;  was interrupted
		4+<PSL$V_CURMOD@-3>(SP)	;
	BNEQ	MPS$UNEXPINT1		; Br if not kernel, go handle AST now
	MTPR	#4,#PR$_ASTLVL		; Don't handle the AST delivery now,
					;  and disable the interrupt
	REI				; Return from interrupt

;
; The following is in the format of the SCB vectors.
;
MPS$UNEXPINT1:
	PUSHL	#<IPL$_ASTDEL @ PSL$V_IPL> ; Return process to primary to do AST
	PUSHAB	G^SCH$ASTDEL		; delivery and execution

	.IF	DF,MPPFMSWT
	BSBW	MPS$PFM_ASTDEL		; Gather performance measurement data
	.ENDC

	BRW	MPS$MPSCHED2		; Go fold up the process & hand it back
	.PAGE
	.SBTTL	MPS$KERSTKNV - KERNEL STACK NOT VALID FAULT
;+
; MPS$KERSTKNV - KERNEL STACK NOT VALID FAULT
;
; FUNCTIONAL DESCRIPTION:
;
; This routine is automatically vectored to when a kernel stack not
; valid is detected during a change mode instruction, during an REI
; instruction, or during an attempt to push exception information on
; the kernel stack.  This exception runs on the interrupt stack.  The
; state of the stack on entry is:
;
;	00(SP) = Exception PC
;	04(SP) = Exception PSL
;
; A fatal kernel stack not valid bugcheck is declared.
;
; ENVIRONMENT:
;
;	Executes on the secondary processor.
;
;-
 
	.ALIGN	LONG
MPS$KERSTKNV::				; Kernel stack not valid fault
	SECBUG_CHECK MPKNLSTKNV,FATAL	; Kernel stack not valid bugcheck

	.PAGE
	.SBTTL	SECONDARY PROCESSOR COMPATIBILITY MODE FAULT HANDLER
;+
; FUNCTIONAL DESCRIPTION:
;
; This routine is automatically vectored to when a compatibility mode
; exception is detected for a process executing on the secondary.  The
; state of the stack on entry is:
;
;	00(SP) = Compatibility exception code
;	04(SP) = Exception PC
;	08(SP) = Exception PSL
;
; Possible compatibility exception codes are:
;
;	0 = Reserved instruction execution
;	1 = BPT instruction execution
;	2 = IOT instruction execution
;	3 = EMT instruction execution
;	4 = Trap instruction execution
;	5 = Illegal instruction execution
;	6 = Odd address fault
;	7 = Tbit trap
;
; The exception name followed by the number of exception arguments are
; pushed on the stack.  Final processing is accomplished in common code.
;
; If there is no compatibility mode handler declared, then the process
; is folded up and handed back to the primary, in such a way that the
; primary will execute in EXCEPTION.
;
; Environment:
;
;	Executed by the secondary processor, in kernel mode, on kernel stack.
;
;-
 
	.ALIGN	LONG
MPS$COMPAT::				; Compatibility mode faults on secondary
	MOVQ	R0,@#CTL$AL_CMCNTX	; Save R0,R1 in compat context region
	MOVAL	@#CTL$AL_CMCNTX+8,R0	; Get addr of compatibility context area
	MOVQ	R2,(R0)+		; Save R2 and R3
	MOVQ	R4,(R0)+		; Save R4 and R5
	MOVL	R6,(R0)+		; Save R6
	MOVL	(SP)+,(R0)+		; Save exception code & clean off stack
	MOVQ	(SP)+,(R0)		; Save PC/PSL and clean off stack
	PUSHL	#<PSL$C_USER@PSL$V_PRVMOD>!<PSL$C_USER@PSL$V_CURMOD> ; Fabricate
					; a PSL for CME
	PUSHL	@#CTL$GL_CMHANDLR	; Compatibility mode handler address
	BEQL	20$			; Branch if none specified
	REI				; Jump to compatibility handler

;
; No compatibility mode handler was declared.  Restore the stack and
; saved register, and return process to primary to continue through
; normal exception code.  R0 now points to the saved PC in the
; compatibility context area.  
;

20$:	MOVQ	(R0),(SP)		; Restore exception PC/PSL
	PUSHL	-(R0)			; Push exception code again
	MOVL	-28(R0),R0		; Restore R0 from context area
	MOVZWL	#SS$_COMPAT,-(SP)	; Set exception name
	PUSHL	#4			; Set number of signal arguments
	EXTZV	#PSL$V_CURMOD,#PSL$S_CURMOD,16(SP),-(SP) ;Create PC/PSL w/prev
	ROTL	#PSL$V_PRVMOD,(SP),(SP)	; mode correct & current mode of kernel
	PUSHAB	G^EXE$EXCEPTION		; Adr where primary will execute
	BRW	MPS$MPSCHED2		; Hand process back to primary

	.END
