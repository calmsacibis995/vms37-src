	.TITLE	BTUTIL - UTILITY ROUTINES FOR BACK TRANSLATOR
	.IDENT	'V03-000'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; H.J.	AUGUST 1977
;
; BACKTRANSLATOR UTILITY ROUTINES
;

;
; MACRO LIBRARY CALLS
;
	$BTDEFS				;DEFINE VALUES TO BE USED LOCALLY

;
; LOCAL SYMBOLS
;

;
; ARGUMENT LIST OFFSET DEFINITIONS
;

;
; LOCAL DATA
;
		IMPURE
;
;
;
ARGSLISTPTR::	.LONG	ARGSLIST	;CURRENT POINTER INTO STRING AREA
ARGSLIST::	.BLKB	120		;AREA TO KEEP ARGS, USUALLY FILESPECS
ARGSENDPTR	==.-1			;HIGH ADDRESS OF LEGEAL AREA

OPTLISTPTR::	.LONG	OPTLIST		;CURRENT POINTER INTO STRING AREA
OPTLIST::	.BLKB	119		;AREA TO KEEP TRANSLATE STRINGS
OPTENDPTR	==.-1			;HIGH ADDRESS OF LEGAL AREA
STRCHAR:	.BYTE			;LOCATION TO KEEP "STRING" FOR ADDCHAR

PARAMLOC::	.BYTE	0		;USED TO CONTAIN FLAG OF WHERE TO PUT
					;RESULT PARAMETERS
LOCATION::	.BYTE	0		;USED TO CONTAIN FLAG OF WHERE TO PUT
					;RESULT OPTIONS
HOLDLOC::	.BYTE	0		;USED TO CONTAIN SAVED FLAG OF WHERE
					;TO PUT RESULT OPTIONS

;	.SBTTL	BUILD TRANSLATE STRING
	PURE
;+
; BACKTRANSLATOR UTILITY ROUTINES
;
; INSERT STRING   ENTER A NEW STRING OF TRANSLATION CHARACTERS
;
; INPUTS:
;   LOCATION::  IDENTIFIES WHERE TEXT IS TO GO IN RESULT STRING
;	R10 ->  RESULT STRING CHARACTERS
;	R9  ->  LENGTH
;
;
; OUTPUTS:
;   USES ROUTINE ADDSTR TO ADD CHARACTERS TO OPTLIST
;
;-
INSERTSTR::
	MOVB	W^LOCATION,@W^OPTLISTPTR;STORE IDENTIFIER FLAG IN STRING
	INCL	W^OPTLISTPTR		;BUMP POINTER
					;FALL INTO ROUTINE ADDSTR

;+
; ADDSTR  ADD TEXT CHARACTERS TO THE LAST IDENTIFIED STRING
;
; INPUTS:
;	R10 ->  RESULT STRING CHARACTERS
;	R9  ->  LENGTH
;
;
; OUTPUTS:
;	OPTLIST CONTAINS ADDITIONAL CHARACTERS
;
;-
ADDSTR::
	MOVL	W^OPTLISTPTR,R3		;GET POINTER TO INSERTION LOCATION
	PUSHAB	W^OPTENDPTR		;CHECK TO MAKE SURE WITHIN BOUNDS
	PUSHAB	(R3)[R9]		;PUSH ADDRESS OF RESULTANT STRING
	CMPL	(SP)+,(SP)+		;ARE WE WITHIN OPTLIST?
	BGEQU	TOOBIG			;BRANCH IF RESULT STRING TOO LARGE
	MOVC3	R9,(R10),(R3)		;PUT IN CHARACTERS

	MOVL	R3,W^OPTLISTPTR		;UPDATE TEXT POINTER
	RSB				;RETURN

TOOBIG:	ERROR	STRINGOVR,FATAL		;ABORT ON ERROR

;+
; ADDCHAR  ADD A SINGLE CHARACTER TO LAST IDENTIFIED STRING
;
; INPUTS:
;	R7 CONTAINS CHARACTER
;
; OUTPUTS:
;   USES ROUTINE ADDSTR TO ADD CHARACTERS TO OPTLIST
;
;-
ADDCHAR::
	PUSHR	#^M<R9,R10>		;SAVE REGS
	MOVAB	W^STRCHAR,R10		;POINT TO DUMMY STRING
	MOVB	R7,(R10)		;STORE THE CHARACTER THERE
	MOVW	#1,R9			;SET UP THE FAKE COUNT
	BSBB	ADDSTR			;INSERT THE CHARACTER
	POPR	#^M<R9,R10>		;RESTORE THE REGS
	RSB				;RETURN

;+
; INSERT ARGUMENT   ENTER A NEW STRING (USUALLY A FILE SPEC)
;		    OF CHARACTERS
;
; INPUTS:
;   PARAMLOC::  IDENTIFIES WHERE TEXT IS TO GO IN RESULT STRING
;	R10 ->  RESULT STRING CHARACTERS
;	R9  ->  LENGTH
;
;
; OUTPUTS:
;	ARGSLIST CONTAINS ADDITIONAL CHARACTERS
;
;-
INSERTARG::
	MOVB	W^PARAMLOC,@W^ARGSLISTPTR ;STORE IDENTIFIER FLAG IN STRING
	INCL	W^ARGSLISTPTR		;BUMP POINTER
	MOVL	W^ARGSLISTPTR,R3	;GET POINTER TO INSERTION LOCATION
	PUSHAB	W^ARGSENDPTR		;CHECK TO MAKE SURE WITHIN BOUNDS
	PUSHAB	(R3)[R9]		;PUSH ADDRESS OF RESULTANT STRING
	CMPL	(SP)+,(SP)+		;ARE WE WITHIN ARGSLIST?
	BGEQU	TOOBIG			;BRANCH IF RESULT STRING TOO LARGE
	MOVC3	R9,(R10),(R3)		;PUT IN CHARACTERS

	MOVL	R3,W^ARGSLISTPTR	;UPDATE TEXT POINTER
	RSB				;RETURN


	.END
