	.TITLE	LINK/RSX - BACKTRANSLATOR FOR LINK/RSX COMMAND
	.IDENT	'V03-000'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; H.J.	DECEMBER 1977
;
; BACKTRANSLATOR LINK/RSX11
;
; MODIFIED BY:
;
;	V001	TMH0001		Tim Halvorsen		04-Feb-1982
;		Remove reference to CLI$K_ symbols at assembly time.
;---

;	.SBTTL INITIALIZE DATA DEFINITIONS
;
; MACRO LIBRARY CALLS
;
	$BTDEFS				;DEFINE VALUES TO BE USED LOCALLY
	$CLIDEF				;DEFINE CLI CODES AND VALUES

;
; LOCAL SYMBOLS
;
	LINK$V_OVERLAY	= 0		;FLAG SET INDICATES /OVERLAY
	LINK$M_OVERLAY	= 1
	LINK$V_OPTIONS	= 1		;FLAG SET INDICATES /OPTIONS:FILESPEC
	LINK$M_OPTIONS	= 2

;
; LOCAL DATA
;
	IMPURE

INPUT_ARGLIST:
	$ARGLST	3,-			;CREATE AN ARGUMENT LIST
		INPUT_DESC,-		;FOR FIRST INPUT PARAMETER
		CLIWORKAREA,-		;WORK AREA FOR CLI
		BITMAP			;BIT ARRAY

INPUT_DESC:
	$CLIREQDESC -			;CREATE A CLI REQUEST DESCRIPTOR
		RQTYPE=CLI$K_INPUT1,-	;FOR PRIMARY INPUT
		RQFLGS=CLI$V_PARMREQ,-	;PRIMARY INPUT IS REQUIRED
		QUALST=INPUT_QUALS	;QUALIFIERS NEEDED ON INPUTS

INPUT_QUALS:
	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_CONC,-;/CONCATENATE
		FLSACT=LINK_NOCONCAT	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_INCL,-;/INCLUDE:(MODULE-LIST)
		TRUACT=LINK_INCLUDE	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_LIBR,-;/LIBRARY
		TRUACT=LINK_LIBRARY	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_SELE,-;/SELECTIVE
		TRUACT=LINK_SELECTIVE	;CALL QUALIFIER HANDLER

	$CLIQUALDESC	END_LIST	;TERMINATE THE QUALIFIER LIST

OUTPUT_ARGLIST:
	$ARGLST	3,-			;CREATE AN ARGUMENT LIST
		OUTPUT_DESC,-		;FOR FIRST OUTPUT PARAMETER
		CLIWORKAREA,-		;WORK AREA FOR CLI
		BITMAP			;BIT ARRAY

OUTPUT_DESC:
	$CLIREQDESC -			;CREATE A CLI REQUEST DESCRIPTOR
		RQTYPE=CLI$K_OUTPUT1	;FOR PRIMARY OUTPUT

OUTPUT2_ARGLIST:
	$ARGLST	3,-			;CREATE AN ARGUMENT LIST
		OUTPUT2_DESC,-		;FOR OPTIONAL SECONDARY OUTPUT
		CLIWORKAREA,-		;WORK AREA FOR CLI
		BITMAP			;BIT ARRAY

OUTPUT2_DESC:
	$CLIREQDESC -			;CREATE A CLI REQUEST DESCRIPTOR
		RQTYPE=CLI$K_OUTPUT2	;FOR SECONDARY OUTPUT

OUTPUT3_ARGLIST:
	$ARGLST	3,-			;CREATE AN ARGUMENT LIST
		OUTPUT3_DESC,-		;FOR OPTIONAL THIRD OUTPUT
		CLIWORKAREA,-		;WORK AREA FOR CLI
		BITMAP			;BIT ARRAY

OUTPUT3_DESC:
	$CLIREQDESC -			;CREATE A CLI REQUEST DESCRIPTOR
		RQTYPE=CLI$K_OUTPUT3	;FOR THIRD OUTPUT

QUAL_ARGLIST:
	$ARGLST	3,-			;CREATE AN ARGUMENT LIST
		QUAL_DESC,-		;FOR COMMAND QUALIFIERS
		CLIWORKAREA,-		;WORK AREA FOR CLI
		BITMAP			;BIT ARRAY

QUAL_DESC:
	$CLIREQDESC -			;CREATE A CLI REQUEST DESCRIPTOR
		RQTYPE=CLI$K_GETQUAL,-	;FOR COMMAND QUALIFIERS
		QUALST=QUAL_LIST	;ADDRESS OF THE QUALIFIERS


QUAL_LIST:
	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_BRIE	;/BRIEF

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_CROS,-;/CROSS_REFERENCE
		TRUACT=LINK_CROSSREF	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_EXIT,-;/EXIT:N
		TRUACT=LINK_EXIT	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_FULL,-;/FULL
		TRUACT=LINK_FULL	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_HEAD,-;/HEADER
		FLSACT=LINK_NOHEADER	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_TKB_,-;/OPTIONS:FILESPEC
		TRUACT=LINK_OPTIONS	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_OVER,-;/OVERLAY
		TRUACT=LINK_OVERLAY	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_POSI,-;/POSITION_INDEPENDENT
		TRUACT=LINK_POSITION	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_POST,-;/POST_MORTEM
		TRUACT=LINK_POST_MORT	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_SEQU,-;/SEQUENTIAL
		TRUACT=LINK_SEQUENTIAL	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_RSX1,-;/RSX11
		TRUACT=LINK_RSX11	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_TRAC,-;/TRACE
		QDFLGS=CLI$V_QDEXPA,-	;QUALIFIER MUST BE EXPLICIT
		TRUACT=LINK_TRACE	;CALL QUALIFIER HANDLER

;	THE FOLLOWING TWO OPTIONS MUST OCCUR AT THE END OF THE LIST TO GET
;	THE FILESPEC PLACEMENT CORECT

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_DEBU,-;/DEBUG[:FILESPEC]
		TRUACT=LINK_DEBUG	;CALL QUALIFIER HANDLER

	$CLIQUALDESC -			;DEFINE A QUALIFIER
		QDCODE=CLI$K_LINK_DEFA,-;/DEFAULT_LIBRARY:FILESPEC
		TRUACT=LINK_DEFAULT	;CALL QUALIFIER HANDLER


	$CLIQUALDESC	END_LIST	;TERMINATE THE QUALIFIER LIST

LINKFLAGS::	.BYTE	0		;LOCAL FLAGS FOR LINK/RSX COMMAND

OPTLINSIZ::	.WORD	0		;SIZE OF OPTIONS INDIRECT FILE SPEC	
OPTLINMAX	==40			;SIZE OF RECORD BUFFER
OPTLINBUF::	.ASCII	\@\		;FILE SPEC STARTS WITH '@'
		.BLKB	OPTLINMAX-1	;ROOM TO STORE RECORD

;	.SBTTL LINK/RSX
;+
; BACKTRANSLATOR LINK/RSX
;
; CONVERTS DCL COMMAND TO COMPATIBILITY MODE IMAGE COMMAND
;
;
; INPUTS:
;  THE INPUTS ARE RETURNED FROM THE DCL RESULT PARSER AS RESPONSES
;  TO THE VARIOUS REQUEST DESCRIPTORS THAT ARE GIVEN TO IT.
;
;
; OUTPUTS:
;  THE OUTPUT CONSISTS OF THE APPROPRIATE TABLES TO CREATE THE
;  COMMAND LINE OF THE TKB INDIRECT FILE TO BE CREATED IN 'LINK2'.
;
;-

	PURE

LINK::
	CALLG	W^OUTPUT_ARGLIST,-	;PROCESS EXECUTE OPTION (.TKB FILE)
		@CLI$A_UTILSERV(AP)
	MOVQ	W^CLI$Q_RQDESC+OUTPUT_DESC,R9 ;PICK UP STRING DESCRIPTOR
	BSBW	INSERTARG		;SAVE IT AS PRIMARY OUTPUT
	DECB	W^PARAMLOC		;SET UP FOR SECONDARY OUTPUT
	CALLG	W^OUTPUT2_ARGLIST,-	;PROCESS MAP OPTION (.MAP FILE)
		@CLI$A_UTILSERV(AP)
	MOVQ	W^CLI$Q_RQDESC+OUTPUT2_DESC,R9 ;PICK UP STRING DESCRIPTOR
	BSBW	INSERTARG		;SAVE IT AS SECONDARY OUTPUT
	$SETSTRING </-SP>,TYPE=INLINE,POS=<PRIM_OUTPUT-1>
					;AVOID SPOOLING OF MAP
	DECB	W^PARAMLOC		;SET UP FOR TERTIARY OUTPUT
	CALLG	W^OUTPUT3_ARGLIST,-	;PROCESS SYMBOLS OPTION (.STB FILE)
		@CLI$A_UTILSERV(AP)
	MOVQ	W^CLI$Q_RQDESC+OUTPUT3_DESC,R9 ;PICK UP STRING DESCRIPTOR
	BSBW	INSERTARG		;SAVE IT AS TERTIARY OUTPUT
	MOVB	#PRIM_INPUT,W^HOLDLOC	;SET UP TO ACCEPT OPTIONS ON FILESPEC
	MOVB	#PRIM_INPUT,W^PARAMLOC	;SET UP TO START ACCEPTING INPUTS
10$:	CALLG	W^INPUT_ARGLIST,-	;PROCESS PRIMARY INPUT
		@CLI$A_UTILSERV(AP)
	MOVQ	W^CLI$Q_RQDESC+INPUT_DESC,R9 ;PICK UP STRING DESCRIPTOR
	BSBW	INSERTARG		;SAVE IT AS PRIMARY INPUT
	DECB	W^PARAMLOC		;PUT NEXT INPUT IN NEXT POSITION
	DECB	W^HOLDLOC		;PUT NEXT OPTION IN NEXT POSITION
	BBS	#CLI$V_CONCATINP,-	;BRANCH IF MORE INPUTS TO GET THEM
		W^CLI$B_RQSTAT+INPUT_DESC,10$
	CLRB	W^HOLDLOC		;PUT DEFAULT QUALIFIERS ON .TSK FILE
	CALLG	W^QUAL_ARGLIST,-	;PROCESS COMMAND QUALIFIER OPTIONS
		@CLI$A_UTILSERV(AP)

	BRW	LINK2			;BRANCH TO REST OF LINK PROCESSING


LINK_CROSSREF:
	$SETSTRING	</CR>,POS=<PRIM_OUTPUT-1> ;INCLUDE CROSS REFERENCE

LINK_DEBUG:
	$INLINE				;THIS OPTION MUST BE CHECKED TO
					;DETERMINE IF IT GOES ON INPUT OR .TKB
	BSBW	GETSUBVAL		;IS IT /DEBUG OR /DEBUG:FILESPEC?
	BLBC	R0,10$			;BRANCH IF /DEBUG
	MOVB	W^PARAMLOC,W^HOLDLOC	;FORCE /DA TO OCCUR ON THIS INPUT
	MOVQ	W^CLI$Q_RQDESC+VALUE_DESC,R9 ;GET DESCRIPTOR FOR FILESPEC
	BSBW	INSERTARG		;PUT IT INTO THE INPUT LIST
	DECB	W^PARAMLOC		;SET UP FOR NEXT POSSIBLE INPUT
10$:	$SETSTRING	</DA>,TYPE=INLINE ;PUT /DA OPTION ON CORRECT FILESPEC
	RET				;RETURN TO CLI

LINK_DEFAULT:
	$INLINE				;INLINE PROCESSING TO PUT /DL ON INPUT
	BSBW	GETSUBVAL		;GET FILESPEC, (CLI HAS 'VALREQ' SET)
	MOVB	W^PARAMLOC,W^HOLDLOC	;FORCE THIS OPTION TO GO ON THIS INPUT
	MOVQ	W^CLI$Q_RQDESC+VALUE_DESC,R9 ;GET DESCRIPTOR FOR FILESPEC
	BSBW	INSERTARG		;PUT IT INTO THE INPUT LIST
	DECB	W^PARAMLOC		;SET UP FOR NEXT POSSIBLE INPUT
	$SETSTRING	</DL>,TYPE=INLINE ;PUT /DL OPTION ON FILESPEC
	RET				;RETURN TO CLI


LINK_EXIT:
	$GETDECNUM	</XT>		;SET TKB EXIT OPTION

LINK_FULL:
	$SETSTRING	</-SH>,POS=<PRIM_OUTPUT-1> ;SET TO CAUSE FULL MAP

LINK_INCLUDE:
	$GETSTRING	</LB:>,FLAGS=<BTR$M_LIST,BTR$M_VALREQ> ;ACCEPT INCLUDE

LINK_LIBRARY:
	$SETSTRING	</LB>		;SET THE LIBRARY IDENTIFIER OPTION

LINK_NOCONCAT:
	$SETSTRING	</-CC>,FLAGS=<BTR$M_EXPLICIT> ;SET UP NOCONCAT

LINK_NOHEADER:
	$SETSTRING	</-HD>,FLAGS=<BTR$M_RETURN,BTR$M_EXPLICIT>
					;SUPPRESS HEADER FROM .TSK
	$SETSTRING	</-HD>,POS=<PRIM_OUTPUT-2>,TYPE=INLINE
					;AND FROM .STB FILE
	RET				;RETURN TO CLI

LINK_OPTIONS:
	$INLINE				;THIS REQUIRES SPECIAL CODE
	BSBW	GETSUBVAL		;GET THE VALUE, (CLI HAS 'VALREQ' SET)
	MOVQ	W^CLI$Q_RQDESC+VALUE_DESC,R9 ;GET DESCRIPTOR FOR FILESPEC
	CMPW	#OPTLINMAX,R9		;WILL FILE NAME FIT IN TEMP BUFFER?
	BLEQ	10$			;BRANCH IF TOO LONG (HANDLES '@')
	MOVC3	R9,(R10),W^OPTLINBUF+1	;PUT NAME AFTER '@'
	INCW	R9			;ADJUST RECORD SIZE
	MOVW	R9,W^OPTLINSIZ		;SAVE IT FOR LATER
	BISB	#LINK$M_OPTIONS,W^LINKFLAGS ;SET FLAG FOR LATER
	RET				;RETURN TO CLI

10$:	ERROR	OPTVALOVR,FATAL		;REPORT THE ERROR AND ABORT


LINK_OVERLAY:
	$SETSTRING	</MP>,POS=<PRIM_INPUT>,FLAGS=<BTR$M_RETURN>
	BISB		#LINK$M_OVERLAY,W^LINKFLAGS ;INDICATE /OVERLAY OPTION
	RET				;RETURN TO CLI

LINK_POSITION:
	$SETSTRING	</PI>,FLAGS=<BTR$M_RETURN> ;INDICATE .TSK IS PIC
	$SETSTRING	</PI>,POS=<PRIM_OUTPUT-2>,TYPE=INLINE ;AND SO IS .STB
	RET				;RETURN TO CLI

LINK_POST_MORT:
	$SETSTRING	</PM>		;SET POST MORTEM DUMP OPTION

LINK_SELECTIVE:
	$SETSTRING	</SS>		;SET SELECTIVE SEARCH OPTION

LINK_SEQUENTIAL:
	$SETSTRING	</SQ>		;SET SEQUENTIAL PSECT ALLOCATE

LINK_RSX11:
	$SETSTRING	<>,TKB,FLAGS=<BTR$M_FORCE> ;INVOKE RSX TKB

LINK_TRACE:
	$SETSTRING	</TR>		;SET TRACE OPTION


	.END
