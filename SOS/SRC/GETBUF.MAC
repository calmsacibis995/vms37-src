	.TITLE	GETBUF - ALLOCATE TEMP LINE BUFFER
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;	01-AUG-77	BILL MARSHALL
;
	.MCALL	CCML$


;	GETBUF(SPACE) - CALLED TO ALLOCATE A TEMPORARY BUFFER
;		OF INDICATED SIZE, TO BE USED FOR AT MOST UNTIL
;		THE NEXT COMMAND IS TYPED.
;		BUFFER SPACE IS ALLOCATED OUT OF THE FSR REGION,
;		AND THE COMMAND FILE IS CLOSED IF NECESSARY.
;		
;		A CORRESPONDING CALL MUST BE MADE TO RLSBUF() BEFORE
;		THE NEXT COMMAND IS READ.

	CODE$	GTBF

GETBUF::JSR	R1,$SAV2		; SAVE REGISTERS WE CHANGE
	MOV	10(SP),R1		; AMOUNT OF SPACE NEEDED
	CMP	R1,BUFPTR+2		; TEST AMOUNT ALREADY HAVE
	BLE	20$			; HAVE ENOUGH ALREADY
	TST	BUFPTR+2		; HAVE WE ANY?
	BEQ	5$			; NO
	JSR	PC,RLSBUF		; RELEASE ANY IF WE HAVE ONE NOW
5$:	MOV	@#.FSRPT,R0		; BUFFER HEADER
	JSR	PC,$RQCB		; REQUEST CORE BLOCK
	BCC	15$			;WE GOT IT!!
	CCML$	#GCMLB			; CLOSE COMMAND FILE
	MOV	@#.FSRPT,R0		;NOW TRY AGAIN
	JSR	PC,$RQCB
	BCC	15$			; GOT IT NOW
	MOV	#2,-(SP)		; ELSE ERROR - ABORT COMMAND
	CLR	-(SP)			; INTERNAL ERROR
	JSR	PC,SOSERR		; CALL ERROR HANDLER
15$:	MOV	R0,BUFPTR		; SAVE POINTER TO SPACE
	MOV	R1,BUFPTR+2		; AND LENGTH OF IT
20$:	RTS	PC			; AND RETURN TO CALLER



;	RLSBUF() - CALLED TO RELEASE THE ABOVE ALLOCATED BUFFER

RLSBUF::JSR	R1,$SAV2		; NEED R1 AND R2
	MOV	BUFPTR,R2		; ADDRESS OF AREA
	BEQ	10$			; NONE
	MOV	BUFPTR+2,R1		; LENGTH OF AREA
	MOV	@#.FSRPT,R0		; FREE CHAIN HEAD
	JSR	PC,$RLCB		; RELEASE CORE BLOCK
10$:	CLR	BUFPTR			; CLEAR POINTER TO BUFFER
	CLR	BUFPTR+2		; AND LENGTH
	RTS	PC			; RETURN

	.END
