	.TITLE	SOSPRS
	.IDENT	/V03000/
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; PETER H. LIPMAN	24-FEB-76
;
;	06.11	10-APR-79	PETER H. LIPMAN
;
;	ADD ENTRY POINT PARSE2
;
;	06.12	1-SEP-79	PETER H. LIPMAN
;
;	DEFAULT DEVICE IS SY0 FOR PARSE AND PARSE2 ENTRY POINTS
;
	.MCALL	NMBLK$,FDOF$L,FCSBT$
	FDOF$L
	FCSBT$

	DATA$	PRS

DFNAM:	NMBLK$

	.IF	DF,R$$STS
SOSTMP:	.WORD	0,0,0,0		;NULL DEVICE & DIRECTORY
	.WORD	LSOS0NN,SOS0NN	;NAME OF TEMP FILE

SOS0NN:	.ASCII	/SOS0NN.XXX/		;TEMP FILE NAME
LSOS0NN	=	.-SOS0NN		;ITS LENGTH
	.ENDC

	CODE$	PRS
;
; R0=FDB ADDRESS
; R2=ADDRESS OF 6 WORD STRING DESCRIPTOR
; PARSE THE FILE NAME
; OUTPUT: R1, R3 ALTERED, C=0 IF OK, C=1 IF ERROR
;
PARSE::	MOV	#DFNAM,R3		;DEFAULT NAME BLOCK
PARSE2::MOV	#"SY,N.DVNM(R3)
	CLR	N.UNIT(R3)
;
; ENTER HERE WITH R3 = DESIRED DEFAULT NAME BLOCK
;
PARSE1::MOV	R0,R1			;FORM R1 = FNB ADDRESS
	ADD	#F.FNB,R1

	.IF	NDF,R$$STS
	JSR	PC,DETACH		;IN CASE TI: IS INVOLVED

	.IFTF
	JSR	PC,.PARSE

	.IFT
	ROR	-(SP)			;SAVE C BIT
	JSR	PC,ATTACH		;REATTACH
	ROL	(SP)+			;AND RESTORE C
	.ENDC

	RTS	PC

;
; SET UP FILE NAME BLOCK OF SPECIFIED FDB
;
; 2(SP) = FILE TYPE IN RAD50
; 4(SP) = FDB ADDRESS
;
ENTRY	SETFIL

	.IF	DF,R$$STS
	JSR	R1,$SAV3		;SAVE 3 REGS
	MOV	14(SP),R0		;GET FDB ADDRESS
	MOV	#SOSTMP,R2		;FILE NAME BLOCK
	MOV	SOSTNM,SOS0NN+4		;STORE JOB NUMBER
	JSR	PC,PARSE		;PARSE THE FILENAME
	MOV	12(SP),N.FTYP(R1)	;SET DIFFERENT EXTENSION
	RTS	PC			;RETURN

	.IFF
	MOV	R1,-(SP)		;SAVE A REGISTER
	MOV	6(SP),R0		;FDB ADDRESS
	ADD	#F.FNB+S.FNB,R0		;ADDRESS OF LAST + 1 WORD OF FNB
	MOV	#S.FNBW,R1		;SIZE OF FILE NAME BLOCK (WORDS)
;
; ZERO THE FILE NAME BLOCK PORTION OF THE FDB
; LEAVE R0 POINTING AT THE FRONT OF THE FNB
;
10$:	CLR	-(R0)			;ZERO EACH WORD
	SOB	R1,10$			;OF THE FILE NAME BLOCK
	MOV	#SOSTNM,R1		;ADDRESS OF DATA TO FILL IN
	MOV	#^RSOS,N.FNAM(R0)	;FILL IN TEMP FILE NAME
	MOV	(R1)+,N.FNAM+2(R0)	;FROM SAVED RAD50
	CLR	N.FNAM+4(R0)		;TERMINAL NUMBER
	MOV	4(SP),N.FTYP(R0)	;SET SPECIFIED FILE TYPE
	MOV	#NB.DIR,N.STAT(R0)	;SET THIS BIT SO THAT PRTFIL WILL
					;PRINT TEMP FILE DIRECTORIES CORRECTLY
	MOV	(R1)+,N.DID(R0)		;SET DEFAULT DIRECTORY ID
	MOV	(R1)+,N.DID+2(R0)
	MOV	(R1)+,N.DID+4(R0)
	MOV	(R1)+,N.DVNM(R0)	;SET DEFAULT DEVICE
	MOV	(R1)+,N.UNIT(R0)	;AND UNIT
	MOV	(SP)+,R1		;RESTORE SAVED REGISTER
	RTS	PC			;AND RETURN

	.ENDC


	.END
