MODULE DOCAT (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!	020705	PHL34845	Peter H. Lipman		30-Jan-1981
!
!		The LISTEN request for the "new file name already
!	exists, delete? Y or N:" must inhibit <^C> and treat it 
!	like a NO answer.  Must not allow the command to abort
!	here.
!

!
!	DOCAT
!
!	THIS ROUTINE WILL RENAME THE SPECIFIED FILE (IN) TO THE
!	NEW NAME GIVEN IN THE FILE SPEC 'NEW'.  NEW IS A FILENAME
!	JUST ENTERED BY THE USER, AND CHECKS ARE MAKE TO INSURE
!	THE FILE DOES NOT EXIST ALREADY (IF SO, SHOULD IT BE DELETED?)
!	AND THAT THE FILE IS VALID.
!
!	PARAMETERS -
!		IN - ADDRESS OF INPUT FILE BLOCK
!		NEW - ADDRESS OF OUTPUT BLOCK (NEW NAME)
!
!	RETURNS -
!		NONE
!
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	CAT,		! TO ATTEMPT RENAME OPERATION
	PRINT,		! PRINT ERROR MESSAGE
	PRINTC,		! PRINT ERROR MESSAGE, END WITH <CR><LF>
	PRINTN,		! PRINT NUMBER
	PRTFIL,		! PRINT FILE SPECIFIER
	LISTEN,		! READ RESPONSE
	TYPE,		! TYPE SINGLE CHAR
	TYPECR,		! TYPE <CR><LF>
	ERASE,		! DELETE FILE IF REQUESTED
	NOCTLO,		! RESET CONTROL O EFFECTS
	SCANFILE;	! PARSE FILE NAME GIVEN



GBLNVROUTINE (DOCAT(IN,NEW))=
BEGIN
LOCAL
    I,
    CHR,
    J;
DO	BEGIN
	I=CAT(.IN,.NEW);
	IF .I EQL 0 THEN EXITLOOP;
	IF .I GTR 0 THEN
		DO BEGIN
		PRINT (PLIT ('New filename already exists: '));
		PRTFIL(.NEW,0);
		PRINT (PLIT ('Delete? Y or N:'));
		CHR=LISTEN(%O'20');	!NO ABORT ON <^C>
		IF .CHR NEQ 'C'-64 THEN
			BEGIN
			TYPE(.CHR);
			CHR = .CHR AND NOT %O'040';
			TYPECR();
			END;
		IF .CHR EQL 'Y' THEN
			BEGIN
			ERASE(.NEW);
			I = 0;
			EXITLOOP;
			END;
		IF .CHR EQL 'N' OR .CHR EQL 'C'-64 THEN EXITLOOP;
		END WHILE 1;
	IF .I LSS 0 THEN
		BEGIN
		PRINT (PLIT ('Bad device or directory: '));
		PRTFIL (.NEW,0);
		END;
	IF .I NEQ 0 THEN
		DO	BEGIN
			PRINT(PLIT ('Enter new file specification: '));
			SCSTRT=CMDBUF;
			INCR J FROM 0 TO 40 DO
				BEGIN
				CMDBUF[.J]=CHR=LISTEN(17);
				IF .CHR EQL %O'15' OR .CHR EQL 'C'-64 THEN EXITLOOP;
				END;
			IF .CHR EQL 'C'-64 THEN
				BEGIN
				NOCTLO();
				PRINTC(PLIT ('New file specification absolutely required.'));
				END
			ELSE
			IF SCANFILE(.NEW) EQL 0 THEN
				EXITLOOP
			ELSE
				PRINTC(PLIT ('Illegal filename syntax.'));
			END WHILE 1;
	END WHILE 1;
END;


END ELUDOM
