MODULE CMDP (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!

!	06	31-JUL-78	PETER H. LIPMAN
!
!	DEFAULT PRINT RANGE FROM ".+1" IF "." WAS JUST PRINTED.
!
!	07	6-SEP-78	PETER H. LIPMAN
!
!	FIX "*P<CR>" TO REPORT "NO SUCH LINE" IF ".+1" IS THE
!		FIRST LINE TO BE PRINTED AND "." IS THE LAST
!		LINE IN THE FILE.
!	IF "*P<CR>" AND IF "." WAS JUST PRINTED, OVER PRINT
!		THE COMMAND LINE IF TERMINAL IS A SCOPE.
!

!
!	CMDP - PROCESSOR FOR THE P COMMAND
!
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM
!


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	RDCMD,		! READ IN ENTIRE COMMAND
	GETDOT,		! FIND CURRENT POSITION
	GETRNG,		! PARSE A RANGE SPEC
	ILLCMD,		! BAD COMMAND ERROR
	ILLNSL,		! NO SUCH LINE ERROR
	FINDR,		! FIND LINE IN RANGE
	PRTLIN,		! PRINT A LINE ON KB:
	NXTLINE,	! ADVANCE TONEXT LINE IN BUF
	PSTRNG,		! DONE TEST
	SCAN,		! COMMAND SCANNER
	SETDOT,		! SET CURRENT LOCATION
	OVRTYP,		! OVER TYPE COMMAND LINE IF IS SCOPE.
	CMDSQN,		! COMMAND SEQUENCE NUMBER FOR THIS COMMAND
	PRTSQN;		! LAST PRINT COMMAND SEQUENCE NUMBER


GBLNVROUTINE (CMDP)=
	BEGIN	! P COMMAND - PRINT SOME LINES ON TERMINAL
	LOCAL
	    ADR,
	    ADR2;
	LOCAL
	    PROPT,
	    SKIP;
	SKIP=0;
	RDCMD();
	GETRNG(RANGE);
	IF .RANGE[4] EQL 0 THEN
		BEGIN
		RANGE[2]=-2;
		RANGE[3]=.SOSFLG[PLINES];
		IF .PRTSQN+1 EQL .CMDSQN THEN
			BEGIN		! LAST COMMAND WAS A PRINT COMMAND
			RANGE[3]=.RANGE[3]+1;
			SKIP=.SKIP+1;	! DON'T PRINT THE CURRENT LINE
			END;
		END;
	PROPT=15;
	IF .SCSYM EQL ', ' THEN
		BEGIN
		SCAN();
		IF .SCSYM EQL 'S ' THEN
			PROPT=12;
		SCAN();
		END;
	IF .SCTYP NEQ SCEOF THEN BADCOMMAND;
	ADR2=FINDR(RANGE);
	DO	BEGIN
		ADR=.ADR2;
		IF .SKIP EQL 0 THEN PRTLIN(.ADR,.PROPT);
		ADR2=NXTLINE(.ADR,1);
		IF .SKIP GTR 0 THEN
			BEGIN
			SKIP=0;
			IF .LINLEN(.ADR2) EQL 0 THEN NOSUCHLINE;
			OVRTYP();
			END;
		END  WHILE NOT PSTRNG(.ADR2,RANGE);
	SETDOT(.ADR);
	PRTSQN=.CMDSQN;
	END;


END ELUDOM
