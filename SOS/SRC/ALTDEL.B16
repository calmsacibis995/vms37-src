MODULE ALTDEL (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!
!	05	4-MAR-78	PETER H. LIPMAN
!
!	DELETED UNUSED REFERENCE TO NEWLIN
!
!	06.07	6-SEP-78	PETER H. LIPMAN
!
!	ADD SCOPE SUPPORT TO ALTER MODE
!
!	06.12	7-MAY-79	PETER H. LIPMAN
!
!	SUPPORT NORMAL INSERT MODE VIA ALTER INSERT MODE
!		BIT 1 OF PTSW IF SET MEANS USE SINGLE BACKSLASH
!
!	WHEN THE SHIFTING BACKWARDS AND THE BEGINNING OF THE LINE IS
!		ENCOUNTERED, CALL ALTBOL TO CLEAN UP A POSSIBLE \...\
!


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	ALTBOL,			! BACKED UP TO BEGINNING OF LINE
	ALTTYPEC;

EXTERNAL
    NCHR: M1TO1;
EXTERNAL
    OLINPT: REF VECTOR[,BYTE];
EXTERNAL
    NSLSH;
EXTERNAL
    TTSCP: BYTE;			! GTR 0 IF TERMINAL IS A SCOPE

GBLNVROUTINE (ALTDEL(N,PTSW))=
BEGIN
!
! PTSW (PRINT SWITCH) BIT DEFINITIONS:
!	BIT 0 - SET IF SUPPOSED TO PRINT (OR ERASE) THE CHARACTER
!		BEING DELETED
!	BIT 1 - SET IF SUPPOSED TO ECHO DELETED CHARACTERS BETWEEN
!		A SINGLE PAIR OF BACKSLASHES RATHER THAN A DOUBLE
!		PAIR OF BACKSLASHES.  IF ERASING CHARACTERS, THIS
!		HAS NO EFFECT.
!	BIT 2 - SET IF EXPERT MODE IS NOT SUPPOSED TO SHUT
!		OFF DELETE ECHOING FOR BACKWARDS DELETES.
!
LOCAL
    POS: REF VECTOR[,BYTE];
LOCAL
    I;
I=1;
IF .N NEQ 0 THEN
	BEGIN
	IF .N LSS 0 THEN I=-.I;
	IF (.SOSFLG AND EXPERT) NEQ 0 AND (.PTSW AND 4) EQL 0 THEN
		IF (.TTSCP EQL 0) OR (.I GTR 0) THEN
			PTSW=.PTSW AND NOT 1;
	DO	BEGIN
		POS=.NCHR[.I];
		IF .POS[0] EQL 0 THEN
			BEGIN
			ALTBOL(.I);	! BUMPED INTO BEGINNING OF LINE
			EXITLOOP;
			END;
		NCHR[.I]=.NCHR[.I]+.I;
		IF .PTSW THEN
			ALTTYPEC(.POS[0],
				(IF (.PTSW AND 2) NEQ 0 THEN .I ELSE 2*.I));
		END WHILE (N=.N-.I) NEQ 0;
	END;
END;

END ELUDOM
