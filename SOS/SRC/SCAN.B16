MODULE SCAN (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!

!
!	04	6-SEP-77	PETER H. LIPMAN
!
!		LAST + 1 BYTE OF SCSYM BUFFER WAS BEING WRITTEN
!	FOR AN ALPHABETIC SYMBOL WHICH WAS TOO LONG.
!
!	05	27-MAR-78	PETER H. LIPMAN
!
!		NUMERIC VALUES WERE BEING TAKE MODULO 65536.
!	ALSO THEY WERE ALLOWED TO BE THE SPECIAL VALUES 65535 (*)
!	AND 65534 (! INDICATOR).  BOUND ALL CASES TO 65533.
!

!
!	SCAN
!
!	THIS ROUTINE WILL SCAN AN INPUT CORE AREA (POINTED
!	TO BY SCSTRT) AND RETURN THE NEXT SYMBOL FOUND.
!	SCSTRT IS UPDATED SO THAT SUCCESSIVE CALLS ON
!	SCAN WILL GET CONSECUTIVE SYMBOLS.
!	SYMBOLS ARE DIVIDED INTO 3 CLASSES -
!		1 - ALPHABETIC SYMBOLS
!		2 - NUMERIC VALUES
!		3 - SPECIAL CHARACTERS
!
!	SPECIAL CHARACTERS ARE ONLY A SINGLE CHARACTER LONG,
!	NUMERIC AND ALPHA CAN BE ANY LENGTH.
!	FOR TYPES 1 AND 3, THE CHARACTER(S) ARE RETURNED IN
!	SCSYM.  FOR TYPE 2, THE ACTUAL DECIMAL VALUE IS
!	RETURNED IN SCSYM.
!	WHEN AN END OF LINE SYMBOL IS DETECTED, SCAN
!	WILL STOP AND RETURN VALUES OF ZERO UNTIL SCSTRT IS
!	SET TO POINT TO A NEW STRING.
!
!	PARAMETERS -
!		NO FORMAL PARAMETERS
!
!	RETURNS -
!		TYPE VALUE 0-3 OF NEXT SYMBOL
!
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM


REQUIRE 'SRC$:SOSREQ.B16';


GBLROUTINE (SCAN)=
BEGIN
LOCAL
    ADR: REF VECTOR[,BYTE];
LOCAL
    CHAR,
    VAL,
    I;
ADR=.SCSTRT;
CHAR=.ADR[0];
WHILE .CHAR EQL ' ' DO
	BEGIN
	ADR=.ADR+1;
	CHAR=.ADR[0];
	END;
SCSYM[0]=.CHAR;
	IF .CHAR EQL 0 OR .CHAR EQL %CHAR(13) THEN
	BEGIN
	SCTYP=SCEOF;
	RETURN SCEOF;
	END;
IF (.CHAR GEQ 'A' AND .CHAR LEQ 'Z') OR
	(.CHAR GEQ 'A'+%O'40' AND .CHAR LEQ 'Z'+%O'40') THEN
	SCTYP=SCALPH
ELSE
IF (.CHAR GEQ '0' AND .CHAR LEQ '9') THEN
	BEGIN
	SCTYP=SCNUM;
	VAL=.CHAR-'0';
	END
ELSE
	BEGIN
	SCTYP=SCSEP;
	SCSTRT=.ADR+1;
	SCSYM[1]=' ';
	SCSYML=1;
	RETURN SCSEP;
	END;
I=0;
DO	BEGIN
	LOCAL
	    TVAL;
	I=.I+1;
	ADR=.ADR+1;
	CHAR=.ADR[0];
	CASE .SCTYP-1 FROM 0 TO 2 OF
	    SET
		    !   1 - ALPHABETIC
		[0]:
		    IF (.CHAR LSS 'A' OR .CHAR GTR 'Z') AND
			    (.CHAR LSS '0' OR .CHAR GTR '9') AND
			    (.CHAR LSS 'A'+%O'40' OR .CHAR GTR 'Z'+%O'40')
			    THEN EXITLOOP;
		    !   2 - NUMERIC
		[1]:
		    IF (.CHAR LSS '0' OR .CHAR GTR '9') THEN EXITLOOP
		    ELSE IF .VAL GTRU 6564 THEN VAL=INF-2
		    ELSE
			    BEGIN
			    TVAL=.VAL*10+.CHAR-'0';
			    IF .TVAL LSSU .VAL OR .TVAL GTRU INF-2
				    THEN VAL=INF-2 ELSE VAL=.TVAL;
			    END;
		[2]:
	    TES;
	SCSYM[.I]=.CHAR;
	END WHILE .I LSS 18;
SCSYM[.I]=' ';
SCSYM[.I+1]=0;
SCSYML=(.I+2)/2;
IF .SCTYP EQL SCNUM THEN
	SCSYM=.VAL;
SCSTRT=.ADR;
RETURN .SCTYP;
END;


END ELUDOM
