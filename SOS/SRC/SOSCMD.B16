MODULE SOSCMD (
	IDENT = 'V03-001'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!	V03-001	PHL42050	Peter H. Lipman		8-Mar-1982
!		Combine CMDA and CMDX code for consistency.  CMDA
!	was incrementing the /SAVE counter, but CMDX was not.
!
!	020704	PHL0704		Peter H. Lipman		11-Oct-1980
!
!			Fix undeclared external reference
!

!
!	SOSCMD - THE MAIN SOS EDITOR
!
!	THIS ROUTINE READS THE NEXT COMMAND, AND EXECUTES IT.
!	IT IS DIVIDED INTO MANY SMALL PHASES THAT EXECUTE EACH
!	INDIVIDUAL COMMAND, CONTROLLED VIA A SELECT COMMAND.
!
!	PARAMETERS -
!		COPMOD	= 1 IF COPY MODE
!			= 0 IF NORMAL PROMPT WITH *
!			= -1 IF SUPPRESS PROMPT (NOT COPY MODE)
!
!	RETURNS -
!		VALUE RETURNED IS -1 TO EXIT, 0 OTHERWISE
!
!		
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM
!


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	PRINT,		! PRINT A MESSAGE
	TYPE,		! TYPE A CHARACTER
	LISTEN,		! READ A CHAR
	SAVE,		! FOR AUTO-SAVE
	CMDSQN,		! COMMAND SEQUENCE NUMBER
	COPTRN,		! COPY AND TRANSFER COMMANDS
	CMDA,DELREP,CMDE,CMDF,CMDH,CMDI,CMDJ,CMDK,CMDL,CMDM,CMDN,
	CMDP,CMDR,CMDS,CMDW,CMD033,CMD012,SWITCH,CMD056,GIVE,
	CMDERR;

GLOBAL
    CMD;
GLOBAL BIND  AUTSAV=PLIT (%STRING('Doing AUTO-SAVE.  Please wait.',%CHAR(13),%CHAR(10)));
BIND ATOZ=UPLIT(
	CMDA,	!	ALTER COMMAND
	CMDERR,	!	B - 
	COPTRN,	!	C - COPY COMMAND
	DELREP,	!	DELETE COMMAND
	CMDE,	!	EXIT COMMAND
	CMDF,	!	FIND COMMAND
	CMDERR,	!	G -
	CMDH,	!	HELP COMMAND
	CMDI,	!	INSERT COMMAND
	CMDJ,	!	JOIN COMMAND
	CMDK,	!	KLEAR COMMAND
	CMDL,	!	LIST COMMAND
	CMDM,	!	MARK COMMAND
	CMDN,	!	NUMBER COMMAND
	CMDERR,	!	O -
	CMDP,	!	PRINT COMMAND
	CMDERR,	!	Q -
	CMDR,	!	REPLACE COMMAND
	CMDS,	!	SUBSTITUTE COMMAND
	COPTRN,	!	T - TRANSFER COMMAND
	CMDERR,	!	U -
	CMDERR,	!	V -
	CMDW,	!	WORLD COMMAND
	CMDA,	!	EXTEND COMMAND
	CMDERR,	!	Y -
	CMDERR	!	Z -
	):VECTOR[,WORD];

GBLROUTINE (SOSCM1(COPMD1))=
BEGIN
LOCAL
    COPMOD;

IF (COPMOD=.COPMD1) GEQ 0 THEN
	BEGIN
	IF .COPMOD GTR 0 THEN TYPE ('C')
		ELSE IF READONLY THEN TYPE ('R');
	TYPE ('*');
	END
ELSE COPMOD=0;
UNTIL (CMD=LISTEN(7)) NEQ ' ' DO;
CMDSQN=.CMDSQN+1;
IF .CMD GEQ 'A' AND .CMD LEQ 'Z' THEN
	(.ATOZ[.CMD-'A'])(.COPMOD,0)	!  CALL THE ROUTINE
ELSE IF .CMD EQL %O'033' THEN CMD033()
ELSE IF .CMD EQL %CHAR(10) THEN CMD012()
ELSE IF .CMD EQL '_' OR .CMD EQL '/' THEN SWITCH()
ELSE IF .CMD EQL '.' THEN CMD056()
ELSE IF .CMD EQL '=' THEN GIVE()
ELSE IF .CMD NEQ %CHAR(13) THEN CMDERR();

IF .CMD EQL 'E' THEN RETURN -1;

IF .SOSFLG[SAVEN] GTR 0 AND .SOSFLG[SAVEN] LEQ .CMDCNT THEN
	BEGIN
	PRINT(AUTSAV);
	SAVE(0,0,0,0,0,1);
	CMDCNT=CMDICNT=0;
	END;

RETURN 0;
END;


END ELUDOM
