MODULE CMDL (
	IDENT = 'V03-001'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!	V03-001	PHL35743	Peter H. Lipman		2-Mar-1982
!		Add end buffer address parameter to NAMFIL arg list
!

!
!	CMDL - LIST ON LPT
!
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM
!


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	RDCMD,		! READ IN COMMAND
	GETRNG,		! PARSE RANGE
	SOSERR,		! ERROR HANDLER
	ILLCMD,		! ILLEGAL - BAD COMMAND
	CMDABO,		! COMMAND ABORT
	FINDR,		! LOCATE LINE IN RANGE
	OPNLST,		! OPEN
	CNVDEC,		! DECIMAL PRINTER
	PUTLST,		! WRITE TO LPT
	SETFIL,		! GENERATE TEMP NAME
	SETDOT,		! SET CURRENT LOCATION
	NXTLINE,	! BUMP BUFFER POINTER
	PSTRNG,		! TELL WHEN DONE
	SCAN,		! SCANNER SERVICE ROUTINE
	SCANFILE,	! FILE SYNTAX CHECKER
	CLOLST,		! FINISH WITH LIST FILE
	NAMFIL,		! CONVERT FILE NAME IN FDB TO STRING
	GDATIM;		! GET DATE AND TIME TO STRING


EXTERNAL
    TTOCTL,				! POINTER TO TERMINAL OUTPUT BUFFER, USE FOR TITLE BUFFER
    FILUSE,				! FILE STATUS WORD
    FILEIN,				! INPUT FILE FDB
    FILEOUT,				! OUTPUT FILE FDB
    FILELST,				! FDB FOR PRINTER
    LSTX2;				! LIST FILE FDB INDEX*256 + SEVERITY CODE 2

GLOBAL BIND  LSTEXT=UPLIT (%RAD50_11 'LST');

GBLNVROUTINE (CMDL)=
	BEGIN		! LIST COMMAND - PRINT ON LPT
	LOCAL
	    I,
	    J,
	    ADR,
	    ADR2: REF VECTOR [,BYTE],
	    DSTFLG,
	    SEQFLG;
	RDCMD();
	GETRNG(RANGE);
	IF .RANGE[4] EQL 0 THEN	!DEFAULT RANGE IS ENTIRE FILE
		BEGIN
		RANGE[0]=0;
		RANGE[1]=0;
		RANGE[2]=.PAGHIG;
		RANGE[3]=INF;
		END;
	ADR=FINDR(RANGE);
	SETFIL(FILELST,.LSTEXT);
	SEQFLG=%O'0140011';
	DSTFLG=.SOSFLG[PRINTR];
	WHILE .SCSYM EQL ', ' DO
		BEGIN
		SCAN();
		IF .SCSYM EQL 'S ' THEN SEQFLG=.SEQFLG OR 2
		ELSE IF .SCSYM EQL 'P ' OR .SCSYM EQL 'F ' THEN
			BEGIN
			IF .SEQFLG GEQ 0 THEN BADCOMMAND;	! P OR F ALREADY SEEN
			IF .SCSYM EQL 'F '
				THEN SEQFLG=.SEQFLG AND NOT %O'0100011'
				ELSE SEQFLG = .SEQFLG AND %O'0377';	! 0 HIGH BYTE FOR 'P '
			SCAN();
			IF .SCSYM NEQ ': ' THEN BADCOMMAND;
			I = .SCSTRT;		! SAVE SCANNER POSITION
			SCAN();			! SEE IF GOT A NUMBER
			IF .SEQFLG<8,8> EQL 0	! TRY UNIT # FOR 'P '
					AND .SCTYP EQL SCNUM
				THEN	DSTFLG = .SCSYM
				ELSE	BEGIN
					SCSTRT = .I;	! RESTORE SCAN START
					IF SCANFILE(FILELST) NEQ 0 THEN
						CMDABO(BADFIL);
					DSTFLG=-2;	! GOT A FILE SPEC
					END;
			END
			ELSE BADCOMMAND;
		SCAN();
		END;
	IF .SCTYP NEQ SCEOF THEN BADCOMMAND;
	IF OPNLST(DSTFLG,FILELST) NEQ 0 THEN
		SOSERR(LSTX2,BADLST);
	J=-1;
	I = .LINPAG(.ADR);
	IF (.SEQFLG AND NOT %O'0377') LEQ 0 THEN
		BEGIN
		SEQFLG = .SEQFLG AND %O'0377';
		IF (.SEQFLG AND 2) EQL 0 THEN
			BEGIN
			LINFLG(.TTOCTL)=2;
			LINPAG(.TTOCTL)=0;
			ADR2=LINTXT(.TTOCTL);
			J=40;
			DO	BEGIN
				.ADR2='  ';
				ADR2=.ADR2+2;
				END WHILE ((J=.J-1) NEQ 0);
			ADR2=LINTXT(.TTOCTL)+NAMFIL(
				(IF .FILUSE EQL 1 OR .FILUSE EQL 4 THEN FILEOUT ELSE FILEIN),
				LINTXT(.TTOCTL),
				LINTXT(.TTOCTL)+42 );	!FILE NAME - 0:41
			ADR2[0] = ' '; ADR2=.ADR2+1;
			ADR2[0] = ' '; ADR2=.ADR2+1;
			GDATIM(.ADR2);			!DATE AND TIME - 42:61
			LINTXT(.TTOCTL)+62='PA';	!PAGE N - 62:71
			LINTXT(.TTOCTL)+64='GE';
			LINTXT(.TTOCTL)+80=0;
			END;
		J=0;
		END;
	DO	BEGIN
		IF .J EQL 0 THEN
			IF (.SEQFLG AND 2) NEQ 0 THEN I=.I-1
			ELSE	BEGIN
				IF .I GTRU .LINPAG(.TTOCTL) THEN
					BEGIN
					LINPAG(.TTOCTL)=.I;
					LINLIN(.TTOCTL)=0;
					END;
				LINLIN(.TTOCTL)=.LINLIN(.TTOCTL)+1;
				J=6;
				ADR2=LINTXT(.TTOCTL)+68;
				DO	BEGIN
					.ADR2='  ';
					ADR2=.ADR2+2;
					END WHILE ((J=.J-1) NEQ 0);
				ADR2=CNVDEC(.I,1);
				J=LINTXT(.TTOCTL)+68;
				DO	BEGIN
					(.J)<0,8>=.(.ADR2)<0,8>;
					J=.J+1;
					ADR2=.ADR2+1;
					END WHILE (.(.ADR2)<0,8> NEQ 0);
				IF .LINLIN(.TTOCTL) GTR 1 THEN
					BEGIN
					(.J)<0,8>='-';
					J=.J+1;
					ADR2=CNVDEC(.LINLIN(.TTOCTL),1);
					DO	BEGIN
						(.J)<0,8>=.(.ADR2)<0,8>;
						J=.J+1;
						ADR2=.ADR2+1;
						END WHILE (.(.ADR2)<0,8> NEQ 0);
					END;
				LINLEN(.TTOCTL)=44;
				I=.I-1;
				PUTLST(UPLIT (3),FILELST,.TTOCTL,I);
				LINLEN(.TTOCTL)=4;
				PUTLST(UPLIT (3),FILELST,.TTOCTL,I);
				J=2;
				END;
		PUTLST(SEQFLG,FILELST,.ADR,I);
		SETDOT(.ADR);
		ADR=NXTLINE(.ADR,1);
		IF .J GEQ 0 THEN
			IF ((J=.J+1) GEQ .SOSFLG[LENN])
				OR (.LINPAG(.ADR) GTRU .I) THEN
				BEGIN
				J=0;
				I=.LINPAG(.ADR);
				END;
		END UNTIL PSTRNG(.ADR,RANGE);
	CLOLST(.DSTFLG,FILELST);
	END;


END ELUDOM
