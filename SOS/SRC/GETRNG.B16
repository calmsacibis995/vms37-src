MODULE GETRNG (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!


!	03	29-JUL-77	BILL MARSHALL
!
!	GIVE SYNTAX ERROR IF RANGE SPECIFIED WITH
!		BOTH LINE NUMBERS AND NUMBER OF LINES
!		E.G.   P.:.+3!7
!

!
!	GETRNG
!
!	THIS ROUTINE WILL SCAN THE COMMAND LINE AND EXTRACT
!	A RANGE SPECIFICATION.  THIS IS EITHER OF THE
!	FORM LING/PG:LING/PG OR CAN BE LINE/PG!N.
!	IF NO FIRST LINE IS SPECIFIED, THE CURRENT LOC (.)
!	IS ASSUMED.  IF NO SECOND ONE IS SPECIFIED, THE SAME
!	AS THE FIRST IS ASSUMED. 
!
!	PARAMETERS -
!		RET - ADDRESS OF 5 WORD RETURN AREA
!
!	RETURNS -
!		IN RET, 	FIRST PAGE NUMBER
!			FIRST LINE NUMBER
!			SECOND PAGE NUM
!			SECOND LINE NUMBER
!			DEFAULTED SPEC INFO
!		VALUE IN RET[4] HAS FOLLOWING MEANING
!			1 BIT - FIRST LINE GIVEN
!			2 BIT - FIRST PG GIVEN
!			4 BIT - SECOND LINE GIVEN
!			10 BIT - SECOND LINE GIVEN
!
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	GETLNP,		! PARSE LINE/PAGE
	SCAN,		! COMMAND SCANNER
	CMDABO,		! COMMAND ABORT
	FIND;		! CONVERT /* OR ^/N

GBLNVROUTINE (GETRNG(RET))=
BEGIN
MAP
    RET: REF VECTOR[,WORD];
LOCAL
    ADR;
RET[0]=.CURPAG;
RET[1]=.CURLIN;
RET[4]=GETLNP(.RET);
RET[2]=.RET[0];
RET[3]=.RET[1];
IF .SCSYM[0] EQL ':' THEN
	BEGIN
	SCAN();
	RET[4]=.RET[4]+4*GETLNP(.RET+4);
	END
ELSE
IF .SCSYM[0] EQL '!' THEN
	BEGIN
	SCAN();
	IF .SCTYP EQL SCNUM THEN
		BEGIN
		RET[2]=-2;
		RET[3]=.SCSYM;
		SCAN();
		RET[4]=.RET[4]+12;
		END
	ELSE
		CMDABO(BADRNG);
	END;
IF (.RET[4] AND 3) EQL 2 THEN
	RET[1]=0;
IF (.RET[4] AND 12) EQL 8 OR (.RET[4] EQL 2) THEN
	RET[3]=INF;
IF .RET[2] EQLU INF OR .RET[2] EQL 0 OR
	.RET[3] EQL 0 THEN
	BEGIN
	ADR=FIND(.RET+4,-1);
	IF .ADR NEQ 0 THEN
		BEGIN
		RET[2]=.LINPAG(.ADR);
		RET[3]=.LINLIN(.ADR);
		END;
	END;
END;


END ELUDOM
