	.TITLE	SOSEVT - EVENT RECORD AND REPORT FOR DEBUG
	.IDENT	/V03000/
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; PETER H. LIPMAN	9-JUN-77
;
	.IF	DF,R$$DBG		;ENTIRE MODULE IS FOR DEBUG ONLY

	CODE$	EVT

	.SBTTL	DELETE & RENAME BUG PRINTOUT
;
; ERROR RENAMING FILE
;
CATBUG::				;RENAME FAILURE
	JSR	PC,BUG1			;EXTRA LEVEL OF SUBR
	JMP	EXIT			;MUST EXIT, CAN'T CONTINUE

BUG1:	NEG	R0			;MAKE FCS ERR CODE POSITIVE
	JSR	PC,BUGINI		;INIT FOR BUG REPORT
	JSR	R1,(R2)
	.WORD	CAT1			;FAILED TO RENAME FILE
	INC	(SP)			;MIN FIELD WIDTH = 1
	JSR	PC,PRINTN		;PRINT DECIMAL NUMBER
	JSR	PC,TYPECR		;CR, LF
	CLR	(SP)			;ZERO AT 0(SP) FOR PRTFIL
	BR	BUG2			;WILL RETURN TO "JMP EXIT" ABOVE

;
; ATTEMPTED A DELETE ON FDB WITH 0 VERSION NUMBER
;
DELBUG::
	JSR	R1,$SAV3		;SAVE R1,R2,R3
	JSR	PC,BUGINI		;INIT FOR BUG PRINTOUT
	CLR	(SP)			;ZERO FOR PRTFIL
	JSR	R1,(R2)
	.WORD	DEL1			;ATTEMPTED TO DELETE FILE
	JSR	PC,(R3)			;PRINT IT'S NAME
BUG2:	JSR	R1,(R2)
	.WORD	DEL2			;FILUSE VALUE
	MOV	FILUSE,-(SP)
	MOV	#1,-(SP)
	JSR	PC,PRINTN		;PRINT ITS VALUE
	CMP	(SP)+,(SP)+
	JSR	PC,TYPECR
	JSR	R1,(R2)
	.WORD	DEL3			;FILEIN FDB
	MOV	#FILEIN,(R1)		;STORE ADDRESS OF FILEIN FDB
	JSR	PC,(R3)			;PRINT FILE NAME
	JSR	R1,(R2)
	.WORD	DEL4			;FILEOUT FDB
	MOV	#FILEOUT,(R1)		;STORE ADDRESS OF FILEOUT FDB
	JSR	PC,(R3)			;PRINT FILE NAME
	JSR	R1,(R2)
	.WORD	DEL5			;FILETMP FDB
	MOV	#FILETMP,(R1)		;STORE ADDRESS OF FILETMP FDB
	JSR	PC,(R3)			;PRINT FILE NAME
	CMP	(SP)+,(SP)+
	JSR	PC,GIVEVT		;TYPE THE EVENT REPORT
	RTS	PC
;
; INIT FOR BUG REPORTING
;	RETURNS WITH 2 ADDITIONAL WORDS ON CALLER'S STACK:
;		0(SP) = 0
;		2(SP) = SAVED R0
;	AND WITH REGISTERS SET AS FOLLOWS:
;		R0 = GARBAGE
;		R1 = POINTER TO 2(SP)
;		R2 = ADR OF PRTINL SUBR
;		R3 = ADR OF PRTFIL SUBR
;
BUGINI:	MOV	(SP),R2			;SAVE RETURN ADDRESS
	MOV	R0,(SP)			;SAVE R0
	MOV	SP,R1			;R1 = ADR TO STORE FDB ADR'S FOR PRTFIL
	CLR	-(SP)			;RESERVE ANOTHER WORD
	MOV	R2,-(SP)		;PUT RETURN ADDRESS BACK ON STACK
	MOV	#PRTINL,R2		;PRINT "IN LINE" MSG SUBR ADR
	MOV	#PRTFIL,R3		;PRINT FILE NAME SUBROUTINE ADR
	JSR	R1,(R2)
	.WORD	BUGM1			;PLEASE REPORT
	RTS	PC			;INIT COMPLETE

	.SBTTL	EVENT RECORDING AND PRINTING ROUTINES
;
; ALL OF THIS CODE IS PRESENT FOR KEEPING A RECORD OF OPENS, RENAMES,
; AND DELETES SO THAT A BUG CAN BE FOUND
;
;
; RECORD AN EVENT
;	R0 = FDB ADDRESS
;	2(SP) = EVENT CODE
;		1 - OPEN FOR READ
;		2 - OPEN FOR WRITE
;		3 - REOPEN FOR READ
;		4 - RENAME FROM FDB GIVEN (NOT TO -1 FDB)
;		5 - RENAME FROM FDB GIVEN TO -1 FDB
;		6 - DELETE (FDB CODE OF 0 IF -1)
; RECORDS 1 WORD IN THE EVENT BUFFER (A RING BUFFER)
;	3 DECIMAL DIGITS FROM HIGH ORDER TO LOW ORDER (LEFT TO RIGHT)
;		1 - EVENT CODE
;		2 - FDB CODE
;			0 - THE -1 FDB WAS GIVEN
;			1 - FILEIN
;			2 - FILEOUT
;			3 - FILETMP
;			4 - FILELST
;			5 - FILECOP = FILENEW
;		3 - FILUSE PARAMETER
;			1 - CREATING AN OUTPUT FILE VIA INPUT
;			2 - EDITING SINGLE FILE, BAK NOT YET DONE
;			3 - EDITING SINGLE FILE, BAK DONE, RO, OR NOBAK
;			4 - TWO FILES GIVEN IN INITIAL COMMAND
;			5 - USING TEMP INPUT, FILEOUT IS REAL DESTINATION
;			6 - USING TEMP INPUT, FILEIN IS REAL DESTINATION
;
RECEVT::MOV	R0,-(SP)		;SAVE SOME REGISTER, ALL ARE PRESERVED
	MOV	R1,-(SP)
	MOV	R0,R1			;FDB ADDRESS OR -1
	MOV	6(SP),R0		;EVENT CODE
	JSR	PC,MUL10		;MULTIPLY R0 BY 10 DECIMAL
	CMP	#-1,R1			;-1 FDB?
	BEQ	25$			;LEAVE 0 FDB CODE
	INC	R0			;OTHERWISE FDB CODES START AT 1
	SUB	#FILEIN,R1		;SUBTRACT OUT BASE OF FDB ARRAY
	BR	20$
10$:	INC	R0			;COUNT ANOTHER FDB
	SUB	#S.FDB,R1		;TAKE ANOTHER FDB'S WORTH AWAY
20$:	BGT	10$			;BRANCH IF NOT DONE
25$:	JSR	PC,MUL10		;MULTIPLY BY 10 AGAIN
	ADD	FILUSE,R0		;PUT IN FILUSE PARAMETER
	MOV	EVTADR,R1		;POINTER TO NEXT WORD IN RING BUFFER
	MOV	R0,(R1)+		;STORE THE EVENT
	CMP	R1,#EVTEND		;AND WRAP
	BLO	30$			;THE POINTER
	MOV	#EVTBEG,R1		;IF NECESSARY
30$:	MOV	R1,EVTADR		;UPDATE THE COPY IN MEMORY
	MOV	(SP)+,R1		;RESTORE REGISTERS
	MOV	(SP)+,R0
	RTS	PC			;AND RETURN
;
; MULTIPLY R0 BY 10
;
MUL10:	MOV	R0,-(SP)
	ASL	R0
	ASL	R0
	ADD	(SP)+,R0
	ASL	R0
	RTS	PC
;
; GIVEVT - GIVE (PRINT) EVENT BUFFER FROM OLDEST TO NEWEST, LEFT TO RIGHT
;	SEE ABOVE FOR FORMAT
;
GIVEVT::JSR	R1,$SAV2		;SAVE SOME REGISTERS, ONLY R0 ALTERED
	JSR	R1,PRTINL		;PRINT IN LINE MSG
	.WORD	EVT1			;EVENTS =
	MOV	#<EVTEND-EVTBEG>/2,R2	;NO. OF WORDS IN EVENT BUFFER
	MOV	EVTADR,R1		;NEXT WORD TO STORE IS FIRST TO PRINT
	CMP	-(SP),-(SP)		;MAKE ROOM FOR 2 PARAMETERS
10$:	MOV	(R1)+,2(SP)		;WORD TO BE CONVERTED TO DECIMAL
	MOV	#3,(SP)			;NO. OF DIGITS
	JSR	PC,PRINTN		;PRINT DECIMAL DIGITS
	MOV	#' ,(SP)		;SEPARATE WITH SPACES
	JSR	PC,TYPE
	CMP	R1,#EVTEND		;WRAP THE POINTER
	BLO	20$			;IF NECESSARY
	MOV	#EVTBEG,R1
20$:	SOB	R2,10$			;PRINT EACH WORD IN RING BUFFER
	JSR	PC,TYPECR		;CR, LF
	CMP	(SP)+,(SP)+		;CLEAN THE STACK
	RTS	PC			;AND RETURN

	PLIT	EVT1,<^Events = ^>
;
; ***** DEBUGING MESSAGES
;
	PLIT	BUGM1,<<15><12>^Please report this output to Software Support^>
	PLIT	DEL1,<<15><12>^Attempted to DELETE: ^>
	PLIT	DEL2,<^FILUSE  = ^>
	PLIT	DEL3,<^FILEIN  = ^>
	PLIT	DEL4,<^FILEOUT = ^>
	PLIT	DEL5,<^FILETMP = ^>
	PLIT	CAT1,<<15><12>^Fatal RENAME Error Code = -^>


	.ENDC
	.END
