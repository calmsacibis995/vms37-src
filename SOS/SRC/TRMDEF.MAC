	.TITLE	TRMDEF - Terminal type definitions
	.IDENT	/V03000/
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; PETER H. LIPMAN	17-JAN-79
;
; MODIFIED BY:
;
;	020702	PHL0702		Peter H. Lipman		21-May-1980
;
;			Save terminal name in TRMNAM string when 
;		entering at TRMMOV.
;
;	020701	PHL0701		Peter H. Lipman		23-Apr-1980
;
;			Redesign for separate CRT table definitions,
;		(CUSTOM.MAC).
;
	CODE$	TRM
;
; MOVE TERMINAL CONTROL STRINGS FOR SPECIFIED TERMINAL TABLE ENTRY
;
;	R0 = TERMINAL TABLE ENTRY NUMBER
;
TRMMOV::
	ASL	R0			;FORM WORD INDEX
	MOV	TRMVEC(R0),R0		;ADR OF TABLE ENTRY
	MOV	#TRMNAM,R1
	MOV	R1,-(SP)
	MOV	#TRMNSZ,R2		;MAXIMUM TERMINAL NAME SIZE
10$:	MOVB	(R0)+,(R1)+		;MOVE THE NAME
	BEQ	20$
	SOB	R2,10$
20$:	JSR	PC,TRMSET
	TST	(SP)+
	RTS	PC
;
; SET TERMINAL CONTROL STRINGS GIVEN TERMINAL NAME
;
;	2(SP) = ADDRESS OF ASCIZ STRING WITH NAME TO LOOK FOR
;
; IF THE NAME IS FOUND, THE SCOPE CONTROL STRINGS ARE SET UP AND
; THE SCOPE BIT IS TURNED ON, OTHERWISE THE SCOPE BIT IS SHUT OFF.
;
TRMSET::
	JSR	R1,$SAV4		;SAVE SOME REGISTERS
	CLRB	TTSCP			;ASSUME TERMINAL IS NOT A CRT
	MOV	#TRMVEC,R4		;ADDRESS OF VECTOR OF TABLE ENTRIES
10$:	MOV	(R4)+,R3		;ADDRESS OF NEXT ENTRY
	BEQ	120$			;BRANCH IF NO MORE ENTRIES
	MOV	R3,R0			;PUT IT IN A WORKING REGISTER
	MOV	14(SP),R1		;ADDRESS OF TERM NAME TO FIND
20$:	TSTB	(R0)			;END OF NAME IN TABLE ENTRY?
	BEQ	40$			;BRANCH IF YES, MATCH FOUND
	MOVB	(R1)+,R2		;END OF NAME TO BE FOUND?
	BEQ	10$			;BRANCH IF YES, TRY NEXT ENTRY
	CMPB	R2,#140			;LOWER CASE ALPHABETIC?
	BLT	30$			;BRANCH IF NOT
	SUB	#40,R2			;CONVERT TO UPPER CASE
					;IGNORING SPECIAL GRAPHICS
30$:	CMPB	(R0)+,R2		;DO THESE CHARACTERS MATCH?
	BEQ	20$			;BRANCH IF YES, TRY NEXT CHARACTERS
	BR	10$			;NO, TRY NEXT TABLE ENTRY
;
; MATCH FOUND
;
40$:	INC	R0			;POINT TO FIRST CONTROL STRING
50$:	MOV	#TRMCTL,R3		;SET UP TABLE OF ADR'S OF PLITS
60$:	MOV	(R3),R2			;NEXT ADR TO STORE PLIT
	BEQ	100$			;BRANCH IF END OF TABLE
70$:	MOVB	(R0)+,R1		;MOVE CONTROL STRING
	CMPB	R1,#33			;CONVERTING ALTMODES
	BNE	80$
	BISB	#200,R1			;SO THAT THEY WILL GET THROUGH
80$:	CMPB	R1,#-1			;END OF THIS CONTROL STRING?
	BEQ	90$			;BRANCH IF YES
	MOVB	R1,(R2)+		;STORE THIS CHARACTER
	BR	70$			;AND GO GET THE NEXT ONE
;
; END OF CONTROL STRING - SET SIZE OF THIS ONE AND GO GET THE NEXT
;
90$:	MOV	(R3)+,R1		;ADR OF FRONT OF PLIT AGAIN
	INC	R2			;FORM
	SUB	R1,R2			;ROUNDED UP
	ASR	R2			;WORD COUNT
	MOV	R2,-(R1)		;AND STORE IT IN PLIT
	BR	60$			;GET NEXT PLIT ADDRESS, IF ANY
;
; LAST CONTROL STRING HAS BEEN STORED
;
100$:	INCB	TTSCP			;THIS TERMINAL IS A SCOPE
120$:	RTS	PC


	.END
