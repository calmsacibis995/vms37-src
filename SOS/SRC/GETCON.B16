MODULE GETCON (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!	020705	PHL33878	Peter H. Lipman		12-Nov-1980
!		For DECIDE mode, force input to terminal even if
!	commands are coming from an indirect file
!

!
!	GETCON	-   PROCESS LINE CONTENTS SPECIFICATION
!
!
!	V02.02	09-JUN-76	WTM
!


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	GETSTR,		! TO READ IN STRING
	ILLCMD,		! ILLEGAL - BAD COMMAND
	CMDABO,		! COMMAND ABORT
	GETRNG,		! INPUT RANGE
	GETDOT,		! LOCATE PLACE
	SCAN,		! COMMAND SCANNER
	FINDR,		! LOCATE US IN RANGE
	SEARCH,		! FIND A STRING
	PRTLIN,		! PRINT LINE ON KB:
	TYPE,		! TYPE A CHAR ON KB:
	LISTEN,		! READ A CHAR
	TYPECR,		! TYPE A <CR> <LF>
	PSTRNG,		! SEE IF DPNE WITH RANGE
	SETDOT,		! SET NEW LOCATION
	RDCMD,		! READ IN COMMAND LINE
	NXTLIN;		! ADVANCE ONE IN BUF
	
EXTERNAL
    SRCON: VECTOR[,WORD],		! SOSDEF COMMON AREA
    SRCONS,				! SEACH STRINGS
    SRCONL;				! LEN OF SEARCH STRING


GBLNVROUTINE (GETCON(RET))=
BEGIN
LOCAL
    RNG: VECTOR[5],
    STRP: VECTOR[4],
    I,
    DF,
    NN,
    ADR;

SRCON[4]=.SRCON[4]+1;

STRP[3]=.SRCON[3];
STRP[1]=.SRCON[3];
I=GETSTR(0,1,STRP+2,SRCONS,SRCONL,0);

IF .I EQL 0 AND .SRCONS NEQ 0 AND .SRCON[4] EQL 1 THEN
	BEGIN
	STRP[1]=STRP[2]=SRCONS;
	DO STRP[2]=.STRP[2]+1 UNTIL .(.STRP[2])<0,8> EQL 0;
	END
ELSE IF .I LEQ 0 THEN BADCOMMAND;
SRCON[3]=.STRP[2];
STRP=1;
RDCMD();
IF .SRCON[4] GEQ 3 AND .SCSYM[0] EQL %O'033' THEN
	BADCOMMAND;	! NESTED TOO FAR
GETRNG(RNG);
IF .RNG[4] EQL 0 THEN
	BEGIN
	GETDOT(RNG);
	RNG[1]=.RNG[1]+1;
	RNG[2]=RNG[3]=INF;
	END;
DF=0;  NN=1;
SOSFLG[FSFLG]=.SOSFLG[FSFLG] AND NOT (EMATCH OR NOMATCH);
IF (.SOSFLG AND EXACT) NEQ 0 THEN SOSFLG[FSFLG]=.SOSFLG[FSFLG] OR EMATCH;
WHILE .SCSYM EQL ', ' DO
	BEGIN
	SCAN();
	IF .SCTYP EQL SCNUM THEN
		NN=.SCSYM
	ELSE
	IF .SCSYM EQL 'D ' THEN
		DF=15
	ELSE
	IF .SCSYM EQL 'N ' THEN
		DF=1
	ELSE
	IF .SCSYM EQL 'E ' THEN
		SOSFLG[FSFLG]=.SOSFLG[FSFLG] OR EMATCH
	ELSE
	IF .SCSYM EQL '- ' THEN
		SOSFLG[FSFLG]=.SOSFLG[FSFLG] OR NOMATCH
	ELSE
		BADCOMMAND;
	SCAN();
	END;
IF .SCTYP NEQ SCEOF THEN BADCOMMAND;
ADR=FINDR(RNG);
DO	BEGIN
	LOCAL
	    J,
	    CH;
	J=SEARCH(.ADR,0,STRP,RNG,SRCON);
	IF .J LSS 0 THEN CMDABO(BADISS);
	IF .J EQL 0 THEN CMDABO(BADSNF);
	ADR=.SRCON[0];
	IF .DF GTR 0 THEN
	    DO	BEGIN
		PRTLIN(.ADR,.DF);
		TYPE ('?');
		CH=LISTEN(%O'100');
		TYPE(.CH);
		TYPECR();
		IF .CH EQL ' ' OR .CH EQL 'Y' OR .CH EQL 'y' THEN
			BEGIN
			NN=1;
			EXITLOOP;
			END
		ELSE IF .CH EQL %O'177' OR .CH EQL 'N' OR .CH EQL 'n' THEN
			BEGIN
			NN=.NN+1;
			EXITLOOP;
			END;
		END WHILE 1;
	SETDOT(.ADR);
	ADR=NXTLIN(.ADR,1);
	END UNTIL (NN=.NN-1) EQL 0 OR PSTRNG(.ADR,RNG);
GETDOT(.RET);
SRCON[3]=.STRP[3];
SRCON[4]=.SRCON[4]-1;
RDCMD();
END;

END ELUDOM
