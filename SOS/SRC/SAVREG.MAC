; File:   SAVREG.MAC
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;    This work was supported by the Advanced Research
;    Projects Agency of the Office of the Secretary of
;    Defense (F44620-73-C-0074) and is monitored by the
;    Air Force Office of Scientific Research.

	.TITLE	SAVREG

	CODE$	SAVR

; The BLISS-11 out-of-line routines to save and restore register contents.
;
; 30-Nov-73			P. Knueven
;
; The routine $SAVn is called at the beginning of a routine
; which modifies the contents of registers 1 thru n.  The calling
; sequence is the following:
;		JSR  R1,$SAVn
;

R0=%0
R1=%1
R2=%2
R3=%3
R4=%4
R5=%5
SP=%6
PC=%7

	.GLOBL	$SAV2,$SAV3
	.GLOBL	$SAV4,$SAV5

$SAVE2::
$SAV2:	MOV	R2,-(SP)
	MOV	R1,-(SP)
	MOV	4(SP),R1
	JSR	PC,@(SP)+
	BR	RE2

$SAVE3::
$SAV3:	MOV	R2,-(SP)
	MOV	R3,-(SP)
	MOV	R1,-(SP)
	MOV	6(SP),R1
	JSR	PC,@(SP)+
	BR	RE3

$SAVE4::
$SAV4:	MOV	R2,-(SP)
	MOV	R3,-(SP)
	MOV	R4,-(SP)
	MOV	R1,-(SP)
	MOV	8.(SP),R1
	JSR	PC,@(SP)+
	BR	RE4

$SAVE5::
$SAV5:	MOV	R2,-(SP)
	MOV	R3,-(SP)
	MOV	R4,-(SP)
	MOV	R5,-(SP)
	MOV	R1,-(SP)
	MOV	10.(SP),R1
	JSR	PC,@(SP)+
	MOV	(SP)+,R5
RE4:	MOV	(SP)+,R4
RE3:	MOV	(SP)+,R3
RE2:	MOV	(SP)+,R2
	MOV	(SP)+,R1
	RTS	PC

	.END
