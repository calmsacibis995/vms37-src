MODULE SOSERR (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!


!	03	29-JUL-77	BILL MARSHALL
!
!	ADDED CALL TO RLSBUF, IN CASE COMMAND ISSUEING ERROR
!		HAD A TEMPORARY BUFFER TIED UP IN FSR REGION
!
!	05	15-FEB-78	PETER H. LIPMAN
!
!		NO LONGER ANY NEED TO SPECIAL CASE SPSAV2=0
!	IT ALWAYS CONTAINS A GOOD ABORT STACK POINTER.
!		ADD RETURN 0 TO RESETSP, WAS RETURNING SP!
!		ADD FDB INDEX PARAMETER AS HIGH BYTE NON-ZERO
!	IN THE SEVERITY CODE PARAMETER.  CALL FDBERR IF THE FDB
!	INDEX IS NON-ZERO AND EXPERT MODE IS NOT SET.
!

!
!	SOSERR
!
!	THIS ROUTINE PRINTS A CANNED ERROR MESSAGE AND
!	DOES SOME PRIMITIVE RECOVERY FROM THE ERROR.
!	THE RECOVERY IS EITHER TO ABORT THE COMMAND,
!	(CODE 2) OR TO ABORT SOS (CODE 3).  THESE ARE
!	DONE BY RESETTING THE STACK POINTER, POPPING
!	MUCH JUNK, AND RETURNING TO SOMEWHERE OTHER
!	THAN THE CALLER.
!
!	PARAMETERS -
!		SEVFBD<0,8> - SEVERITY CODE OF ERROR
!		SEVFDB<8,8> - FDB INDEX IF NOT ZERO
!		NUM         - NUMBER OF ERROR - SEE MACROS FOR DEFS
!
!	RETURNS -
!		DOES NOT RETURN
!
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	COPERR,		! COPY COMMAND ERROR HANDLER
	RLSBUF,		! RELEASE TEMPORARY BUFFER
	EMSG,		! PRINT ERROR MESSAGE
	FDBERR,		! PRINT FILE NAME AND ERROR CODE FROM FDB
	RESETSP;	! RESET THE STACK FRAME

EXTERNAL
    ETBL,				! ERROR TABLE
    COPERF,				! ERROR IN COPY FLAG
    SRCON: VECTOR[,WORD],		! LINE CONTENTS FIELDS
    SRCONS,				! TO RESET ON ERROR
    ONUM,				! LAST ERROR NUMBER
    OFDB,				! FDB ADDRESS FROM LAST ERROR IF NON-ZERO
    FDBSIZ,				! SIZE IN BYTES OF AN FDB (EQUATED SYMBOL)
    FILEIN,				! ADDRESS OF FIRST FDB
    SPSAV1,				! ABORT SOS FRAME POINTER
    SPSAV2;				! ABORT COMMAND FRAME POINTER

GBLNVROUTINE (SOSRRR(SEVFDB,NUM))=
BEGIN
LOCAL
    SEV,
    NOTEXP;
MAP
    ETBL: REF VECTOR[,WORD];
ETBL=PLIT(
!
!	EXPERT MESSAGE		NOVICE MESSAGE
PLIT ('Internal error'),PLIT ('You confounded SOS.  Sorry.'),
PLIT ('Order'),PLIT ('Lines out of order'),
PLIT ('Bad line/page'),PLIT ('Illegal specification in line or page number'),
PLIT ('Bad range'),PLIT ('Illegal specification in range'),
PLIT ('Bad switch'),PLIT ('Illegal switch name'),
PLIT ('Bad param to switch'),PLIT ('Illegal parameter to switch '),
PLIT (%STRING('File','?')),PLIT ('Illegal file specification'),
PLIT (%STRING('?','?')),PLIT ('Illegal syntax of command'),
PLIT ('NSL'),PLIT ('No such line exists'),
PLIT ('ISS'),PLIT ('Illegal search string given'),
PLIT ('Not there'),PLIT ('String not found, search failed'),
PLIT (%STRING('Page','?')),PLIT ('Page does not exist'),
PLIT ('Nothing'),PLIT ('Range given does not contain any lines'),
PLIT ('DEL no pages'),PLIT ('Cannot delete across pages'),
PLIT ('Line too long'),PLIT ('Line is too long'),
PLIT ('HELP trouble'),PLIT ('Cannot access HELP file'),
PLIT ('Too long'),PLIT ('Search strings exceeded buffer length'),
PLIT ('Too many'),PLIT ('Too many search strings given'),
PLIT ('File RO'),PLIT ('Command invalid for read only file'),
PLIT ('K/1 bad'),PLIT ('Cannot kill initial page mark'),
PLIT ('No room'),PLIT ('Insufficient line numbers for insertion'),
PLIT ('LIST trouble'),PLIT ('Cannot access LIST file'),
PLIT ('Step too big'),PLIT ('Line number step too large - lines out of order'));
!		MORE WILL GO IN HERE
!

ONUM=.NUM;
SEV=.SEVFDB<0,8>;
IF .SEV EQL 2 AND .COPERF NEQ 0 THEN COPERR(.NUM);
NOTEXP=0;
IF (.SOSFLG AND EXPERT) EQL 0 THEN NOTEXP=.NOTEXP+1;
IF .NUM GEQ 0 THEN
EMSG(.ETBL[2*.NUM+.NOTEXP]);
IF (OFDB=.SEVFDB<8,8>) GTR 0 THEN
	OFDB=(.OFDB-1)*FDBSIZ+FILEIN;
IF (.OFDB NEQ 0) AND (.NOTEXP GTR 0) THEN FDBERR(0,.OFDB);
IF .SEV EQL 3 THEN RESETSP(.SPSAV1);
IF .SEV EQL 2 THEN
	BEGIN
	SRCON[3]=SRCONS;
	SRCON[4]=0;
	RLSBUF();
	RESETSP(.SPSAV2);
	END;
END;


END ELUDOM
