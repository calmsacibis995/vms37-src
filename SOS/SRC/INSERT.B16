MODULE INSERT (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!


!	03	29-JUL-77	BILL MARSHALL
!
!	REMOVED OVERLAY KLUDGES - FINDX, PRVLIX
!	SET DOT CORRECTLY IF INSERT ENDED VIA ^C
!
!	05	15-FEB-78	PETER H. LIPMAN
!
!		SET ILIN TO REMEMBER LAST INSERT POINT
!	FOR REPLACE AS WELL AS INSERT COMMAND.
!		COUNT CMDCNT ONLY AFTER INSERTING FIRST LINE AND
!	ONLY IF THIS IS NOT A REPLACE COMMAND (NEW PARAMETER)
!		NEWLIN IS NOW POINTED TO BY NEWLNA
!
!	06.11	20-APR-79	PETER H. LIPMAN
!
!		SUPPORT /NONUM SWITCH BY NOT PRINTING THE LINE NUMBER
!
!	06.12	19-MAY-79	PETER H. LIPMAN
!
!		IN ORDER TO HANDLE THE LONG INSERT FROM A TERMINAL WHICH
!	USED TO REQUIRE MULTIPLE READS AND EXHIBITED THE PROBLEM THAT
!	CHARACTERS FROM PREVIOUS READS COULD NOT BE ERASED,  THE ALTER
!	ROUTINE IS NOW USED TO READ THE INPUT AND INSERT CHARACTERS.
!	AS AN ADDITIONAL FEATURE IT IS NOW POSSIBLE TO TRIGGER REAL
!	ALTER MODE ON THE LINE BEING INSERTED.  THIS IS DONE BY THE
!	SEQUENCE <^A><ESC>.
!
!	06.14	5-NOV-79	PETER H. LIPMAN
!
!	REMEMBER CURRENT INCREMENT FOR USE BY DEFAULT I COMMAND
!	CURLPG IS SET BY CMDI OR CMDR (DELREP) WHEN APPROPRIATE
!
!	06.15	9-NOV-79	PETER H. LIPMAN
!
!	SET THE NEW INCREMENT FOR DEFAULT I COMMAND ONLY WHEN
!		SETTING A NEW STARTING LOCATION
!

!
!	INSERT
!
!	THIS ROUTINE DOES INSERTIONS INTO THE FILE.
!	IT IS CALLED BY BOTH THE I AND R COMMANDS.
!	IT COMPUTES AN INCREMENT TO USE IF NONE IS SPECIFIED,
!	AND THEN IT CALL ON THE ALTER ROUTINE TO DO THE INSERT.
!	THE CASE OF INSERT FOR A NEW FILE IS NOW HANDLED BY
!	BUILDING THE APPROPRIATE INSERT COMMAND AND CALLING
!	THE COMMAND RECEIVER NORMALLY.
!
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	FLINC,		! COMPUTE THE INSERT INCREMENT
	ALTER,		! THE ALTER COMMAND NOW DOES THE INSERT
	INSLINE,	! INSERT LINE IN BUFFER
	GETDOT,		! GET CURRENT LOCATION
	CMDABO,		! ABORT COMMAND WITH ERROR CODE
	PRINT,		! MESSAGE PRINTER
	SAVE,		! DOING AUTO-SAVE
	FIND,		! GET BACK AFTER SAVE
	NXTLINE,	! FORWARD ONE LINE IN BUFFER
	PRVLINE;	! BACK UP ONE IN BUFFER

EXTERNAL
    AUTSAV,				! AUTO SAVE MESSAGE PLIT
    ILIN: VECTOR[3];			! POSITION OF LAST INSERT/REPLACE

EXTERNAL
    NEWLNA: REF VECTOR[,BYTE];		! POINTER TO NEW LINE BUILD AREA

GBLNVROUTINE (INSERT(RANGE,INC,NL,REPLACE))=
BEGIN
LOCAL
    ADR: VECTOR[2],
    INSCNT,
    TMPLIN: VECTOR[5],
    I,
    LLIN,
    SAVILIN;
MAP
    RANGE: REF VECTOR;
ADR=FLINC(.RANGE,LINLIN(TMPLIN),LLIN,INC,NL);
IF (ADR[1]=.INC) EQL 0 THEN CMDABO(BADINC);
INSCNT=0;
SAVILIN=.ILIN[0];
ILIN[0]=0;				! INVALIDATE THIS CONTEXT SO THAT ^C OUT OF INSERT
			! WILL NOT LEAVE GARBAGE FOR LAST INSERT POINT
LINPAG(TMPLIN)=.RANGE[0];
TMPLIN[0]=5;
LINFLG(TMPLIN)=0;
TMPLIN[4]=0;
DO	BEGIN
	I=ALTER(TMPLIN,0,(PLIT ('I')),ADR);
!
! I = -1 IF NULL TEXT LINE WAS SPECIFIED AND TERMINATED WITH <ESC>
! I =  0 IF A TEXT LINE WAS ENTERED AND TERMINATED WITH <ESC>
! I =  ADDRESS TO INSERT NEXT LINE IN FRONT OF.  ALSO RETURNED IN ADR[0]
!
	IF .I NEQ -1 THEN
		BEGIN
		I=.I-1;		! CONVERT 0 RETURN TO -1
		CMDICNT=.CMDICNT+1;
		IF .SOSFLG[ISAVE] GTR 0 AND .SOSFLG[ISAVE] LEQ .CMDICNT THEN
			BEGIN
			PRINT(AUTSAV);
			SAVE(0,0,0,0,0,-1);
			ADR=NXTLINE(FIND(CURPAG,0),1);
			CMDICNT=CMDCNT=0;
			END;
		IF .INSCNT EQL 0 THEN
			BEGIN
			INSCNT=.INSCNT+1;
			IF .REPLACE EQL 0 THEN CMDCNT=.CMDCNT+1;
			END;
		LINLIN(TMPLIN)=.LINLIN(TMPLIN)+.INC;
		END;
	IF .I EQL -1 THEN EXITLOOP;
	IF (NL=.NL-1) EQL 0 THEN EXITLOOP;
	IF .LINLIN(TMPLIN) GTRU .LLIN THEN EXITLOOP;
	END WHILE 1;
IF .INSCNT GTR 0 THEN	! IF AT LEAST ONE LINE WAS INSERTED
	BEGIN
	GETDOT(ILIN);	! THEN SET DEFAULT INSERT POINT
	ILIN[2]=.INC;	! AND THE CORRESPONDING INCREMENT
	END
ELSE ILIN[0]=.SAVILIN;	! OTHERWISE RESTORE PREVIOUS DEFAULT INSERT POINT
END;



END ELUDOM
