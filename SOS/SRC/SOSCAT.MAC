	.TITLE	SOSCAT - CATALOGUE (RENAME) ROUTINE
	.IDENT	/V03000/
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; PETER H. LIPMAN	22-AUG-77
;
	.MCALL	FDOF$L,QIOSY$
	FDOF$L
	QIOSY$

	.IF	DF,R$$STS

	DATA$	CAT

BAKBAK::.RAD50	/BAK/			;BACKUP EXTENSION

	.ENDC

	.SBTTL	CAT - RENAME FILES

	CODE$	CAT

ENTRY	CAT

	.IF	DF,R$$STS
	MOV	R1,-(SP)		;SAVE SOME REGS FIRST
	MOV	6(SP),R0		;FIRST FILE POINTER

	.IF	DF,R$$DBG
	MOV	#4,-(SP)		;***** EVENT 4 IF NOT .BAK
	CMP	#-1,6(SP)		;***** .BAK?
	BNE	2$			;***** BRANCH IF NOT
	INC	(SP)			;***** YES, EVENT 5 FOR .BAK
2$:	JSR	PC,RECEVT		;***** RECORD THE EVENT
	TST	(SP)+			;***** CLEAN THE STACK
	.ENDC

	MOV	R5,-(SP)		;TEMP SAVE OF R5
	JSR	PC,..IFQB		;SETUP FIRQB FROM FDB
	MOV	(SP)+,R5		;RESTORE R5
	MOV	4(SP),R1		;SECOND FILE POINTER
	CMP	R1,#-1			;BACKUP IT?
	BEQ	15$			;YES.  IT IS SPECIAL
	MOV	N.DID+F.FNB(R0),N.DID+F.FNB(R1)
	MOV	N.FNAM+F.FNB(R1),FIRQB+FQNAM2;SET UP NEW NAME
	MOV	N.FNAM+2+F.FNB(R1),FIRQB+FQNAM2+2
	MOV	N.FTYP+F.FNB(R1),FIRQB+FQNAM2+4;
3$:	CLR	FIRQB+FQPROT-1		;DO NOT CHANGE PROT
	MOVB	#RENFQ,FIRQB+FQFUN	;SET FUNCTION CODE TO RENAME
	CALFIP
	TSTB	IOSTS			;ANY ERROR?
	BEQ	5$			;NO.  RET 0
	CMPB	IOSTS,#FIEXST		;NEW FILE EXISTS?
	BEQ	7$
	CMPB	IOSTS,#BADNAM		;INVALID FILENAME
	BEQ	8$
	JSR	PC,IOCHK		;ANYTHING ELSE ERROR
5$:	MOV	4(SP),R1		;SECOND FILE NAME
	CMP	R1,#-1			;BACKUP FILE?
	BEQ	6$
	MOV	N.DID+F.FNB(R0),N.DID+F.FNB(R1)
	MOV	N.DVNM+F.FNB(R0),N.DVNM+F.FNB(R1)
	MOV	N.UNIT+F.FNB(R0),N.UNIT+F.FNB(R1)
6$:	CLR	R0
	BR	10$
7$:	MOV	#1,R0			;FILE EXISTS RETURN
	BR	10$
8$:	MOV	#-1,R0			;BAD NAME RETURN
10$:
	MOV	(SP)+,R1
	RTS	PC			;AND RETURN

15$:	MOV	#FIRQB,R1		;POINT INTO FIRQB
	MOV	FQNAM1(R1),FQNAM2(R1)	;COPY INFO FOR .BAK
	MOV	FQNAM1+2(R1),FQNAM2+2(R1)
	MOV	BAKBAK,FQNAM2+4(R1)
	BR	3$

	.IFF
	JSR	R1,$SAV2		;PUSHES 3 WORD, SAVE R1,R2
	CLR	-(SP)			;INITIALIZE RETURN VALUE
	MOV	14(SP),R0		;FDB OF EXISTING FILE
	MOV	12(SP),R1		;FDB OF NEW NAME

	.IF	DF,R$$DBG
	MOV	#4,-(SP)		;***** EVENT 4 IF NOT .BAK
	CMP	#-1,R1			;***** .BAK?
	BNE	2$			;***** BRANCH IF NOT
	INC	(SP)			;***** YES, EVENT 5
2$:	JSR	PC,RECEVT		;***** RECORD THE EVENT
	TST	(SP)+			;***** CLEAN THE STACK
	.ENDC

	CMP	#-1,R1			;SUPPOSED TO RENAME TO .BAK?
	BNE	5$			;BRANCH IF NO
;
; -1 INSTEAD OF THE NEW NAME FDB MEANS THAT WE ARE SUPPOSED TO RENAME
; THE EXISTING FILE TO ITS ORIGINAL NAME WITH TYPE .BAK.
; SINCE THIS ISN'T NECESSARY WITH FILES-11 VERSION NUMBERS, THE RENAME
; REQUEST IS ESSENTIALLY IGNORED.  BUT THIS DOES TURN OUT
; TO BE THE RIGHT PLACE TO GUARANTEE THAT WHEN THE UPDATED FILE
; IS RENAMED TO ITS DESIRED NAME, A NEW VERSION IS FORCED.
; ZERO THE ORIGINAL FILE VERSION NUMBER HERE.  IT MUST BE THE
; ORIGINAL INPUT FILE BECAUSE IT'S SUPPOSED TO BE RENAMED TO
; .BAK.  IF A NEW NAME WAS GIVE ON THE "E" COMMAND OR
; ITS EQUIVALENTS, THEN FORCING A NEW VERSION IS NOT AN ISSUE
; AND IF THE ORIGINAL SOS COMMAND LINE SAID
;	OUTPUT FILE = INPUT FILE
; THEN SOS IS ALWAYS PRODUCING THE OUTPUT FILE UNDER ITS CORRECT
; NAME AND A RENAME DOESN'T OCCUR.
;
	CLR	F.FVER(R0)
	BR	20$
5$:	CMP	F.DVNM(R0),F.DVNM(R1)	;MUST BE SAME DEVICE
	BNE	8$
	CMP	F.UNIT(R0),F.UNIT(R1)	;AND UNIT, ELSE ERROR
	BNE	8$
	JSR	PC,REMOVE		;REMOVE THE ORIGINAL FILE
	BCS	15$			;BRANCH IF ERROR
	MOV	12(SP),R0		;NEW FILE NAME FDB
	MOV	(R1),F.FNB(R0)		;COPY FILE ID JUST REMOVED
					;FOR ENTERING WITH NEW NAME
	CLR	(R1)+			;AND ZERO IT
	MOV	(R1)+,F.FNB+2(R0)
	MOV	(R1),F.FNB+4(R0)
	JSR	PC,ENTER		;MAKE ENTRY FOR NEW FILE NAME
	BCC	10$			;BRANCH IF ALL OK
;
; FAILED TO ENTER NEW NAME, PUT OLD ONE BACK
;
	MOV	F.ERR(R0),(SP)		;SAVE ERROR CODE
	MOV	14(SP),R0		;RECOVER FDB ADDRESS
	MOV	(R1),F.FNB(R0)		;PUT FILE ID BACK
	CLR	(R1)			;ZERO IT
	JSR	PC,ENTER		;RESTORE OLD NAME
	BR	16$			;AND REPORT ERROR
8$:	MOV	#<IE.2DV&377>,(SP)	;2 DEVICE ERROR FROM RENAME
	BR	16$
;
; RENAME SUCCESSFUL, R1 = FNB ADDRESS OF NEW NAME
;
10$:	JSR	PC,WRTFNM		;WRITE FILE NAME IN FILE HEADER
	BCC	20$			;BRANCH IF DONE SUCCESSFULLY
	MOV	IOSTS,(SP)		;SET ERROR CODE
	BR	20$			;AND EXIT
15$:	MOV	F.ERR(R0),(SP)		;RETURN NEGATIVE FOR ERROR
16$:	CMPB	#IE.DUP,(SP)		;UNLESS NEW NAME ALREADY EXISTS
	BNE	20$			;BRANCH IF NOT THAT ERROR
	NEGB	(SP)			;RETURN GTR 0
20$:	MOVB	(SP),R0			;RETURN VALUE
	BGE	30$			;BRANCH IF OK OR IE.DUP
	CMP	#IE.PRI,R0		;PRIVILEGE VIOLATION FOR
	BEQ	30$			;UNWRITABLE DIRECTORY
	CMP	#IE.2DV,R0		;TWO DEVICES FOR ATTEMPT TO
	BEQ	30$			;RENAME ACROSS DEVICES
	JMP	CATERR			;GO PRINT THE ERROR INFO
30$:	TST	(SP)+			;CLEAN ERROR CODE OFF STACK
	RTS	PC
	.ENDC

	.SBTTL	DIRECTORY ROUTINES FOR RENAMING
;
; ENTER, REMOVE
;	PERFORM THE SPECIFIED DIRECTORY OPERATION
; INPUTS:
;	R0 = FDB ADDRESS
;
; OUTPUTS:
;	C=0 IF SUCCESSFUL, C=1 IF ERROR, F.ERR(R0)=ERROR CODE
;	R0 PRESERVED
;	R1 = FILE NAME BLOCK ADDRESS FOR FDB IN R0
;	R2 ALTERED
;
	.IF	NDF,R$$STS		;RSX ONLY

	.ENABL	LSB

ENTER:	MOV	#.ENTER,R2		;ENTER ROUTINE
	BR	10$
REMOVE:	MOV	#.REMOV,R2		;REMOVE ROUTINE
10$:	MOV	R0,R1			;FORM ADDRESS
	ADD	#F.FNB,R1		;OF FILE NAME BLOCK
15$:	JSR	PC,(R2)			;CALL THE DIRECTORY ROUTINE
	BCC	30$			;BRANCH IF SUCCESSFUL
	CMPB	#IE.NOD,F.ERR(R0)	;DYNAMIC POOL PROBLEMS?
	BNE	20$			;BRANCH IF NOT, REPORT ERROR
	JSR	PC,WAITND		;WAIT A BIT
	BR	15$			;AND RETRY
20$:	SEC				;REPORT AN ERROR CONDITION
30$:	RTS	PC			;RETURN WITH C SET IF ERROR

	.DSABL	LSB

	.ENDC


	.END
