MODULE CMDH (
	IDENT = 'V03-000'
		) =

BEGIN

!
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! PETER H. LIPMAN	18-JUN-77
!


! MODIFIED BY:
!
!	04	5-SEP-77	PETER H. LIPMAN
!
!		BROKE OUT A CLOSED ROUTINE CMDHDO TO BE CALLED
!	FROM SWITCH PROCESSING CODE THUS IMPLEMENTING /HELP.
!		REMOVED LOGICAL UNIT NUMBER FROM OPNHLP 
!	SUBROUTINE CALL.
!
!	05	24-FEB-78	PETER H. LIPMAN
!
!		CHANGED FILE OPEN ERROR REPORT TO USE SOSERR.
!		NEWLIN IS NOW POINTED AT BY NEWLNA
!
!	07	5-SEP-78	PETER H. LIPMAN
!
!		CHECK AND CLEAR ^C THROUGH A CLOSED ROUTINE.
!

!
!	CMDH - HELP COMMAND
!
!
!	V02.02	18-JUN-77	PHL
!


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	RDCMD,		! READ IN COMMAND
	SOSERR,		! ERROR HANDLER
	ILLCMD,		! BAD COMMAND ERROR
	OPNHLP,		! OPEN
	SCAN,		! SCANNER SERVICE ROUTINE
	GETHLP,		! GET RECORD FROM HELP FILE
	NEWLNX,		! LOAD NEW LINE BUFFER OVERLAY
	NOCTLO,		! CANCEL THE EFFECTS OF CONTROL O
	TYPSTR,		! TYPE STRING OF SPECIFIED SIZE
	TYPECR,		! TYPE A CR, LF
	CLOSE,		! CLOSE THE HELP FILE
	CKCTLC;		! CHECK FOR CONTROL C

EXTERNAL
    FILELST,				! FDB FOR HELP FILE
    LSTX2;				! LIST FILE FDB INDEX*256 + SEVERITY CODE 2

EXTERNAL
    NEWLNA: REF VECTOR[,BYTE];		! POINTER TO NEW LINE BUFFER

GBLNVROUTINE (CMDHDO(STPAG))=
	BEGIN
	LOCAL
	    I,
	    CURPAG;
	CURPAG=0;
	IF (OPNHLP(FILELST) EQL 0) THEN
		BEGIN
		NEWLNX();
		DO	BEGIN
			I=GETHLP(FILELST,.NEWLNA,512);
			IF (.I EQL 1) AND (.NEWLNA[0] EQL %CHAR(12)) THEN
				BEGIN
				I=0;
				CURPAG=.CURPAG+1;
				IF .CURPAG GEQ .STPAG THEN NOCTLO();
				END;
			IF .CURPAG GEQ .STPAG THEN
				BEGIN
				IF .I GTR 0 THEN TYPSTR(.I,.NEWLNA);
				TYPECR();
				END;
			END UNTIL ((.I LSS 0) OR (CKCTLC() GTR 0));
		CLOSE(FILELST);
		END
		ELSE SOSERR(LSTX2,BADHLP);
	END;

GBLNVROUTINE (CMDH)=
	BEGIN		! HELP COMMAND - PRINT HELP FILE AT TERMINAL
	LOCAL
	    STPAG;
	RDCMD();
	STPAG=0;
	IF .SCTYP EQL SCALPH THEN SCAN();
	IF .SCSYM EQL ': ' THEN
		BEGIN
		SCAN();
		IF .SCTYP EQL SCNUM THEN
			BEGIN
			STPAG=.SCSYM;
			SCAN();
			END
			ELSE BADCOMMAND;
		END;
	IF .SCTYP NEQ SCEOF THEN BADCOMMAND;
	CMDHDO(.STPAG);
	END;


END ELUDOM
