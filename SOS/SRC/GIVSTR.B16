MODULE GIVSTR (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!


!	03	18-AUG-77	BILL MARSHALL
!
!	ADDED ENTRIES TO SPECIAL CHARACTER TABLE FOR NEW
!	STRING SEARCHING CAPABILITY.
!
!	07	2-NOV-78	PETER H. LIPMAN
!
!	MADE THE "SPECIAL" TABLE EXTERNAL SO THAT THERE WOULD
!		BE ONLY ONE COPY (IN GETSTR).
!

!
!	GIVSTR - PROCESS =STRING COMMAND
!
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	PRINTC,		! PRINT PLIT WITH TRAILING <CR><LF>
	TYPECR,		! TYPE A <CR><LF>
	TYPE;		! TYPE A CHARACTER

EXTERNAL
    SPECIAL: VECTOR[,WORD],	! WORD VECTOR (PLIT) OF THE SPECIAL CHARACTERS
    MATCHR: BYTE,		! MATCH CHAR FOR SPECIAL SEARCH CHARS
    S1STRLST: VECTOR[,WORD],	! SUBSTITUTE 'FIND' STRING LIST
    S2STRLST: VECTOR[,WORD],	! SUBSTITUTE 'SUB' STRING LIST
    FSTRLST: VECTOR[,WORD];	! FIND STRING LIST

NVROUTINE (GIVS2(STRLST))=
	BEGIN
	LOCAL
	    I,
	    J,
	    CHAR,
	    SPECHR;
	LOCAL
	    TADR: REF VECTOR[,BYTE];
	MAP
	    STRLST: REF VECTOR[,WORD];
	IF (SPECHR<0,8> = .MATCHR<0,8>) EQL 0 THEN SPECHR = %O'077';
	INCR I FROM 1 TO .STRLST[0] DO
		BEGIN
		IF .STRLST[.I] NEQ 0 THEN
			BEGIN
			TADR = .STRLST[.I];
			CHAR = .TADR[0];
			UNTIL .CHAR EQL 0 DO
				BEGIN
				IF .CHAR LSS %O'040' OR
					(.CHAR EQL %O'0174' AND
					.MATCHR<0,8> NEQ 0) THEN
					INCR J FROM 0 TO .SPECIAL[-1]-1 DO
						IF .CHAR EQL .(SPECIAL[.J])<8,8> THEN
							BEGIN
							TYPE (.SPECHR);
							CHAR = .(SPECIAL[.J])<0,8>;
							EXITLOOP;
							END;
				TYPE (.CHAR);
				TADR = .TADR + 1;
				CHAR = .TADR[0];
				END;
			END;
		TYPECR();
		END;
	END;

GBLNVROUTINE (GIVSTR)=
	BEGIN
	PRINTC(PLIT ('  Find-'));
	GIVS2(FSTRLST);
	PRINTC(PLIT ('  Substitute-'));
	GIVS2(S2STRLST);
	PRINTC(PLIT ('  For-'));
	GIVS2(S1STRLST);
	END;


END ELUDOM
