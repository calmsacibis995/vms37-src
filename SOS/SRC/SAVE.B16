MODULE SAVE (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!	020705	PHL0705		Peter H. Lipman		29-Jun-1981
!		If "E" command, then call new FIXPRO entry point.
!		This will make the file protection correct if we
!		were preserving the file protection and the owner
!		did not have all access to the file.
!

!
!	SAVE
!
!	THIS ROUTINE PERFORMS THE AUTO-SAVE AND EXIT COMMAND
!	FUNCTIONS OF UPDATING THE FILE AS SPECIFIED BY THE
!	INITIAL COMMAND STRING.  IT ALSO HANDLES THE VARIOUS
!	OPTIONS ON THE 'E' COMMAND, AND THE CORRESPONDING
!	SWITCH SETTINGS.
!
!	PARAMETERS -
!		SOPT - OPTION TO UNSEQUENCE THE OUTPUT FILE
!		BOPT - NOBACK OPTION
!		DOPT - DELETE INPUT FILE OPTION
!		NEW - FILE ADR OF NEW FILE IF RENAME, OR ZERO
!		QOPT - QUIT OPTION FROM E COMMAND
!		REOPN - NON-ZERO IF FILE IS TO BE POSITIONED
!			BACK TO CURRENT SPOT.
!
!	RETURNS -
!		IF REOPEN, RETURNS NEW ADDRESS OF CURRENT LINE
!
!

!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	CLOSE,		! CLOSE A FILE SPEC
	CLSDEL,		! IF FILE IS OPEN CLOSE AND DELETE IT
	FIND,		! POSITION TO START OF FILE
	NXTLINE,	! ADVANCE ONE LINE
	SAVEXX,		! TO CLOSE UP OUTPUT FILE
	SETFIL,		! SETUP EXT FOR TEMP FILE
	BUFINIT,	! RE-INIT BUFFER AFTER W
	GETLINE,	! READ IN FIRST LINE AFTER W
	FINOUT,		! FINISH WRITING OUTPUT FILE
	FIXPRO,		! FIX FILE PROTECTION
	PRTFIL;		! PRINT FILE NAME SAVED

EXTERNAL
    FILUSE,				! FILE STATUS WORD
    FILEIN,				! PRIMARY INPUT FDB
    FILEOUT,				! PRIMARY OUTPUT FDB
    FILETMP;				! TEMP OUTPUT FDB


GBLROUTINE (SAVE(SOPT,BOPT,DOPT,NEW,QOPT,REOPN))=
BEGIN
LOCAL
    ADR,
    SAVLIN: VECTOR[2],
    NUSE,
    I,
    CHGS;
LABEL L2;
!
!	IF WE SHOULD QUIT FAST,.....
!
IF .QOPT NEQ 0 AND .REOPN EQL 0 THEN
	BEGIN
	CLOSE(FILEIN);	
	CLSDEL(FILEOUT);
	CLSDEL(FILETMP);
	RETURN 0;
	END;
IF (.SOSFLG AND NOBAK) NEQ 0 THEN BOPT=1;
IF (.SOSFLG AND DELETE) NEQ 0 THEN DOPT=1;
IF (.SOSFLG AND UNSEQ) NEQ 0 THEN SOPT=.SOPT OR 1;
IF (.BOPT NEQ 0) AND ((.FILUSE EQL 2) OR (.FILUSE EQL 5)) THEN
	FILUSE=.FILUSE+1;
IF .REOPN NEQ 0 THEN
	BEGIN
	SOPT=0;
	DOPT=0;
	END;
SAVLIN[0]=.CURPAG;
SAVLIN[1]=.CURLIN;
IF READONLY THEN NEW=SOPT=BOPT=DOPT=0;
!
!	CHECK IF WE SHOULD REMOVE SEQ NUMBERS FIRST
!
IF .SOPT NEQ 0 OR (.SOSFLG AND REQCPY) NEQ 0 THEN
	BEGIN
	LOCAL
	    NULRNG: VECTOR[2];
	NULRNG[0]=NULRNG[1]=0;
	FIND(NULRNG,1);
	IF (.SOPT AND 2) NEQ 0 THEN BUFPGW=INF;
	ADR=.BUFFIR;
	DO	BEGIN
		IF .SOPT NEQ 0 THEN SETFLG(.ADR,FLGNOLN);
		ADR=NXTLINE(.ADR,1);
		END WHILE .LINLEN(.ADR) NEQ  0;
	BUFCHG=1;
	END;
ADR=.BUFFIR;
IF .BUFCHG NEQ 0 OR .NEW NEQ 0 OR .FILUSE EQL 4 THEN
	BEGIN
	WHILE .LINLEN(.ADR) NEQ 0 DO ADR=NXTLINE(.ADR,100);
	FINOUT(FILEOUT);
	END;

NUSE=SAVEXX(.DOPT,.NEW,.REOPN);

CHGS=.BUFCHG;
IF .REOPN NEQ 0 THEN
	BEGIN
	IF .SOPT EQL 0 THEN
		SOSFLG=.SOSFLG AND NOT NOLINS;
	FILUSE=.NUSE;
	BUFINIT();
	GETLINE(FILEIN);
	IF .REOPN GTR 0 THEN
		I=FIND(SAVLIN,1)
	ELSE
		I=0;
	END
ELSE	FIXPRO(FILEIN);
IF NOT READONLY THEN
	PRTFIL(FILEIN,(IF .CHGS EQL 0 THEN 3 ELSE 2));
RETURN .I;
END;


END ELUDOM
