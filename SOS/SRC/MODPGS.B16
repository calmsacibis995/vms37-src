MODULE MODPGS (
	IDENT = 'V03-000'
		) =

BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982 BY					    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! WILLIAM T. MARSHALL	19-MAR-76
!
! MODIFIED BY:
!
!


!
!	MODPGS
!
!	THIS ROUTINE IS USED BY THE K (KILL PG MARK) AND M
!	(MARK) COMMANDS TO MODIFY PAGE NUMBERS FOR THE REST
!	OF THE FILE BEYOND A CERTAIN POINT.  
!
!	IF THE NEW PAGE NUMBERING WILL CAUSE A SEQUENCE ERROR,
!	A MESSAGE IS PRODUCED.
!
!	THE PAGE NUMBERS OF THE LINES IN THE BUFFER ARE
!	MODIFIED BY THE VALUE GIVEN, AND THE PAGE NUMBER OF
!	THE LAST LINE (IN THE BUFFER HEADER) IS UPDATED.
!	THIS CAUSES 'GETLINE' TO DECREASE/INCREASE ALL
!	SUBSEQUENT PAGE NUMBERS ACCORDINGLY.
!
!	PARAMETERS -
!		ADRST - ADDRESS OF FIRST LINE TO MODIFY
!		MODIF - CHANGE TO MAKE IN PAGE NUMBERS
!			USUALLY JUST +1 OR -1.
!
!	RETURNS -
!		NONE
!
!
!	V02.02	09-JUN-76	WTM
!	V02.01	19-MAR-76	WTM


REQUIRE 'SRC$:SOSREQ.B16';

EXROUTINE
	CMDABO,		! COMMAND ABORT
	PRVLINE,	! BACK UP ONE LINE
	PRINTC,		! PRINT AN ORDER MESSAGE WITH TRAILING <CR><LF>
	NXTLINE;	! FORWARD A LINE


GBLNVROUTINE (MODPGS(ADRST,MODIF))=
BEGIN
LOCAL
    ADR;
IF READONLY THEN CMDABO(INTERNAL);
IF .MODIF LSS 0 THEN
	BEGIN
	ADR=PRVLINE(.ADRST,1);
	IF .ADR EQL 0 AND (.LINPAG(.ADRST)+.MODIF) LSS 1
		THEN CMDABO(BADKLR);
	ADRST=NXTLINE(.ADR,1);
	IF .ADR NEQ 0 AND .LINPAG(.ADR) EQL .LINPAG(.ADRST)+.MODIF
		AND .LINLIN(.ADR) GTRU .LINLIN(.ADRST) THEN
		PRINTC(PLIT ('Lines out of order.'));
	END;
ADR=.ADRST;
DO	BEGIN
	LINPAG(.ADR)=.LINPAG(.ADR)+.MODIF;
	IF .ADR EQL .BUFLST THEN EXITLOOP;
	ADR=NXTLINE(.ADR,1);
	END 	WHILE .LINLEN(.ADR) NEQ 0;
IF .LINLEN(.ADR) NEQ 0 THEN
	BUFPG2=.BUFPG2+.MODIF;
IF .PAGHIG LSSU INF-1 THEN
	PAGHIG=.PAGHIG+.MODIF;
IF .ADRST EQL .BUFFIR THEN
	BUFPG1=.BUFPG1+.MODIF;
BUFCHG=.BUFCHG+1;
END;

END ELUDOM
